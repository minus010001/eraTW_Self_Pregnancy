@SAMEN_DIRECTION(ARG,ARG:1)
#FUNCTION
;ARG 射精する人
;ARG:1 射精される人
LOCALS = /{ARG:1}/
SIF STRCOUNT(CSTR:ARG:10,LOCALS)
	RETURNF 1

@SAMEN_DIRECTION2(ARG,ARG:1)
#FUNCTION
;ARG 射精する人
;ARG:1 射精される人
LOCALS = /{ARG:1}/
SIF STRCOUNT(CSTR:ARG:11,LOCALS)
	RETURNF 1

@EVENT_SHOOT
#DIM PARTICIPANTS	;参加人数
VARSET LOCAL
;TCVAR:2 射精部位フラグ（1=膣内 2=肛門 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
;TCVAR:4 射精快感強度
;射精前判定
FOR LOCAL,0,CHARANUM
	SIF !HAS_PENIS(LOCAL)
		CONTINUE
	;避孕套
	IF TEQUIP:LOCAL:避孕套
		TCVAR:LOCAL:避孕套 = 1
	ELSE
		TCVAR:LOCAL:避孕套 = 0
	ENDIF
	SIF LOCAL > 0
		CALL TARGET_EJAC_CHECK(LOCAL)
	;雙重口交などのふたりめ補正に使用
	PARTICIPANTS = 0
	FOR LOCAL:1,0,CHARANUM
		IF LOCAL:1 == 0
			LOCAL:3 = 0
		ELSE
			SIF TARGET:(LOCAL:1) <= 0
				CONTINUE
			LOCAL:3 = TARGET:(LOCAL:1)
		ENDIF
		SIF SAMEN_DIRECTION(LOCAL,LOCAL:3)
			CALL EJAC_CHECK(LOCAL,LOCAL:3,PARTICIPANTS)
		PARTICIPANTS ++
	NEXT
NEXT
;射精処理
FOR LOCAL, 0, GET_TARGETNUM() + 1
	LOCAL:1 = TARGET:LOCAL
	SIF !LOCAL
		LOCAL:1 = MASTER
	;射精処理
	SIF TCVAR:(LOCAL:1):射精快感強度
		CALL EJAC_CHECK_AFTER(LOCAL:1)
	SIF NOWEX:(LOCAL:1):射精
		CALL SAMEN_SHOOT(LOCAL:1)
NEXT

;必須ではないが口上表示をわかりやすくするために射精状況をTFLAG:1に割り振る(現状では必須です)
;PLAYERから射精対象への膣内～肛門奉仕射精まではTCVAR:12(=1～9)に準拠
SIF SAMEN_DIRECTION2(PLAYER,MASTER_POSE(0,SET_P))
	SETBIT TFLAG:1,TCVAR:PLAYER:12

FOR LOCAL,1,CHARANUM
	;避孕套判定（角色分）
	IF TCVAR:LOCAL:避孕套 == 1 && NOWEX:LOCAL:射精
		;20190823 現状無いけど角色側に避孕套つけた場合を追加するならここを想定
	ENDIF
	IF SAMEN_DIRECTION2(PLAYER,LOCAL)
		SETBIT TCVAR:LOCAL:50,TCVAR:PLAYER:12
	ENDIF
NEXT

;射精部位が特殊な場合

;避孕套判定（你分）
IF TCVAR:PLAYER:避孕套 == 1 && NOWEX:PLAYER:射精
	SETBIT TFLAG:1,0
	SETBIT TCVAR:50,0
ENDIF
;膣外射精（あなた分）
IF TCVAR:PLAYER:外に出すから == 1 && NOWEX:PLAYER:射精
	;あなたが絶倫またはＶ性交経験が少ないと約10％の確率で中出し
	IF TALENT:PLAYER:精力超群 || EXP:PLAYER:Ｖ性交経験 <= 100
		SELECTCASE RAND:100
		CASE IS > 10
			SETBIT TFLAG:1,8
			SETBIT TCVAR:50,8
		CASEELSE
			SETBIT TFLAG:1,0
			SETBIT TCVAR:50,0
		ENDSELECT
	;それ以外は約5％の確率で中出し
	ELSE
		SELECTCASE RAND:100
		CASE IS > 5
			SETBIT TFLAG:1,8
			SETBIT TCVAR:50,8
		CASEELSE
			SETBIT TFLAG:1,0
			SETBIT TCVAR:50,0
		ENDSELECT
	ENDIF
ENDIF
;手淫口交
IF TFLAG:50 == 5 && NOWEX:PLAYER:射精
	SETBIT TFLAG:1,20
	SETBIT TCVAR:50,20
ENDIF
;乳夾口交
IF TFLAG:50 == 12 && NOWEX:PLAYER:射精
	SETBIT TFLAG:1,21
	SETBIT TCVAR:50,21
ENDIF
;授乳PLAY
IF TFLAG:50 == 19 && NOWEX:PLAYER:射精
	SETBIT TFLAG:1,26
	SETBIT TCVAR:50,26
ENDIF
;泡泡浴でTARGET射精
IF TCVAR:PLAYER:12 == 8
	SETBIT TFLAG:1,8
	SETBIT TCVAR:50,8
ENDIF
;六九式
IF TFLAG:50 == 1 && (NOWEX:PLAYER:射精 || NOWEX:射精)
	SETBIT TFLAG:1,22
	SETBIT TCVAR:50,22
ENDIF
;深喉插入
IF SELECTCOM == 140 && NOWEX:PLAYER:射精
	SETBIT TFLAG:1,23
	SETBIT TCVAR:50,23
ENDIF
;子宮口責め
IF TFLAG:50 == 7 && NOWEX:PLAYER:射精
	SETBIT TFLAG:1,24
	SETBIT TCVAR:50,24
ENDIF
SIF !GETBIT(TFLAG:一日一回,8) && NOWEX:PLAYER:射精
	SETBIT TFLAG:一日一回,8


@EJAC_CHECK(ARG,PRIMARY, SECONDARY)
#DIM 技巧補正
#DIM ゲージ増加量
#DIM 総計快楽強度
#DIM Ｐ感度補正
#DIM 従順補正
#DIM 欲望補正
#DIM 奉仕精神補正
#DIM 潤滑補正
#DIM PRIMARY	;射精させる人
#DIM SECONDARY	;W口交等におけるふたりめ補正
#DIM divisor
;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
;LOCAL:1～に補正値をぶっこんでいきます
;CFLAG:101 射精部位フラグ（1=膣内 2=肛門 3=手淫 4=口淫 5=乳交 6= 

;技巧（共通）
LOCAL = ABL:PRIMARY:技巧
技巧補正 = 10 + GET_REVISION(LOCAL, 5 , 5)

;従順（共通）
LOCAL = 0
IF PRIMARY == MASTER
	LOCAL = 3
ELSE
	LOCAL = ABL:PRIMARY:従順
ENDIF
従順補正 = 10 + GET_REVISION(LOCAL, 3 , 5)

;奉仕精神(奉仕系)
LOCAL = 0
IF TCVAR:PRIMARY:奉仕FLAG || TCVAR:(ARG):奉仕FLAG
	LOCAL = ABL:PRIMARY:奉仕精神
	奉仕精神補正 = 10 + GET_REVISION(LOCAL, 10 , 5)
ELSE
	奉仕精神補正 = 10
ENDIF

;欲望(性交系)
欲望補正 = 10
IF TCVAR:ARG:2 <= 2
	LOCAL = 0
	IF PRIMARY == MASTER
		LOCAL = 3
	ELSE
		LOCAL = ABL:PRIMARY:欲望
	ENDIF
	欲望補正 = 10 + GET_REVISION(LOCAL, 5 , 3)
ENDIF

;潤滑
潤滑補正 = 10
IF TCVAR:ARG:2 <= 2 || TCVAR:ARG:2 == 6
	IF PALAM:潤滑 < PALAMLV:2
		潤滑補正 = 2
	ELSEIF PALAM:潤滑 < PALAMLV:3
		潤滑補正 = 4
	ELSEIF PALAM:潤滑 < PALAMLV:4
		潤滑補正 = 6
	ELSEIF PALAM:潤滑 < PALAMLV:7
		潤滑補正 = 8
	ELSE
		潤滑補正 = 10
	ENDIF
ENDIF

;プレイヤーのＣ感覚をみる
Ｐ感度補正 = 10 + ABL:ARG:0 / 2

DEBUGPRINTFORML 補正前快楽強度{TCVAR:ARG:4}
DEBUGPRINTFORML 技巧補正{技巧補正}
DEBUGPRINTFORML 欲望補正{欲望補正}
DEBUGPRINTFORML 潤滑補正{潤滑補正}
DEBUGPRINTFORML 奉仕精神補正{奉仕精神補正}
DEBUGPRINTFORML Ｐ感度補正{Ｐ感度補正}
DEBUGPRINTFORML 部位補正{部位補正(ARG,PRIMARY,SECONDARY)}
総計快楽強度 = TCVAR:ARG:4 * 技巧補正 * 従順補正 * Ｐ感度補正 * 奉仕精神補正 * 潤滑補正 * 欲望補正 * 部位補正(ARG,PRIMARY,SECONDARY) / 10000000 * (SECONDARY + 1)

SIF CFLAG:ARG:诶嘿嘿 != 2
	総計快楽強度 = 総計快楽強度 * (BASE:ARG:精力 + MAXBASE:ARG:精力) / (MAXBASE:ARG:精力 * 2)
SIF TCVAR:ARG:避孕套
	総計快楽強度 = 総計快楽強度 * 4 / 5
SIF TEQUIP:ARG:挑逗モード
	総計快楽強度 = 総計快楽強度 * 4 / 5
SIF TALENT:ARG:路人
	総計快楽強度 = 総計快楽強度 * 4 / 5
ゲージ増加量 = 総計快楽強度

DEBUGPRINTFORML 補正後快楽強度{総計快楽強度}

SELECTCASE BASE:ARG:勃起
	CASE IS < 300
		総計快楽強度 /= 5
	CASE IS < 500
		総計快楽強度 /= 4
	CASE IS < 1000
		総計快楽強度 /= 3
	CASE IS < 1500
		総計快楽強度 /= 2
	CASE 1500
		総計快楽強度 = 総計快楽強度 * 2 / 3
ENDSELECT
BASE:ARG:勃起 = BASE:ARG:勃起 + 総計快楽強度 / 5

SELECTCASE BASE:ARG:勃起
	CASE IS < 500
		ゲージ増加量 /= 5
	CASE IS < 1000
		ゲージ増加量 /= 3
	CASE IS < 1500
		ゲージ増加量 = ゲージ増加量 / 5 * 2
	CASE 1500
		ゲージ増加量 /= 2
ENDSELECT

IF !TCVAR:ARG:要去了
	divisor = POWER(2, TCVAR:ARG:射精我慢)
	ゲージ増加量 = ゲージ増加量 / divisor
ENDIF

BASE:ARG:射精 += ゲージ増加量

;PRINTFORMW %CALLNAME:(PRIMARY)%によるゲージ増加【{ゲージ増加量}】

TCVAR:ARG:射精槽増加量 = ゲージ増加量

@部位補正(ARG,PRIMARY,SECONDARY)
;-------------------------------------------------
;LOCAL 補正前の快楽強度
;ARG 射精する人
;PRIMARY 射精させる人
;SECONDARY W口交等におけるふたりめ補正
;-------------------------------------------------
#FUNCTION
#DIM PRIMARY
#DIM SECONDARY
LOCAL = 10
;恋慕
SIF TALENT:PRIMARY:恋慕
	LOCAL += 2

SELECTCASE TCVAR:ARG:2
	;膣
	CASE 1
		;処女
		SIF EX:PRIMARY:処女喪失记号
			LOCAL += 10
		SIF TALENT:PRIMARY:淫壺
			LOCAL += 3
		SIF !FLAG:70
			LOCAL += ((ABL:PRIMARY:腰 + ABL:PRIMARY:膣 * 2) / 2 - 3)
		SIF NOWEX:PRIMARY:Ｖ絶頂
			LOCAL += 1
		SIF 挿入不可(PRIMARY) == -1 && ABL:PRIMARY:膣 < 2
			LOCAL -= 2
		SIF 挿入不可(PRIMARY) == -2 && ABL:PRIMARY:膣 < 3
			LOCAL -= 5
	;肛門
	CASE 2
		SIF !EXP:PRIMARY:Ａ性交経験
			LOCAL += 8
		SIF TALENT:PRIMARY:尻穴狂
			LOCAL += 3
		SIF !FLAG:70
			LOCAL += ((ABL:PRIMARY:腰 + ABL:PRIMARY:肛門 * 2) / 2 - 3)
		SIF NOWEX:PRIMARY:Ａ絶頂
			LOCAL += 1
		SIF 挿入不可(PRIMARY) == -1 && ABL:PRIMARY:肛門 < 2
			LOCAL -= 2
		SIF 挿入不可(PRIMARY) == -2 && ABL:PRIMARY:肛門 < 3
			LOCAL -= 5
	;手淫
	CASE 3
		SIF TALENT:PRIMARY:靈活手指
			LOCAL += 2
		SIF !FLAG:70 && !CFLAG:恶作剧
			LOCAL += (ABL:PRIMARY:指 - 2) / 2
	;口淫
	CASE 4
		SIF TALENT:PRIMARY:舌頭靈活
			LOCAL += 2
		SIF TALENT:PRIMARY:猫舌
			LOCAL -= 2
		SIF !FLAG:70 && !CFLAG:PRIMARY:恶作剧
			LOCAL += (ABL:PRIMARY:舌 - 2) / 2
		;乳夾口交
		IF TFLAG:50 == 12
			LOCAL += TALENT:PRIMARY:胸圍
			IF !FLAG:70 && !CFLAG:PRIMARY:恶作剧
				SELECTCASE ABL:PRIMARY:胸
					CASE 0 TO 1
						LOCAL = LOCAL - 6 + ABL:PRIMARY:胸
					CASE IS >= 2
						LOCAL += (ABL:PRIMARY:胸 - 2) / 2
				ENDSELECT
			ENDIF
		ENDIF
		;ついでにお掃除フラグ
		IF ABL:PRIMARY:奉仕精神 >= 2 && ABL:PRIMARY:技巧 >= 2 && TEQUIP:PRIMARY:口球 == 0 && EXP:PRIMARY:精飲経験 > 20
			STAIN:ARG:2 = 2
			IF TFLAG:50 == 9 && PRIMARY == MASTER_POSE(SET_M,SET_P,1)
				TFLAG:8 |= 2
			ELSEIF PRIMARY == TARGET
				TFLAG:8 |= 1
			ENDIF
		ENDIF
	;乳交
	CASE 5
		LOCAL += TALENT:PRIMARY:胸圍
		IF !FLAG:70 && !CFLAG:PRIMARY:恶作剧
			SELECTCASE ABL:PRIMARY:胸
				CASE 0 TO 1
					LOCAL = LOCAL - 6 + ABL:PRIMARY:胸
				CASE IS >= 2
					LOCAL += (ABL:PRIMARY:胸 - 2) / 2
			ENDSELECT
		ENDIF
		;乳夾口交
		IF TFLAG:50 == 12
			SIF TALENT:PRIMARY:舌頭靈活
				ARG += 2
			SIF TALENT:PRIMARY:猫舌
				ARG -= 2
			LOCAL += (ABL:PRIMARY:舌 - 2) / 2
		ENDIF
		;お掃除フラグ
		IF ABL:PRIMARY:奉仕精神 >= 2 && ABL:PRIMARY:技巧 >= 2 && TEQUIP:PRIMARY:口球 == 0 && EXP:PRIMARY:精飲経験 > 20
			STAIN:ARG:2 = 2
			IF PRIMARY == ASSI
				TFLAG:8 |= 2
			ELSEIF PRIMARY == TARGET
				TFLAG:8 |= 1
			ENDIF
		ENDIF
	;素股
	CASE 6
	;足交
	CASE 7
		SIF TALENT:PRIMARY:施虐狂
			LOCAL += 2
	;体表(泡泡浴)
	CASE 8
	;肛門奉仕
	CASE 9
		SIF TALENT:PRIMARY:舌頭靈活
			LOCAL += 2
		SIF TALENT:PRIMARY:猫舌
			LOCAL -= 2
		SIF !FLAG:70
			LOCAL += (ABL:PRIMARY:舌 - 2) / 2
	;道具
	CASE 10
	;触手
	CASE 11
	;Ａ責め
	CASE 12
	;Ｖ責め
	CASE 13
ENDSELECT

RETURNF LOCAL
