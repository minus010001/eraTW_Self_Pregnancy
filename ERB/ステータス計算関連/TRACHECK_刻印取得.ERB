
;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;刻印取得のチェック
;CALL		USER
;ARG		0:角色NO
;RETURN		VOID
;COMMENT	21/08/27 反発刻印取得仕様を一部変更
;-----------------------------------------------------------
@MARK_GOT_CHECK(ARG)
#DIM 刻印取得値
;-------------------------------------------------
;苦痛刻印
;-------------------------------------------------
IF CUP:ARG:苦痛 >= 2000 && CUP:ARG:苦痛 < 4000 && MARK:ARG:苦痛刻印 <= 0
	MARK:ARG:苦痛刻印 = 1
	TFLAG:苦痛刻印取得 = 1
ELSEIF CUP:ARG:苦痛 >= 4000 && CUP:ARG:苦痛 < 8000 && MARK:ARG:苦痛刻印 <= 1
	MARK:ARG:苦痛刻印 = 2
	TFLAG:苦痛刻印取得 = 2
	;堅強・感情缺乏の場合は苦痛刻印による従順UPは無
	IF !ABL:ARG:従順 && TALENT:ARG:胆量 > 0 && !TALENT:ARG:感情缺乏
		ABL:ARG:従順 = 1
		TFLAG:刻印従順変化 = 1
	ENDIF
ELSEIF CUP:ARG:苦痛 >= 8000 && MARK:ARG:苦痛刻印 <= 2
	MARK:ARG:苦痛刻印 = 3
	TFLAG:苦痛刻印取得 = 3
	;堅強・感情缺乏の場合は苦痛刻印による従順UPは無
	IF !ABL:ARG:従順 && TALENT:ARG:胆量 > 0 && !TALENT:ARG:感情缺乏
		ABL:ARG:従順 = 1
		TFLAG:刻印従順変化 = 1
	ENDIF
	IF TALENT:PLAYER:施虐狂
		EXP:ARG:異常経験 += 2
	ELSEIF TALENT:PLAYER:擅長用針 && SELECTCOM == 103
		EXP:ARG:異常経験 += 2
	ELSE
		EXP:ARG:異常経験 += 1
	ENDIF
ENDIF
;-------------------------------------------------
;時姦刻印
;-------------------------------------------------
IF TCVAR:ARG:時姦刻印取得用 == 1 && MARK:ARG:時姦刻印 <= 0
	MARK:ARG:時姦刻印 = 1
	TFLAG:時姦刻印取得 = 1
ELSEIF TCVAR:ARG:時姦刻印取得用 == 2 && MARK:ARG:時姦刻印 <= 1
	MARK:ARG:時姦刻印 = 2
	TFLAG:時姦刻印取得 = 2
ELSEIF TCVAR:ARG:時姦刻印取得用 == 3 && MARK:ARG:時姦刻印 <= 2
	MARK:ARG:時姦刻印 = 3
	TFLAG:時姦刻印取得 = 3
ENDIF
;-------------------------------------------------
;快楽刻印
;-------------------------------------------------
IF TCVAR:ARG:快楽強度 == 1 && MARK:ARG:快楽刻印 <= 0
	MARK:ARG:快楽刻印 = 1
	TFLAG:23 = 1
ELSEIF TCVAR:快楽強度 == 2 && MARK:ARG:快楽刻印 <= 1
	MARK:ARG:快楽刻印 = 2
	TFLAG:23 = 2
ELSEIF TCVAR:快楽強度 == 3 && MARK:ARG:快楽刻印 <= 2
	MARK:ARG:快楽刻印 = 3
	TFLAG:23 = 3
	;自制心・感情缺乏の場合は快楽刻印による従順UPは無
	IF !ABL:ARG:従順 && !TALENT:ARG:自制心 && !TALENT:ARG:感情缺乏
		ABL:ARG:従順 = 1
		TFLAG:刻印従順変化 = 1
	ENDIF
;ELSEIF  TCVAR:快楽強度 == 3 && TALENT:幼稚 && !TALENT:初潮
;		TALENT:初潮 = 1
;		TFLAG:23 = 4
ENDIF
;-------------------------------------------------
;不埒刻印
;-------------------------------------------------
IF FLAG:70 == 1 || CFLAG:ARG:恶作剧
ELSE
[SKIPSTART]
	A = CUP:ARG:快Ｃ + CUP:ARG:快Ｖ + CUP:ARG:快Ｂ + CUP:ARG:快Ａ

	;快楽によるLvUPは10番+取得した不埒刻印のレベルのTFLAGを与える
	IF A >= 3000 && A < 5000 && MARK:ARG:快楽刻印 > MARK:ARG:不埒刻印 && MARK:ARG:不埒刻印 <= 0
		MARK:ARG:不埒刻印 = 1
		TFLAG:24 = 11
	ELSEIF A >= 5000 && A < 9000 && MARK:ARG:快楽刻印 > MARK:ARG:不埒刻印 && MARK:ARG:不埒刻印 <= 1
		MARK:ARG:不埒刻印 = 2
		TFLAG:24 = 12
		;感情缺乏の場合は不埒刻印による従順UPは無
		IF !ABL:ARG:従順 && !TALENT:ARG:感情缺乏
			ABL:ARG:従順 = 1
			TFLAG:刻印従順変化 = 1
		ENDIF
	ELSEIF A >= 9000 && MARK:ARG:快楽刻印 > MARK:ARG:不埒刻印 && MARK:ARG:不埒刻印 <= 2
		MARK:ARG:不埒刻印 = 3
		TFLAG:24 = 13
		;感情缺乏の場合は不埒刻印による従順UPは無
		IF ABL:ARG:従順 <= 1 && !TALENT:ARG:感情缺乏
			ABL:ARG:従順 = 2
			TFLAG:刻印従順変化 = 1
		ENDIF
	ENDIF
	A = CUP:ARG:苦痛 + CUP:ARG:恐怖

	;苦痛と恐怖によるLvUPは20番+取得した不埒刻印のレベルのTFLAGを与える
	IF A >= 1500 && A < 2400 && MARK:ARG:不埒刻印 <= 0
		MARK:ARG:不埒刻印 = 1
		TFLAG:24 = 21
	ELSEIF A >= 2400 && A < 6000 && MARK:ARG:不埒刻印 <= 1
		MARK:ARG:不埒刻印 = 2
		TFLAG:24 = 22
		;感情缺乏の場合は不埒刻印による従順UPは無
		IF !ABL:ARG:従順 && !TALENT:ARG:感情缺乏
			ABL:ARG:従順 = 1
			TFLAG:刻印従順変化 = 1
		ENDIF
	ELSEIF A >= 6000 && MARK:ARG:不埒刻印 <= 2
		MARK:ARG:不埒刻印 = 3
		TFLAG:24 = 23
		;感情缺乏の場合は不埒刻印による従順UPは無
		IF ABL:ARG:従順 <= 1 && !TALENT:ARG:感情缺乏
			ABL:ARG:従順 = 2
			TFLAG:刻印従順変化 = 1
		ENDIF
	ENDIF
[SKIPEND]

A = CUP:ARG:恭順 + CUP:ARG:屈服

;恭順と屈服によるLvUPは30番+取得した不埒刻印のレベルのTFLAGを与える
IF A >= 4500 && A < 7000 && MARK:ARG:不埒刻印 <= 0
	MARK:ARG:不埒刻印 = 1
	TFLAG:24 = 31
ELSEIF A >= 7000 && A < 10000 && MARK:ARG:不埒刻印 <= 1
	MARK:ARG:不埒刻印 = 2
	TFLAG:24 = 32
	;感情缺乏の場合は不埒刻印による従順UPは無
	IF !ABL:ARG:従順 && !TALENT:ARG:感情缺乏
		ABL:ARG:従順 = 1
		TFLAG:刻印従順変化 = 1
	ENDIF
ELSEIF A >= 10000 && MARK:ARG:不埒刻印 <= 2
	MARK:ARG:不埒刻印 = 3
	TFLAG:24 = 33
	;感情缺乏の場合は不埒刻印による従順UPは無
	IF ABL:ARG:従順 <= 1 && !TALENT:ARG:感情缺乏
		ABL:ARG:従順 = 2
		TFLAG:刻印従順変化 = 1
	ENDIF
ENDIF

;-------------------------------------------------
;反発刻印
;------------------------------------------------- 
;MASTERは除外
SIF TALENT:ARG:反発抑制
	RETURN 0
SIF CFLAG:ARG:恶作剧
	RETURN 0
SIF ARG == MASTER
	RETURN 0
SIF TFLAG:150
	RETURN 0
刻印取得値 = CUP:ARG:反感+CUP:ARG:不快
;反発履歴残している場合、少しだけ反発刻印を付きづらくする
;同レベル以下の刻印が付かないだと強すぎるので、刻印レベルを１段階引き下げに
SIF MARK:ARG:反発取得履歴
		刻印取得値 = INRANGE(刻印取得値, 500, 700) ? 0 # 刻印取得値
SIF MARK:ARG:反発取得履歴 > 1
		刻印取得値 = INRANGE(刻印取得値, 1001, 1300) ? 1000 # 刻印取得値
SIF MARK:ARG:反発取得履歴 > 2
		刻印取得値 = INRANGE(刻印取得値, 2001, 2500) ? 2000 # 刻印取得値
SELECTCASE 刻印取得値
	CASE 500 TO 1000
		IF MARK:ARG:反発刻印 < 1
			MARK:ARG:反発刻印 = 1
			MARK:ARG:反発取得履歴 = MAX(MARK:ARG:反発取得履歴,1)
			TFLAG:反発刻印取得 = 1
			CFLAG:ARG:心情不快 = 1
			CFLAG:ARG:ブチギレ = 1
			TALENT:ARG:心情 = -1
		ENDIF
	CASE 1001 TO 2000
		IF MARK:ARG:反発刻印 < 2
			MARK:ARG:反発刻印 = 2
			TFLAG:反発刻印取得 = 2
			MARK:ARG:反発取得履歴 = MAX(MARK:ARG:反発取得履歴,2)
			CFLAG:ARG:心情不快 = 2
			CFLAG:ARG:ブチギレ = 1
			TALENT:ARG:心情 = -1
			IF MARK:ARG:反発取得履歴 < 2 || 刻印取得値 >= 1200
				;感情缺乏の場合は反発刻印による従順ダウンは無
				IF INRANGE(ABL:ARG:従順,1,5) && !TALENT:ARG:感情缺乏
					ABL:従順 = MAX(ABL:従順 - 1, 0)
					ABL:親密 = MAX(ABL:親密 - 1, 0)
					TFLAG:刻印従順変化 = 1
				ELSEIF ABL:ARG:従順 > 5 && !TALENT:ARG:感情缺乏
					ABL:従順 = MAX(ABL:従順 - 1, 0)
					TFLAG:刻印従順変化 = 2
				ENDIF
			ENDIF
		ENDIF
	CASE IS > 2000
		IF MARK:ARG:反発刻印 < 3
			MARK:ARG:反発刻印 = 3
			TFLAG:反発刻印取得 = 3
			MARK:ARG:反発取得履歴 = MAX(MARK:ARG:反発取得履歴,3)
			CFLAG:ARG:心情不快 = 3
			CFLAG:ARG:ブチギレ = 1
			TALENT:ARG:心情 = -1
			IF MARK:ARG:反発取得履歴 < 3 || 刻印取得値 >= 2400
				;感情缺乏の場合は反発刻印による従順ダウンは無
				IF INRANGE(ABL:ARG:従順,1,5) && !TALENT:ARG:感情缺乏
					ABL:従順 = MAX(ABL:従順 - 2, 0)
					ABL:親密 = MAX(ABL:親密 - 2, 0)
					TFLAG:刻印従順変化 = 1
				ELSEIF ABL:ARG:従順 > 5 && !TALENT:ARG:感情缺乏
					ABL:従順 = MAX(ABL:従順 - 1, 0)
					ABL:親密 = MAX(ABL:親密 - 1, 0)
					TFLAG:刻印従順変化 = 2
				ENDIF
			ENDIF
		ENDIF
ENDSELECT
ENDIF
;警告牌発行チェック
SIF !TALENT:ARG:恋慕 || RAND:(ABL:ARG:親密 + 1) == 0 || MARK:ARG:反発刻印 == 3 && FLAG:18
	CALL CARD_GOT_CHECK(ARG)
TFLAG:150 = 0