;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;好感度上昇計算処理
;CALL		USER
;ARG		0:角色NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@FAVOR_CALC(ARG)
#DIM 憤怒上昇値
#DIM 情緒上昇値
#DIM 信賴度キャップ補正
#DIM 理性減少値
SIF FLAG:70 == 1 && CFLAG:TARGET:344 == 0
	RETURN

;COMABLE範囲外抑止
SIF !TFLAG:102
	RETURN 0
;対象がMASTER以外の時起動
SIF !ARG
	RETURN 0
;主人(你)と対象の能力を計算
;ABL:従順をみる
TFLAG:97 += (ABL:ARG:従順 + ABL:ARG:親密) / 2

;主人と対象が同性
IF HETEROSEX(TARGET,PLAYER)
	;男なら断袖属性、女なら百合属性を見る
	IF ABL:ARG:(HETEROSEX(TARGET,PLAYER) + 16) == 0
		TFLAG:97 -= 1
	ELSEIF ABL:ARG:(HETEROSEX(TARGET,PLAYER) + 16) < 5
		TFLAG:97 += 0
	ELSEIF ABL:ARG:(HETEROSEX(TARGET,PLAYER) + 16) < 9
		TFLAG:97 += 1
	ELSE
		TFLAG:97 += 2
	ENDIF
	;男ならＢＬ中毒、女なら百合中毒を見る
	IF ABL:ARG:(HETEROSEX(TARGET,PLAYER) + 31) > 9
		TFLAG:97 += 2
	ELSEIF ABL:ARG:(HETEROSEX(TARGET,PLAYER) + 31) > 4
		TFLAG:97 += 1
	ENDIF
	;兩面通吃
	SIF TALENT:ARG:性別嗜好 == -1
		TFLAG:97 += 1
ENDIF
;素質による増減
;叛逆
SIF TALENT:ARG:態度 > 0
	TFLAG:97 -= 2
;自尊心高
SIF TALENT:ARG:自尊心 > 0
	TFLAG:97 -= 1
;坦率
SIF TALENT:ARG:態度 < 0 && TFLAG:97 > 0
	TFLAG:97 += 1
;自制心
SIF TALENT:ARG:自制心
	TFLAG:97 -= 1
;感情缺乏
SIF TALENT:ARG:感情缺乏
	TFLAG:97 -= 1
;抵抗
SIF TALENT:ARG:抵抗
	TFLAG:97 -= 1
;献身的
SIF TALENT:ARG:献身的 && TFLAG:97 > 0
	TFLAG:97 += 1
;開朗同士
SIF TALENT:ARG:開朗／阴郁 > 0 && TALENT:MASTER:開朗／阴郁 > 0
	TFLAG:97 += 1

;阴郁同士
SIF TALENT:ARG:開朗／阴郁 < 0 && TALENT:MASTER:開朗／阴郁 < 0
	TFLAG:97 += 1
;接受快感
IF TALENT:ARG:快感応答 > 0
	TFLAG:97 += 1
;否定快感
ELSEIF TALENT:ARG:快感応答 < 0
	TFLAG:97 -= 1
ENDIF
;心情
TFLAG:97 += TALENT:ARG:15
;討厭男人
IF OTOKOGIRAI(ARG)
	TFLAG:97 -= 3
	IF GETBIT (CFLAG:ARG:继承, 1)
		TFLAG:97 += 2
	ELSEIF GETBIT (CFLAG:ARG:继承, 0)
		TFLAG:97 += 1
	ENDIF
ENDIF
;恋慕
IF TALENT:ARG:恋慕
	TFLAG:97 += 2
;催情薬
ELSEIF TCVAR:ARG:催情薬
	TFLAG:97 ++
	SIF CFLAG:ARG:2 < 1000
		TFLAG:97 ++
ENDIF
;仙香玉兎
IF IsInsenceValid(ARG)
	TFLAG:97 ++
	SIF CFLAG:ARG:2 < 1000
		TFLAG:97 ++
ENDIF
;毒气花园
SIF IsGardenValid(ARG)
	TFLAG:97 ++

SIF TALENT:ARG:動物耳 && TALENT:ARG:動物耳 == TFLAG:耳補正
	TFLAG:97 += 3
;思慕
SIF TALENT:ARG:思慕
	TFLAG:97 += 1
;主人の容姿
;TFLAG:97 += TALENT:MASTER:容姿
;主人の魅力
SIF TALENT:MASTER:魅力
	TFLAG:97 += 1
;主人の魅惑
SIF TALENT:MASTER:魅惑
	TFLAG:97 += 1
;主人の謎之魅力
SIF TALENT:MASTER:謎之魅力
	TFLAG:97 += 1
;主人の人気
SIF TALENT:MASTER:人気
	TFLAG:97 += 1

IF GETBIT (CFLAG:ARG:继承, 1)
	TFLAG:97 += 2
ELSEIF GETBIT (CFLAG:ARG:继承, 0)
	TFLAG:97 += 1
ENDIF

LOCAL = 0
LOCAL:1 = 0
;各種快感
;50上限、30000と+300 この数値が大きければSOURCE:ARGの増加に対して鈍感になる
LOCAL += (100 - 30000 / (SOURCE:ARG:快Ｃ + SOURCE:ARG:快Ｖ + SOURCE:ARG:快Ａ + SOURCE:ARG:快Ｂ + 300)) * (1 + TALENT:ARG:快感応答)
;情愛
LOCAL += (100 - 100000 / (SOURCE:ARG:情愛 + 1000))
LOCAL:1 += (100 - 100000 / (SOURCE:ARG:情愛 + 1000))
;欲情
LOCAL += (20 - 20000 / (SOURCE:ARG:欲情 + 1000))
LOCAL:1 += (100 - 100000 / (SOURCE:ARG:欲情 + 1000))
;達成感
LOCAL += (30 - 30000 / (SOURCE:ARG:達成 + 1000))
;恭順
LOCAL += (20 - 20000 / (SOURCE:ARG:恭順 + 1000))
;屈服
LOCAL += (20 - 20000 / (SOURCE:ARG:屈従 + 1000))
;露出
LOCAL += (30 - 30000 / (SOURCE:ARG:露出 + 1000)) * (ABL:ARG:露出癖 - 3) / 3

;苦痛
LOCAL += (50 - 20000 / (SOURCE:ARG:苦痛 + 400)) * (ABL:ARG:受虐属性 - 3) / 3
;歓楽
LOCAL += (50 - 10000 / (SOURCE:ARG:歓楽 + 2000))
;征服
LOCAL += (30 - 90000 / (SOURCE:ARG:征服 + 3000))
;受動
LOCAL += (30 - 90000 / (SOURCE:ARG:受動 + 3000))
;恐怖
LOCAL -= (50 - 20000 / (SOURCE:ARG:恐怖 + 400)) * (ABL:ARG:従順 - 3) / 3
;不潔
LOCAL -= (50 - 25000 / (SOURCE:ARG:不潔 + 500)) * (2 - TALENT:ARG:汚臭耐性) / 2
;鬱屈
LOCAL -= (50 - 15000 / (SOURCE:ARG:鬱屈 + 300))
;逸脱
LOCAL -= (50 - 25000 / (SOURCE:ARG:逸脱 + 500))
;反発
LOCAL -= (50 - 10000 / (SOURCE:ARG:反感 + 200))

;好感度上昇分に計算値追加
TFLAG:97 += LOCAL / 10

;日常時ON
IF TFLAG:102 != 2
	;情緒に好感度上昇分追加
	情緒上昇値 = MIN(ABL:ARG:親密 + 1,10) * LOCAL / 3
	SIF FLAG:约会的对象 == ARG
		TFLAG:情緒BOUNS *= 2
	情緒上昇値 = GET_REVISION3(情緒上昇値, 50 + TFLAG:情緒BOUNS, 100 + TFLAG:情緒BOUNS, 200 + TFLAG:情緒BOUNS)

	;抑制フラグが立ってる
	IF TFLAG:情緒上昇抑制
		情緒上昇値 /= 5
		SIF BASE:ARG:情緒 > MAXBASE:ARG:情緒 / 2
		情緒上昇値 = 0
	ENDIF
	;路人がいると上昇量半減
	SIF WITH_MOB()
		情緒上昇値 /= 2
		IF WEATHERDLC
			IF TIME:5 == 17
				情緒上昇値 = 情緒上昇値 * 12 / 10
			ELSEIF TIME:5 == 20
				情緒上昇値 = 情緒上昇値 * 8 /10
			ENDIF
		ENDIF
		SIF CFLAG:ARG:睡眠 ;修正BUG，防止睡著了還會漲
			情緒上昇値 = 0

	BASE:ARG:情緒 += MIN(情緒上昇値,200)
	BASE:ARG:情緒 = MIN(BASE:ARG:情緒 , MAXBASE:ARG:情緒)
	;同室にパルパルがいる状態で他の娘と情緒を上げると憤怒が溜まる
	IF ARG != 60 && CFLAG:ARG:現在位置 == CFLAG:60:現在位置
		IF TALENT:60:恋慕
			BASE:60:憤怒 += MIN(ABL:60:親密 + 1,10) * LOCAL / 3
		ELSEIF TALENT:60:思慕
			BASE:60:憤怒 += MIN(ABL:60:親密 + 1,10) * LOCAL / 5
		ELSEIF CFLAG:60:2 > 500
			BASE:60:憤怒 += MIN(ABL:60:親密 + 1,10) * LOCAL / 8
		ENDIF
	ENDIF
	;理性を快感度分減少
	理性減少値 = LOCAL:1 * (GETPALAMLV(PALAM:ARG:欲情,10) + 1) / 10 * (CFLAG:ARG:积攒度 + 1000) / 2000
	IF TFLAG:理性削り
		理性減少値 *= 4
		SIF CFLAG:ARG:体目当て
			理性減少値 *= 3
	ENDIF
	BASE:ARG:理性 = MAX(BASE:ARG:理性 - 理性減少値, 0 )
	憤怒上昇値 = 0
	;日常ONかつ反感ソース有りで憤怒値上昇
	SIF SOURCE:ARG:反感 > 0 && TFLAG:102 == 1
		憤怒上昇値 = SOURCE:ARG:反感 * 3 * (2000 - BASE:ARG:情緒) * (500 + BASE:ARG:理性) / ((2000 + BASE:ARG:情緒) * (500 + MAXBASE:ARG:理性))
	;振動棒胖次や触手胖次を是てる時は憤怒値が上昇しない 逆ギレ防止
	SIF SOURCE:ARG:反感 > 0 && TFLAG:102 == 1 && (EQUIP:ARG:下半身内衣２ == 28 || EQUIP:ARG:下半身内衣２ == 29 || EQUIP:ARG:下半身内衣２ == 30)
		憤怒上昇値 = 0
	;貞操帯を是てる時は憤怒上昇値が増加
	SIF SOURCE:ARG:反感 > 0 && TFLAG:102 == 1 && EQUIP:ARG:下半身内衣２ == 27
		憤怒上昇値 = 憤怒上昇値 * 4 / 3
	;心情で憤怒上昇値が変化
	IF TALENT:ARG:15 == 1
		憤怒上昇値 = 憤怒上昇値 * 7 / 10
	ELSEIF TALENT:ARG:15 == -1
		憤怒上昇値 = 憤怒上昇値 * 3 / 2
	ENDIF
	SIF TCVAR:ARG:睡眠薬強度 == 1 && CFLAG:ARG:恶作剧
		憤怒上昇値 = 憤怒上昇値 / 2
	SIF TCVAR:ARG:睡眠薬強度 == 2 && CFLAG:ARG:恶作剧
		憤怒上昇値 = 憤怒上昇値 / 5
	SIF TCVAR:ARG:睡眠薬強度 == 3 && CFLAG:ARG:恶作剧
		憤怒上昇値 = 0
	;BASE:ARG:憤怒 += 憤怒上昇値
	;反発抑制
	SIF TALENT:ARG:反発抑制
		憤怒上昇値 = 憤怒上昇値 / 3
	SIF FLAG:催眠強度
		憤怒上昇値 = 憤怒上昇値 * (101 - FLAG:催眠強度) / 100
	BASE:ARG:憤怒 += 憤怒上昇値
	;憤怒が有頂天で憤怒フラグON
	SIF BASE:ARG:憤怒 > MAXBASE:ARG:憤怒
		CALL GET_ANGRY(ARG)
	BASE:ARG:憤怒 = MIN(BASE:ARG:憤怒 , MAXBASE:ARG:憤怒 )
	;心情不快（反発刻印取得時）は常に憤怒
	IF CFLAG:ARG:心情不快
		BASE:ARG:憤怒 = MAXBASE:ARG:憤怒
		CFLAG:ARG:ブチギレ = 1
	ENDIF
	;怒ると目覚める
	IF CFLAG:ARG:ブチギレ && CFLAG:ARG:睡眠
		CALL CHARA_AWAKE(ARG,1)
		IF CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置 && CFLAG:ARG:現在位置 == CFLAG:ARG:311
			IF IS_LOVER(ARG)
				IF CFLAG:ARG:衰弱
					PRINTFORML %CALLNAME:ARG%確認来訪者是%CALLNAME:MASTER%之后就疲惫的睡着了…
				ELSE
					PRINTFORML %CALLNAME:ARG%確認来訪者是%CALLNAME:MASTER%之后露出了安心的表情…
				ENDIF
			ELSE
				IF CFLAG:ARG:衰弱
					PRINTFORML %CALLNAME:ARG%疲惫的睡着了…
				ELSE
					PRINTFORML 被%CALLNAME:ARG%赶出来了…
				ENDIF
				CALL GETOUT(MASTER)
			ENDIF
		ENDIF
	ENDIF
	;警告牌発行チェック
	SIF (CFLAG:ARG:ブチギレ == 1) && RAND:((ABL:ARG:親密 + 1) * 3) == 0 && !CFLAG:ARG:睡眠 && FLAG:18
		CALL CARD_GOT_CHECK(ARG)
ENDIF

;反発刻印補正
TFLAG:97 -= (FLAG:4 + 1) * MARK:反発刻印

;結果が一定数未満の場合
IF MARK:反発刻印 >= 3 && TFLAG:97 < -3
	TFLAG:97 = -3
ELSEIF MARK:反発刻印 == 2 && TFLAG:97 < -2
	TFLAG:97 = -2
ELSEIF MARK:反発刻印 == 1 && TFLAG:97 < -1
	TFLAG:97 = -1
ELSEIF TFLAG:97 < 0
	TFLAG:97 = 0
ENDIF

;冷漠の場合、恋慕がないと好感度が上がりにくい
IF TALENT:ARG:冷漠 && !TALENT:ARG:恋慕
	IF TFLAG:97 > 1
		TFLAG:97 /= 2
	ELSEIF TFLAG:97 > 0
		TFLAG:97 = 1
	ENDIF
	SIF ABL:ARG:施虐属性 >= 4
		TFLAG:97 += 1
ENDIF
;風騷だと親密５から上げにくくなる
IF TALENT:ARG:風騷
	TFLAG:97 += 2
	SIF ABL:ARG:親密 >= 5
		TFLAG:97 /= 2
ENDIF
[SKIPSTART]
;不誠実度によるペナルティ
SELECTCASE TCVAR:ARG:不誠実度
	CASE IS <= 10
		 TFLAG:97 ++
	CASE 11 TO 20
	CASE 21 TO 50
		TFLAG:97 -= 1
	CASE 51 TO 100
		TFLAG:97 -= 3
	CASE 101 TO 300
		TFLAG:97 -= 5
	CASEELSE
		TFLAG:97 -= 10
ENDSELECT
[SKIPEND]
;助手調教中
SIF ASSIPLAY == 1
	TFLAG:97 = 0
;强行
SIF TCVAR:ARG:强行
	TFLAG:97 = MIN(0,TFLAG:97 * -3)
;対象が眠っていたら上昇値0
SIF !SHIRAHU(ARG)
	TFLAG:97 = 0
;好感度が信賴度の4倍を超えると上昇値が低下　ただし约会中は例外
;好感上升界限 被移除
;IF CFLAG:ARG:2 > 500 && CFLAG:ARG:好感度 > CFLAG:ARG:信賴度 * 4
;	信賴度キャップ補正 = CFLAG:ARG:好感度
;	SIF FLAG:约会的对象 == ARG
;		信賴度キャップ補正 /= 2
;	IF TALENT:ARG:恋慕
;		TFLAG:97 = GET_REVISION(CFLAG:ARG:信賴度 * 40, TFLAG:97,信賴度キャップ補正)
;	ELSEIF TALENT:ARG:思慕
;		TFLAG:97 = GET_REVISION(CFLAG:ARG:信賴度 * 30, TFLAG:97,信賴度キャップ補正)
;	ELSE
;		TFLAG:97 = GET_REVISION(CFLAG:ARG:信賴度 * 20, TFLAG:97,信賴度キャップ補正)
;	ENDIF
;ENDIF
;同時に絶頂
SIF SYNCED_ORGASM(ARG)
	TFLAG:97 = TFLAG:97 * 3 / 2
;对方主導诶嘿嘿
SIF CFLAG:诶嘿嘿 == 2
	TFLAG:97 = TFLAG:97 * 6 / 5
;路人
SIF TALENT:ARG:路人 == 1
	CALL YUKIZURI_BONUS(ARG)
;好感度减少
SIF TFLAG:好感度减少 && TFLAG:97 > 0
	TFLAG:97 = TFLAG:97 * -1 *TFLAG:好感度减少
SIF TFLAG:好感度BOUNS && TFLAG:97 > 0
	;TFLAG:97 = TFLAG:97 * TFLAG:好感度BOUNS
	TFLAG:97 = TFLAG:97 * (100 + TFLAG:好感度BOUNS) / 100

;好感度上昇が低い場合、拠点住人であり路人が同じ場所にいればBOUNS
SIF TFLAG:97 < 3 && WITH_MOB() && MAP_住人(MAIN_MAP,ARG)
	TFLAG:97 ++
;指令失敗した場合好感度は上がらない
SIF TFLAG:98 < 0 || TFLAG:好感度没有取得
	TFLAG:97 = MIN(0,TFLAG:97)

SIF TFLAG:3 == 300 && TFLAG:193 < 0
	TFLAG:97 = 0

;烂酔奸バレ時は問答無用で好感度大幅低下
SIF TCVAR:ARG:烂酔奸バレ
	TFLAG:97 -= MAX(30 - ABL:ARG:親密, 0) * 3

SIF FLAG:好感度上昇率 > 0
	TFLAG:97 = TFLAG:97 * (FLAG:好感度上昇率 + TALENT:94 * 2)

;好感度に上昇値を加算
CFLAG:ARG:2 += TFLAG:97

;好感度は0以下にも100万以上にもならない
CFLAG:ARG:2 = LIMIT(CFLAG:ARG:2, 0, 1000000)
LOCAL:1 = 0
FOR LOCAL,0,100
	SIF SOURCE:ARG:LOCAL
		LOCAL:1 ++
NEXT
SIF !LOCAL:1
	TFLAG:97 = 0

;好感度表示
SIF TFLAG:97
	PRINTFORML \@(TFLAG:97 >= 0) ? 好感度＋{TFLAG:97} # 好感度－{-TFLAG:97}\@
;好感度上昇値合算
FLAG:12 += TFLAG:97

@YUKIZURI_BONUS(ARG)
TFLAG:97 *= 10
SIF CFLAG:ARG:体目当て
	TFLAG:97 *= 2
LOCAL = GETPALAMLV(PALAM:ARG:好意,10) - 1
CALL YUKIZURI_BONUS_1(ARG, "親密", LOCAL)

LOCAL = GETPALAMLV(PALAM:ARG:習得,3)
CALL YUKIZURI_BONUS_1(ARG, "技巧", LOCAL)

LOCAL = MAX(GETPALAMLV(PALAM:ARG:恭順,3), GETPALAMLV(PALAM:ARG:屈服,3))
CALL YUKIZURI_BONUS_1(ARG, "従順", LOCAL)

LOCAL = GETPALAMLV(PALAM:ARG:恭順,3) - 1
CALL YUKIZURI_BONUS_1(ARG, "奉仕精神", LOCAL)


@YUKIZURI_BONUS_1(C_ID, CHK_ABL, ARG)
#DIM C_ID
#DIMS CHK_ABL

SIF !ARG
	RETURN
SIF ABL:C_ID:CHK_ABL >= ARG
	RETURN
LOCAL = FINDELEMENT(ABLNAME, CHK_ABL)
CALL COLORMESSAGE(@"%ABLNAME:LOCAL%上升到{ARG}",C_YELLOW,2,1)
ABL:C_ID:CHK_ABL = ARG