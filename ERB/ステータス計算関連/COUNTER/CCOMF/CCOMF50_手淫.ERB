;*********************************************************************************************
;Counter指令：手淫
;ARG = 対象の角色番号
;*********************************************************************************************
@CCOMF50(ARG)

	;体勢の変更
	;=============================
	CALL TOUCH_SET(SET_FROM_YUBI,1,ARG)
	TFLAG:MASTERのＣ使用中 ++

	;ソース計算および内部処理系統
	;=============================
	IF TEQUIP:PLAYER:0
		IF TEQUIP:PLAYER:0 & 1
			CALL DATUI_BOTTOM(ARG,1)
		ELSEIF TEQUIP:PLAYER:0 & 8
			CALL DATUI_BOTTOM(ARG,2)
		ENDIF
	ENDIF
	SOURCE:ARG:性行動 += 250
	SOURCE:ARG:誘惑 += 500
	SOURCE:ARG:挑発 += 100
	SOURCE:ARG:加虐 += 100
	SOURCE:ARG:征服 += 200
	SIF !(TALENT:PLAYER:2 & 2)
		SOURCE:PLAYER:快Ｃ += 300
	;射精部位（1=膣内 2=肛門 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
	CALL 射精対象登録(PLAYER, ARG, 3)
	;強度
	TCVAR:PLAYER:4 += 500
	EXP:ARG:手淫経験 ++
	;奉仕FLAG
	TCVAR:ARG:108 = 1
	TCVAR:ARG:嗜虐フラグ = 1
	CALL EVENT_COUNTER_POSE_69(ARG,2)

	;マ斯塔Counter制御
	;(ビット立ちで不可 0=摸屁股 1=擁抱 2=接吻 3=胸愛撫 4=肛門愛撫 5=陰蒂愛撫 6=指挿入れ 7=掀裙子)
	;=============================
	;CALL Counter挿入され継続中処理(ARG)

RETURN 1

;*********************************************************************************************
;Counter実行可否出力関数
;ARG = 対象の角色番号
;*********************************************************************************************
@CCOM_ABLE50(ARG)

	SIF TFLAG:62
		RETURN 0
	SIF ABL:ARG:指 < 1
		RETURN 0
	SIF TEQUIP:ARG:50 >= 0 || TEQUIP:ARG:51 >= 0
		RETURN 0
	SIF TEQUIP:MASTER:50 >= 0 && !HAS_PENIS(MASTER)
		RETURN 0
RETURN 1

;*********************************************************************************************
;Counter実行判定値算出関数
;ARG = 対象の角色番号
;*********************************************************************************************
@CCOM_ORDER50(指令番号, ARG, 基本値)
#DIM 指令番号
#DIM 基本値
#DIMS Counterタイプ

	;Counterタイプ設定
	;=============================
	Counterタイプ = 脱衣愛撫

	;実行判定値算出式
	;=============================
	LOCAL = 基本値 + 2 * TALENT:ARG:靈活手指 + ABL:ARG:指 + BASE:MASTER:勃起 / 500

	;Counterタイプ別計算処理 実質実行判定処理を含む
	;=============================
	LOCAL = MAX(LOCAL + COUNTER_SIKOU:ARG:指令番号, 1)
	LOCAL *= COUNTER_TYPE(Counterタイプ, ARG)

RETURN LOCAL
