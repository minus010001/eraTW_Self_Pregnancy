;挿入継続の処理
@EQUIP_INCERT_V(ARG)
#DIM 竿役

竿役 = TEQUIP:ARG:50
;-------------------------------------------------
;行為者
;-------------------------------------------------
TCVAR:ARG:116 = 竿役
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛門 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
;TCVAR:4 射精快感強度

CSTR:竿役:10 = @"/{ARG}/"
TCVAR:竿役:射精部位 = 1

CSTR:ARG:10 += @"{TEQUIP:ARG:50}/"
SIF TCVAR:ARG:射精部位 == 0 || TCVAR:ARG:射精部位 >= 8
	TCVAR:ARG:射精部位 = 13


IF ARG == MASTER
	PRINTFORML ＜%CALLNAME:竿役%插入Ｖ中＞

	TCVAR:竿役:射精快感強度 = 850 + TEQUIP:ARG:子宮 * 100
	TCVAR:ARG:射精快感強度 = 500 + (100 * ABL:ARG:Ｖ感覚)

	DOWNBASE:竿役:体力 += 15
	DOWNBASE:竿役:気力 += 20
	DOWNBASE:ARG:体力 += 10
	DOWNBASE:ARG:気力 += 20
	SOURCE:ARG:快Ｖ += 200
	SOURCE:竿役:快Ｃ += 200
	SOURCE:竿役:情愛 += 25
	SOURCE:竿役:性行動 += 250
	SOURCE:竿役:達成 += 25
	SOURCE:竿役:征服 += 500
	SOURCE:竿役:逸脱 += 50

ELSE
	PRINTFORML ＜%CALLNAME:ARG%Ｖ被插入中＞

	TCVAR:竿役:射精快感強度 = 700 + TEQUIP:ARG:子宮 * 100
	TCVAR:ARG:射精快感強度 = 750
	DOWNBASE:竿役:体力 += 15
	DOWNBASE:竿役:気力 += 20
	;時間停止中
	IF FLAG:70
		SOURCE:ARG:快Ｖ += 150
		CALL 挿入継続Ｖ苦痛処理(ARG)

	ELSE
		DOWNBASE:ARG:体力 += 10
		DOWNBASE:ARG:気力 += 20
		SIF TEQUIP:ARG:体位 == 6
			SOURCE:ARG:苦痛 += 300
		SIF TALENT:竿役:形状 == 5
			SOURCE:ARG:苦痛 += 200
		SOURCE:ARG:快Ｖ += 150
		SOURCE:ARG:情愛 += 75
		SOURCE:ARG:露出 += 25
		SIF TCVAR:ARG:强行
			SOURCE:ARG:反感 += 1000
		;体位BOUNS
		IF TEQUIP:ARG:体位 == CFLAG:ARG:喜欢的体位
			SOURCE:ARG:情愛 += 100
			SOURCE:ARG:恭順 += 50
		ENDIF
		CALL 挿入継続Ｖ苦痛処理(ARG)
	ENDIF

	IF CFLAG:ARG:诶嘿嘿 == 2
		SOURCE:ARG:快Ｖ += 150
		SOURCE:ARG:情愛 += 100
		TCVAR:竿役:射精快感強度 += 150
	ENDIF
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:竿役:挿入経験 ++
EXP:ARG:Ｖ経験 ++
SIF !FLAG:70 && !CFLAG:ARG:恶作剧
EXP:ARG:Ｖ性交経験 ++
SIF TEQUIP:ARG:体位 == 6
	EXP:ARG:Ｖ拡張経験 ++
SIF FLAG:70 == 1
	EXP:MASTER:時姦経験 += 1
IF CFLAG:MASTER:恶作剧 == 2
	EXP:MASTER:睡眠姦経験 += 1
	EXP:ARG:睡眠姦経験 += 1
ENDIF
RETURN 1


@EQUIP_INCERT_A(ARG)
#DIM 竿役

竿役 = TEQUIP:ARG:51
;-------------------------------------------------
;行為者
;-------------------------------------------------
TCVAR:ARG:116 = 竿役
;TCVAR:0 射精対象フラグ（ビット0=TARGET 1=PLAYER 2=MASTER 3=ASSI 4=触手 5=其他）
;TCVAR:1 予備射精対象フラグ（ビットを使いたくない人用、CFLAG:100に翻訳されます）
;	（1=TARGET 2=PLAYER 3=MASTER 4=ASSI 5=TARGET+ASSI 6=MASTER+ASSI -1=触手 -2=其他）
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛門 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
;TCVAR:4 射精快感強度
CSTR:竿役:10 = @"/{ARG}/"
TCVAR:竿役:射精部位 = 2

CSTR:ARG:10 += @"{TEQUIP:ARG:51}/"
SIF TCVAR:ARG:射精部位 == 0 || TCVAR:ARG:射精部位 >= 8
	TCVAR:ARG:射精部位 = 12


IF ARG == MASTER
	PRINTFORML ＜%CALLNAME:竿役%插入Ａ中＞

	TCVAR:竿役:射精快感強度 = 850
	TCVAR:ARG:射精快感強度 = 500 + (100 * ABL:ARG:Ａ感覚)

	DOWNBASE:竿役:体力 += 15
	DOWNBASE:竿役:気力 += 20
	DOWNBASE:ARG:体力 += 20
	DOWNBASE:ARG:気力 += 20
	SOURCE:ARG:快Ａ += 200
	SOURCE:竿役:快Ｃ += 200
	SOURCE:竿役:情愛 += 25
	SOURCE:竿役:性行動 += 250
	SOURCE:竿役:達成 += 25
	SOURCE:竿役:征服 += 500
	SOURCE:竿役:逸脱 += 50

ELSE
	PRINTFORML ＜%CALLNAME:ARG%Ａ被插入中＞
	TCVAR:竿役:射精快感強度 = 900
	TCVAR:ARG:射精快感強度 = 750
	DOWNBASE:竿役:体力 += 15
	DOWNBASE:竿役:気力 += 20
	;時間停止中
	IF FLAG:70
		SOURCE:ARG:快Ａ += 200
		CALL 挿入継続Ａ苦痛処理(ARG)

	ELSE
		DOWNBASE:ARG:体力 += 20
		DOWNBASE:ARG:気力 += 20
		SIF TALENT:竿役:形状 == 5
			SOURCE:ARG:苦痛 += 300
		SOURCE:ARG:快Ａ += 200
		SOURCE:ARG:情愛 += 50
		SOURCE:ARG:露出 += 30
		SOURCE:ARG:逸脱 += 100
		SIF TEQUIP:ARG:体位 == 6
			SOURCE:ARG:苦痛 += 300
		;体位BOUNS
		IF TEQUIP:ARG:体位 == CFLAG:ARG:喜欢的体位 - 5
			SOURCE:ARG:情愛 += 100
			SOURCE:ARG:恭順 += 50
		ENDIF
		CALL 挿入継続Ａ苦痛処理(ARG)
	ENDIF

	IF CFLAG:ARG:诶嘿嘿 == 2
		SOURCE:ARG:快Ａ += 150
		SOURCE:ARG:情愛 += 100
		TCVAR:竿役:射精快感強度 += 150
	ENDIF
ENDIF
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:竿役:挿入経験 ++
EXP:ARG:Ａ経験 ++
SIF !FLAG:70 && !CFLAG:ARG:恶作剧
	EXP:ARG:Ａ性交経験 ++
SIF TEQUIP:ARG:体位 == 6
	EXP:ARG:Ａ拡張経験 ++
RETURN 1


@挿入継続Ｖ苦痛処理(ARG)
;EXP:Ｖ経験をみる（処女はありえない）
LOCAL = 200
LOCAL:1 = 150
IF EXP:ARG:Ｖ経験 < EXPLV:2
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 1.20
ELSEIF EXP:ARG:Ｖ経験 < EXPLV:3
	TIMES LOCAL , 0.40
ELSEIF EXP:ARG:Ｖ経験 < EXPLV:4
	TIMES LOCAL , 0.10
ELSEIF EXP:ARG:Ｖ経験 < EXPLV:5
	TIMES LOCAL , 0.00
ELSEIF EXP:ARG:Ｖ経験 >= EXPLV:5
	TIMES LOCAL , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:ARG:潤滑 < PALAMLV:1
	TIMES LOCAL, 3.00
	TIMES LOCAL:1 , 3.00
ELSEIF PALAM:ARG:潤滑 < PALAMLV:2
	TIMES LOCAL , 2.00
	TIMES LOCAL:1 , 2.50
ELSEIF PALAM:ARG:潤滑 < PALAMLV:3
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 1.50
ELSEIF PALAM:ARG:潤滑 < PALAMLV:4
	TIMES LOCAL , 0.50
	TIMES LOCAL:1 , 1.00
ELSEIF PALAM:ARG:潤滑 < PALAMLV:5
	TIMES LOCAL , 0.10
	TIMES LOCAL:1 , 0.50
ELSEIF PALAM:ARG:潤滑 >= PALAMLV:5
	TIMES LOCAL , 0.02
	TIMES LOCAL:1 , 0.30
ENDIF

SOURCE:ARG:苦痛 += LOCAL

SIF !FLAG:70
	SOURCE:ARG:反感 += LOCAL:1


@挿入継続Ａ苦痛処理(ARG)
LOCAL = 400
LOCAL:1 = 250

;EXP:Ａ経験をみる
IF EXP:ARG:Ａ経験 < EXPLV:1
	TIMES LOCAL , 3.00
	TIMES LOCAL:1 , 3.00
ELSEIF EXP:ARG:Ａ経験 < EXPLV:2
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 2.00
ELSEIF EXP:ARG:Ａ経験 < EXPLV:3
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 1.50
ELSEIF EXP:ARG:Ａ経験 < EXPLV:4
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 1.20
ELSEIF EXP:ARG:Ａ経験 < EXPLV:5
	TIMES LOCAL , 0.40
ELSEIF EXP:ARG:Ａ経験 >= EXPLV:5
	TIMES LOCAL , 0.20
ENDIF

;PALAM:潤滑をみる
IF PALAM:ARG:潤滑 < PALAMLV:1
	TIMES LOCAL, 3.00
	TIMES LOCAL:1 , 3.00
ELSEIF PALAM:ARG:潤滑 < PALAMLV:2
	TIMES LOCAL , 2.00
	TIMES LOCAL:1 , 2.50
ELSEIF PALAM:ARG:潤滑 < PALAMLV:3
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 1.50
ELSEIF PALAM:ARG:潤滑 < PALAMLV:4
	TIMES LOCAL , 0.50
	TIMES LOCAL:1 , 1.00
ELSEIF PALAM:ARG:潤滑 < PALAMLV:5
	TIMES LOCAL , 0.30
	TIMES LOCAL:1 , 0.50
ELSEIF PALAM:ARG:潤滑 >= PALAMLV:5
	TIMES LOCAL , 0.20
	TIMES LOCAL:1 , 0.30
ENDIF

SOURCE:ARG:苦痛 += LOCAL

SIF !FLAG:70 && !CFLAG:ARG:恶作剧
	SOURCE:ARG:反感 += LOCAL:1


@挿入不可(ARG)
;-2 = ガバガバで気持ちよくない -1 = 刺激が弱い 1= 経験が必要 2= 経験と知識が必要 3= 小人飛機杯
#FUNCTION
#DIM 你の尺寸
#DIM 尺寸差
IF ARG == PLAYER
	RETURNF 0
ENDIF

SELECTCASE TALENT:PLAYER:形状
	CASE 0,3,4
		你の尺寸 = 0
	CASE 1
		你の尺寸 = -1
	CASE 2
		你の尺寸 = -2
	CASE 6
		你の尺寸 = 1
	CASE 5,10
		你の尺寸 = 2
ENDSELECT

尺寸差 = (TALENT:PLAYER:体型 - TALENT:ARG:体型) * 2 + 你の尺寸

SELECTCASE 尺寸差
	CASE IS > 8
		RETURNF 3
	CASE 5 TO 8
		RETURNF 2
	CASE 3 TO 4
		RETURNF 1
	CASE -2 TO 2
		RETURNF 0
	CASE -3 TO -6
		RETURNF -1
	CASE IS < -6
		RETURNF -2
ENDSELECT
	