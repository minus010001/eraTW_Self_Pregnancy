;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;信賴度上昇計算処理
;CALL		USER
;ARG		0:角色NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@TRUST_CALC(ARG)
#DIM 信賴度上昇値
DEBUGPRINTFORML 【TRUST_CALC】TRAST_CALC_START ,{ARG}
;COMABLE範囲外抑止
SIF !TFLAG:102
	RETURN 0
;対象がMASTER以外の時起動
SIF !ARG
	RETURN 0

;失敗指令フラグ
SIF TFLAG:98 < 0
	TFLAG:99 ++

信賴度上昇値 = TFLAG:98

;親密度に合わせて上昇修正
信賴度上昇値 += ABL:ARG:親密 / 5
DEBUGPRINTFORML 【TRUST_CALC】初期値 TFLAG98={信賴度上昇値} 失敗フラグ {TFLAG:99}

;憤怒値によって信賴度DOWN
SIF BASE:ARG:憤怒 > 0 && TFLAG:98 < 0
	信賴度上昇値 -= (BASE:ARG:憤怒 / 100)
DEBUGPRINTFORML 【TRUST_CALC】憤怒変動 TFLAG98={信賴度上昇値}

;素質による増減

;叛逆
SIF TALENT:ARG:態度 > 0 && !TALENT:ARG:恋慕
	信賴度上昇値 -= 1
;心情
信賴度上昇値 += TALENT:ARG:15
;坦率
SIF TALENT:ARG:態度 < 0 && TFLAG:98 > 0
	信賴度上昇値 += 1
;開朗同士
SIF TALENT:ARG:開朗／阴郁 > 0 && TALENT:MASTER:開朗／阴郁 > 0
	信賴度上昇値 += 1

;阴郁同士
SIF TALENT:ARG:開朗／阴郁 < 0 && TALENT:MASTER:開朗／阴郁 < 0
	信賴度上昇値 += 1

;感情缺乏
SIF TALENT:ARG:感情缺乏
	信賴度上昇値 -= 1
;抵抗
SIF TALENT:ARG:抵抗
	信賴度上昇値 -= 2
;献身的
SIF TALENT:ARG:献身的 && TFLAG:98 > 0
	信賴度上昇値 += 1
;討厭男人,討厭女人
SIF OTOKOGIRAI(ARG)
	信賴度上昇値 -= 1
;主人の謎之魅力
SIF TALENT:MASTER:謎之魅力
	信賴度上昇値 += 1
SIF TALENT:ARG:路人 == 1
	信賴度上昇値 += 10
;恋慕
SIF TALENT:ARG:恋慕
	信賴度上昇値 += 2
;思慕
SIF TALENT:ARG:思慕
	信賴度上昇値 += 1
SIF TALENT:ARG:動物耳 && TALENT:ARG:動物耳 == TFLAG:耳補正
	信賴度上昇値 += 3
DEBUGPRINTFORML 【TRUST_CALC】素質変動 TFLAG98={信賴度上昇値}

LOCAL = 0
LOCAL:1 = 0
;情愛
LOCAL += (50 - 250000 / (SOURCE:ARG:情愛 + 5000))
;達成感
LOCAL += (30 - 30000 / (SOURCE:ARG:達成 + 1000))
;恭順
LOCAL += (20 - 20000 / (SOURCE:ARG:恭順 + 1000))
LOCAL:2 += (20 - 20000 / (SOURCE:ARG:恭順 + 1000))
;屈服
LOCAL += (15 - 15000 / (SOURCE:ARG:屈従 + 1000))
LOCAL:2 += (10 - 10000 / (SOURCE:ARG:屈従 + 1000))
;歓楽
LOCAL += (30 - 150000 / (SOURCE:ARG:歓楽 + 5000))
;征服
LOCAL += (30 - 90000 / (SOURCE:ARG:征服 + 3000))
;受動
LOCAL += (30 - 90000 / (SOURCE:ARG:受動 + 3000))
LOCAL:1 += LOCAL
;欲情
;LOCAL:2 += (30 - 90000 / (SOURCE:ARG:欲情 + 3000))
;快ＶＡＣＢ
;LOCAL:2 += (20 - 200000 / (SOURCE:ARG:快Ｖ + 10000))
;LOCAL:2 += (20 - 200000 / (SOURCE:ARG:快Ａ + 10000))
;LOCAL:2 += (10 - 100000 / (SOURCE:ARG:快Ｃ + 10000))
;LOCAL:2 += (10 - 100000 / (SOURCE:ARG:快Ｂ + 10000))
DEBUGPRINTFORML 【TRUST_CALC】UPPERソース変動計算 {LOCAL}

;屈服
;LOCAL -= (50 - 50000 / (SOURCE:ARG:屈従 + 1000))
;LOCAL:2 += (50 - 50000 / (SOURCE:ARG:屈従 + 1000))
;恐怖
LOCAL -= (50 - 20000 / (SOURCE:ARG:恐怖 + 400)) * (ABL:ARG:従順 - 3) / 3
LOCAL:2 += (100 - 40000 / (SOURCE:ARG:恐怖 + 400)) * (ABL:ARG:従順 - 3) / 3
;苦痛
LOCAL -= (50 - 20000 / (SOURCE:ARG:苦痛 + 400)) * (ABL:ARG:受虐属性 - 3) / 3
LOCAL:2 += (100 - 40000 / (SOURCE:ARG:苦痛 + 400)) * (ABL:ARG:受虐属性 - 3) / 3
;不潔
LOCAL -= (50 - 25000 / (SOURCE:ARG:不潔 + 500)) * (2 - TALENT:ARG:汚臭耐性) / 2
;鬱屈
LOCAL -= (50 - 15000 / (SOURCE:ARG:鬱屈 + 300))
;逸脱
LOCAL -= (50 - 25000 / (SOURCE:ARG:逸脱 + 500))
;反発
LOCAL -= (30 - 10000 / (SOURCE:ARG:反感 + 500))


DEBUGPRINTFORML 【TRUST_CALC】DOWNERソース変動計算 {LOCAL:1 - LOCAL} ,{LOCAL}

信賴度上昇値 += LOCAL / 10
DEBUGPRINTFORML 【TRUST_CALC】ソース変動 TFLAG98={信賴度上昇値}

;技能が低いと减少補正
IF (TALENT:ARG:性的兴趣 == -1 || TALENT:ARG:自尊心 > 0) && 信賴度上昇値 > 0
	SIF ABL:ARG:教養 > ABL:MASTER:教養 || ABL:ARG:戦闘能力 > ABL:MASTER:戦闘能力
		信賴度上昇値 -= 1
ELSE
SIF (ABL:ARG:教養 > ABL:MASTER:教養 + 2 || ABL:ARG:戦闘能力 > ABL:MASTER:戦闘能力 + 2) && 信賴度上昇値 > 0
	信賴度上昇値 -= 1
ENDIF
IF GETBIT (CFLAG:ARG:继承, 1)
	信賴度上昇値 += 2
ELSEIF GETBIT (CFLAG:ARG:继承, 0)
	信賴度上昇値 += 1
ENDIF
;反発刻印補正
信賴度上昇値 -= (FLAG:4 + 1) * MARK:ARG:反発刻印

;結果が一定数未満の場合
;IF MARK:反発刻印 >= 3 && 信賴度上昇値 < -3
;	信賴度上昇値 = -3
;ELSEIF MARK:反発刻印 == 2 && 信賴度上昇値 < -2
;	信賴度上昇値 = -2
;ELSEIF MARK:反発刻印 == 1 && 信賴度上昇値 < -1
;	信賴度上昇値 = -1
;ENDIF

;同性は信賴度が上がりやすく。
SIF HETEROSEX(ARG,PLAYER) && !OTOKOGIRAI(ARG)
	信賴度上昇値 += 1

;冷漠か難以越過的底線の場合、恋慕がないと信賴度が上がりにくい
IF (TALENT:ARG:冷漠 || TALENT:ARG:難以越過的底線) && !TALENT:ARG:恋慕
	IF 信賴度上昇値 > 1
		信賴度上昇値 /= 2
	ELSEIF 信賴度上昇値 > 0
		信賴度上昇値 = 1
	ENDIF
ENDIF


IF FLAG:70
;	SIF 信賴度上昇値 > 0
;		信賴度上昇値 = 0
;	IF MARK:反発刻印 >= 3
;		CFLAG:ARG:344 += TFLAG:98
;	ELSEIF MARK:反発刻印 == 2
;		CFLAG:ARG:344 += TFLAG:98 / 2
;	ELSEIF MARK:反発刻印 == 1
;		CFLAG:ARG:344 += TFLAG:98 / 3
;	ELSE
;		CFLAG:ARG:344 += TFLAG:98 / 4
;	ENDIF
ELSE
[SKIPSTART]
;不誠実度によるペナルティ
SELECTCASE TCVAR:ARG:不誠実度
	CASE IS <= 10
		 信賴度上昇値 ++
	CASE 11 TO 20
	CASE 21 TO 50
		信賴度上昇値 -= 1
	CASE 51 TO 100
		信賴度上昇値 -= 3
	CASE 101 TO 300
		信賴度上昇値 -= 5
	CASEELSE
		信賴度上昇値 -= 7
ENDSELECT
[SKIPEND]
IF CFLAG:ARG:体目当て
	SIF 信賴度上昇値 > 0
		信賴度上昇値 /= 2
ENDIF

;理性と情緒で修正
IF BASE:ARG:情緒 == MAXBASE:ARG:情緒
	信賴度上昇値 += 2
	;情緒が全開なら信賴度减少無し
	SIF 信賴度上昇値 < 0
		信賴度上昇値 = 0
ELSEIF BASE:ARG:情緒 >= (MAXBASE:ARG:情緒 / 5 * 4) || BASE:ARG:理性 <= (MAXBASE:ARG:理性 / 2)
	信賴度上昇値 += 1
	;情緒が4/5以上または理性が1/2以下の時、信賴度减少が半分
	SIF 信賴度上昇値 < 0
		信賴度上昇値 += ABS(信賴度上昇値) / 2
ENDIF
DEBUGPRINTFORML 【TRUST_CALC】情緒変動 {信賴度上昇値}

SIF TFLAG:好感度BOUNS && 信賴度上昇値 > 0
	;信賴度上昇値 = 信賴度上昇値 * TFLAG:好感度BOUNS
	信賴度上昇値 = 信賴度上昇値 * (100 + TFLAG:好感度BOUNS) / 100

;信賴度上昇が低い場合、拠点住人であり路人が同じ場所にいればBOUNS
SIF 信賴度上昇値 < 3 && WITH_MOB() && MAP_住人(MAIN_MAP,ARG)
	信賴度上昇値 ++
;失敗フラグが立ってなければ信頼は下がらない
SIF !TFLAG:99 && 信賴度上昇値 < 0
	信賴度上昇値 = 0
SIF TFLAG:99 && 信賴度上昇値 > 0
	信賴度上昇値 = 0
;ソフトウフフ時はプラス無し
SIF SELECTCOM >= 313 && SELECTCOM <= 330 && 信賴度上昇値 > 0
	信賴度上昇値 = 0
;什麼也不做を使用した場合は上限を0にする
SIF 信賴度上昇値 > 0 && SELECTCOM == 21
	信賴度上昇値 = 0
;助手調教中
SIF ASSIPLAY == 1
	信賴度上昇値 = 0
;睡眠中は上昇値0
SIF CFLAG:ARG:睡眠 || TCVAR:TARGET:烂醉
	信賴度上昇値 = 0

;信賴度上昇の下限設定。（反発刻印のレベルに心情の修整を加える）×-5が下限）
信賴度上昇値 = MAX(信賴度上昇値 , MAX(MARK:ARG:反発刻印 - TALENT:ARG:15 , 0) * -5)
DEBUGPRINTFORML 【TRUST_CALC】信賴度変動 {信賴度上昇値}
SIF LOCAL:1
	CFLAG:ARG:信賴度 += 信賴度上昇値
;信賴度は0以下にも100万以上にもならない
CFLAG:ARG:信賴度 = LIMIT(CFLAG:ARG:信賴度, 0, 999999)
LOCAL:1 = 0
FOR LOCAL,0,100
	SIF SOURCE:ARG:LOCAL
		LOCAL:1 ++
NEXT
SIF !LOCAL:1
	信賴度上昇値 = 0

;信賴度表示
SIF 信賴度上昇値
PRINTFORML \@(信賴度上昇値 >= 0) ? 信賴度＋{信賴度上昇値} # 信賴度－{-信賴度上昇値}\@
ENDIF
TFLAG:98 = 0
TFLAG:99 = 0
信賴度上昇値 = 0