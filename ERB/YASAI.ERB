@YASAI
FOR LOCAL,0,5
	IF PLANT_TYPE:LOCAL
		SIF FLAG:澆水代行
			PLANT_DRY:LOCAL = 0
		PLANT_DAYS:LOCAL ++
		PLANT_DRY:LOCAL ++

		IF MAIN_MAP == 9
			PLANT_GROW:LOCAL ++
			;肥やしを撒いてある場合は追加
			SIF PLANT_FOOD:LOCAL && PLANT_GROW:LOCAL < 10
				PLANT_GROW:LOCAL ++
		ELSE
			SELECTCASE TIME:5
			CASE 0,1
				PLANT_GROW:LOCAL ++
				;肥やしを撒いてある場合は追加
				SIF PLANT_FOOD:LOCAL && PLANT_GROW:LOCAL < 10
					PLANT_GROW:LOCAL ++
			CASE 2,3
				PLANT_GROW:LOCAL += RAND:2
				SIF PLANT_FOOD:LOCAL && PLANT_GROW:LOCAL < 10
					PLANT_GROW:LOCAL ++
			CASE 4 TO 7
				PLANT_DRY:LOCAL = 0
				;肥やしを撒いてある場合は1/2の確率でさらに追加
				SIF PLANT_FOOD:LOCAL && PLANT_GROW:LOCAL < 10
					PLANT_GROW:LOCAL += RAND:2
			CASE 8 TO 13
				PLANT_GROW:LOCAL -= RAND:2
			ENDSELECT
		ENDIF
		;三粒の天滴を所有している場合は澆水済み＆成長に補正
		IF ITEM:三粒の天滴
			IF FLAG:天滴坤神 && PLANT_DRY:LOCAL > 2
				PRINTFORML 坤神的恩惠滋润了干涸的田地
				PLANT_DRY:LOCAL = 0
				FLAG:天滴坤神 = 0
			ENDIF
			SIF PLANT_GROW:LOCAL < 10 && !RAND:4
				PLANT_GROW:LOCAL ++
		ENDIF
	ENDIF
NEXT

