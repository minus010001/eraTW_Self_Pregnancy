;関数置き場
;-------------------------------------------------
;道具販売状況@ITEMSTOCK
;戻り値1 道具が存在しない
;戻り値2 卖完
;戻り値3 お金が足りない
;戻り値4 条件を満たしていない
;戻り値5 すでに99個所持している
;-------------------------------------------------
@ITEMSTOCK(ARG, 値段)
#FUNCTION
#DIM 値段
SIF !STRLENS(ITEMNAME:ARG)
	RETURNF 1
SIF ITEMSALES:ARG == -2
	RETURNF 4
SIF ITEMSALES:ARG == -1
	RETURNF 2
SIF MONEY < 値段
	RETURNF 3
SIF ITEM:ARG >= 99
	RETURNF 5
RETURNF 0

;-------------------------------------------------
;関数名:CHOICE
;概　要:２～４択関数
;引　数:ARGS:0…質問内容
;      :ARGS:1～4…選択肢の文字列(3,4は省略可)
;戻り値:ユーザ入力結果(0～3)
;選択条件の無い、樸素的選択肢作成関数
;口上とかでも使えるかもしれない
;とりあえず4択まで。増やしてもいいけど
;-------------------------------------------------
@CHOICE(ARGS:0, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5)
PRINTSL ARGS:0
FOR LOCAL, 0, 5
	SIF !STRLENS(ARGS:(1 + LOCAL))
		CONTINUE
	PRINTFORML [{LOCAL}] - %ARGS:(1 + LOCAL)%
;	SIF LOCAL && !STRLENS(ARGS:(2 + LOCAL))
;		BREAK
NEXT
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 0 TO LOCAL - 1
	CASEELSE
		;CALL SIMATTYAUOJISAN
		CLEARLINE 1
		GOTO INPUT_LOOP
ENDSELECT
RETURN RESULT
;-------------------------------------------------
;選択肢を表示し入力結果を返す関数 条件を満たさない選択肢をグレー表示にして無効にすることが可能
;引数にセリフと条件(0で無効、非0で有効)を指定。最大7個まで
;戻り値は選択した選択肢の番号
;-------------------------------------------------
@ASK_M(ARGS:0, ARG:0, ARGS:1, ARG:1, ARGS:2, ARG:2, ARGS:3, ARG:3, ARGS:4, ARG:4, ARGS:5, ARG:5, ARGS:6, ARG:6, ARGS:7, ARG:7, ARGS:8, ARG:8)
#DIM CONST 選択肢上限 = 8
#DIM PREV_REDRAW
PREV_REDRAW = CURRENTREDRAW()
REDRAW 0
FOR LOCAL:0, 0, (選択肢上限 + 1)
	IF ARGS:(LOCAL:0) != ""
		IF ARG:(LOCAL:0)
			PRINTBUTTON @"{LOCAL:0, 2, RIGHT}[%ARGS:(LOCAL:0)%]", LOCAL:0
		ELSE
			SETCOLOR 0x606060
			PRINTPLAINFORM {LOCAL:0, 2, RIGHT}[%ARGS:(LOCAL:0)%]
			RESETCOLOR
		ENDIF
		PRINTL
	ENDIF
NEXT
$INPUT_LOOP
INPUT
IF RESULT < 0 || RESULT > 選択肢上限 || ARGS:RESULT == "" || !ARG:RESULT
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
REDRAW PREV_REDRAW
RETURN RESULT
;-------------------------------------------------
;現金と筹码どちらでも払える選択肢のための関数
;-------------------------------------------------
@PAY_YEN_OR_CM(ARG,ARG:1)
PRINTFORML [0] 放弃
PRINTFORML [1] 支付%金額表示(ARG)%　　（現在的所持金：%金額表示(MONEY)%）
PRINTFORML [2] 支付CM%三桁区切(ARG:1)%　（現在的所持筹码：CM%三桁区切(MONEY:2)%）
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 0
	CASE 1
		IF MONEY < ARG
			PRINTFORMW 金錢不足
			GOTO INPUT_LOOP
		ENDIF
		MONEY -= ARG
	CASE 2
		IF MONEY:2 < ARG:1
			PRINTFORMW 籌碼不足
			GOTO INPUT_LOOP
		ENDIF
		MONEY:2 -= ARG:1
	CASEELSE
		GOTO INPUT_LOOP
ENDSELECT
RETURN RESULT
;-------------------------------------------------
;関数名:GET_REVISION
;概　要:汎用補正値取得関数
;引　数:ARG:0…参照する数値
;戻り値:補正後の値
;備　考:式中関数
;ある数値を、上限値に漸近していく数値に変換したいときに
;上昇率が小さいとすぐに上限に達する、大きいと逆になかなか上限まで近づかない
;-------------------------------------------------
@GET_REVISION(ARG:0, 上限, 上昇率)
#FUNCTION
#DIM 上限
#DIM 上昇率
RETURNF 上限 - 上昇率 * 上限 / (上昇率 + ARG:0)

;eg 10000,2000,20000だと2000-2000*20000/30000

@GET_REVISION2(ARG:0, 上限, 上昇率)
#FUNCTION
#DIM 上限
#DIM 上昇率
RETURNF 上限 * (ARG:0 + 1) / (上昇率 + ARG:0 + 1)

;-------------------------------------------------
;関数名:GET_REVISION3
;概　要:汎用補正値取得関数
;引　数:num…参照する数値　TH:0~2…しきい値1~3　
;-------------------------------------------------
@GET_REVISION3(num, TH:0, TH:1, TH:2)
#FUNCTION
#DIM TH, 3
#DIM num

FOR LOCAL,0,3
	SIF num <= TH:LOCAL || !TH:LOCAL
		RETURNF num
	;引数がTHを超えてたら超過分をSQRT*2
	num = TH:LOCAL + SQRT(num - TH:LOCAL) * 2
NEXT

RETURNF num

;10000, 50, 100, 200なら124



;-------------------------------------------------
;関数名:PRINT_BASE
;概　要:汎用補正値取得関数
;引　数:ARGS バーの名前
;		ARG:0…参照する角色番号
;       ARG:1…ベースの番号
;       ARG:2…バーの長さ
;BARを表示させたいときに
;-------------------------------------------------
@PRINT_BASE(ARGS,ARG:0,ARG:1,ARG:2)
PRINTFORM %ARGS,%
BAR BASE:ARG:(ARG:1),MAXBASE:ARG:(ARG:1),ARG:2
PRINTFORML ({BASE:ARG:(ARG:1)}/{MAXBASE:ARG:(ARG:1)})

;FileName_COMMON.ERB ------------------------------- Rev1.00
;文字入りライン表示関数
;CALL		USER
;ARG		ARGS:表示ライン名, ARG:0 line_to_"-", ARG:1 ライン長
;RETURN		VOID
;COMMENT
;-----------------------------------------------------------
@DRAW_INFORMATIONLINE(ARGS,ARG,ARG:1)
#DIMS EQUAL
#DIMS MINUS
EQUAL = =
MINUS = -
LOCAL = ARG:1

IF ARGS =="Info"
	SIF TFLAG:Info表示中
		RETURN
	TFLAG:Info表示中 = 1
ENDIF
SIF !ARG:1
	LOCAL = 150
IF ARG == 1
	PRINTSINGLEFORMS "-- %ARGS% " + "%(MINUS * MAX((LOCAL - STRLENS(ARGS)),0))%"
ELSE
	PRINTSINGLEFORMS "== %ARGS% " + "%(EQUAL * 150)%"
ENDIF
;-------------------------------------------------
;絶頂時など、ＡとＢとＤと…みたいな感じで「と」でつないで列挙するための関数
;引数に表示する文字列と条件(0で無効、非0で有効)を指定。最大5個まで
;-------------------------------------------------
@PRINT_EMUNERATE(表示:1,条件:1,表示:2,条件:2,表示:3,条件:3,表示:4,条件:4,表示:5,条件:5)
#DIMS 表示,6
#DIM 条件,6

SIF 条件:1
	PRINTFORM %表示:1%
SIF 条件:1 && (条件:2 || 条件:3 || 条件:4 || 条件:5)
	PRINT と
SIF 条件:2
	PRINTFORM %表示:2%
SIF 条件:2 && (条件:3 || 条件:4 || 条件:5)
	PRINT と
SIF 条件:3
	PRINTFORM %表示:3%
SIF 条件:3 && (条件:4 || 条件:5)
	PRINT と
SIF 条件:4
	PRINTFORM %表示:4%
SIF 条件:4 && 条件:5
	PRINT と
SIF 条件:5
	PRINTFORM %表示:5%
;-------------------------------------------------
;悪魔で女僕から流用させていただきました。多謝。
;単語1/単語2/…/単語n の書式で書かれたARGS:0～の単語のどれかを等確率で返す
;(ARGSが複数あるのは関数などが突っ込まれることを想定しているため)
;＿は何も出力しない選択とする
;選択肢の数を100まで拡張、RESULT退避処理追加
;抽選処理が等確率ならFOR-NEXT使わなくてよいと思ったのでRANDで済ませる
;-------------------------------------------------
@TEXTR(ARGS, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6)
#FUNCTIONS
#DIM  L_RESULT               ;RESULT退避用
#DIMS DYNAMIC CHOICES        ;結果
#DIMS DYNAMIC STR_ARRAY, 100 ;選択肢格納用の一時変数
L_RESULT = RESULT
;まず一つにまとめる
ARGS = %ARGS%\/%ARGS:1%\/%ARGS:2%\/%ARGS:3%\/%ARGS:4%\/%ARGS:5%\/%ARGS:6%
;不要な"/"を削る作業
;空のARGSがあると//がたくさん出来て判定の邪魔になるので
;1.//は置換で削る
;2.最末尾が/なら削る（SPLITとRANDをそのまま使用都合上）
ARGS '= REPLACE(ARGS, "\/+", "/")
SIF CHARATU(ARGS, STRLENSU(ARGS) - 1) == "/"
	ARGS '= SUBSTRINGU(ARGS, 0, STRLENSU(ARGS) - 1)
SPLIT ARGS, "/", STR_ARRAY
CHOICES '= STR_ARRAY:(RAND:RESULT)
SIF CHOICES == "＿"
	CHOICES =
RESULT = L_RESULT
RETURNF CHOICES
;-------------------------------------------------
;未読判定関数@ONCE
;式中で使用する関数です
;ARGSはイベント番号、ARGは真だと初回判定を更新しない、ARG:1は角色登録番号（省略するとTARGET,-1ならMASTER）
;{イベント番号}/　という文字列をCSTR:1にぶっこんでいきます
;-------------------------------------------------
@ONCE(ARGS,ARG, ARG:1)
#FUNCTION
;ARG:2の変換
ARG:1 = ARG:1 ? ARG:1 # TARGET
ARG:1 = ARG:1 == -1 ? MASTER # ARG:1

SIF CSTR:(ARG:1):1 == ""
	CSTR:(ARG:1):1 = /

LOCALS = /%ARGS%/
IF STRCOUNT(CSTR:(ARG:1):1,LOCALS)
	RETURNF 0
ELSEIF ARG
	RETURNF 1
ENDIF
;初回判定更新
CSTR:(ARG:1):1 = %CSTR:(ARG:1):1%%ARGS%/
RETURNF 1

;FileName_COMMON.ERB ------------------------------- Rev1.00
;フラグリセット処理
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT
;-----------------------------------------------------------
@EVENTTRAIN
SIF SaveDataInTrain
	RETURN
FOR LOCAL,0,CHARANUM
	CSTR:LOCAL:1 =
NEXT

;-------------------------------------------------
;未読判定関数@FIRSTTIME
;式中で使用する関数です。
;ARGSはイベント番号、ARGは真だと初回判定を更新しない、ARG:1は角色登録番号（省略するとTARGET,-1ならMASTER）
;{イベント番号}/　という文字列をCSTR:0にぶっこんでいきます
;-------------------------------------------------
@FIRSTTIME(ARGS,ARG, ARG:1)
#FUNCTION
;ARG:1の変換
ARG:1 = ARG:1 ? ARG:1 # TARGET
ARG:1 = ARG:1 == -1 ? MASTER # ARG:1
SIF CSTR:(ARG:1):0 == ""
	CSTR:(ARG:1):0 = /

LOCALS = /%ARGS%/
IF STRCOUNT(CSTR:(ARG:1):0,LOCALS)
	RETURNF 0
ELSEIF ARG
	RETURNF 1
ENDIF
;初回判定更新
CSTR:(ARG:1):0 = %CSTR:(ARG:1):0%%ARGS%/
RETURNF 1

;-------------------------------------------------
;関数名:EXP_UP
;概　要:経験値変動チェック
;引　数:ARG:0…参照するEXP
;       ARG:1…参照する角色番号
;-------------------------------------------------
@EXP_UP(ARG,ARG:1)
#FUNCTION
RETURNF EXP:(ARG:1):ARG - TCVAR:(ARG:1):(400 + ARG)

;-------------------------------------------------
;関数名:INVAGINA
;概　要:Vに何が入っているのかを返す
;引　数:ARG:0…参照する角色番号
;戻り値:0無
;戻り値:1 阴茎
;戻り値:2 振動棒
;-------------------------------------------------
@INVAGINA(ARG)
#FUNCTION
IF TEQUIP:ARG:50
	IF TALENT:PLAYER:2 & 2
		RETURNF 1
	ELSE
		RETURNF 3
	ENDIF
ELSEIF TEQUIP:ARG:振動棒
	RETURNF 2
ENDIF
;-------------------------------------------------
;関数名:INANAL
;概　要:Aに何が入っているのかを返す
;引　数:ARG:0…参照する角色番号
;戻り値:0無
;戻り値:1 阴茎
;戻り値:2 肛用振動棒
;戻り値:3 後庭拉珠
;-------------------------------------------------
@INANAL(ARG)
#FUNCTION
IF TEQUIP:ARG:51
	RETURNF 1
ELSEIF TEQUIP:ARG:肛用振動棒
	RETURNF 2
ELSEIF TEQUIP:ARG:後庭拉珠
	RETURNF 3
ENDIF
;-------------------------------------------------
;関数名:HETEROSEX
;ARGとARG:1の性別が同じかどうかを返すほんとはHOMOSEXだけど語感が悪いので
;女性器を持つものは女、持たないものを男とする
;戻り値0=異性
;戻り値1=同性(レズ)
;戻り値2=同性(ゲイ)
;-------------------------------------------------
@HETEROSEX(ARG,ARG:1)
#FUNCTION
IF GETBIT(TALENT:ARG:2,0) == GETBIT(TALENT:(ARG:1):2,0)
	IF GETBIT(TALENT:ARG:2,0)
		RETURNF 1
	ELSE
		RETURNF 2
	ENDIF
ELSE
	RETURNF 0
ENDIF

;-------------------------------------------------
;関数名:NameToID
;引数にCALLNAMEを入れると角色番号を返す
;-------------------------------------------------
@NameToID(ARGS)
#FUNCTION
LOCAL = FINDCHARA(CALLNAME, @"%ARGS%" , 1)
RETURNF LOCAL

;-------------------------------------------------
;関数名:GET_TARGETNUM
;同室中のTARGETの数を返す
;引数廃止してFOR-NEXTを排除して高速化
;-------------------------------------------------
@GET_TARGETNUM()
#FUNCTION
RETURNF MAX(0, FINDELEMENT(TARGET, 0) - 1)
;-------------------------------------------------
;関数名:IN_ROOM_MEMBER
;部屋の中で特定要素での最低・最高を持つ角色を取得
;ARGS MAXかMINかを選択
;ARG 部屋の番号
;ARGS:1 対象となる変数の名前
;ARG:1 変数の番号
;-------------------------------------------------
@IN_ROOM_MEMBER, ARGS, ARG, ARGS:1, ARG:1
#FUNCTION
VARSET LOCAL, 0
SELECTCASE ARGS:1
	CASE "CFLAG"
		FOR LOCAL, 1, CHARANUM
			SIF CFLAG:LOCAL:現在位置 != ARG
				CONTINUE
			IF LOCAL:3++ == 0
				LOCAL:1 = CFLAG:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ELSEIF ARGS == "MAX" && LOCAL:1 < CFLAG:LOCAL:(ARG:1)
				LOCAL:1 = CFLAG:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ELSEIF ARGS == "MIN" && LOCAL:1 > CFLAG:LOCAL:(ARG:1)
				LOCAL:1 = CFLAG:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ELSEIF LOCAL:1 == CFLAG:LOCAL:(ARG:1) && RAND:2
				LOCAL:1 = CFLAG:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ENDIF
		NEXT
		RETURNF LOCAL:2
	CASE "TCVAR"
		FOR LOCAL, 1, CHARANUM
			SIF CFLAG:LOCAL:現在位置 != ARG
				CONTINUE
			IF LOCAL:3++ == 0
				LOCAL:1 = TCVAR:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ELSEIF ARGS == "MAX" && LOCAL:1 < TCVAR:LOCAL:(ARG:1)
				LOCAL:1 = TCVAR:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ELSEIF ARGS == "MIN" && LOCAL:1 > TCVAR:LOCAL:(ARG:1)
				LOCAL:1 = TCVAR:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ELSEIF LOCAL:1 == TCVAR:LOCAL:(ARG:1) && RAND:2
				LOCAL:1 = TCVAR:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ENDIF
		NEXT
		RETURNF LOCAL:2
	CASE "ABL"
		FOR LOCAL, 1, CHARANUM
			SIF CFLAG:LOCAL:現在位置 != ARG
				CONTINUE
			IF LOCAL:3++ == 0
				LOCAL:1 = ABL:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ELSEIF ARGS == "MAX" && LOCAL:1 < ABL:LOCAL:(ARG:1)
				LOCAL:1 = ABL:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ELSEIF ARGS == "MIN" && LOCAL:1 > ABL:LOCAL:(ARG:1)
				LOCAL:1 = ABL:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ELSEIF LOCAL:1 == ABL:LOCAL:(ARG:1) && RAND:2
				LOCAL:1 = ABL:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ENDIF
		NEXT
		RETURNF LOCAL:2
	CASE "TALENT"
		FOR LOCAL, 1, CHARANUM
			SIF CFLAG:LOCAL:現在位置 != ARG
				CONTINUE
			IF LOCAL:3++ == 0
				LOCAL:1 = TALENT:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ELSEIF ARGS == "MAX" && LOCAL:1 < TALENT:LOCAL:(ARG:1)
				LOCAL:1 = TALENT:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ELSEIF ARGS == "MIN" && LOCAL:1 > TALENT:LOCAL:(ARG:1)
				LOCAL:1 = TALENT:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ELSEIF LOCAL:1 == TALENT:LOCAL:(ARG:1) && RAND:2
				LOCAL:1 = TALENT:LOCAL:(ARG:1)
				LOCAL:2 = LOCAL
			ENDIF
		NEXT
		RETURNF LOCAL:2
ENDSELECT
;FileName_COMMON.ERB ------------------------------- Rev1.00
;TARGET再設定関数
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT
;-----------------------------------------------------------
;
;列表関数とソート関数ができたのでかなり簡易化できたかな
@TARGETSET_CHACK(OP)
#DIMS OP         ;SHOW_STATUSは同室更新入れるとマズイのでスキップする用
#DIM  TARGET_CNT ;ターゲットの数
#DIM  L_ID       ;REF対策、渡すだけ
#DIM  SWAP_ID    ;現在的TARGET指定処理用の一時変数
;诶嘿嘿中はTARGETを逃がす（情事発覚とかでズレちゃうので）
SIF !TFLAG:現在的TARGET
	TFLAG:現在的TARGET = TARGET

CALL MAKE_CHARA_LIST("TARGET列表", L_ID, TARGET, TARGET_CNT)
SIF !TARGET_CNT
	RETURN
CALL SORT_LIST("好感降順", TARGET, TARGET_CNT)

LOCAL:1 = TARGET
IF TFLAG:現在的TARGET > 0
	LOCAL:1 = FINDELEMENT(TARGET, TFLAG:現在的TARGET)
	IF LOCAL:1 == -1
		LOCAL:1 = 0
		TFLAG:現在的TARGET = 0
	ENDIF
	LOCAL:1 = TARGET:(LOCAL:1)
ENDIF
ARRAYSHIFT TARGET, 1, LOCAL:1
[SKIPSTART]
SIF STRCOUNT(OP, "「同室更新無し」")
	RETURN

;同室確認は列表側でやっているのでここでは必要ない
FOR LOCAL, 1, 1 + TARGET_CNT
	CFLAG:(TARGET:LOCAL):同室 = 1
NEXT
[SKIPEND]


;====================================================
@性技補正(ARG)
#FUNCTION
RETURNF 1 + (ABL:PLAYER:ARG - 2) / 10
;====================================================

;-------------------------------------------------
;関数名:RECOVER
;概　要:BASE回復処理
;		表示文字列が空の場合かつ回復量が100未満非表示
;		表示文字列がある場合は表示
;引　数:ARG:0	参照する角色番号
;		回復量	ARGSに対応するBASEに加算する値（負数の場合は減らす）
;       ARGS	ベース名
;		ARGS:1	表示文字列
;-------------------------------------------------
@RECOVER(ARG,回復量,ARGS,ARGS:1)
#DIM BASE_ID
#DIM 回復量
SIF 回復量 == 0 || !CHECKE_BASE(ARG, ARGS)
	RETURN
;回復処理
BASE:ARG:(ARGS) = LIMIT(BASE:ARG:(ARGS) + 回復量, 0, MAXBASE:ARG:(ARGS))
;表示処理
SIF 回復量 > 0 && 回復量 < 100 && ARGS:1 == ""
	RETURN
IF CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
	BASE_ID = FINDELEMENT(BASENAME, ARGS)
	LOCALS = \@ ARGS:1 != "" ? （%ARGS:1%） # \@
	IF 回復量 > 0
		CALL COLORMESSAGE(@"%BASENAME:BASE_ID%＋{回復量} （%CALLNAME:ARG%）%LOCALS%",C_GREEN,1)
	ELSEIF 回復量 < 0
		CALL COLORMESSAGE(@"%BASENAME:BASE_ID%－{ABS(回復量)} （%CALLNAME:ARG%）%LOCALS%",C_RED,1)
	ENDIF
ENDIF

;-------------------------------------------------
;関数名:RECOVER_PERMIL
;概　要:BASE割合回復処理
;引　数:ARG:0		参照する角色番号
;		回復割合	MAXBASE対して算出する値（負数の場合は減らす）
;       ARGS		ベース名
;       回復速度の影響
;		ARGS:1		表示文字列
;回復割合は0.1%単位
;-------------------------------------------------
@RECOVER_PERMIL(ARG,回復割合,ARGS,回復速度の影響,ARGS:1="")
#DIM 回復割合
#DIM 回復速度の影響
#DIM 回復量
SIF !CHECKE_BASE(ARG, ARGS)
	RETURN
回復量 = MAXBASE:ARG:(ARGS) * 回復割合 / 1000
SIF 回復速度の影響
	回復量 = 回復量 * (TALENT:ARG:回復速度 + 4) / 4
CALL RECOVER(ARG, 回復量, ARGS, ARGS:1)

;-------------------------------------------------
;関数名:RECOVER_PERMIL_BOTH
;概　要:RECOVER_PERMILを你と対象角色両方やる
;-------------------------------------------------
@RECOVER_PERMIL_BOTH(ARG,回復割合,ARGS,回復速度の影響,ARGS:1="")
#DIM 回復割合
#DIM 回復速度の影響
CALL RECOVER_PERMIL(ARG,回復割合,ARGS,回復速度の影響,ARGS:1)
SIF ARG != MASTER
	CALL RECOVER_PERMIL(MASTER,回復割合,ARGS,回復速度の影響,ARGS:1)

;-------------------------------------------------
;関数名:CHECKE_BASE
;概　要:ARGが使えるBASEか判定
;戻り値:0：使用不可
;		1：使用可能
;-------------------------------------------------
@CHECKE_BASE(ARG, ARGS)
#FUNCTION
SELECTCASE ARGS
	CASE "体力", "気力"
	;竿無ならスキップ
	CASE "精力", "勃起", "射精"
		SIF !HAS_PENIS(ARG)
			RETURNF 0
	;你以外TSPスキップ
	CASE "TSP"
		SIF ARG != MASTER
			RETURNF 0
	CASE "母乳"
		SIF !TALENT:ARG:母乳体質
			RETURNF 0
ENDSELECT
RETURNF 1

;-------------------------------------------------
;関数名:BUFF_BASE
;概　要:特定BASEを一時的に強化
;-------------------------------------------------
@BUFF_BASE(ARG,強化能力,変動値,メッセージ)
#DIM 変動値
#DIM 強化能力
#DIM メッセージ
SIF !CHECKE_BASE(ARG, BASENAME:強化能力)
	RETURN
SIF 変動値 < 0 && ABS(変動値) > MAXBASE:ARG:強化能力
	変動値 = (MAXBASE:ARG:強化能力 -1) * -1

MAXBASE:ARG:強化能力 += 変動値
BASE:ARG:強化能力 += 変動値
BUFF:ARG:強化能力 += 変動値
SIF メッセージ
	CALL COLORMESSAGE(@"%CALLNAME:ARG%的%BASENAME:強化能力%暂时的\@ 変動値 > 0? 上升了# 下降了\@{ABS(変動値)}",C_YELLOW,1)
;-------------------------------------------------
;関数名:BUFF_RESET
;概　要:Buffを完全にリセット
;-------------------------------------------------
@BUFF_RESET
FOR LOCAL,0,CHARANUM
	FOR LOCAL:1, 0, 16
		MAXBASE:LOCAL:(LOCAL:1) -= BUFF:LOCAL:(LOCAL:1)
		BASE:LOCAL:(LOCAL:1) -= BUFF:LOCAL:(LOCAL:1)
		BUFF:LOCAL:(LOCAL:1) = 0
	NEXT
NEXT

;-------------------------------------------------
;関数名:CHANGE_CFLAG
;概　要:メッセージ付きCFLAG変動
;减少にはならないようになってるので注意
;-------------------------------------------------
@CHANGE_CFLAG(変動フラグ,対象,変動数)
#DIM 変動フラグ
#DIM 対象
#DIM 変動数

CFLAG:対象:変動フラグ = MAX(CFLAG:対象:変動フラグ + 変動数, 0)
IF 変動数 > 0
	CALL COLORMESSAGE(@"%CFLAGNAME:変動フラグ%＋{変動数}（%CALLNAME:対象%）",C_YELLOW,2,1)
ELSE
	CALL COLORMESSAGE(@"%CFLAGNAME:変動フラグ%－{-変動数}（%CALLNAME:対象%）",C_RED,2,1)
ENDIF

;-------------------------------------------------
;関数名:AddEXP
;概　要:メッセージ付きEXP加算
;减少にはならないようになってるので注意
;-------------------------------------------------
@AddEXP(VAR_NAME,C_ID,変動数)
#DIMS VAR_NAME
#DIM VAR_ID
#DIM C_ID
#DIM 変動数

SIF 変動数 == 0
	RETURN

VAR_ID = FINDELEMENT(EXPNAME, VAR_NAME)
EXP:C_ID:VAR_ID += 変動数
CALL COLORMESSAGE(@"%VAR_NAME%＋{変動数}（%CALLNAME:C_ID%）",C_YELLOW,2,1)

;-------------------------------------------------
;関数名:SHIRAHU
;概　要:普通に意識がある状態か　2だとウフフ中
;-------------------------------------------------
@SHIRAHU(ARG)
#FUNCTION
IF FLAG:70
	RETURNF 0
ELSEIF !CFLAG:ARG:睡眠 && !CFLAG:ARG:陪睡中 && !TCVAR:ARG:烂醉 && !CFLAG:ARG:衰弱
	IF TFLAG:102 == 2
		RETURNF 2
	ELSE
		RETURNF 1
	ENDIF
ELSE
	RETURNF 0
ENDIF

;-------------------------------------------------
;関数名:BETWEENTIME
;概　要:TIMEがARGからARG:1までの間なのか返す関数
;-------------------------------------------------
@BETWEENTIME(ARG,ARG:1)
#FUNCTION
IF ARG < ARG:1
	IF TIME >= ARG && TIME < ARG:1
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ELSE
	IF TIME >= ARG || TIME < ARG:1
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ENDIF
;-------------------------------------------------
;関数名:VISIT
;概　要:その角色の活動時間帯かどうかを返す関数
;-------------------------------------------------
@VISIT(ARG)
#FUNCTION
RETURNF BETWEENTIME(CFLAG:ARG:来訪時間,CFLAG:ARG:帰宅時間)
;-------------------------------------------------
;関数名:睡眠時間
;概　要:住宿人物が寝てるかどうかを返す関数だったのを全員使えるように
;寝てると1を返す
;-------------------------------------------------
@睡眠時間(ARG)
#FUNCTION
SIF ARG == MASTER
	RETURNF CFLAG:MASTER:睡眠
IF CFLAG:ARG:衰弱
	RETURNF 1
ELSE
	;睡眠時間をチェックする
	CALLF 睡眠時間２(ARG)
	IF !BETWEENTIME(CFLAG:ARG:起床時間, CFLAG:ARG:就寝時間 + CFLAG:ARG:清醒劑)
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ENDIF
;-------------------------------------------------
;関数名:睡眠時間２
;概　要:角色ARGが寝てから起きるまでの時間の計測
;逸脱していた場合は自動で修正する
;-------------------------------------------------
@睡眠時間２(ARG)
#FUNCTION
#DIM SLEEPTIME
#DIM WAKEUPTIME
;清醒劑の強度分延長する
WAKEUPTIME = CFLAG:ARG:起床時間
SLEEPTIME = CFLAG:ARG:就寝時間 + CFLAG:ARG:清醒劑
;就寝時間が24時を超えている場合
WHILE SLEEPTIME >= 1440
	SLEEPTIME -= 1440
WEND
;前日の夜に寝て翌日の朝に起きる場合
SIF SLEEPTIME > WAKEUPTIME
	WAKEUPTIME += 1440
;睡眠時間が2～14時間を逸している場合はチェック
IF !INRANGE(WAKEUPTIME - SLEEPTIME, 120, 840)
	;清醒劑が原因
	IF CFLAG:ARG:清醒劑
		;30分ずつ是正していく
		CFLAG:ARG:清醒劑 -= 30
		RESTART
	;設定がおかしい
	ELSE
		[IF_DEBUG]
			;チェックのためTHROWしておく,路人子は許可,一時的に守矢神簽対策,あとで修正,THROWはアレなのでデ背包用に
			;SIF ARG != [[路人子]] && CFLAG:ARG:起床時間 != CFLAG:ARG:就寝時間
				;THROW
		[ENDIF]
	ENDIF
ENDIF
RETURNF WAKEUPTIME - SLEEPTIME

;-------------------------------------------------
;角色の生活時間を設定する関数
;一日の始まりに設定する
;-------------------------------------------------
@生活時間設定(CHARA)
#DIM CHARA
TRYCALLFORM CHARAMOVE_DATA_{CHARA}(3)
CFLAG:CHARA:起床時間 = 時刻計算(CSVCFLAG(CHARA, GETNUM(CFLAG, "起床時間")) + CFLAG:CHARA:生活時間補正)
CFLAG:CHARA:来訪時間 = 時刻計算(CSVCFLAG(CHARA, GETNUM(CFLAG, "来訪時間")) + CFLAG:CHARA:生活時間補正)
CFLAG:CHARA:帰宅時間 = 時刻計算(CSVCFLAG(CHARA, GETNUM(CFLAG, "帰宅時間")) + CFLAG:CHARA:生活時間補正)
CFLAG:CHARA:就寝時間 = 時刻計算(CSVCFLAG(CHARA, GETNUM(CFLAG, "就寝時間")) + CFLAG:CHARA:生活時間補正)
;大晦日はみんな夜ふかし
IF FESTIVAL() == "大晦日"
	IF INRANGE(CFLAG:CHARA:帰宅時間, 900 , 1440) || INRANGE(CFLAG:CHARA:帰宅時間, 0 , 59) || INRANGE(CFLAG:CHARA:就寝時間, 900 , 1440) || INRANGE(CFLAG:CHARA:就寝時間, 0 , 119)
		CFLAG:CHARA:帰宅時間 = 60
		CFLAG:CHARA:就寝時間 = 120
	ENDIF
ENDIF
;-------------------------------------------------
;時刻の計算
;時刻X+Yを時刻で返す
;-------------------------------------------------
@時刻計算(ARG)
#FUNCTION
LOCAL = ARG
WHILE LOCAL > 1440
	LOCAL -= 1440
WEND
WHILE LOCAL < 0
	LOCAL += 1440
WEND
RETURNF LOCAL

;-------------------------------------------------
;関数名:兒童の睡眠時間
;概　要:角色ARGの兒童が寝てるかどうかを返す関数
;寝てると1を返す
;-------------------------------------------------
@兒童の睡眠時間(ARG)
#FUNCTION
#DIM 兒童の起床時間
#DIM 兒童の就寝時間
SIF !TALENT:ARG:育児中
	RETURNF 0
兒童の起床時間 = CFLAG:ARG:起床時間
兒童の就寝時間 = CFLAG:ARG:就寝時間 - 120 + (CFLAG:ARG:就寝時間 - 120 < 0 ? 1440 # 0 )
IF !BETWEENTIME(兒童の起床時間, 兒童の就寝時間)
	RETURNF 1
ELSE
	RETURNF 0
ENDIF
;-------------------------------------------------
;関数名:WORKING
;概　要:その角色の仕事時間帯かどうかを返す関数
;-------------------------------------------------
@WORKING(ARG)
#FUNCTION
IF !SHIRAHU(ARG)
	RETURNF 0
ELSEIF !BETWEENTIME(TCVAR:ARG:工作開始,TCVAR:ARG:工作結束)
	RETURNF 0
ELSEIF BASE:ARG:工作量 <= 0
	RETURNF 0
ELSEIF CHARA_HOLIDAY(ARG)
	RETURNF 0
ELSE
	RETURNF 1
ENDIF
;-------------------------------------------------
;関数名:連続実行
;概　要:同一指令連続実行した際ペナルティが発生するか
;戻り値:0だとペナが発生しない
;-------------------------------------------------
@連続実行()
#FUNCTION
SIF TARGET != TFLAG:103
	RETURNF 0
SELECTCASE PREVCOM
	CASE 1,60,61,65,67,68,300,304,368,410,442,445,446,604,621
		RETURNF 0
	CASEELSE
		RETURNF 1
ENDSELECT

;-------------------------------------------------
;関数名:Counter不発指令
;戻り値:0でないとCounter不発
;-------------------------------------------------
@Counter不発指令(ARG)
#FUNCTION
SELECTCASE ARG
	;(天候パッチ)「眺望天空」はCounter不発
	CASE 495
		RETURNF 1
	;炮友契約
	CASE 357
		RETURNF 1
	CASE 697 TO 699
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT
;-------------------------------------------------
;関数名:ENDUFUFU
;概　要:诶嘿嘿終了時リセット処理
;-------------------------------------------------
@ENDUFUFU(ARG)
[IF DEBUG]
	PRINTFORML 诶嘿嘿終了処理
[ENDIF]
UFUFU_START_TIME = 0
UFUFU_START_DAY = 0
;時間停止中は服装セットしない
;睡眠中分岐などは全部リセット処理側にあります
SIF !FLAG:時間停止
	CALL CHANGE_CLOTHES(ARG, "复原衣服")
;服装をTEQUIPへ
CALL CLOTHES_SETTING_TRAIN(ARG)

;MASTERとの接触位置をリセット
CALL TOUCH_SET(0, 0, 0, 1)

;ウフフ時系装備リセット
;TEQUIP:ARG:10-TEQUIP:ARG:149を初期化する
VARSET TEQUIP:ARG:0, 0, 10, 150

;射精関係リセット
IF TFLAG:102 < 2
	BASE:ARG:射精 = 0
	TCVAR:ARG:要去了 = 0
ENDIF
CFLAG:ARG:诶嘿嘿 = 0
TEQUIP:ARG:50 = -1
TEQUIP:ARG:51 = -1
TEQUIP:ARG:上半身裸露状態 = 0
TEQUIP:ARG:下半身挪開状態 = 0
TEQUIP:ARG:上衣脱衣完毕 = 0
TCVAR:ARG:强行 = 0
TCVAR:ARG:随便 = 0

;一応MASTERも初期化（しとかないと、終了後も○○装着中が表示される可能性があるので
IF CFLAG:MASTER:诶嘿嘿
	CFLAG:MASTER:诶嘿嘿 = 0
	CALL CHANGE_CLOTHES(MASTER, "复原衣服")
	CALL CLOTHES_SETTING_TRAIN(MASTER)
	VARSET TEQUIP:MASTER:0, 0, 10, 150
	TEQUIP:MASTER:50 = -1
	TEQUIP:MASTER:51 = -1
	TEQUIP:MASTER:上半身裸露状態 = 0
	TEQUIP:MASTER:下半身挪開状態 = 0
ENDIF

IF CFLAG:睡眠
	IF CFLAG:ARG:口内精液
		IF TALENT:ARG:精愛味覚 == 1
			SETCOLOR C_PINK
			PRINTFORML ❤~
			PRINTFORML 梦中的%CALLNAME:ARG%自然的饮下了%CALLNAME:MASTER%赠与的白浊，随后就像触发了什么开关一样缓缓将手移到身下，满面红潮
			TCVAR:ARG:発情 = 1
			CFLAG:ARG:积攒度 += LIMIT((10 + RAND:20 + RAND:20 + RAND:20) * ABL:LOCAL:欲望,200,1000)
		ELSEIF ABL:ARG:精液中毒 > 4
			SETCOLOR C_YELLOW
			PRINTFORML 嗯~
			PRINTFORML %CALLNAME:ARG%在美梦中熟练的吞下了%CALLNAME:MASTER%留在口中的罪恶，但似乎没能满足似的舔舐着口中残存的味道
			CFLAG:ARG:积攒度 += LIMIT((10 + RAND:20 + RAND:20 + RAND:20) * ABL:LOCAL:欲望,200,1000)
		ELSEIF ABL:ARG:精液中毒 > 2
			SETCOLOR C_AQUA
			PRINTFORML 唔...
			PRINTFORML %CALLNAME:ARG%不自觉的咽下%CALLNAME:MASTER%留在口中的证据，然后美美的微笑着发出一声梦呓
		ELSE
			SETCOLOR C_RED
			PRINTFORML 咳...咳...
			PRINTFORML %CALLNAME:MASTER%処理掉了%CALLNAME:ARG%口中残留的精液，%CALLNAME:ARG%在睡梦中轻声咳了一声
		ENDIF
			CFLAG:ARG:口内精液 = 0
			RESETCOLOR
	ENDIF
	IF CFLAG:ARG:脸上精液 || CFLAG:ARG:手上精液
		PRINTFORM %CALLNAME:MASTER%把粘在%CALLNAME:ARG%
		IF CFLAG:ARG:脸上精液 && !CFLAG:ARG:手上精液
			PRINTFORM 脸上的
		ELSEIF !CFLAG:ARG:脸上精液 && CFLAG:ARG:手上精液
			PRINTFORM 手上的
		ELSE
			PRINTFORM 脸上和手上的
		ENDIF
		PRINTFORML 精液擦掉了
		CFLAG:ARG:脸上精液 = 0
		CFLAG:ARG:手上精液 = 0
	ENDIF
ENDIF
IF !FLAG:時間停止
	TFLAG:運搬 = 0
	IF CFLAG:ARG:口内精液
		PRINTFORML %CALLNAME:ARG%把嘴里残留着的精液\@ ABL:ARG:精液中毒 > 2 ? 喝下去了 # 吐出来了 \@
		CFLAG:ARG:口内精液 = 0
	ENDIF
	IF CFLAG:ARG:脸上精液 || CFLAG:ARG:手上精液
		PRINTFORM %CALLNAME:ARG%把
		IF CFLAG:ARG:脸上精液 && !CFLAG:ARG:手上精液
			PRINTFORM 粘在脸上的
		ELSEIF !CFLAG:ARG:脸上精液 && CFLAG:ARG:手上精液
			PRINTFORM 粘在手上的
		ELSE
			PRINTFORM 粘在脸上和手上的
		ENDIF
		PRINTFORML 精液\@ ABL:ARG:精液中毒 > 2 ? 含进嘴里 # 擦掉了 \@
		CFLAG:ARG:脸上精液 = 0
		CFLAG:ARG:手上精液 = 0
	ENDIF
ENDIF

;潜伏モード解除
SIF GETBIT(FLAG:潜伏,0)
	CALL 潜伏モード終了処理

;-------------------------------------------------
;関数名:ENDUFUFU_ALL
;概　要:全員にENDUFUFU
;人数を返す
;-------------------------------------------------
@ENDUFUFU_ALL
VARSET LOCAL
FOR LOCAL, 0, CHARANUM
	IF CFLAG:LOCAL:诶嘿嘿
		CALL ENDUFUFU(LOCAL)
		LOCAL:1 ++
	ENDIF
NEXT
TFLAG:MASTERのＰ挿入中 = 0
RETURN LOCAL:1

;-------------------------------------------------------------
;ARGと你が同性だった場合、両者にレズ（ゲイ）経験を追加する関数
;-------------------------------------------------------------
@ADD_EXP_LESNIAN(UP_EXP,ARG,ARGS)
#DIM UP_EXP
#DIM SAMESEX

;第二引数を省略するとTARGET扱い
SIF !ARG
	ARG = TARGET
IF HETEROSEX(ARG,MASTER)
	SAMESEX = 50 + HETEROSEX(ARG,MASTER)
	EXP:SAMESEX += UP_EXP
	EXP:PLAYER:SAMESEX += UP_EXP
	IF ARGS != ""
		CALL COLORMESSAGE(@"%EXPNAME:SAMESEX%＋{UP_EXP}（%CALLNAME:ARG%）",C_YELLOW,2,1)
		CALL COLORMESSAGE(@"%EXPNAME:SAMESEX%＋{UP_EXP}（%CALLNAME:MASTER%）",C_YELLOW,2,1)
	ENDIF
ENDIF

;----------------------------------------------------
;性別判定用
;----------------------------------------------------
@HAS_PENIS(ARG)
#FUNCTION
SIF GETBIT(TALENT:ARG:性別, 1)
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;イベントなどでキレた場合の処理
;-------------------------------------------------
@GET_ANGRY(ARG, 刻印付与)
#DIM 刻印付与

CFLAG:ARG:心情不快 = 1
CFLAG:ARG:ブチギレ = 1
BASE:ARG:憤怒 = MAXBASE:ARG:憤怒
TALENT:ARG:心情 = -1
MARK:ARG:反発刻印 = MIN(MARK:ARG:反発刻印 + 刻印付与, 3)
MARK:ARG:反発取得履歴 = MAX(MARK:ARG:反発取得履歴,MARK:ARG:反発刻印)

;FileName_COMMON.ERB ------------------------------- Rev1.00
;酒酔い状態取得
;CALL		USER
;ARG		角色No
;RETURN		酒酔い状態_toSTR
;COMMENT
;-----------------------------------------------------------
@ALCOHOL_FACE(ARG)
#FUNCTIONS
SIF BASE:ARG:酒気 <= 2
	RETURNF "　清醒　"
SELECTCASE BASE:ARG:酒気
	CASE IS > (MAXBASE:ARG:酒気 / 10) * 8
		RETURNF "不省人事"
;	CASE IS > (MAXBASE:ARG:酒気 / 10) * 7
;		RETURNF "酩酊大醉"
	CASE IS > (MAXBASE:ARG:酒気 / 10) * 5
		RETURNF "酔酒"
	CASE IS <= (MAXBASE:ARG:酒気 / 10) * 5
		RETURNF "微醺"
ENDSELECT
;-------------------------------------------------
;是／否 の選択肢を表示し入力結果を返す関数
;引数 0=肯定選択肢の文字列、1=否定選択肢の文字列
;戻り値 0=是、1=否
;-------------------------------------------------
@ASK_YN(ARGS:0 = "是", ARGS:1 = "否")
#DIM PREV_REDRAW
PREV_REDRAW = CURRENTREDRAW()
REDRAW 0
PRINTBUTTON @" [%ARGS:0%]", 0
PRINTL
PRINTBUTTON @" [%ARGS:1%]", 1
PRINTL
$INPUT_LOOP
INPUT
IF RESULT != 0 && RESULT != 1
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
REDRAW PREV_REDRAW
RETURN RESULT
;-------------------------------------------------
;特殊な機能を持つボタンを作成する関数
;ARGは入力される数字、ARG:1に1を設定すると灰色に、
;ARG:2に1を設定するとボタンが黄色、ARG:3に1を設定するとボタンが無効(灰色)
;-------------------------------------------------
@PRINTBUTTON_EX(ARGS:0, ARG,ARG:1,ARG:2,ARG:3)
IF ARG:3
	SETCOLOR C_L_GRAY
	PRINTFORM %ARGS:0%
ELSE
	SIF ARG:2
		SETCOLOR C_YELLOW
	SIF ARG:1
		SETCOLOR C_L_GRAY
	PRINTBUTTON @"%ARGS:0%", ARG
ENDIF
RESETCOLOR
;-------------------------------------------------
;特殊な機能を持つボタンを作成する関数
;ARGは入力される数字、ARG:1に1を設定すると灰色に、
;ARG:2に1を設定するとボタンが黄色、ARG:3に1を設定するとボタンが無効(灰色)
;-------------------------------------------------
@PRINTBUTTON_EXC(ARGS:0, ARG,ARG:1,ARG:2,ARG:3)
IF ARG:3
	SETCOLOR C_L_GRAY
	PRINTFORMC %ARGS:0%
ELSE
	SIF ARG:2
		SETCOLOR C_YELLOW
	SIF ARG:1
		SETCOLOR C_L_GRAY
	PRINTBUTTONC @"%ARGS:0%", ARG
ENDIF
RESETCOLOR
;-------------------------------------------------
;Fisher-Yatesアルゴリズムによるシャッフル配列の作成
;専用配列SHAFFLE_ARRAYを使用
;引数 0:配列の長さ（0～ARG:0-1までのシャッフル）
;-------------------------------------------------
@FISHER_YATES_SHAFFLE(ARG:0)
;シャッフル配列の初期化
SIF ARG:0 < 1 || ARG:0 > 999
   ARG:0 = 1 + RAND:998
SIF !INRANGE(ARG:0 , 1 , 999)
	THROW 引数({ARG:0})が異常です。1～999の範囲で指定してください。
VARSET SHAFFLE_ARRAY , 0
;SHAFFLE_ARRAYの初期化
FOR LOCAL:0 , 0 , ARG:0
	SHAFFLE_ARRAY:(LOCAL:0) = LOCAL:0
NEXT

FOR LOCAL:0 , 0 , ARG:0 - 1
	LOCAL:1 = RAND(LOCAL:0 , ARG:0)
	SWAP SHAFFLE_ARRAY:(LOCAL:0), SHAFFLE_ARRAY:(LOCAL:1)
NEXT
;終端用に-1をセット
SHAFFLE_ARRAY:(ARG:0) = -1
RETURN 0

@NEMUKE()
#FUNCTION
LOCAL =  TIME + 1440 * DAY - TFLAG:你起床 - TFLAG:198 - CFLAG:MASTER:清醒劑
RETURNF LOCAL

;-------------------------------------------------
;時刻表示(ARG)
;与えられたTIMEをH時MM分表示に変換
;-------------------------------------------------
@時刻表示(ARG)
#FUNCTIONS
#DIM HOUR
#DIM MINUTE
#DIMS 時刻
HOUR   = ARG / 60
MINUTE = ARG % 60
時刻 = %TOSTR(HOUR)%時%TOSTR(MINUTE,"00")%分
RETURNF 時刻

;-------------------------------------------------
;時刻変換(HOUR, MINUTE)
;与えられたHOUR時MINUTE分をTIME用に変換
;念のためにHOURが25だったりMINUTEが61だったりした場合にも対応した
;例）CLOCK_TO_TIME(10, 0)をすると返る数値は600
;eraTWの一日は00:01～24:00の範囲、詳しくは@INFO_GO_NEXTDAYを見よ
;-------------------------------------------------
@CLOCK_TO_TIME(HOUR, MINUTE)
#FUNCTION
#DIM HOUR
#DIM MINUTE
SIF HOUR == 24 && MINUTE == 0
	RETURNF 24 * 60
HOUR   = (HOUR + MINUTE / 60) % 24
MINUTE = MINUTE % 60
RETURNF HOUR * 60 + MINUTE



;-------------------------------------------------
;TIMESTOP(ARG)
;その角色の時間停止状態を返す
;0=時間停止無（意識あり）　1=時間停止世界入り（意識あり）　2=完全に止められてる（意識がない状態）
;-------------------------------------------------
@TIMESTOP(ARG)
#FUNCTION
;引数を略すとTARGET
SIF !ARG
	ARG = TARGET
IF !FLAG:70
	LOCAL = 0
ELSEIF CFLAG:ARG:344 == 1
	LOCAL = 1
ELSE
	LOCAL = 2
ENDIF
RETURNF LOCAL

@KIGEN_CHANGE(ARG,確率,傾向)
#DIM 確率
#DIM 傾向
;傾向を1にすると+のみ、-1にすると-のみ
SIF TALENT:ARG:感情缺乏
	確率 /= 2
IF RAND:100 < 確率
	IF !傾向
		IF !RAND:2
			TALENT:ARG:心情 ++
		ELSE
			TALENT:ARG:心情 --
		ENDIF
	ELSE
		TALENT:ARG:心情 += 傾向
	ENDIF
ENDIF
TALENT:ARG:心情 = LIMIT(TALENT:ARG:心情,-1,1)

;-------------------------------------------------
;ARGが討厭男人で你が男なら1
;ARGが討厭女人で你が女なら2を返す
;恋慕愛欲兩面通吃で無効
;-------------------------------------------------
@OTOKOGIRAI(ARG)
#FUNCTION
;兩面通吃
SIF TALENT:ARG:性別嗜好 == -1
	RETURNF 0
;恋慕
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF TALENT:ARG:愛欲
	RETURNF 0
;討厭男人
IF TALENT:ARG:性別嗜好 & 1 && !(TALENT:PLAYER:2 & 1)
	RETURNF 1
;討厭女人
ELSEIF TALENT:ARG:性別嗜好 & 2 && !(TALENT:PLAYER:2 & 2)
	RETURNF 2
ENDIF
RETURNF 0



;-------------------------------------------------
;劣情察知の個別判定F関数
;-------------------------------------------------
;実績解除しなくても察知できるように
;指令としての劣情察知は使用不可能
@NOTICE_LUST(C_ID)
#FUNCTION
#DIM C_ID
SIF C_ID == MASTER
	RETURNF 0
SIF TALENT:MASTER:劣情察知
	RETURNF 1
SIF EXP:C_ID:約會経験 + EXP:C_ID:愛情経験 >= 100
	RETURNF 1
RETURNF 0



;-------------------------------------------------
;生理周期可視化の個別判定F関数
;-------------------------------------------------
;実績解除しなくても可視化できるように
@NOTICE_CYCLE(C_ID)
#FUNCTION
#DIM C_ID
SIF !GETBIT(TALENT:C_ID:性別, 0)
	RETURNF 0
;自分のは把握しとるじゃろ
SIF C_ID == MASTER
	RETURNF 1
SIF FLAG:生理周期可視化
	RETURNF 1
SIF EXP:C_ID:約會経験 + EXP:C_ID:愛情経験 >= 200
	RETURNF 1
RETURNF 0



;-------------------------------------------------
;角色チェックF関数
;-------------------------------------------------
;角色が特定条件に合致しているかどうかを返す
;条件はTYPEごとに指定
;複合した条件に名前をつける時は角色列表クラス関数を増設することにした
;-------------------------------------------------
@CHECK_CHARA(C_ID, TYPE = "存在")
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS DYNAMIC TYPE ;判定タイプ、DYNAMICしたので多重呼び出し可能
#DIM C_ID          ;登録番号
SELECTCASE TYPE
;基本条件
CASE "存在"
	RETURNF !CFLAG:C_ID:出禁
CASE "陥落"
	RETURNF (TALENT:C_ID:思慕 || TALENT:C_ID:恋慕 || TALENT:C_ID:愛欲 || TALENT:C_ID:恋人 || TALENT:C_ID:炮友)
CASE "人类"
	RETURNF TALENT:C_ID:人类
CASE "仙人"
	RETURNF GET_TRIBENAME(C_ID, GETNUM(TALENT, "人类")) == "仙人"
CASE "天人"
	RETURNF GET_TRIBENAME(C_ID, GETNUM(TALENT, "人类")) == "天人"
CASE "月人"
	RETURNF GET_TRIBENAME(C_ID, GETNUM(TALENT, "人类")) == "月人"
CASE "魔界人"
	RETURNF GET_TRIBENAME(C_ID, GETNUM(TALENT, "人类")) == "魔界人"
CASE "外界人"
	RETURNF GET_TRIBENAME(C_ID, GETNUM(TALENT, "人类")) == "外界人"
CASE "妖怪"
	RETURNF TALENT:C_ID:妖怪
CASE "鬼"
	RETURNF GROUPMATCH(GET_TRIBENAME(C_ID, GETNUM(TALENT, "妖怪")), "鬼", "吸血鬼")
CASE "吸血鬼"
	RETURNF GET_TRIBENAME(C_ID, GETNUM(TALENT, "妖怪")) == "吸血鬼"
CASE "河童"
	RETURNF GET_TRIBENAME(C_ID, GETNUM(TALENT, "妖怪")) == "河童"
CASE "天狗"
	RETURNF GET_TRIBENAME(C_ID, GETNUM(TALENT, "妖怪")) == "天狗"
CASE "妖獣"
	RETURNF GROUPMATCH(GET_TRIBENAME(C_ID, GETNUM(TALENT, "妖怪")), "妖獣", "妖鳥", "妖蟲")
CASE "妖精"
	RETURNF TALENT:C_ID:妖精
CASE "神霊"
	RETURNF TALENT:C_ID:神霊
CASE "幽霊"
	RETURNF TALENT:C_ID:幽霊
CASE "付喪神"
	RETURNF TALENT:C_ID:付喪神
CASE "人形"
	RETURNF TALENT:C_ID:人形
CASE "巫女"
	RETURNF GET_TRIBENAME(C_ID, GETNUM(TALENT, "追加種族")) == "巫女"
CASE "魔法使"
	RETURNF GET_TRIBENAME(C_ID, GETNUM(TALENT, "追加種族")) == "魔法使"
CASE "女僕"
	RETURNF GET_TRIBENAME(C_ID, GETNUM(TALENT, "追加種族")) == "女僕"
CASE "蓬莱人"
	RETURNF GET_TRIBENAME(C_ID, GETNUM(TALENT, "追加種族")) == "蓬莱人"
CASE "半人类"
	RETURNF TALENT:C_ID:人类 && (TALENT:C_ID:妖怪 || TALENT:C_ID:妖精 || TALENT:C_ID:神霊 || TALENT:C_ID:幽霊 || TALENT:C_ID:付喪神 || TALENT:C_ID:人形)
CASE "掌握把柄"
;C_IDがMASTERの把柄を握っている
	RETURNF CFLAG:C_ID:掌握把柄
CASE "被掌握把柄"
;C_IDがMASTERに把柄を握られている
	RETURNF CFLAG:C_ID:被掌握把柄
CASE "反発刻印"
	RETURNF (MARK:C_ID:反発刻印 >= 1)
CASE "時姦刻印"
	RETURNF (MARK:C_ID:時姦刻印 >= 1)
CASE "時姦経験"
	RETURNF (EXP:C_ID:時姦経験 >= 1)
CASE "清い交際"
	RETURNF (CFLAG:C_ID:清い交際 >= 1)
CASE "口上実装状況"
	RETURNF (CFLAG:C_ID:口上実装状況 >= 1)
CASE "子持ち"
	RETURNF (CFLAG:C_ID:兒童人数 >= 1)
CASE "同室"
	RETURNF (CFLAG:C_ID:現在位置 == CFLAG:MASTER:現在位置)
CASE "全裸"
	{
	SIF EQUIP:C_ID:飾品 || EQUIP:C_ID:帽子
		 || EQUIP:C_ID:下半身内衣１ || EQUIP:C_ID:下半身内衣２
		 || EQUIP:C_ID:上半身内衣１ || EQUIP:C_ID:上半身内衣２
		 || EQUIP:C_ID:緊身内衣 || EQUIP:C_ID:連体内衣 || EQUIP:C_ID:連衣裙 || EQUIP:C_ID:和服
		 || EQUIP:C_ID:裤子 || EQUIP:C_ID:下身衣服 || EQUIP:C_ID:裙子
		 || EQUIP:C_ID:上身衣服１ || EQUIP:C_ID:上身衣服２ || EQUIP:C_ID:外搭
		 || EQUIP:C_ID:其他１ || EQUIP:C_ID:其他２ || EQUIP:C_ID:其他３ || EQUIP:C_ID:腕部装束
	}
		RETURNF 0
	RETURNF 1
CASE "女性"
	RETURNF TALENT:C_ID:性別 == 1
CASE "妊娠"
	RETURNF TALENT:C_ID:妊娠
CASE "育児中"
	RETURNF TALENT:C_ID:育児中
CASE "扶她"
	RETURNF TALENT:C_ID:性別 == 3
CASE "ケモミミ"
	RETURNF TALENT:C_ID:動物耳 != 0
CASE "人类耳"
	RETURNF TALENT:C_ID:動物耳 == 0
CASE "非処女"
	SIF TALENT:C_ID:性別 == 2
		RETURNF 0
	RETURNF GROUPMATCH(TALENT:C_ID:処女, 0, -1)
CASEELSE
	THROW 不明なTYPE%TYPE%
ENDSELECT



;-------------------------------------------------
;角色比較関数
;-------------------------------------------------
;全引数必須
;スマートとは程遠いコピペゴリ押しである
;-------------------------------------------------
@CHARA_COMPARE(VAR_NAME, VAR_ID, CHARA_A, TYPE, CHARA_B)
#FUNCTION
#DIMS VAR_NAME ;比較対象角色変数
#DIM  VAR_ID   ;比較対象角色変数のID
#DIM  CHARA_A  ;比較対象角色Aの登録番号
#DIMS TYPE     ;比較内容
#DIM  CHARA_B  ;比較対象角色Bの登録番号
SELECTCASE VAR_NAME
CASE "ABL"
	SELECTCASE TYPE
	CASE "=="
		RETURNF ABL:CHARA_A:VAR_ID == ABL:CHARA_B:VAR_ID
	CASE ">"
		RETURNF ABL:CHARA_A:VAR_ID > ABL:CHARA_B:VAR_ID
	CASE ">="
		RETURNF ABL:CHARA_A:VAR_ID >= ABL:CHARA_B:VAR_ID
	CASE "<"
		RETURNF ABL:CHARA_A:VAR_ID < ABL:CHARA_B:VAR_ID
	CASE "<="
		RETURNF ABL:CHARA_A:VAR_ID <= ABL:CHARA_B:VAR_ID
	CASEELSE
		THROW 不明なTYPE　%TYPE%
	ENDSELECT

CASE "BASE"
	SELECTCASE TYPE
	CASE "=="
		RETURNF BASE:CHARA_A:VAR_ID == BASE:CHARA_B:VAR_ID
	CASE ">"
		RETURNF BASE:CHARA_A:VAR_ID > BASE:CHARA_B:VAR_ID
	CASE ">="
		RETURNF BASE:CHARA_A:VAR_ID >= BASE:CHARA_B:VAR_ID
	CASE "<"
		RETURNF BASE:CHARA_A:VAR_ID < BASE:CHARA_B:VAR_ID
	CASE "<="
		RETURNF BASE:CHARA_A:VAR_ID <= BASE:CHARA_B:VAR_ID
	CASEELSE
		THROW 不明なTYPE　%TYPE%
	ENDSELECT

CASE "MAXBASE"
	SELECTCASE TYPE
	CASE "=="
		RETURNF MAXBASE:CHARA_A:VAR_ID == MAXBASE:CHARA_B:VAR_ID
	CASE ">"
		RETURNF MAXBASE:CHARA_A:VAR_ID > MAXBASE:CHARA_B:VAR_ID
	CASE ">="
		RETURNF MAXBASE:CHARA_A:VAR_ID >= MAXBASE:CHARA_B:VAR_ID
	CASE "<"
		RETURNF MAXBASE:CHARA_A:VAR_ID < MAXBASE:CHARA_B:VAR_ID
	CASE "<="
		RETURNF MAXBASE:CHARA_A:VAR_ID <= MAXBASE:CHARA_B:VAR_ID
	CASEELSE
		THROW 不明なTYPE　%TYPE%
	ENDSELECT

CASE "EXP"
	SELECTCASE TYPE
	CASE "=="
		RETURNF EXP:CHARA_A:VAR_ID == EXP:CHARA_B:VAR_ID
	CASE ">"
		RETURNF EXP:CHARA_A:VAR_ID > EXP:CHARA_B:VAR_ID
	CASE ">="
		RETURNF EXP:CHARA_A:VAR_ID >= EXP:CHARA_B:VAR_ID
	CASE "<"
		RETURNF EXP:CHARA_A:VAR_ID < EXP:CHARA_B:VAR_ID
	CASE "<="
		RETURNF EXP:CHARA_A:VAR_ID <= EXP:CHARA_B:VAR_ID
	CASEELSE
		THROW 不明なTYPE　%TYPE%
	ENDSELECT

CASE "CFLAG"
	SELECTCASE TYPE
	CASE "=="
		RETURNF CFLAG:CHARA_A:VAR_ID == CFLAG:CHARA_B:VAR_ID
	CASE ">"
		RETURNF CFLAG:CHARA_A:VAR_ID > CFLAG:CHARA_B:VAR_ID
	CASE ">="
		RETURNF CFLAG:CHARA_A:VAR_ID >= CFLAG:CHARA_B:VAR_ID
	CASE "<"
		RETURNF CFLAG:CHARA_A:VAR_ID < CFLAG:CHARA_B:VAR_ID
	CASE "<="
		RETURNF CFLAG:CHARA_A:VAR_ID <= CFLAG:CHARA_B:VAR_ID
	CASEELSE
		THROW 不明なTYPE　%TYPE%
	ENDSELECT

CASE "PALAM"
	SELECTCASE TYPE
	CASE "=="
		RETURNF PALAM:CHARA_A:VAR_ID == PALAM:CHARA_B:VAR_ID
	CASE ">"
		RETURNF PALAM:CHARA_A:VAR_ID > PALAM:CHARA_B:VAR_ID
	CASE ">="
		RETURNF PALAM:CHARA_A:VAR_ID >= PALAM:CHARA_B:VAR_ID
	CASE "<"
		RETURNF PALAM:CHARA_A:VAR_ID < PALAM:CHARA_B:VAR_ID
	CASE "<="
		RETURNF PALAM:CHARA_A:VAR_ID <= PALAM:CHARA_B:VAR_ID
	CASEELSE
		THROW 不明なTYPE　%TYPE%
	ENDSELECT

CASE "TALENT"
	SELECTCASE TYPE
	CASE "=="
		RETURNF TALENT:CHARA_A:VAR_ID == TALENT:CHARA_B:VAR_ID
	CASE ">"
		RETURNF TALENT:CHARA_A:VAR_ID > TALENT:CHARA_B:VAR_ID
	CASE ">="
		RETURNF TALENT:CHARA_A:VAR_ID >= TALENT:CHARA_B:VAR_ID
	CASE "<"
		RETURNF TALENT:CHARA_A:VAR_ID < TALENT:CHARA_B:VAR_ID
	CASE "<="
		RETURNF TALENT:CHARA_A:VAR_ID <= TALENT:CHARA_B:VAR_ID
	CASEELSE
		THROW 不明なTYPE　%TYPE%
	ENDSELECT

CASE "TCVAR"
	SELECTCASE TYPE
	CASE "=="
		RETURNF TCVAR:CHARA_A:VAR_ID == TCVAR:CHARA_B:VAR_ID
	CASE ">"
		RETURNF TCVAR:CHARA_A:VAR_ID > TCVAR:CHARA_B:VAR_ID
	CASE ">="
		RETURNF TCVAR:CHARA_A:VAR_ID >= TCVAR:CHARA_B:VAR_ID
	CASE "<"
		RETURNF TCVAR:CHARA_A:VAR_ID < TCVAR:CHARA_B:VAR_ID
	CASE "<="
		RETURNF TCVAR:CHARA_A:VAR_ID <= TCVAR:CHARA_B:VAR_ID
	CASEELSE
		THROW 不明なTYPE　%TYPE%
	ENDSELECT

CASE "MARK"
	SELECTCASE TYPE
	CASE "=="
		RETURNF MARK:CHARA_A:VAR_ID == MARK:CHARA_B:VAR_ID
	CASE ">"
		RETURNF MARK:CHARA_A:VAR_ID > MARK:CHARA_B:VAR_ID
	CASE ">="
		RETURNF MARK:CHARA_A:VAR_ID >= MARK:CHARA_B:VAR_ID
	CASE "<"
		RETURNF MARK:CHARA_A:VAR_ID < MARK:CHARA_B:VAR_ID
	CASE "<="
		RETURNF MARK:CHARA_A:VAR_ID <= MARK:CHARA_B:VAR_ID
	CASEELSE
		THROW 不明なTYPE　%TYPE%
	ENDSELECT

CASEELSE
	THROW 不明なVAR_NAME　%VAR_NAME%
ENDSELECT

[SKIPSTART]
コレをコピペして置換すれば項目をふやせる
CASE "CVAR"
	SELECTCASE TYPE
	CASE "=="
		RETURNF CVAR:CHARA_A:VAR_ID == CVAR:CHARA_B:VAR_ID
	CASE ">"
		RETURNF CVAR:CHARA_A:VAR_ID > CVAR:CHARA_B:VAR_ID
	CASE ">="
		RETURNF CVAR:CHARA_A:VAR_ID >= CVAR:CHARA_B:VAR_ID
	CASE "<"
		RETURNF CVAR:CHARA_A:VAR_ID < CVAR:CHARA_B:VAR_ID
	CASE "<="
		RETURNF CVAR:CHARA_A:VAR_ID <= CVAR:CHARA_B:VAR_ID
	CASEELSE
		THROW 不明なTYPE　%TYPE%
	ENDSELECT
[SKIPEND]



;-------------------------------------------------
;ステータス評価表示関数
;-------------------------------------------------
;元は@PRINT_字母表顺序評価
;引数を評価対象数値にすることで汎用化し
;RANK_SCALEの名称をわかりやすくした
;-------------------------------------------------
@PRINT_RANK(VAR_VALUE, RANK_SCALE)
#DIM  VAR_VALUE  ;評価対象数値
#DIMS RANK_SCALE ;評価に使用する尺度の種類
#DIM  RANK       ;評価ランク、数が小さいほど優秀
#DIM  RANK_NUM   ;評価尺度に含まれる評価の種数
CALL CALC_RANK(VAR_VALUE, RANK_SCALE, RANK, RANK_NUM)
CALL PRINT_ALPHABET(RANK, RANK_NUM)



;-------------------------------------------------
;ステータス評価算出関数
;-------------------------------------------------
;RANK_SCALEにもとづいてVAR_VALUEの評価を行う
;REFによりRANK/RANK_NUMとして渡された引数の数値を直接書き換える
;-------------------------------------------------
@CALC_RANK(VAR_VALUE, RANK_SCALE, RANK, RANK_NUM)
#DIM      VAR_VALUE  ;評価対象数値
#DIMS     RANK_SCALE ;評価に使用する尺度の種類
#DIM  REF RANK       ;評価ランク、数が小さいほど優秀
#DIM  REF RANK_NUM   ;評価尺度に含まれる評価の種数
SELECTCASE RANK_SCALE
CASE "MAX_50000"
;好感度用
	RANK_NUM = 10
	SELECTCASE VAR_VALUE
	CASE IS >= 50000
		RANK = 0
	CASE IS >= 30000
		RANK = 1
	CASE IS >= 10000
		RANK = 2
	CASE IS >=  5000
		RANK = 3
	CASE IS >=  3000
		RANK = 4
	CASE IS >=  1500
		RANK = 5
	CASE IS >=  1000
		RANK = 6
	CASE IS >=   500
		RANK = 7
	CASE IS >=   100
		RANK = 8
	CASEELSE
		RANK = 9
	ENDSELECT
CASE "MAX_20000"
;信賴度用
	RANK_NUM = 10
	SELECTCASE VAR_VALUE
	CASE IS >= 20000
		RANK = 0
	CASE IS >= 10000
		RANK = 1
	CASE IS >=  3000
		RANK = 2
	CASE IS >=  1000
		RANK = 3
	CASE IS >=   500
		RANK = 4
	CASE IS >=   150
		RANK = 5
	CASE IS >=   100
		RANK = 6
	CASE IS >=    50
		RANK = 7
	CASE IS >=    10
		RANK = 8
	CASEELSE
		RANK = 9
	ENDSELECT
CASE "MAX_99"
;技術系以外のABL
	;数字が縦に並ぶと非常に見にくかったんで意図的にインデントずらしてます
	RANK_NUM = 10
	SELECTCASE VAR_VALUE
	CASE IS >= 99
			RANK = 0
	CASE IS >= 75
			RANK = 1
	CASE IS >= 50
			RANK = 2
	CASE IS >= 20
			RANK = 3
	CASE IS >= 10
			RANK = 4
	CASE IS >=  5
			RANK = 5
	CASE IS >=  3
			RANK = 6
	CASE IS >=  2
			RANK = 7
	CASE IS >=  1
			RANK = 8
	CASEELSE
			RANK = 9
	ENDSELECT
CASE "MAX_6"
;技能系ABL
	RANK_NUM = 7
	RANK = MAX(6 - VAR_VALUE, 0)
CASEELSE
	THROW 不明なRANK_SCALE %RANK_SCALE%
ENDSELECT



;-------------------------------------------------
;ランク評価表示関数
;-------------------------------------------------
;ランク評価(数字が小さいほど優秀)の内容を字母表顺序表記してカラー表示する
;形式さえあってりゃ別にPRINT_RANKでなくとも使えるぞ
;ARG=1で大文字
;-------------------------------------------------
@PRINT_ALPHABET(RANK, RANK_NUM, ARG)
#DIM RANK
#DIM RANK_NUM
#DIMS CONST STR_RANK10, 10 = "Ex", "SS", " S", " A", " B", " C", " D", " E", " F", " G"
#DIMS CONST STR_RANK7,   7 = "Ex", " S", " A", " B", " C", " D", " E"
#DIMS CONST STR_RANK10_F, 10 = "ＳＳＳ", "ＳＳ", "Ｓ", "Ａ", "Ｂ", "Ｃ", "Ｄ", "Ｅ", "Ｆ", "Ｇ"
#DIMS CONST STR_RANK7_F,   7 = "ＥＸ", "Ｓ", "Ａ", "Ｂ", "Ｃ", "Ｄ", "Ｅ"
{
#DIMS  CONST C_RANK10, 10 =
	"C_P_PURPLE", "C_WHITE", "C_WHITE", "C_PINK", "C_RED",
	"C_ORANGE", "C_YELLOW", "C_GREEN", "C_AQUA", "C_GRAY"
}
#DIMS  CONST C_RANK7,   7 = "C_P_PURPLE", "C_WHITE", "C_PINK", "C_RED", "C_ORANGE", "C_YELLOW", "C_GRAY"
IF ARG
	SELECTCASE RANK_NUM
	CASE 10
		CALL COLOR_PRINT(STR_RANK10_F:RANK, C_RANK10:RANK, 2)
	CASE  7
		CALL COLOR_PRINT(STR_RANK7_F:RANK, C_RANK7:RANK, 2)
	CASEELSE
		THROW 错误的RANK_NUM {RANK_NUM}
	ENDSELECT
ELSE
	SELECTCASE RANK_NUM
	CASE 10
		CALL COLOR_PRINT(STR_RANK10:RANK, C_RANK10:RANK, 2)
	CASE  7
		CALL COLOR_PRINT(STR_RANK7:RANK, C_RANK7:RANK, 2)
	CASEELSE
		THROW 错误的RANK_NUM {RANK_NUM}
	ENDSELECT
ENDIF
RESETCOLOR



;-------------------------------------------------
;色付きPRINT関数
;-------------------------------------------------
;渡した文字を渡した色でPRINTしてRESETCOLOR
;広域変数でDIMってある色名のみ可能（追加は容易）
;-------------------------------------------------
@COLOR_PRINT(ARGS, C_NAME, STR_NUM = 2)
#DIMS C_NAME
#DIM  STR_NUM
SIF !STRLENS(ARGS)
	RETURN
SELECTCASE C_NAME
CASE "C_P_PURPLE"
	SETCOLOR C_P_PURPLE
CASE "C_WHITE"
	SETCOLOR C_WHITE
CASE "C_PINK"
	SETCOLOR C_PINK
CASE "C_RED"
	SETCOLOR C_RED
CASE "C_ORANGE"
	SETCOLOR C_ORANGE
CASE "C_YELLOW"
	SETCOLOR C_YELLOW
CASE "C_GREEN"
	SETCOLOR C_GREEN
CASE "C_AQUA"
	SETCOLOR C_AQUA
CASE "C_GRAY"
	SETCOLOR C_GRAY
CASEELSE
	THROW 错误的C_NAME %C_NAME%
ENDSELECT
PRINTFORM %ARGS, STR_NUM%
RESETCOLOR



;-------------------------------------------------
;BASEカラーバー関数
;-------------------------------------------------
;体力などのカラーバー
;-------------------------------------------------
@BASE_BAR(C_ID, BASE_NAME, BAR_LENGTH, COLOR_NAME, OP)
#DIM  C_ID
#DIMS BASE_NAME
#DIM  BAR_LENGTH
#DIMS COLOR_NAME
#DIMS OP
#DIM  BASE_ID
BASE_ID = FINDELEMENT(BASENAME, BASE_NAME)
SIF BASE_ID < 0
	THROW 存在しないBASE_NAME %BASE_NAME%
SIF !STRCOUNT(OP, "「種類表記無し」")
	PRINTFORM %BASE_NAME, 4%
CALL PRINT_COLORBAR(BASE:C_ID:BASE_ID, MAXBASE:C_ID:BASE_ID, BAR_LENGTH, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET(COLOR_NAME), RESULT:1)
IF !STRCOUNT(OP, "「数字表記無し」")
	IF BUFF:C_ID:BASE_ID > 0
		SETCOLOR C_AQUA
	ELSEIF BUFF:C_ID:BASE_ID < 0
		SETCOLOR C_YELLOW
	ENDIF
	PRINTFORM  ({BASE:C_ID:BASE_ID,5}/{MAXBASE:C_ID:BASE_ID,5})
	RESETCOLOR
ENDIF
SIF STRCOUNT(OP, "「改行」")
	PRINTFORML

;-------------------------------------------------
;INFO用の改良型BASEカラーバー関数
;-------------------------------------------------
@BASE_BAR_2(C_ID, BASE_NAME, BASE_NAME_SHORT, BAR_LENGTH, COLOR_NAME)
#DIM  C_ID
#DIMS BASE_NAME
#DIMS BASE_NAME_SHORT
#DIM  BAR_LENGTH
#DIMS COLOR_NAME
#DIM  BASE_ID
BASE_ID = FINDELEMENT(BASENAME, BASE_NAME)
SIF BASE_ID < 0
	THROW 存在しないBASE_NAME %BASE_NAME%
IF BASE_NAME == "精力" && TALENT:C_ID:濃厚精液
	FONTBOLD
	SETCOLOR C_CREAM
	PRINTFORM %BASE_NAME_SHORT%
	FONTREGULAR
	RESETCOLOR
ELSEIF BASE_NAME == "酒気" && BASE:C_ID:酒気 > MAXBASE:C_ID:酒気 * 2 / 3
	SETCOLOR C_RED
	PRINTFORM %BASE_NAME_SHORT%
	RESETCOLOR
ELSE
	PRINTFORM %BASE_NAME_SHORT%
ENDIF
CALL PRINT_COLORBAR(BASE:C_ID:BASE_ID, MAXBASE:C_ID:BASE_ID, BAR_LENGTH, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET(COLOR_NAME), RESULT:1)
IF BUFF:C_ID:BASE_ID > 0
	SETCOLOR C_AQUA
ELSEIF BUFF:C_ID:BASE_ID < 0
	SETCOLOR C_YELLOW
ENDIF
PRINTFORM ({BASE:C_ID:BASE_ID, 5})
RESETCOLOR

;-------------------------------------------------
;カラースタンプ関数
;-------------------------------------------------
;特定の記号を特定の色で並べる
;MARKの表示用だけどなんか其他にも使えるでしょう
;ARG=1なら左寄せ
;-------------------------------------------------
@COLOR_STAMP(STAMP_NUM, MAX_NUM, STAMP_STR, STAMP_COLOR, ARG)
#DIM  STAMP_NUM   ;並べるスタンプの数
#DIM  MAX_NUM     ;スタンプの最大数
#DIMS STAMP_STR   ;スタンプに使用する文字列
#DIM  STAMP_COLOR ;スタンプの色
#DIM  STAMP_LENGTH;スタンプ文字列の長さ
STAMP_LENGTH = STRLENS(STAMP_STR)
SIF !STAMP_LENGTH
	THROW STAMP_STRが指定されていません
SIF STAMP_COLOR
	SETCOLOR STAMP_COLOR
SIF !ARG
	PRINTFORM %" " * (MAX_NUM - STAMP_NUM) * STAMP_LENGTH%
PRINTFORM %STAMP_STR * STAMP_NUM%
RESETCOLOR

;-------------------------------------------------
;角色抽選関数
;-------------------------------------------------
;角色列表から随机で1名を選出する
;戻り値は登録番号、失敗したら-1
;-------------------------------------------------
@RAND_CHARASELECT(L_TYPE = "主人公以外的角色列表")
#DIMS L_TYPE                       ;抽選対象タイプ
#DIM  L_ID                         ;列表のクラスID
#DIM  CHARA_LIST, 人物数量上限 ;作成する列表
#DIM  CHARA_CNT                    ;角色カウンタ
CALL MAKE_CHARA_LIST("主人公以外的角色列表", L_ID, CHARA_LIST, CHARA_CNT)
SIF !CHARA_CNT
	RETURN -1
RETURN CHARA_LIST:(RAND:CHARA_CNT)



;----------------------------------------------------------------------------------------------------
;角色の登場判定
;とりあえず你は弾くようにした
;----------------------------------------------------------------------------------------------------
@CAN_MEET(CHARA)
#FUNCTION
#DIM CHARA
SIF !CHARA
	RETURNF 0
SIF !EXISTCSV(CHARA)
	RETURNF 0
SIF CFLAG:CHARA:出禁
	RETURNF 0
SIF CHARA == FLAG:扮演
	RETURNF 0
RETURNF 1

;----------------------------------------------------------------------------------------------------
;現在地点から移動可能な場所へ移動
;部屋から追い出されるときなど
;----------------------------------------------------------------------------------------------------
@GETOUT(ARG)
TCVAR:ARG:追い出され時刻 = TIME
TCVAR:ARG:追い出され地点 = CFLAG:ARG:現在位置
FOR LOCAL, MINROOM(), MAXROOM
	;全くもってスマートでないが、番号の関係上幽香私室からは露台に追い出されてしまうため個別処理する
	IF CFLAG:ARG:現在位置 == P463幽香私室
		CFLAG:ARG:現在位置 = P465大堂
		BREAK
	ENDIF
	IF CAN_MOVE(CFLAG:ARG:現在位置, LOCAL) & 1
		CFLAG:ARG:現在位置 = LOCAL
		BREAK
	ENDIF
NEXT


;----------------------------------------------------------------------------------------------------
;角色の随机選抜関数
;手抜き処理でできるだけ簡素な抽選関数をと思った（RAND_CHARASELECTはちと凝り過ぎ）
;渡した角色は選ばれない
;出禁角色が多すぎると無限ループのおそれがあるのが難点
;難点と是うが全角色対象の抽選では
;極端なプレイングをしなければ問題にはならない、はず
;----------------------------------------------------------------------------------------------------
@RAND_PERSON(CHARA)
#FUNCTION
#DIM CHARA
#DIM HIT_CHARA
DO
	HIT_CHARA = RAND(1, 人物数量上限)
	SIF !CAN_MEET(HIT_CHARA) || HIT_CHARA == CHARA || TALENT:CHARA:路人
		CONTINUE
	BREAK
LOOP 1
RETURNF HIT_CHARA



;----------------------------------------------------------------------------------------------------
;自宅位置取得関数
;----------------------------------------------------------------------------------------------------
@GET_CHARAHOME(CHARA)
#FUNCTION
#DIM CHARA
;MASTERは初期位置決まってる
SIF CHARA == MASTER
	RETURNF CFLAG:CHARA:初期位置
;住み込むフラグ立ってるなら決まってる
SIF CHARA == FLAG:住宿人物
	RETURNF CFLAG:CHARA:初期位置
;住所不定の角色は先に
SIF GROUPMATCH(CSVCFLAG(CHARA, GETNUM(CFLAG, "初期位置")), 0, 99)
	RETURNF -100
RETURNF CSVCFLAG(CHARA, GETNUM(CFLAG, "初期位置"))


;----------------------------------------------------------------------------------------------------
;場所名取得関数
;MAIN_MAP次第で挙動が変わってしまって個人的に気持ち悪い
;問題点：GET_MAP_REPLACEMENTを使用済みのPLACE_IDを受け取ると誤動作≒散步Mapの場所IDは受け取れません
;----------------------------------------------------------------------------------------------------
@GET_PLACENAME(PLACE_ID)
#FUNCTIONS
#DIM PLACE_ID
SIF PLACE_ID == SUKIMA()
	RETURNF "隙間空間"
SIF PLACE_ID == OMANEKIBEYA()
	RETURNF OMANEKI_PLACE()
IF GET_MAPID(PLACE_ID) == MAIN_MAP
	RETURNF NAME_FROM_PLACE(PLACE_ID)
ELSE
	RETURNF STR:(6000 + GET_MAP_REPLACEMENT(PLACE_ID) / 10)
ENDIF

;----------------------------------------------------------------------------------------------------
;配列抽選F関数
;----------------------------------------------------------------------------------------------------
@ARRAY_HIT(HIT_LIST, LIST_SIZE)
#FUNCTION
#DIM REF HIT_LIST ;確率分布列表
#DIM     LIST_SIZE;確率分布列表の尺寸
#DIM     CHK_ID   ;判定対象となる要素
#DIM     HIT_RAND ;抽選乱数処理
LOCAL = MAX(SUMARRAY(HIT_LIST, 0, LIST_SIZE),1)
HIT_RAND = RAND(0, LOCAL)
;DEBUGPRINTFORML LIST_SIZE＝{LIST_SIZE}　HIT_RAND{HIT_RAND}
FOR CHK_ID, 0, LIST_SIZE
	SIF HIT_RAND < SUMARRAY(HIT_LIST, 0, CHK_ID + 1)
		RETURNF CHK_ID
NEXT
;THROW 抽選失敗
;Counter抽選失敗によるTHROW落ちが発生してるのでｰ1を返すことにする
RETURNF -1

;----------------------------------------------------------------------------------------------------
;配列RAND化関数
;重複制限無なので適宜FISHER_YATES_SHAFFLEと使い分けて下さい
;----------------------------------------------------------------------------------------------------
@ARRAY_RAND(RAND_ARRAY, ARRAY_SIZE, RAND_MIN, RAND_MAX)
#DIM REF RAND_ARRAY; 対象配列
#DIM     ARRAY_SIZE; 配列尺寸（1以上）
#DIM     RAND_MIN  ; RANDの下限（0以上）
#DIM     RAND_MAX  ; RANDの上限-1、この数自体は抽選に選ばれない（RAND_MIN+1以上）
#DIM     ID_ARRAY  ; ループ用
FOR ID_ARRAY, 0, ARRAY_SIZE
	RAND_ARRAY:ID_ARRAY = RAND(RAND_MIN, RAND_MAX)
NEXT


;----------------------------------------------------------------------------------------------------
;FISHER_YATES_SHAFFLE（F関数版）
;ソースはwikipedia
;CALLF FUNC_FISHER_YATES_SHAFFLE(LOCAL, 10)
;これでLOCAL:0～9に0～9が重複無で代入される
;----------------------------------------------------------------------------------------------------
@FUNC_FISHER_YATES_SHAFFLE(ARRAY, ARRAY_SIZE)
#FUNCTION
#DIM REF ARRAY   ;対象配列
#DIM ARRAY_SIZE  ;配列の長さ（2以上の数値にして下さい）
#DIM ELE_SHAFFLE ;シャッフル対象要素
VARSET ARRAY, -1
ARRAY:0 = 0
;要素0に対する↓の処理は必ず0になるんだから1以降でおｋ
FOR LOCAL:0, 1, ARRAY_SIZE
	ELE_SHAFFLE = RAND(0, LOCAL:0 + 1)
	;挙動変わらないようならif設けるより素通しの方が処理数少なくなったりせんか？
	;SIF ELE_SHAFFLE != LOCAL:0
	ARRAY:(LOCAL:0) = ARRAY:ELE_SHAFFLE
	ARRAY:ELE_SHAFFLE = LOCAL:0
NEXT


;----------------------------------------------------------------------------------------------------
;複数行自動表示関数
;一種の超簡易アニメーション
;1秒に1行追記していくよ関数だよ
;渡された配列の要素を順番に表示する
;本格的に動かしたいならぱにめーしょん使え
;使用箇所は納茲琳の灵摆查找指令
;----------------------------------------------------------------------------------------------------
@PRINT_GROUP(COMMENT, NUM_COMMENT, TIME_REDRAW = 1000)
#DIMS REF COMMENT         ;表示する文字列の配列
#DIM  NUM_COMMENT         ;配列の要素数
#DIM  PREV_REDRAW         ;一時退避用
#DIM  TIME_REDRAW         ;行追加の間隔、標準で1秒
#DIM  DYNAMIC NOW_COMMENT ;表示隠蔽制御用
PREV_REDRAW = CURRENTREDRAW()
REDRAW 0
FOR LOCAL:0, 0, NUM_COMMENT
	FOR LOCAL:1, 0, NUM_COMMENT
		IF LOCAL:1 <= NOW_COMMENT
			PRINTFORML %COMMENT:(LOCAL:1)%
		ELSE
			PRINTFORML
		ENDIF
	NEXT
	TWAIT TIME_REDRAW, 0
	NOW_COMMENT ++
	SIF LOCAL:0 < NUM_COMMENT -1
		CLEARLINE NUM_COMMENT
NEXT
REDRAW PREV_REDRAW
WAIT


;----------------------------------------------------------------------------------------------------
;ARG(DAY)を年月日に変換する関数
;----------------------------------------------------------------------------------------------------
@DAY_TO_DATE(ARG)
#FUNCTION
VARSET LOCAL
;冬之月31日は例外
SIF ARG % 124 == 0
	LOCAL:5 = 1
;年目
LOCAL:1 = ARG / 124
SIF !LOCAL:5
	LOCAL:1 += 1
;月
IF LOCAL:5
	LOCAL:2 = 4
ELSEIF ARG % 124 % 31 == 0
	LOCAL:2 = ARG % 124 / 31
ELSE
	LOCAL:2 = ARG % 124 / 31 + 1
ENDIF
;日
IF LOCAL:5 || ARG % 124 % 31 == 0
	LOCAL:3 = 31
ELSE
	LOCAL:3 = ARG % 124 % 31
ENDIF
LOCAL = LOCAL:1 * 1000 + LOCAL:2 * 100 + LOCAL:3
RETURNF LOCAL

;----------------------------------------------------------------------------------------------------
;ARG(年月日)をDAYに変換する関数
;----------------------------------------------------------------------------------------------------
@DATE_TO_DAY(ARG)
#FUNCTION
VARSET LOCAL
LOCAL += (ARG / 1000 - 1) * 124
LOCAL += (ARG % 1000 / 100 - 1) * 31
LOCAL += ARG % 1000 % 100
RETURNF LOCAL

;----------------------------------------------------------------------------------------------------
;ARG(DAY)を年月日（曜日）で表示する関数
;ARGが年月日であるならARG:1を真にすること
;----------------------------------------------------------------------------------------------------
@PRINT_DATE(ARG, ARG:1)
;ARGをDAYと判定
IF !ARG:1
	LOCAL:1 = DAY_TO_DATE(ARG)
	LOCAL:2 = ARG
;ARGを年月日と判定
ELSE
	LOCAL:1 = ARG
	LOCAL:2 = DATE_TO_DAY(ARG)
ENDIF
LOCAL:3 = LOCAL:1 % 1000 / 100
PRINTFORM 第{LOCAL:1 / 1000}年 %GET_MONTH(LOCAL:3)% {LOCAL:1 % 1000 % 100}日（%GET_DAY(LOCAL:2)%）

@PRINT_DATE_F(ARG)
#FUNCTIONS
LOCAL = DAY_TO_DATE(ARG)
LOCAL:1 = LOCAL / 1000
LOCAL:2 = LOCAL % 1000 / 100
LOCAL:3 = LOCAL % 1000 % 100
LOCALS = 第{LOCAL:1}年 %GET_MONTH(LOCAL:2)%第{LOCAL:3}日
RETURNF LOCALS

;----------------------------------------------------------------------------------------------------
;複数文字列カウントF関数
;----------------------------------------------------------------------------------------------------
;TARGET_STRに含まれる複数の文字列の数を検索する
;TARGET_STR、KEY_STRともに"/"を仕切りとして複数の単語をカウントする
;TARGET_STR、KEY_STRで一度に渡す単語の種類はそれぞれ100種類未満、語群は1000個未満にすること
;
;動作例）
;TARGET_STR："うさぎ/うさぎ/うさぎ/きつね/ねこ/ねこ/"
;例１）
;　KEY_STR："うさぎ/ねこ/"
;　"うさぎ"が3、"ねこ"が2で合計値の5を返す
;例２）
;　KEY_STR："きつね/ねこ/"
;　"きつね"が1、"ねこ"が2で合計値の3を返す
;例３）
;　KEY_STR："うさぎ/うさぎ/"
;　"うさぎ"が3、の処理を2回分やって6を返す
;----------------------------------------------------------------------------------------------------
@STR_MULTI_COUNT(TARGET_STR, KEY_STR)
#FUNCTION
#DIMS TARGET_STR             ;検索対象文字列
#DIMS KEY_STR                ;検索したい語群、語群の種類の上限は100種類
#DIMS DYNAMIC KEY_LIST, 100  ;分割後の単語列表
#DIM  DYNAMIC KEY_CNT,  100  ;各単語の出現回数
#DIM  DYNAMIC KEY_NUM        ;単語の総数
#DIM          KEY_ID         ;ループ処理用
#DIM  DYNAMIC STR_CNT        ;カウント個数
#DIMS DYNAMIC TEMP_STR, 1000 ;SPLIT用一次配列
#DIM  DYNAMIC TEMP_NUM       ;一次配列で使用した要素数
#DIM          TEMP_RESULT    ;RESULT退避
;念のため
TARGET_STR '= REPLACE(TARGET_STR, "\/+", "/")
CALLF CNT_SPLIT(KEY_STR, KEY_LIST, KEY_CNT, KEY_NUM)

TEMP_RESULT = RESULT
SPLIT TARGET_STR, "/", TEMP_STR
TEMP_NUM = RESULT
FOR KEY_ID, 0, KEY_NUM
	STR_CNT += MATCH(TEMP_STR, KEY_LIST:KEY_ID, 0, TEMP_NUM) * KEY_CNT:KEY_ID
NEXT
RESULT = TEMP_RESULT
RETURNF STR_CNT


;----------------------------------------------------------------------------------------------------
;カウント機能つきSPLIT関数
;----------------------------------------------------------------------------------------------------
;・RESULTを汚染せず、F関数中でも使用可能
;・CALLFで使用のこと、REFを使用して変数を直接書き換える
;・引数必須、単語列表/出現個数列表の配列上限に注意！
;・区切り文字は"/"で固定
;・STR用の配列変数と別に、各単語の出現回数列表を追加
;・戻り値１）STR_LIST、"/"で分割され出力される単語列表
;・戻り値２）CNT_LIST、各単語の出現回数列表
;・戻り値３）STR_NUM、単語の種類の総数
;・TARGET_STRで一度に渡す単語の種類は100種類未満、語群は1000個未満にすること
;動作例)
;TARGET_STR："霊夢/霊夢/霊夢/霊夢/魔理沙/魔理沙/早苗/"
;　STR_LIST:0 == "霊夢"
;　STR_LIST:1 == "魔理沙"
;　STR_LIST:2 == "早苗"
;　CNT_LIST:0 == 4   ;"霊夢"の出現回数は4回
;　CNT_LIST:1 == 2   ;"魔理沙"の出現回数は2回
;　CNT_LIST:2 == 1   ;"早苗"の出現回数は1回
;　STR_NUM    == 3   ;"霊夢"、"魔理沙"、"早苗"で合計3種類
;----------------------------------------------------------------------------------------------------
@CNT_SPLIT(TARGET_STR, STR_LIST, CNT_LIST, STR_NUM)
#FUNCTION
#DIMS         TARGET_STR     ;分割対象STR
#DIMS         NEW_STR        ;更新用一時STR
#DIMS REF     STR_LIST       ;単語列表
#DIM  REF     CNT_LIST       ;各単語の出現個数列表
#DIM  REF     STR_NUM        ;単語の種類の総数
#DIMS DYNAMIC TEMP_STR, 1000 ;SPLIT用一次配列
#DIM  DYNAMIC TEMP_NUM       ;一次配列で使用した要素数
#DIM          TEMP_RESULT    ;RESULT退避
#DIM          TEMP_ID        ;ループ用
VARSET STR_LIST
VARSET CNT_LIST
STR_NUM = 0

TEMP_RESULT = RESULT
SPLIT TARGET_STR, "/", TEMP_STR
TEMP_NUM = RESULT
FOR TEMP_ID, 0, TEMP_NUM
	;文字長判定
	SIF !STRLENS(TEMP_STR:TEMP_ID)
		CONTINUE
	;重複判定
	SIF FINDELEMENT(STR_LIST, TEMP_STR:TEMP_ID, 0, STR_NUM, 1) != -1
		CONTINUE
	STR_LIST:STR_NUM '= TEMP_STR:TEMP_ID
	CNT_LIST:STR_NUM  = MATCH(TEMP_STR, TEMP_STR:TEMP_ID)
	STR_NUM ++
NEXT
RESULT = TEMP_RESULT

;----------------------------------------------------------------------------------------------------
;同行中の人数を返す関数
;----------------------------------------------------------------------------------------------------
@同行人数()
#FUNCTION
RETURNF (CHARANUM - 1) - CMATCH(CFLAG:同行 , 0, 1, CHARANUM)
;MASTERが数に入らないのでCHARANUM - 1です

;----------------------------------------------------------------------------------------------------
;DAILY_EV0から切り出し
;主にMAPのYOGORE関連判定
;懸念事項：名無し地点のYOGOREの扱いってどうなってんだろ？
;----------------------------------------------------------------------------------------------------
;拠点Map的污垢の合計値を返す
@SUM_ALL_YOGORE()
#FUNCTION
RETURNF SUMARRAY(YOGORE, MINROOM(), MAXROOM)

;拠点Map的污垢の平均値を返す
@AVERAGE_YOGORE()
#FUNCTION
RETURNF SUM_ALL_YOGORE() / ROOM_NUM()

;拠点Mapに存在する地点の数を返す
;名無し地点を考慮してない点に注意
@ROOM_NUM()
#FUNCTION
RETURNF MAXROOM % 100


;----------------------------------------------------------------------------------------------------
;メッセージ付きで道具/円/筹码を入手したり支払ったりする関数
;とりあえず正の数だけ
;----------------------------------------------------------------------------------------------------
@GET_ITEM(ITEM_NAME, ITEM_NUM, PRINT_TYPE)
#DIMS ITEM_NAME
#DIM ITEM_ID
#DIM ITEM_NUM
#DIM PRINT_TYPE;表示形式　0=通常　1=L　2=W

ITEM_ID = GETNUM(ITEM, ITEM_NAME)
IF ITEM_ID == -1
	PRINTFORML %ITEM_NAME%不存在
ENDIF
DEBUGPRINTFORML %ITEM_NAME%を取得、IDは{ITEM_ID}
;魔改物品上限
IF ITEM:ITEM_ID + ITEM_NUM >= 1000
	CALL COLORMESSAGE(@"%ITEMNAME:ITEM_ID%已经拿不下了", C_YELLOW, PRINT_TYPE)
	ITEM:ITEM_ID = MAX(ITEM:ITEM_ID, 999)
ELSE
	CALL COLORMESSAGE(@"得到了{ITEM_NUM}个%ITEMNAME:ITEM_ID%", C_YELLOW, PRINT_TYPE)
	ITEM:ITEM_ID += ITEM_NUM
ENDIF

@LOST_ITEM(ITEM_NAME, ITEM_NUM, PRINT_TYPE)
#DIMS ITEM_NAME
#DIM ITEM_ID
#DIM ITEM_NUM
#DIM PRINT_TYPE;表示形式　0=通常　1=L　2=W

ITEM_ID = GETNUM(ITEM, ITEM_NAME)

SIF ITEM:ITEM_ID <= 0
	RETURN
CALL COLORMESSAGE(@"失去了{ITEM_NUM}個%ITEMNAME:ITEM_ID%", C_YELLOW, PRINT_TYPE)
ITEM:ITEM_ID = MAX(ITEM:ITEM_ID - ITEM_NUM, 0)

@GET_MONEY_YEN(VALUE, PRINT_TYPE)
#DIM VALUE
#DIM PRINT_TYPE;表示形式　0=通常　1=L　2=W
SIF VALUE <= 0
	RETURN
CALL COLORMESSAGE(@"得到了\\{VALUE}", C_YELLOW, PRINT_TYPE)
MONEY += VALUE

@GET_SPELL_CARD_BONUS_YEN(VALUE, PRINT_TYPE)
#DIM VALUE
#DIM PRINT_TYPE;表示形式　0=通常　1=L　2=W
SIF VALUE <= 0
	RETURN
CALL COLORMESSAGE(@"GET ", 0xFFFFFF, 0)
CALL COLORMESSAGE(@"SpEll ", 0xC8FFFF, 0)
CALL COLORMESSAGE(@"CARd ", 0x64FFDC, 0)
CALL COLORMESSAGE(@"BONUS!!", 0x00FFB4, 1)
CALL COLORMESSAGE(@"        \\{VALUE}", C_YELLOW, PRINT_TYPE)
MONEY += VALUE

@GET_MONEY_CM(VALUE, PRINT_TYPE)
#DIM VALUE
#DIM PRINT_TYPE;表示形式　0=通常　1=L　2=W
SIF VALUE <= 0
	RETURN
CALL COLORMESSAGE(@"得到了{VALUE}個筹码", C_YELLOW, PRINT_TYPE)
MONEY:2 += VALUE

@LOST_MONEY_YEN(VALUE, PRINT_TYPE)
#DIM VALUE
#DIM PRINT_TYPE;表示形式　0=通常　1=L　2=W
SIF VALUE <= 0
	RETURN
CALL COLORMESSAGE(@"失去了\\{VALUE}", C_YELLOW, PRINT_TYPE)
MONEY = MAX(0, MONEY - VALUE)

@LOST_MONEY_CM(VALUE, PRINT_TYPE)
#DIM VALUE
#DIM PRINT_TYPE;表示形式　0=通常　1=L　2=W
SIF VALUE <= 0
	RETURN
CALL COLORMESSAGE(@"失去了{VALUE}筹码", C_YELLOW, PRINT_TYPE)
MONEY:2 = MAX(0, MONEY:2 - VALUE)

@PAY_MONEY_YEN(VALUE, PRINT_TYPE)
#DIM VALUE
#DIM PRINT_TYPE;表示形式　0=通常　1=L　2=W
SIF VALUE <= 0
	RETURN
CALL COLORMESSAGE(@"支付了\\{VALUE}", C_YELLOW, PRINT_TYPE)
MONEY = MAX(0, MONEY - VALUE)

@PAY_MONEY_CM(VALUE, PRINT_TYPE)
#DIM VALUE
#DIM PRINT_TYPE;表示形式　0=通常　1=L　2=W
SIF VALUE <= 0
	RETURN
CALL COLORMESSAGE(@"支付了{VALUE}筹码", C_YELLOW, PRINT_TYPE)
MONEY:2 = MAX(0, MONEY:2 - VALUE)

;角色番号C_IDのABLをAvalue変化させる
@ChangeABL(C_ID, Aname, Avalue)
#DIM C_ID
#DIM A_ID
#DIMS Aname
#DIM Avalue

A_ID = FINDELEMENT(ABLNAME, Aname)
ABL:C_ID:A_ID += Avalue
CALL COLORMESSAGE(@"%CALLNAME:C_ID%的%Aname%\@ Avalue > 0 ?上升 # 下降 \@至了{ABL:C_ID:A_ID}",C_YELLOW,2)
;----------------------------------------------------------------------------------------------
; Author: えにゅあら
; Licensed by: CC0
; L_SEEDの値が同じなら同じ結果を返す疑似乱数（ノイズ関数）
; 0～L_SCALEの数値を返す（L_SCALEは含まない）
; L_SCALEの最大値は多分8589934592　計算の都合上これ以上を指定しても切り捨てられるはず
;----------------------------------------------------------------------------------------------
@NOISE(L_SEED, L_SCALE)
#FUNCTION
#DIM L_SEED
#DIM L_VAL
#DIM L_SCALE
; 随意googleで随机生成した数値をシードの初期位置にした。これやらないと0付近に全然随机感が出ない気がする
L_SEED = L_SEED ^ 0x145579EC
L_VAL = (L_SEED >> 13) ^ L_SEED
L_VAL = (L_VAL * (L_VAL * L_VAL * 60493 + 19990303) + 1376312589) & 0x7fffffff
RETURNF L_VAL * L_SCALE / 0x80000000


; ノイズを二つ加算したものを返す。平均値付近の値が出やすい。出現分布はきれいな三角形。
@NOISE2(L_SEED, L_SCALE)
#FUNCTION
#DIM L_SCALE
#DIM L_SCALE2
#DIM L_SEED
L_SCALE2 = L_SCALE / 2
L_SCALE = L_SCALE - L_SCALE2
RETURNF NOISE(L_SEED, L_SCALE) + NOISE(L_SEED ^ 0x1D47E2A3, L_SCALE2)


; ノイズを三つ加算したものを返す。平均値付近の値がとても出やすい。出現分布は山なりの曲線(正規分布)。
@NOISE3(L_SEED, L_SCALE)
#FUNCTION
#DIM L_SCALE
#DIM L_SCALE2
#DIM L_SEED
L_SCALE2 = L_SCALE / 3
L_SCALE = L_SCALE - L_SCALE2*2
RETURNF NOISE(L_SEED, L_SCALE) + NOISE(L_SEED ^ 0xFA7B7E26, L_SCALE2) + NOISE(L_SEED ^ 0x1D47E2A3, L_SCALE2)

;４つ以上加算しても３つ版と分布の形状は変わらず、単に平均値を中心に縮小されたものになるので、省く



; 使えそうか試しに表示してみる
@TEST_PRINTNOISE()
#FUNCTION
PRINTL NOISE1
FOR LOCAL, 0, 100
	PRINTFORML {NOISE(LOCAL, 100)}
NEXT
PRINTL NOISE2
FOR LOCAL, 0, 100
	PRINTFORML {NOISE2(LOCAL, 100)}
NEXT
PRINTL NOISE3
FOR LOCAL, 0, 100
	PRINTFORML {NOISE3(LOCAL, 100)}
NEXT

;お金の単位付きで3桁ごとにカンマ表示（関数名は仮）
@金額表示(ARG)
#FUNCTIONS
RETURNF MONEYSTR(ARG, "#,##0")

@三桁区切(ARG)
#FUNCTIONS
RETURNF TOSTR(ARG, "#,##0")

@数え方(ITEM_NO, ITEM_NUM = 1)
#FUNCTIONS
#DIM ITEM_NO
#DIM ITEM_NUM
SELECTCASE ITEM_NO
	CASE 627
		RETURNF "瓶"
	CASE 706
		RETURNF "匹"
	CASE 707, 708
		RETURNF "尾"
	CASEELSE
		RETURNF \@ ITEM_NUM < 10 ? つ # 個 \@
ENDSELECT

@IS_LOVER(ARG)
#FUNCTION
SIF TALENT:ARG:恋慕
	RETURNF 1
SIF TALENT:ARG:愛欲
	RETURNF 1
RETURNF 0

@CHARASORT_EX
VARSET RESULT, -1
RESULT:0 = MASTER
RESULT:1 = [[霊夢]]
RESULT:2 = [[魔理沙]]
RESULT:3 = [[露米亜]]
RESULT:4 = [[大妖精]]
RESULT:5 = [[琪露諾]]
RESULT:6 = [[美鈴]]
RESULT:7 = [[小悪魔]]
RESULT:8 = [[帕秋莉]]
RESULT:9 = [[咲夜]]
RESULT:10 = [[蕾米莉亜]]
RESULT:11 = [[芙兰]]
RESULT:12 = [[蕾蒂]]
RESULT:13 = [[橙]]
RESULT:14 = [[愛麗絲]]
RESULT:15 = [[莉莉Ｗ]]
RESULT:16 = [[莉莉Ｂ]]
RESULT:17 = [[莉莉喀]]
RESULT:18 = [[梅露蘭]]
RESULT:19 = [[露娜薩]]
RESULT:20 = [[妖夢]]
RESULT:21 = [[幽幽子]]
RESULT:22 = [[藍]]
RESULT:23 = [[紫]]
RESULT:24 = [[萃香]]
RESULT:25 = [[莉格露]]
RESULT:26 = [[米斯蒂亜]]
RESULT:27 = [[慧音]]
RESULT:28 = [[帝]]
RESULT:29 = [[鈴仙]]
RESULT:30 = [[永琳]]
RESULT:31 = [[輝夜]]
RESULT:32 = [[妹紅]]
RESULT:33 = [[文]]
RESULT:34 = [[梅蒂欣]]
RESULT:35 = [[幽香]]
RESULT:36 = [[小町]]
RESULT:37 = [[映姫]]
RESULT:38 = [[静葉]]
RESULT:39 = [[穣子]]
RESULT:40 = [[雛]]
RESULT:41 = [[荷取]]
RESULT:42 = [[椛]]
RESULT:43 = [[早苗]]
RESULT:44 = [[神奈子]]
RESULT:45 = [[諏訪子]]
RESULT:46 = [[桑尼]]
RESULT:47 = [[露娜]]
RESULT:48 = [[斯塔]]
RESULT:49 = [[阿求]]
RESULT:50 = [[蓮子]]
RESULT:51 = [[梅莉]]
RESULT:52 = [[衣玖]]
RESULT:53 = [[天子]]
RESULT:54 = [[豊姫]]
RESULT:55 = [[依姫]]
RESULT:56 = [[鈴仙二號]]
RESULT:57 = [[琪斯美]]
RESULT:58 = [[山女]]
RESULT:59 = [[帕露西]]
RESULT:60 = [[勇儀]]
RESULT:61 = [[覚]]
RESULT:62 = [[阿燐]]
RESULT:63 = [[阿空]]
RESULT:64 = [[恋]]
RESULT:65 = [[朱鷺子]]
RESULT:66 = [[納茲琳]]
RESULT:67 = [[小傘]]
RESULT:68 = [[一輪]]
RESULT:69 = [[村紗]]
RESULT:70 = [[星]]
RESULT:71 = [[白蓮]]
RESULT:72 = [[鵺]]
RESULT:73 = [[極]]
RESULT:74 = [[華扇]]
RESULT:75 = [[響子]]
RESULT:76 = [[芳香]]
RESULT:77 = [[青娥]]
RESULT:78 = [[屠自古]]
RESULT:79 = [[布都]]
RESULT:80 = [[神子]]
RESULT:81 = [[猯藏]]
RESULT:82 = [[小鈴]]
RESULT:83 = [[心]]
RESULT:84 = [[若鷺姫]]
RESULT:85 = [[蛮奇]]
RESULT:86 = [[影狼]]
RESULT:87 = [[弁弁]]
RESULT:88 = [[八橋]]
RESULT:89 = [[正邪]]
RESULT:90 = [[針妙丸]]
RESULT:91 = [[雷鼓]]
RESULT:92 = [[菫子]]
RESULT:93 = [[清蘭]]
RESULT:94 = [[鈴瑚]]
RESULT:95 = [[哆来咪]]
RESULT:96 = [[探女]]
RESULT:97 = [[克勞恩皮絲]]
RESULT:98 = [[純狐]]
RESULT:99 = [[赫喀提亞]]
RESULT:100 = [[拉瓦爾]]
RESULT:101 = [[合歡乃]]
RESULT:102 = [[阿吽]]
RESULT:103 = [[成美]]
RESULT:104 = [[舞]]
RESULT:105 = [[里乃]]
RESULT:106 = [[隠岐奈]]
RESULT:107 = [[女苑]]
RESULT:108 = [[紫苑]]
RESULT:109 = [[瓔花]]
RESULT:110 = [[潤美]]
RESULT:111 = [[久侘歌]]
RESULT:112 = [[八千慧]]
RESULT:113 = [[磨弓]]
RESULT:114 = [[袿姫]]
RESULT:115 = [[早鬼]]
RESULT:116 = [[美宵]]
RESULT:117 = [[三花]]
RESULT:118 = [[高岭]]
RESULT:119 = [[駒草]]
RESULT:120 = [[魅須丸]]
RESULT:121 = [[典]]
RESULT:122 = [[龍]]
RESULT:123 = [[千亦]]
RESULT:124 = [[百百世]]
RESULT:125 = [[尤魔]]
RESULT:126 = [[影華扇]]
RESULT:127 = [[美天]]
RESULT:128 = [[慧ノ子]]
RESULT:129 = [[ちやり]]
RESULT:130 = [[日狭美]]
RESULT:131 = [[残無]]

RESULT:201 = [[マガン]]
RESULT:202 = [[エリス]]
RESULT:203 = [[サリエル]]
RESULT:204 = [[キクリ]]
RESULT:205 = [[コンガラ]]
RESULT:206 = [[理香子]]
RESULT:207 = [[明羅]]
RESULT:208 = [[魅魔]]
RESULT:209 = [[艾倫]]
RESULT:210 = [[小兎姫]]
RESULT:211 = [[喀娜]]
RESULT:212 = [[理香子]]
RESULT:213 = [[千百合]]
RESULT:214 = [[夢美]]
RESULT:215 = [[留琴]]
RESULT:216 = [[オレンジ]]
RESULT:217 = [[胡桃]]
RESULT:218 = [[艾麗]]
RESULT:219 = [[夢月]]
RESULT:220 = [[幻月]]
RESULT:221 = [[サラ]]
RESULT:222 = [[露易茲]]
RESULT:223 = [[雪]]
RESULT:224 = [[​舞​]]
RESULT:225 = [[夢子]]
RESULT:226 = [[神綺]]

RESULT:301 = [[先代]]

RESULT:399 = [[路人子]]
