;
;スロットのメインプログラム
;


@CASINO_SLOT
;リール配列
;	左  	中  	右 
;1	REP 	MEL 	BEL
;2	BL7 	BL7 	BL7
;3	CHE 	REP 	MEL
;4	MEL 	BEL 	CHE
;5	BEL 	CHE 	REP
;6	REP 	CHE 	BEL
;7	CHE 	BAR 	RD7
;8	BAR 	REP 	MEL
;9	MEL 	BEL 	BAR
;10	BEL 	CHE 	REP
;11	REP 	RD7 	BEL
;12	BAR 	CHE 	BEL
;13	CHE 	REP 	MEL
;14	MEL 	BEL 	CHE
;15	BEL 	BAR 	REP
;16	REP 	MEL 	BEL
;17	RD7 	CHE 	BAR
;18	CHE 	REP 	MEL
;19	MEL 	BEL 	CHE
;20	BEL 	CHE 	REP

;絵柄一覧
;1 RD7 赤7(BOUNS図柄)
;2 BL7 青7(BOUNS図柄)
;3 BEL ベル
;4 CHE チェリー
;5 MEL 西瓜
;6 BAR BAR(BOUNS図柄)
;7 REP 回放
;8 NUL 未選択

;=============================
;変数定義
;=============================
#DIM 初期所持筹码
#DIM 総回転数
#DIM 上次当选的旋转数
#DIM 短周期連開始時所持筹码
#DIM 閉店までに可能な回転数

#DIM オートモード
#DIM 死机回数
#DIM CONST 検定回数 = 100000

#DIM 当選履歴, 11
#DIM 当選種類履歴, 11
#DIM 当選枚数履歴, 11

#DIM 小役カウンタ, 100

#DIM 情報パネル表示页

#DIM 営業開始時刻
#DIM 営業終了時刻

;一回転分の掛け金は3筹码
#DIM CONST 掛け金 = 3

;各絵柄の略称(B_A_Rのみ5文字なのに注意!!)
#DIM CONST RD7 = 1
#DIM CONST BL7 = 2
#DIM CONST BEL = 3
#DIM CONST CHE = 4
#DIM CONST MEL = 5
#DIM CONST B_A_R = 6
#DIM CONST REP = 7
#DIM CONST NUL = 8

;当選小役の種類
#DIM CONST 当選小役_赤7BOUNS = 0
#DIM CONST 当選小役_青7BOUNS = 1
#DIM CONST 当選小役_レギュラーBOUNS = 2
#DIM CONST 当選小役_中樱桃 = 3
#DIM CONST 当選小役_大樱桃 = 4
#DIM CONST 当選小役_小樱桃 = 5
#DIM CONST 当選小役_強西瓜 = 6
#DIM CONST 当選小役_弱西瓜 = 7
#DIM CONST 当選小役_ベル = 8
#DIM CONST 当選小役_回放 = 9
#DIM CONST 当選小役_落空了 = 99
;枠の色
#DIM 色_枠
#DIM CONST 削除行数 = 51

#DIM 絵柄_左上
#DIM 絵柄_左中
#DIM 絵柄_左下
#DIM 絵柄_中上
#DIM 絵柄_中中
#DIM 絵柄_中下
#DIM 絵柄_右上
#DIM 絵柄_右中
#DIM 絵柄_右下
#DIM 暗転_左上
#DIM 暗転_左中
#DIM 暗転_左下
#DIM 暗転_中上
#DIM 暗転_中中
#DIM 暗転_中下
#DIM 暗転_右上
#DIM 暗転_右中
#DIM 暗転_右下

;NULがDIM中で使用できないので直打ち
#DIM 絵柄_左上_回転中
#DIM 絵柄_左中_回転中
#DIM 絵柄_左下_回転中
#DIM 絵柄_中上_回転中
#DIM 絵柄_中中_回転中
#DIM 絵柄_中下_回転中
#DIM 絵柄_右上_回転中
#DIM 絵柄_右中_回転中
#DIM 絵柄_右下_回転中
#DIM 暗転_左上_回転中
#DIM 暗転_左中_回転中
#DIM 暗転_左下_回転中
#DIM 暗転_中上_回転中
#DIM 暗転_中中_回転中
#DIM 暗転_中下_回転中
#DIM 暗転_右上_回転中
#DIM 暗転_右中_回転中
#DIM 暗転_右下_回転中

#DIM 設定
#DIM 内部状態
#DIM 短周期連継続率
#DIM 短周期連セット数
#DIM 累計短周期連セット数
#DIM 前兆
#DIM 当選的小役
#DIM 当選したBB種類
#DIM 小役抽選不会
#DIM 投入筹码
#DIM 嫌がらせボタン番号

;=============================
;各変数の初期化
;=============================
;のちのち状況に応じて枠の色を変える予定あり
色_枠 = 0xE6B422
初期所持筹码 = MONEY:2
短周期連開始時所持筹码 = 0
総回転数 = 0
上次当选的旋转数 = 0

;開店は07時
営業開始時刻 = 7 * 60
;閉店は22時
営業終了時刻 = 22 * 60
閉店までに可能な回転数 = 閉店まで回せる回転数(営業終了時刻)

オートモード = 0
死机回数 = 0

短周期連セット数 = 0
累計短周期連セット数 = 0

情報パネル表示页 = 0

;0回転当選が存在しないため0で初期化
VARSET 当選履歴, 0

VARSET 当選種類履歴, -1
VARSET 当選枚数履歴, 0

VARSET 小役カウンタ, 0

絵柄_左上 = REP
絵柄_左中 = RD7
絵柄_左下 = CHE
絵柄_中上 = CHE
絵柄_中中 = RD7
絵柄_中下 = CHE
絵柄_右上 = BEL
絵柄_右中 = RD7
絵柄_右下 = MEL
暗転_左上 = 0
暗転_左中 = 0
暗転_左下 = 0
暗転_中上 = 0
暗転_中中 = 0
暗転_中下 = 0
暗転_右上 = 0
暗転_右中 = 0
暗転_右下 = 0

絵柄_左上_回転中 = NUL
絵柄_左中_回転中 = NUL
絵柄_左下_回転中 = NUL
絵柄_中上_回転中 = NUL
絵柄_中中_回転中 = NUL
絵柄_中下_回転中 = NUL
絵柄_右上_回転中 = NUL
絵柄_右中_回転中 = NUL
絵柄_右下_回転中 = NUL
暗転_左上_回転中 = 0
暗転_左中_回転中 = 0
暗転_左下_回転中 = 0
暗転_中上_回転中 = 0
暗転_中中_回転中 = 0
暗転_中下_回転中 = 0
暗転_右上_回転中 = 0
暗転_右中_回転中 = 0
暗転_右下_回転中 = 0

;数字によって色んな期待値が変わるアレ(内部状態とは異なる)
;設定 = RAND(6) + 1
CALL 設定決定(設定)


;内部状態:機械内の状態を表す
;1:通常,2:高確(各フラグによる抽選が2回になる)
;3超高確:(各フラグによる抽選が3回になる),4:短周期連(天井３２Ｇ),5死机
内部状態 = 1

;短周期連継続率:内部状態が３２Ｇ連のときのループ継続率
;0～95の値が入る予定
短周期連継続率 = 0
;発生していないときは-1,発生した場合1ゲームごとに値を減らし0になると当選
前兆 = -1

;1が立っている場合小役抽選を行わない
;情報パネルの操作を行うとさっき成立した小役による各種抽選をまた行ってしまうためこのフラグを立てる
;無抽選モードとかにも悪用できそう
小役抽選不会 = 0

;当選的小役 
;0:斯喀雷特BB当選
;1:十六夜BB当選
;2:REG当選
;3:中段樱桃当選
;4:大樱桃当選
;5:小樱桃当選
;6:強西瓜当選
;7:弱西瓜当選
;8:ベル当選
;9:回放当選
;99:落空了
当選的小役 = 当選小役_落空了

;当選したBB種類　-1:当選してないよ,0:斯喀雷特BB当選，1:十六夜BB当選，2:REG当選
当選したBB種類 = -1

;=============================
;開店前の場合
;=============================
IF TIME < 営業開始時刻
	PRINTW 看来来得太早了
	PRINTW 柜台前立着『準備中』的牌子
	PRINTFORMW 『今天的营业时间是%時刻表示(営業開始時刻)% ~ %時刻表示(営業終了時刻)%、在開店前请再稍等一会儿』
	RETURN 0
ENDIF

;=============================
;開店後の場合
;=============================
IF TIME >= 営業終了時刻
	PRINTW …看来来的太晚了
	PRINTW 柜台前立着『準備中』的牌子
	PRINTFORMW 『今天的营业结束了、期待您的再次光临　明天%時刻表示(営業開始時刻)%開始营业』
	RETURN 0
ENDIF

;=============================
;初期文章表示
;=============================
PRINTW 这里是角落的老虎机。
PRINTW 请好好地享受。

$INPUT_LOOP
CALL SLOT_情報パネル表示(情報パネル表示页,総回転数,上次当选的旋转数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
DRAWLINE
;=================================
;PRINTL1行を使ってメッセージを挿入
;=================================
CALL 一言メッセージ(短周期連セット数,設定,前兆,当選したBB種類,内部状態,短周期連継続率,当選的小役,上次当选的旋转数,短周期連開始時所持筹码)

PRINTFORML 所持籌碼 {MONEY:2} 籌碼差枚 {MONEY:2-初期所持筹码}
PRINTFORML 回転数 {総回転数}G 距离上次的大奖{上次当选的旋转数}G
PRINTFORML [0] - 放棄遊戲
PRINTFORML [2] - 操作情报面板
PRINTFORML [1] - 玩老虎機

;=============================
;いろんなものの修正処理
;=============================
;当選していた場合前兆,当選したBB種類を-1にセットし直す
SIF 前兆 == 0
	前兆 = -1
SIF 当選したBB種類 > -1
	当選したBB種類 = -1

;死机した場合３２Ｇ連状態にセット
SIF 内部状態 == 5
	内部状態 = 4
;短周期連継続率が95％を超えた場合95％に戻す(レア小役引きすぎるとこうなる)
SIF 短周期連継続率 > 95
	短周期連継続率 = 95
	
;情報パネルの操作ボタンを押してここに回来了場合さっき引いた小役でもう一回抽選を行うため
;このフラグが立っていたら強制的に小役による抽選をスキップする(情報パネルの操作選択時にフラグは立つ)
IF 小役抽選不会
	小役抽選不会 = 0
	GOTO 抽選終わり
ENDIF
;=====================================
;今回当たっていた小役に対応した抽選
;=====================================
$小役抽選開始
;～～～～～～～～～～～～～～～～～～～～～～～
;３２Ｇ連最終ゲームの場合
;～～～～～～～～～～～～～～～～～～～～～～～
IF 内部状態 == 4 && 上次当选的旋转数 >= 31
	
	;成立したフラグによって変化
	SELECTCASE 当選的小役
	
	;3:中樱桃当選の場合(継続確定)
	CASE 当選小役_中樱桃
		
		;短周期連継続率変更抽選
		;短周期連継続率を95％に上書き
		短周期連継続率 = 95
		
		;継続確定抽選
		IF RAND(100) < 中段樱桃_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			前兆 = 0
			
			GOTO 抽選終わり
		ENDIF
	
	;4:大樱桃当選の場合
	CASE 当選小役_大樱桃
		
		;短周期連継続率変更抽選
		;短周期連継続率を増加
		短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 大樱桃_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			前兆 = 0
			GOTO 抽選終わり
		ENDIF
	
	;5:小樱桃当選の場合
	CASE 当選小役_小樱桃
		
		;短周期連継続率変更抽選
		;50％の確率で短周期連継続率を増加
		SIF RAND(100) < 50
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 小樱桃_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			前兆 = 0
			GOTO 抽選終わり
		ENDIF
	
	;6:強西瓜当選の場合
	CASE 当選小役_強西瓜
		
		;短周期連継続率変更抽選
		;短周期連継続率を増加
		短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 強西瓜_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			前兆 = 0
			GOTO 抽選終わり
		ENDIF
	
	;7:弱西瓜当選の場合
	CASE 当選小役_弱西瓜
		
		;短周期連継続率変更抽選
		;25％の確率で短周期連継続率を増加
		SIF RAND(100) < 25
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 弱西瓜_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			前兆 = 0
			GOTO 抽選終わり
		ENDIF
	
	;8:ベル当選の場合
	CASE 当選小役_ベル
		
		;短周期連継続率変更抽選(ベルでは行わない)
		
		
		;継続確定抽選
		IF RAND(1000) < ベル_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			前兆 = 0
			GOTO 抽選終わり
		ENDIF
	
	;9:回放当選の場合
	CASE 当選小役_回放
		
		;短周期連継続率変更抽選(回放では行わない)
		
		
		;継続確定抽選
		IF RAND(1000) < 回放_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			前兆 = 0
			GOTO 抽選終わり
		ENDIF
	
	;99:落空了当選の場合
	CASE 当選小役_落空了
		
		;短周期連継続率変更抽選
		;50％の確率で短周期連継続率を増加
		SIF RAND(100) < 50
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 落空了_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			前兆 = 0
			GOTO 抽選終わり
		ENDIF
	
	;其他(默认では存在しないはず)
	CASEELSE
		PRINTFORMW 存在不存在的小役当选了 小役番号:{当選的小役}
	ENDSELECT
	
	
	;======================================================================
	;短周期連(32Ｇ連)終了確定
	;ここ来了ということは短周期連終了は確定しているので内部状態振り分け
	;======================================================================
	;設定によって内部状態振り分けは異なる
	IF 前兆 == -1
		内部状態 = 短周期連終了時状態振り分け(設定)
		短周期連セット数 = 0
	ENDIF
	

;～～～～～～～～～～～～～～～～～～～～～～～
;３２Ｇ連中(最終ゲーム除く)の場合
;～～～～～～～～～～～～～～～～～～～～～～～
ELSEIF 内部状態 == 4
	
	;成立したフラグによって変化
	SELECTCASE 当選的小役
	
	;BOUNS当選時はBOUNS当選直後に継続抽選を行う
	;0:斯喀雷特BB当選の場合
	CASE 当選小役_赤7BOUNS
		短周期連セット数 ++
		SIF RAND(100) < 短周期連継続率
			前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数)
	
	;BOUNS当選時はBOUNS当選直後に継続抽選を行う
	;1:十六夜BB当選の場合
	CASE 当選小役_青7BOUNS
		短周期連セット数 ++
		SIF RAND(100) < 短周期連継続率
			前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数)
	
	;BOUNS当選時はBOUNS当選直後に継続抽選を行う
	;2:REG当選の場合
	CASE 当選小役_レギュラーBOUNS
		短周期連セット数 ++
		SIF RAND(100) < 短周期連継続率
			前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数)
	
	;レア小役
	;3:中樱桃当選の場合(継続確定)
	CASE 当選小役_中樱桃
		
		;短周期連継続率変更抽選
		;短周期連継続率を95％に上書き
		短周期連継続率 = 95
		
		;継続確定抽選
		IF RAND(100) < 中段樱桃_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数,前兆)
			ENDIF
			GOTO 抽選終わり
		ENDIF
	
	;4:大樱桃当選の場合
	CASE 当選小役_大樱桃
		
		;短周期連継続率変更抽選
		;40％の確率で短周期連継続率を増加
		SIF RAND(100) < 40
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 大樱桃_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数,前兆)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;5:小樱桃当選の場合
	CASE 当選小役_小樱桃
		
		;短周期連継続率変更抽選
		;20％の確率で短周期連継続率を増加
		SIF RAND(100) < 20
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 小樱桃_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数,前兆)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;6:強西瓜当選の場合
	CASE 当選小役_強西瓜
		
		;短周期連継続率変更抽選
		;25％の確率で短周期連継続率を増加
		SIF RAND(100) < 25
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 強西瓜_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数,前兆)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;7:弱西瓜当選の場合
	CASE 当選小役_弱西瓜
		
		;短周期連継続率変更抽選
		;10％の確率で短周期連継続率を増加
		SIF RAND(100) < 10
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 弱西瓜_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数,前兆)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;8:ベル当選の場合
	CASE 当選小役_ベル
		
		;短周期連継続率変更抽選(ベルでは行わない)
		
		
		;継続確定抽選
		IF RAND(1000) < ベル_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数,前兆)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;9:回放当選の場合
	CASE 当選小役_回放
		
		;短周期連継続率変更抽選(回放では行わない)
		
		
		;継続確定抽選
		IF RAND(1000) < 回放_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数,前兆)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;99:落空了当選の場合
	CASE 当選小役_落空了
		
		;短周期連継続率変更抽選
		;25％の確率で短周期連継続率を増加
		SIF RAND(100) < 25
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 落空了_32G連中(設定)
			;次ゲームBOUNS当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 上次当选的旋转数,前兆)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;其他(默认では存在しないはず)
	CASEELSE
		PRINTFORMW 存在不存在的小役当选了 小役番号:{当選的小役}
	ENDSELECT
	
;～～～～～～～～～～～～～～～～～～～～～～～
;通常,高確,超高確の場合
;～～～～～～～～～～～～～～～～～～～～～～～
ELSE
	;成立したフラグによって変化
	SELECTCASE 当選的小役
	
	;BOUNS当選時はBOUNS当選直後に短周期連突入抽選を行う
	;0:斯喀雷特BB当選の場合(1/2で短周期連突入)
	CASE 当選小役_赤7BOUNS
		IF !RAND(2)
			;内部状態を短周期連に
			内部状態 = 4
			;継続率の決定
			短周期連継続率 = 短周期連継続率設定(設定)
			;短周期連セット数の初期化(GOTOした後に1足されるのでここでは0)
			短周期連セット数 = 0
			短周期連開始時所持筹码 = MONEY:2
			SIF 内部状態 == 4
				累計短周期連セット数 ++
			;小役抽選開始地点に戻す
			GOTO 小役抽選開始
		ENDIF
	
	;BOUNS当選時はBOUNS当選直後に短周期連突入抽選を行う
	;1:十六夜BB当選の場合(1/3で短周期連突入)
	CASE 当選小役_青7BOUNS
		IF !RAND(3)
			;内部状態を短周期連に
			内部状態 = 4
			;継続率の決定
			短周期連継続率 = 短周期連継続率設定(設定)
			;短周期連セット数の初期化(GOTOした後に1足されるのでここでは0)
			短周期連セット数 = 0
			短周期連開始時所持筹码 = MONEY:2
			SIF 内部状態 == 4
				累計短周期連セット数 ++
			;小役抽選開始地点に戻す
			GOTO 小役抽選開始
		ENDIF
	
	;BOUNS当選時はBOUNS当選直後に短周期連突入抽選を行う
	;2:REG当選の場合(1/5で短周期連突入)
	CASE 当選小役_レギュラーBOUNS
		IF !RAND(5)
			;内部状態を短周期連に
			内部状態 = 4
			;継続率の決定
			短周期連継続率 = 短周期連継続率設定(設定)
			;短周期連セット数の初期化(GOTOした後に1足されるのでここでは0)
			短周期連セット数 = 0
			短周期連開始時所持筹码 = MONEY:2
			SIF 内部状態 == 4
				累計短周期連セット数 ++
			;小役抽選開始地点に戻す
			GOTO 小役抽選開始
		ENDIF
	
	;レア小役当選時はBOUNS当選抽選を行う
	;3:中樱桃当選の場合
	;中段樱桃の処理は特別なものになる
	;斯喀雷特BB確定＆継続率90％以上短周期連突入確定
	CASE 当選小役_中樱桃
		前兆 = 0
		GOTO 抽選終わり
	
	;レア小役当選時は内部状態抽選とBOUNS当選抽選を行う
	;4:大樱桃当選の場合
	CASE 当選小役_大樱桃
		
		;内部状態抽選
		;大樱桃は50％で内部状態が昇格する
		IF 内部状態 < 3
			SIF !RAND(2)
				内部状態 ++
		ENDIF
		
		;BOUNS当選抽選
		IF 中奖_非32G連(当選的小役,設定,内部状態)
			;次ゲームBOUNS当選
			;前兆中にBOUNS当選した場合前兆の短いほうが選ばれもう片方は捨てられる
			IF 前兆 != -1
				前兆 = MIN(前兆,前兆設定(内部状態))
			ELSE
				前兆 = 前兆設定(内部状態)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;レア小役当選時は内部状態抽選とBOUNS当選抽選を行う
	;5:小樱桃当選の場合
	CASE 当選小役_小樱桃
		
		;内部状態抽選
		;小樱桃は30％で内部状態が昇格する
		IF 内部状態 < 3
			SIF RAND(10) < 3
				内部状態 ++
		ENDIF
		
		;BOUNS当選抽選
		IF 中奖_非32G連(当選的小役,設定,内部状態)
			;次ゲームBOUNS当選
			;前兆中にBOUNS当選した場合前兆の短いほうが選ばれもう片方は捨てられる
			IF 前兆 != -1
				前兆 = MIN(前兆,前兆設定(内部状態))
			ELSE
				前兆 = 前兆設定(内部状態)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;レア小役当選時は内部状態抽選とBOUNS当選抽選を行う
	;6:強西瓜当選の場合
	CASE 当選小役_強西瓜
		
		;内部状態抽選
		;強西瓜は50％で内部状態が昇格する
		IF 内部状態 < 3
			SIF !RAND(2)
				内部状態 ++
		ENDIF
		
		;BOUNS当選抽選
		IF 中奖_非32G連(当選的小役,設定,内部状態)
			;次ゲームBOUNS当選
			;前兆中にBOUNS当選した場合前兆の短いほうが選ばれもう片方は捨てられる
			IF 前兆 != -1
				前兆 = MIN(前兆,前兆設定(内部状態))
			ELSE
				前兆 = 前兆設定(内部状態)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;レア小役当選時は内部状態抽選とBOUNS当選抽選を行う
	;7:弱西瓜当選の場合
	CASE 当選小役_弱西瓜
		
		;内部状態抽選
		;弱西瓜は25％で内部状態が昇格する
		IF 内部状態 < 3
			SIF !RAND(4)
				内部状態 ++
		ENDIF
		
		;BOUNS当選抽選
		IF 中奖_非32G連(当選的小役,設定,内部状態)
			;次ゲームBOUNS当選
			;前兆中にBOUNS当選した場合前兆の短いほうが選ばれもう片方は捨てられる
			IF 前兆 != -1
				前兆 = MIN(前兆,前兆設定(内部状態))
			ELSE
				前兆 = 前兆設定(内部状態)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;小役当選時は内部状態抽選とBOUNS当選抽選を行う
	;8:ベル当選の場合
	CASE 当選小役_ベル
		
		;BOUNS当選抽選
		IF 中奖_非32G連(当選的小役,設定,内部状態)
			;次ゲームBOUNS当選
			;前兆中にBOUNS当選した場合前兆の短いほうが選ばれもう片方は捨てられる
			IF 前兆 != -1
				前兆 = MIN(前兆,前兆設定(内部状態))
			ELSE
				前兆 = 前兆設定(内部状態)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;小役当選時は内部状態抽選とBOUNS当選抽選を行う
	;9:回放当選の場合
	CASE 当選小役_回放
		
		;BOUNS当選抽選
		IF 中奖_非32G連(当選的小役,設定,内部状態)
			;次ゲームBOUNS当選
			;前兆中にBOUNS当選した場合前兆の短いほうが選ばれもう片方は捨てられる
			IF 前兆 != -1
				前兆 = MIN(前兆,前兆設定(内部状態))
			ELSE
				前兆 = 前兆設定(内部状態)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;小役当選時は内部状態抽選とBOUNS当選抽選を行う
	;99:落空了当選の場合
	CASE 当選小役_落空了
		
		;内部状態抽選
		;落空了は2％で内部状態が降格する
		IF 内部状態 > 1
			SIF !RAND(50)
				内部状態 --
		ENDIF
		
		;BOUNS当選抽選
		IF 中奖_非32G連(当選的小役,設定,内部状態)
			;次ゲームBOUNS当選
			;前兆中にBOUNS当選した場合前兆の短いほうが選ばれもう片方は捨てられる
			IF 前兆 != -1
				前兆 = MIN(前兆,前兆設定(内部状態))
			ELSE
				前兆 = 前兆設定(内部状態)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;其他(默认では存在しないはず)
	CASEELSE
		PRINTFORMW 存在不存在的小役当选了 小役番号:{当選的小役}
	ENDSELECT
ENDIF


;当選ゲーム数の設定(短周期連→通常転落時)
IF 前兆 == -1 && 内部状態 < 4
	前兆 = 当選ゲーム数設定()
	上次当选的旋转数 = 0
ENDIF
$抽選終わり

IF オートモード == 1 && 総回転数 < 検定回数
	PRINTL 
	RESULT = 1
ELSE
	INPUT
ENDIF


IF RESULT == 1
	;=============================
	;所持筹码が足りない場合
	;※スロットをやめさせる
	;=============================
	IF MONEY:2 < 掛け金
		PRINTL 沒有籌碼用來投幣
		GOTO GAME_END
	ENDIF
	
	;=============================
	;閉店時間の場合
	;※スロットをやめさせる
	;=============================
	IF 総回転数 >= 閉店までに可能な回転数 && オートモード == 0
		PRINTW 「…」
		PRINTW 「…」
		PRINTW 「……客……客…」
		PRINTW 「……客人！」
		PRINTL 
		PRINTW 回头一看、那里有一个店员的身影……
		PRINTW 「…客人、我们要关门了」
		PRINTL 
		PRINTFORML %CALLNAME:MASTER%环顾四周
		PRINTW …客人都已经散去的几乎没有了
		PRINTL 
		PRINTL 「本店现在停止今天的营业」
		PRINTW 「关店后的保障、第二天的结转等概不应对、请谅解」
		PRINTL 「回去的时候从那边的门的离開、感谢您今天光临本店」
		PRINTW 「回去的时候请小心」
		
		;マクロ使ってる人类への嫌がらせポイント
		;個人的にはマクロは使いたいなら勝手に使えばよろしいという立場
		;ツール使わず手動で回すぶんにはこんなスロット金策にもなりゃしないのに※設定３なら1万回回して+300程度の期待値
		;ツール使用すること前提のチートに対して対処しろだの考えとけ馬鹿だの知らんがな(´・ω・`)
		;しかも、いい結果が出るまでセーブ＆ロードされるのが問題って…
		;良い設定ツモったら100％勝てるとか勘違いしてませんかね
		;とりあえず10万回程度じゃ確率収束なんて不会うに設計してるので、数千回程度で勝った負けたは設定じゃなくてヒキの問題です
		;負けたらリロードするんだからそりゃ甜だろうなぁ、低設定でも噴くときゃ噴くし
		;まぁ、これ入れときゃマクラーはほぼ壊滅
		;更にスロットの利用者更に減って誰も使わなくなるだろうからもう何も言われることもなくなるでしょう
		
		
		;まずは抽選する
		嫌がらせボタン番号 = RAND(1000000)
		;番号が1にはならないように退避
		SIF 嫌がらせボタン番号 >= 1
			嫌がらせボタン番号 ++
		PRINTL 
		PRINTFORM [{嫌がらせボタン番号}] 小心地走出店门
		
		LOCAL:1 = 0
		$IYAGARASE_INPUT
		INPUT
		;ちゃんとクリックしたら（まあ正しい番号を入力してもいいけど）退店できる
		IF RESULT == 嫌がらせボタン番号
			GOTO GAME_END
		;死ぬが良い、骨は拾ってやる
		ELSEIF RESULT == 1 && LOCAL:1 == 0
			;初回に1を押したらボタンまで消去される、これで正しい番号入れない限り退店できなくなる
			;2回目以降に1を押すとボタンまで消えないが、そういった人たちはマクロ使いではないのでセーフ
			;0～1000000の中から正しい数字をピンポイントで選んでね
			CLEARLINE 2
			SETCOLOR 0xFF0000
			PRINTL 「啊、请 小 心 点 地回去吧？」
			PRINTL 
			RESETCOLOR
			LOCAL:1 ++
		;穏当に警告　間違えて番号打っちゃった人はちゃんとボタンを押せばいいので大丈夫
		;マクラーは…ま、これが出てる時点で手遅れなんですけどね？
		ELSE
			CLEARLINE 1
			SIF LOCAL:1 == 1
				CLEARLINE 1
			PRINTL 「啊、请小心点儿回去吧？」
		ENDIF
		GOTO IYAGARASE_INPUT
	ENDIF
	
	;=============================
	;一回転分の掛け金徴収
	;=============================
	MONEY:2 -= 掛け金
	
	;=============================
	;前回のスロット画面を消去
	;=============================
	CLEARLINE 削除行数
	;=============================
	;回転数を1増加
	;=============================
	総回転数 ++
	上次当选的旋转数 ++
	SIF 前兆 > 0
		前兆 --
	
	;=============================
	;死机(1/131072)
	;死机した場合は処理が特別になるのでここで記述
	;死机恩恵　斯喀雷特BB確定 継続率95％連
	;=============================
	IF !RAND(131072)
		;全リール暗転
		暗転_左上 = 1
		暗転_左中 = 1
		暗転_左下 = 1
		暗転_中上 = 1
		暗転_中中 = 1
		暗転_中下 = 1
		暗転_右上 = 1
		暗転_右中 = 1
		暗転_右下 = 1
		
		;死机したので内部状態を5にする
		内部状態 = 5
		
		CALL SLOT_情報パネル表示(情報パネル表示页,総回転数,上次当选的旋转数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
		CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
		DRAWLINE
		CALL 一言メッセージ(短周期連セット数,設定,前兆,当選したBB種類,内部状態,短周期連継続率,当選的小役,上次当选的旋转数,短周期連開始時所持筹码)
		PRINTFORML 所持籌碼 {MONEY:2} 籌碼差枚 {MONEY:2-初期所持筹码}
		PRINTFORML 回転数 {総回転数}G 距离上次的大奖{上次当选的旋转数}G
		PRINTL 
		PRINTL 
		PRINTL 
		
		;3秒間停止させる
		TWAIT 3000,1
		;=============================
		;前回のスロット画面を消去
		;=============================
		CLEARLINE 削除行数
		
		;全リール明るいままでの斯喀雷特BB(通常時とは異なる)
		絵柄_左上 = REP
		絵柄_左中 = RD7
		絵柄_左下 = CHE
		絵柄_中上 = CHE
		絵柄_中中 = RD7
		絵柄_中下 = CHE
		絵柄_右上 = BEL
		絵柄_右中 = RD7
		絵柄_右下 = MEL
		暗転_左上 = 0
		暗転_左中 = 0
		暗転_左下 = 0
		暗転_中上 = 0
		暗転_中中 = 0
		暗転_中下 = 0
		暗転_右上 = 0
		暗転_右中 = 0
		暗転_右下 = 0
		
		;斯喀雷特BB当選
		当選的小役 = 0
		
		死机回数 ++
		;死机恩恵 斯喀雷特BB確定 継続率95％連をセット
		当選したBB種類 = 0
		
		
		;=============================
		;筹码の獲得
		;=============================
		CALL 小役支付戻し(当選的小役)
		
		
		;=============================
		;小役カウンタ
		;=============================
		小役カウンタ:当選的小役 ++
		
		FOR LOCAL,0,10
			SIF 当選種類履歴:(9-LOCAL) == -1
				CONTINUE
			当選種類履歴:(10-LOCAL) = 当選種類履歴:(9-LOCAL)
			当選履歴:(10-LOCAL) = 当選履歴:(9-LOCAL)
			当選枚数履歴:(10-LOCAL) = 当選枚数履歴:(9-LOCAL)
		NEXT
		
		SIF 内部状態 == 5
			累計短周期連セット数 ++
		当選種類履歴:0 = 当選的小役
		当選履歴:0 = 上次当选的旋转数
		IF 短周期連セット数
			当選枚数履歴:0 = MONEY:2 - 短周期連開始時所持筹码
		ELSE
			SELECTCASE 当選的小役
			CASE 当選小役_赤7BOUNS
				当選枚数履歴:0 = 400
			CASE 当選小役_青7BOUNS
				当選枚数履歴:0 = 200
			CASE 当選小役_レギュラーBOUNS
				当選枚数履歴:0 = 100
			CASEELSE
			ENDSELECT
		ENDIF
		
		上次当选的旋转数 = 0
		短周期連継続率 = 95
		
		SIF 短周期連セット数 == 0
			短周期連開始時所持筹码 = MONEY:2
		
		;INPUT_LOOPへ
		GOTO INPUT_LOOP
	ENDIF
	
	;=============================
	;前回の結果を初期化
	;=============================
	絵柄_左上 = NUL
	絵柄_左中 = NUL
	絵柄_左下 = NUL
	絵柄_中上 = NUL
	絵柄_中中 = NUL
	絵柄_中下 = NUL
	絵柄_右上 = NUL
	絵柄_右中 = NUL
	絵柄_右下 = NUL
	暗転_左上 = 0
	暗転_左中 = 0
	暗転_左下 = 0
	暗転_中上 = 0
	暗転_中中 = 0
	暗転_中下 = 0
	暗転_右上 = 0
	暗転_右中 = 0
	暗転_右下 = 0
	
	;=============================
	;回転最中のスロット表示
	;=============================
	CALL SLOT_情報パネル表示(情報パネル表示页,総回転数,上次当选的旋转数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
	CALL SLOT_全体表示(色_枠,絵柄_左上_回転中,絵柄_左中_回転中,絵柄_左下_回転中,絵柄_中上_回転中,絵柄_中中_回転中,絵柄_中下_回転中,絵柄_右上_回転中,絵柄_右中_回転中,絵柄_右下_回転中,暗転_左上_回転中,暗転_左中_回転中,暗転_左下_回転中,暗転_中上_回転中,暗転_中中_回転中,暗転_中下_回転中,暗転_右上_回転中,暗転_右中_回転中,暗転_右下_回転中)
	DRAWLINE
	PRINTL 
	PRINTFORML 所持籌碼 {MONEY:2} 籌碼差枚 {MONEY:2-初期所持筹码}
	PRINTFORML 回転数 {総回転数}G 距离上次的大奖{上次当选的旋转数}G
	PRINTL  
	PRINTL  
	PRINTFORML [1] - 停止转动
	;リール回転
	IF オートモード == 1 && 総回転数 < 検定回数
		PRINTL 
		RESULT = 1
	ELSE
		INPUT
	ENDIF
	CLEARLINE 削除行数
	
	;=============================
	;特殊な状態の場合は個々に記述する
	;=============================
	
	;=============================
	;前兆が0GならばBB当選
	;=============================
	IF 前兆 == 0
		SIF 当選的小役 == 当選小役_中樱桃
			GOTO 斯喀雷特BB
		IF RAND(2)
			;合計1/3で十六夜BB(200)
			IF RAND(3)
$十六夜BB
				;=============================
				;当選したBB種類に1(十六夜BB)を代入
				;=============================
				当選したBB種類 = 1
				
				;=============================
				;左絵柄決定
				;=============================
				絵柄_左上 = REP
				絵柄_左中 = BL7
				絵柄_左下 = CHE
				暗転_左上 = 1
				暗転_左中 = 0
				暗転_左下 = 1
				;=============================
				;回転最中のスロット表示
				;=============================
				CALL SLOT_情報パネル表示(情報パネル表示页,総回転数,上次当选的旋转数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
				CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
				DRAWLINE
				PRINTL 
				PRINTFORML 所持籌碼 {MONEY:2} 籌碼差枚 {MONEY:2-初期所持筹码}
				PRINTFORML 回転数 {総回転数}G 距离上次的大奖{上次当选的旋转数}G
				PRINTL  
				PRINTL  
				PRINTFORML [1] - 停止转动
				;リール回転
				IF オートモード == 1 && 総回転数 < 検定回数
					PRINTL 
					RESULT = 1
				ELSE
					INPUT
				ENDIF
				CLEARLINE 削除行数
				
				;=============================
				;中絵柄決定
				;=============================
				絵柄_中上 = MEL
				絵柄_中中 = BL7
				絵柄_中下 = REP
				暗転_中上 = 1
				暗転_中中 = 0
				暗転_中下 = 1
				;=============================
				;回転最中のスロット表示
				;=============================
				CALL SLOT_情報パネル表示(情報パネル表示页,総回転数,上次当选的旋转数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
				CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
				DRAWLINE
				PRINTL 
				PRINTFORML 所持籌碼 {MONEY:2} 籌碼差枚 {MONEY:2-初期所持筹码}
				PRINTFORML 回転数 {総回転数}G 距离上次的大奖{上次当选的旋转数}G
				PRINTL  
				PRINTL  
				PRINTFORML [1] - 停止转动
				;リール回転
				IF オートモード == 1 && 総回転数 < 検定回数
					PRINTL 
					RESULT = 1
				ELSE
					INPUT
				ENDIF
				CLEARLINE 削除行数
				
				;=============================
				;右絵柄決定
				;=============================
				絵柄_右上 = BEL
				絵柄_右中 = BL7
				絵柄_右下 = MEL
				暗転_右上 = 1
				暗転_右中 = 0
				暗転_右下 = 1
				;=============================
				;回転最中のスロット表示
				;=============================
				;CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
				
				;十六夜BB当選
				当選的小役 = 1
				
				;=============================
				;筹码の獲得
				;=============================
				CALL 小役支付戻し(当選的小役)
			;合計1/6で斯喀雷特BB(400)
			ELSE
$斯喀雷特BB
				;=============================
				;当選したBB種類に0(斯喀雷特BB)を代入
				;=============================
				当選したBB種類 = 0
				
				;=============================
				;左絵柄決定
				;=============================
				絵柄_左上 = REP
				絵柄_左中 = RD7
				絵柄_左下 = CHE
				暗転_左上 = 1
				暗転_左中 = 0
				暗転_左下 = 1
				;=============================
				;回転最中のスロット表示
				;=============================
				CALL SLOT_情報パネル表示(情報パネル表示页,総回転数,上次当选的旋转数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
				CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
				DRAWLINE
				PRINTL 
				PRINTFORML 所持籌碼 {MONEY:2} 籌碼差枚 {MONEY:2-初期所持筹码}
				PRINTFORML 回転数 {総回転数}G 距离上次的大奖{上次当选的旋转数}G
				PRINTL  
				PRINTL  
				PRINTFORML [1] - 停止转动
				;リール回転
				IF オートモード == 1 && 総回転数 < 検定回数
					PRINTL 
					RESULT = 1
				ELSE
					INPUT
				ENDIF
				CLEARLINE 削除行数
				
				;=============================
				;中絵柄決定
				;=============================
				絵柄_中上 = CHE
				絵柄_中中 = RD7
				絵柄_中下 = CHE
				暗転_中上 = 1
				暗転_中中 = 0
				暗転_中下 = 1
				;=============================
				;回転最中のスロット表示
				;=============================
				CALL SLOT_情報パネル表示(情報パネル表示页,総回転数,上次当选的旋转数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
				CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
				DRAWLINE
				PRINTL 
				PRINTFORML 所持籌碼 {MONEY:2} 籌碼差枚 {MONEY:2-初期所持筹码}
				PRINTFORML 回転数 {総回転数}G 距离上次的大奖{上次当选的旋转数}G
				PRINTL  
				PRINTL  
				PRINTFORML [1] - 停止转动
				;リール回転
				IF オートモード == 1 && 総回転数 < 検定回数
					PRINTL 
					RESULT = 1
				ELSE
					INPUT
				ENDIF
				CLEARLINE 削除行数
				
				;=============================
				;右絵柄決定
				;=============================
				絵柄_右上 = BEL
				絵柄_右中 = RD7
				絵柄_右下 = MEL
				暗転_右上 = 1
				暗転_右中 = 0
				暗転_右下 = 1
				;=============================
				;回転最中のスロット表示
				;=============================
				;CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
				
				;=============================
				;中段樱桃から直接ここへ来た場合の処理
				;=============================
				IF 当選的小役 == 当選小役_中樱桃
					内部状態 = 4
					;短周期連セット数の初期化
					短周期連セット数 = 1
					短周期連開始時所持筹码 = MONEY:2
					短周期連継続率 = 90
					SIF !RAND(10)
						短周期連継続率 = 95
				ENDIF
				
				
				
				;斯喀雷特BB当選
				当選的小役 = 0
				
				;=============================
				;筹码の獲得
				;=============================
				CALL 小役支付戻し(当選的小役)
			ENDIF
		;合計1/2でREG(100)
		ELSE
$REG
			;=============================
			;当選したBB種類に2(REG)を代入
			;=============================
			当選したBB種類 = 2
			;=============================
			;左絵柄決定
			;=============================
			絵柄_左上 = REP
			絵柄_左中 = RD7
			絵柄_左下 = CHE
			暗転_左上 = 1
			暗転_左中 = 0
			暗転_左下 = 1
			;=============================
			;回転最中のスロット表示
			;=============================
			CALL SLOT_情報パネル表示(情報パネル表示页,総回転数,上次当选的旋转数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
			CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
			DRAWLINE
			PRINTL 
			PRINTFORML 所持籌碼 {MONEY:2} 籌碼差枚 {MONEY:2-初期所持筹码}
			PRINTFORML 回転数 {総回転数}G 距离上次的大奖{上次当选的旋转数}G
			PRINTL  
			PRINTL  
			PRINTFORML [1] - 停止转动
			;リール回転
			IF オートモード == 1 && 総回転数 < 検定回数
				PRINTL 
				RESULT = 1
			ELSE
				INPUT
			ENDIF
			CLEARLINE 削除行数
			
			;=============================
			;中絵柄決定
			;=============================
			絵柄_中上 = CHE
			絵柄_中中 = RD7
			絵柄_中下 = CHE
			暗転_中上 = 1
			暗転_中中 = 0
			暗転_中下 = 1
			;=============================
			;回転最中のスロット表示
			;=============================
			CALL SLOT_情報パネル表示(情報パネル表示页,総回転数,上次当选的旋转数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
			CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
			DRAWLINE
			PRINTL 
			PRINTFORML 所持籌碼 {MONEY:2} 籌碼差枚 {MONEY:2-初期所持筹码}
			PRINTFORML 回転数 {総回転数}G 距离上次的大奖{上次当选的旋转数}G
			PRINTL  
			PRINTL  
			PRINTFORML [1] - 停止转动
			;リール回転
			IF オートモード == 1 && 総回転数 < 検定回数
				PRINTL 
				RESULT = 1
			ELSE
				INPUT
			ENDIF
			CLEARLINE 削除行数
			
			;=============================
			;右絵柄決定
			;=============================
			絵柄_右上 = BEL
			絵柄_右中 = B_A_R
			絵柄_右下 = MEL
			暗転_右上 = 1
			暗転_右中 = 0
			暗転_右下 = 1
			;=============================
			;回転最中のスロット表示
			;=============================
			;CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
			
			;REG当選
			当選的小役 = 2
			
			;=============================
			;筹码の獲得
			;=============================
			CALL 小役支付戻し(当選的小役)
		ENDIF
		
		;=============================
		;小役カウンタ
		;=============================
		小役カウンタ:当選的小役 ++
		
		FOR LOCAL,0,10
			SIF 当選種類履歴:(9-LOCAL) == -1
				CONTINUE
			当選種類履歴:(10-LOCAL) = 当選種類履歴:(9-LOCAL)
			当選履歴:(10-LOCAL) = 当選履歴:(9-LOCAL)
			当選枚数履歴:(10-LOCAL) = 当選枚数履歴:(9-LOCAL)
		NEXT
		
		SIF 内部状態 == 4
			累計短周期連セット数 ++
		当選種類履歴:0 = 当選的小役
		当選履歴:0 = 上次当选的旋转数
		IF 内部状態 == 4
			当選枚数履歴:0 = MONEY:2 - 短周期連開始時所持筹码
		ELSE
			SELECTCASE 当選的小役
			CASE 当選小役_赤7BOUNS
				当選枚数履歴:0 = 400
			CASE 当選小役_青7BOUNS
				当選枚数履歴:0 = 200
			CASE 当選小役_レギュラーBOUNS
				当選枚数履歴:0 = 100
			CASEELSE
			ENDSELECT
		ENDIF
		;=============================
		;各種情報の初期化
		;=============================
		上次当选的旋转数 = 0
		GOTO INPUT_LOOP
	ENDIF
	;=============================
	;BB当選時の記述ここまで
	;=============================
	
	;=============================
	;子役抽選
	;=============================
	当選的小役 = 小役抽選(内部状態)
	
	CALL スロット絵柄決定(当選的小役,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下)
	
	
	;=============================
	;回転最中のスロット表示(左停止)
	;=============================
	CALL SLOT_情報パネル表示(情報パネル表示页,総回転数,上次当选的旋转数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
	;中リール右リールには「_回転中」が付いた変数を入れる
	CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上_回転中,絵柄_中中_回転中,絵柄_中下_回転中,絵柄_右上_回転中,絵柄_右中_回転中,絵柄_右下_回転中,暗転_左上,暗転_左中,暗転_左下,暗転_中上_回転中,暗転_中中_回転中,暗転_中下_回転中,暗転_右上_回転中,暗転_右中_回転中,暗転_右下_回転中)
	DRAWLINE
	PRINTL 
	PRINTFORML 所持籌碼 {MONEY:2} 籌碼差枚 {MONEY:2-初期所持筹码}
	PRINTFORML 回転数 {総回転数}G 距离上次的大奖{上次当选的旋转数}G
	PRINTL  
	PRINTL  
	PRINTL  
	
	;左リールが通常と違う場合更に1秒間停止させる(目押し感の演出)
	SIF 絵柄_左上 != BEL
		TWAIT 1000,0
	;0.1秒間停止させる
	TWAIT 100,0
	;以下、コメントアウト
	;PRINTFORML [1] - 停止转动
	;リール回転
	;INPUT
	;CLEARLINE 削除行数
	
	CLEARLINE 削除行数 - 1
	
	;=============================
	;回転最中のスロット表示(中停止)
	;=============================
	CALL SLOT_情報パネル表示(情報パネル表示页,総回転数,上次当选的旋转数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
	;右リールには「_回転中」が付いた変数を入れる
	CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上_回転中,絵柄_右下_回転中,絵柄_右下_回転中,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上_回転中,暗転_右中_回転中,暗転_右下_回転中)
	DRAWLINE
	PRINTL 
	PRINTFORML 所持籌碼 {MONEY:2} 籌碼差枚 {MONEY:2-初期所持筹码}
	PRINTFORML 回転数 {総回転数}G 距离上次的大奖{上次当选的旋转数}G
	PRINTL  
	PRINTL  
	PRINTL  
	
	;左リールが通常と違う場合更に1秒間停止させる(目押し感の演出)
	;左リール西瓜滑りからの中段ベルが存在するので「絵柄_中中 != BEL」が追加
	SIF 絵柄_左上 != BEL && 絵柄_中中 != BEL
		TWAIT 1000,0
	;0.1秒間停止させる
	TWAIT 100,0
	;以下、コメントアウト
	;PRINTFORML [1] - 停止转动
	;リール回転
	;INPUT
	;CLEARLINE 削除行数
	
	CLEARLINE 削除行数 - 1
	
	CALL 暗転設定(当選的小役,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
	
	;=============================
	;小役カウンタ
	;=============================
	小役カウンタ:当選的小役 ++
	;=============================
	;筹码支付戻し
	;=============================
	CALL 小役支付戻し(当選的小役)
	
	
	
	;=============================================================
	;全停止後のリール表示はINPUT_LOOPにループしたあと206行目で行う
	;=============================================================
	GOTO INPUT_LOOP

ELSEIF RESULT == 2
	情報パネル表示页 ++
	情報パネル表示页 %= 2
	CLEARLINE 削除行数
	小役抽選不会 = 1
	GOTO INPUT_LOOP
ELSEIF RESULT == 0
ELSE
	CLEARLINE 1
	REUSELASTLINE 無効な数字
	GOTO INPUT_LOOP
ENDIF
$GAME_END
;筹码会員用のカモられ指数を上昇
;回放は in 0なのでそれを引いた（ゲーム数 * 3）が投入筹码
投入筹码 = (総回転数 - 小役カウンタ:当選小役_回放) * 3
CALL CASINO_MONEY("カモ", 投入筹码)
