;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;地点の属性設定
;場所_ワンルーム   家主が不在のとき鍵がかかる,被窝・屋内・厨房・勉強・休憩・飲食・プライベートが自動で付与
;場所_被窝         休憩が自動で付与
;場所_洋室         屋内だけど靴を履いたまま,屋内が自動で付与
;場所_屋内         
;場所_屋根         屋外だけど屋根がある場所,屋内でも屋根でも無い場合は野外判定（旧露天）
;場所_广阔         
;場所_赏月         屋内や屋根があっても月が見える場所
;場所_休憩         
;場所_風呂         
;場所_廁所       
;場所_厨房         つまみ食いが自動で付与
;場所_訓練         
;場所_学习         
;場所_天之声       
;場所_菜園         
;場所_打水       
;場所_身体測定     
;場所_物干し       風景描写用
;場所_飲食         自由行動用
;場所_つまみ食い   自由行動用
;場所_留まらない   あまり角色が留まらない（移動しやすい）
;場所_過疎         あまり角色が立ち入らない（移動されにくい）
;場所_プライベート 節度のある角色は入ってこない,主に私室と風呂・廁所
;場所_炬燵         自由行動用
;場所_公共の場     推倒てこない
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
@ROOMSETTING_2
#DIM ROOM
VARSET ROOMDATA
MAXROOM = 282
ENTRANCE = P201人里的門
SUMIKOMI_ROOM = P241空的部屋
避難处 = P220瞭望樓
SIF GET_MAPID(拠点_酒虫) != MAIN_MAP
	拠点_酒虫 = P230空的部屋
MAP_PAGES = 4
MAPNAME:1 = 人里
MAPNAME:2 = 長屋
MAPNAME:3 = 鈴奈庵
MAPNAME:4 = 鯢呑亭

FOR LOCAL, MINROOM(), MAXROOM
	ROOM = LOCAL % 100
	SELECTCASE LOCAL
		CASE P280警察署
			ROOMDATA:ROOM |= 場所_屋内
			ROOMDATA:ROOM |= 場所_休憩
			ROOMDATA:ROOM |= 場所_学习
			ROOMDATA:ROOM |= 場所_身体測定
			ROOMDATA:ROOM |= 場所_公共の場
		CASE P281小兎姫私室
			ROOMDATA:ROOM |= 場所_屋内
			ROOMDATA:ROOM |= 場所_被窝
			ROOMDATA:ROOM |= 場所_プライベート
	ENDSELECT

	SELECTCASE LOCAL
	;被窝・屋内・厨房・勉強・休憩・飲食・プライベートが自動で付与
	CASE P208雷鼓的房間, P209八橋的房間, P210弁弁的房間, P224慧音宅, P231蛮奇的房間, P232蓮子的房間, P233梅莉的房間, P234雪的房間, P235​舞​的房間, P241空的部屋
		ROOMDATA:ROOM |= 場所_ワンルーム
	;ワンルームだけ付いてない
	CASE P230空的部屋
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_被窝
		ROOMDATA:ROOM |= 場所_厨房
		ROOMDATA:ROOM |= 場所_学习
		ROOMDATA:ROOM |= 場所_休憩
		ROOMDATA:ROOM |= 場所_飲食
		ROOMDATA:ROOM |= 場所_プライベート
	CASE P201人里的門
		ROOMDATA:ROOM |= 場所_留まらない
		ROOMDATA:ROOM |= 場所_公共の場
	CASE P202中央広場
		ROOMDATA:ROOM |= 場所_广阔
		ROOMDATA:ROOM |= 場所_訓練
		ROOMDATA:ROOM |= 場所_公共の場
	CASE P203南大街, P204東大街, P205北大街, P206西大街
		ROOMDATA:ROOM |= 場所_广阔
		ROOMDATA:ROOM |= 場所_公共の場
		ROOMDATA:ROOM |= 場所_留まらない
	CASE P207龍神像
		ROOMDATA:ROOM |= 場所_天之声
		ROOMDATA:ROOM |= 場所_公共の場
	CASE P211酒屋, P212咖啡館, P217甜品店, P218小料理屋, P238鯢呑亭
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_飲食
		ROOMDATA:ROOM |= 場所_つまみ食い
		ROOMDATA:ROOM |= 場所_公共の場
	CASE P213鈴奈庵, P215花屋, P216食料品店, P227八百屋
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_公共の場
	CASE P214長屋前, P220瞭望樓
		ROOMDATA:ROOM |= 場所_公共の場
	CASE P219集会所, P222寺子屋
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_休憩
		ROOMDATA:ROOM |= 場所_学习
		ROOMDATA:ROOM |= 場所_身体測定
		ROOMDATA:ROOM |= 場所_公共の場
	CASE P221稗田邸
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_休憩
		ROOMDATA:ROOM |= 場所_厨房
		ROOMDATA:ROOM |= 場所_学习
		ROOMDATA:ROOM |= 場所_炬燵
	CASE P223銭湯
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_身体測定
		ROOMDATA:ROOM |= 場所_公共の場
	CASE P225情人用宿屋
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_被窝
	CASE P226小鈴私室, P229阿求私室
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_被窝
		ROOMDATA:ROOM |= 場所_学习
		ROOMDATA:ROOM |= 場所_プライベート
	CASE P228貸切浴場
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_風呂
	CASE P236公用廁所
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_廁所
	CASE P237公用水井
		ROOMDATA:ROOM |= 場所_打水
	CASE P239美宵的房間
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_被窝
		ROOMDATA:ROOM |= 場所_プライベート
	ENDSELECT
NEXT

;-----------------------------------------
;默认の初期位置と大家の設定
;-----------------------------------------
@DEFAULT_OWNER_2
默认初期位置 = P230空的部屋
拠点_家主 = [[阿求]]

;-----------------------------------------
;お出かけMapの設定
;ARG   対象の地点
;ARGS  判定文字典数
;-----------------------------------------
@ODEKAKEMAP_SETTING_2(ARG, ARGS)
#FUNCTION
;＼1 2 3 4 5 6 7 8 9 
;01　　　　　　　　　
;02　　　　　　　　　
;03　　　　　　４　　
;04　６　　　　　　　
;05　　　　１　２　　
;06　　　　　　　　　
;07　　７　３　５　　
;08　　　　　　　　　
;09　　　　　　　　　
SELECTCASE ARG
CASE 広場
	SIF ARGS == "座標"
		RETURNF 55
	SIF GROUPMATCH(ARGS, "广阔", "路人")
		RETURNF 1
CASE 商家町
	SIF ARGS == "座標"
		RETURNF 75
	SIF GROUPMATCH(ARGS, "屋根", "赏月", "路人")
		RETURNF 1
CASE 呑屋小道
	SIF ARGS == "座標"
		RETURNF 57
	SIF GROUPMATCH(ARGS, "屋根", "赏月", "路人")
		RETURNF 1
CASE 寺子屋
	SIF ARGS == "座標"
		RETURNF 73
	SIF GROUPMATCH(ARGS, "屋内", "赏月", "休憩", "勉強", "路人")
		RETURNF 1
CASE 大衆浴場
	SIF ARGS == "座標"
		RETURNF 77
	SIF GROUPMATCH(ARGS, "屋内", "風呂", "身体測定", "路人")
		RETURNF 1
CASE 情人用宿屋
	SIF ARGS == "座標"
		RETURNF 24
	SIF GROUPMATCH(ARGS, "被窝", "屋内", "休憩")
		RETURNF 1
CASE 長屋街
	SIF ARGS == "座標"
		RETURNF 37
	SIF GROUPMATCH(ARGS, "屋内", "赏月", "厨房", "路人")
		RETURNF 1
ENDSELECT
