;--------------------------------------------------
;地点名変更関数
;--------------------------------------------------
@MAP_PLACENAME_5(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE P531人偶洋館・空的部屋
			IF CFLAG:MASTER:初期位置 == ARG
				RETURNF "人偶洋館・" + CALLNAME:MASTER + "的房間"
			ELSE
				RETURNF "人偶洋館・空的部屋"
			ENDIF
		CASE SUMIKOMI_ROOM
			;※このMapでは未実装
			IF FLAG:住宿人物
				RETURNF CALLNAME:(FLAG:住宿人物) + "的房间"
			ELSE
				RETURNF "空的部屋"
			ENDIF
		CASE MINROOM() TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE OMANEKIBEYA()
			RETURNF OMANEKI_PLACE()
		CASE SUKIMA()
			RETURNF "隙間空間"
		CASE MAXROOM
			RETURNF "森林之外"
		CASEELSE
			RETURNF "不明"
	ENDSELECT
ELSE
	RETURNF "森林之外"
ENDIF

;--------------------------------------------------
;場所ARGがどのエリアにあるか
;表示するMapの番号と連動（TFLAG:地圖切換）
;ARG     場所ID
;--------------------------------------------------
@MAP_AREASELECT_5(ARG)
#FUNCTION
SELECTCASE ARG
	;魔理沙宅
	CASE P510霧雨魔法店 TO P515魔理沙宅・倉庫
		RETURNF 3
	;人偶的洋館
	CASE P521人偶洋館・愛麗絲的房間 TO P531人偶洋館・空的部屋
		RETURNF 2
	;软绵绵艾倫の店
	CASE P534輕飄飄的艾倫魔法商店 TO P537艾倫宅・風呂
		RETURNF 4
	;魔法之森
	CASEELSE
		RETURNF 1
ENDSELECT

;--------------------------------------------------
;内部構造がある場所の内部の判定
;TFLAG:地圖切換  内部察知が必要なMap番号
;                P_ID  表示ボタンの場所ID
;                Q_ID  判定する内部の場所ID,必ずP_IDと一致した数値も含める
;--------------------------------------------------
@GETMAP_BUILDING_5(P_ID, Q_ID)
#FUNCTION
#DIM P_ID
#DIM Q_ID
IF TFLAG:地圖切換 == 1
	SELECTCASE P_ID
		;魔理沙宅
		CASE P510霧雨魔法店
			SELECTCASE Q_ID
			CASE P510霧雨魔法店 TO P514魔理沙宅・風呂
				RETURNF 1
			ENDSELECT
		;人偶洋館
		CASE P525人偶洋館・生活間
			SELECTCASE Q_ID
			CASE P521人偶洋館・愛麗絲的房間 TO P531人偶洋館・空的部屋
				RETURNF 1
			ENDSELECT
		;艾倫宅
		CASE P534輕飄飄的艾倫魔法商店
			SELECTCASE Q_ID
			CASE P534輕飄飄的艾倫魔法商店 TO P537艾倫宅・風呂
				RETURNF 1
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;--------------------------------------------------
;移動制限設定
;ARG    場所ID
;CHARA  移動する角色ID
;RETURNF 1=移動できる,0=移動できない
;TFLAG:移動不能メッセージ   ↓の@MAP_CANNOT_WORD用の設定
;--------------------------------------------------
@MAP_CAN_MOVE_5(ARG, CHARA)
#FUNCTION
#DIM CHARA
SELECTCASE ARG
	;香霖堂,10時～18時
	CASE P502香霖堂
		IF !INRANGE(TIME, 600, 1080)
			TFLAG:移動不能メッセージ = 1
			RETURNF 0
		ENDIF
ENDSELECT
RETURNF 1

;--------------------------------------------------
;移動不能時に表示されるメッセージ
;--------------------------------------------------
@MAP_CANNOT_WORD_5(ARG)
SELECTCASE TFLAG:移動不能メッセージ
	CASE 1
		PRINTFORMW 非営業時間
		CLEARLINE 2
ENDSELECT
REUSELASTLINE

;-----------------------------------------
;拠点専用の情景描写
;WAITはかけないように
;-----------------------------------------
@MAP_UNIQUEMESSAGE_5
SELECTCASE CFLAG:MASTER:現在位置
	CASE P502香霖堂
		PRINTFORML 狹窄的地方對著各種雜物
		PRINTFORML 有很多都隨便貼著「非売品」……
	CASE P503岔道
		PRINTFORML 看見了魔理沙家的後門和水井
;	CASE 520
;		PRINTFORML 擺放著寫著「慶賀新築」的大花圈
	CASE P524人偶洋館・研究室
		PRINTFORML 書架上擺放著药瓶和人偶
	CASE P543露米亜的棲息処
		PRINTFORML 角落裡散落著不認識的骨頭
ENDSELECT

RETURN 1
;-----------------------------------------
;非自宅時に差し替える場所IDを返す関数
;ARG,対象ID
;RETURNF,差し替え済み対象ID
;-----------------------------------------
@MAP_SEARCH_REPLACEMENT_5(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P502香霖堂
		RETURNF 香霖堂
	CASE P540古木的大樹 TO P544成美の小屋
		RETURNF 古木的大樹
	CASE P510霧雨魔法店 TO P515魔理沙宅・倉庫
		RETURNF 霧雨魔法店
	CASE P520人偶洋館前 TO P531人偶洋館・空的部屋
		RETURNF 人偶的洋館
	CASE P506無縁塚
		RETURNF 無縁塚
	CASE P509再思之道
		RETURNF 再思之道
	CASEELSE
		RETURNF 森林的入口
ENDSELECT
RETURNF 0

;--------------------------------------------------
;Mapの紹介文
;--------------------------------------------------
@Map紹介_5
;とりあえず5行
PRINTFORML 這裡是以魔理沙宅邸、人形館、軟綿綿艾倫的店為中心的地區。
PRINTFORML 這裡有著不需要工具也能進行合成的場所、以及多種類的採集地點。
PRINTFORML 而其中【蘑菇群生地】也只有以此為據點時才會出現的採集地點。若想採集蘑菇類的物品時、推薦來這裡。
PRINTFORML 房間也相對的多、很適合展現你的掃除技能。但能搭把手幫忙的工作則相對比較少。
PRINTFORML 從無縁塚到這裡有互通的道路。
