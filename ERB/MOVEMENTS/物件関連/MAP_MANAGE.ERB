;------------------------------------------
;	引っ越しの表示と処理
;------------------------------------------
@SET_MAINHOME
#DIM CHK_TRAINING
#DIM CHK_GARDENING
#DIM 設定前MAIN_MAP
#DIM 設定前酒虫
#DIM 設定前初期位置
#DIM 設定前大家
#DIM ANIMATION_SEED
#DIM PREV_REDRAW

ANIMATION_SEED = 0
PREV_REDRAW = CURRENTREDRAW()

;Map Animation Code
$MAPANIMATION
LINESTART = LINECOUNT
SIF ANIMATERECOLOREDMAPS && !FLAG:70
	ANIMATION_SEED ++
;
;
;メンバーの起床時間再設定
;
IF !引っ越し
	IF ゲーム開始処理中 || ゲーム继承処理中
		MAIN_MAP = 0
		TRYCALLFORM ROOMSETTING_{MAIN_MAP}
	ENDIF
	設定前MAIN_MAP = MAIN_MAP
	設定前酒虫 = 拠点_酒虫
	設定前初期位置 = 默认初期位置
	設定前大家 = 拠点_家主
	引っ越し = 1
ENDIF
IF FLAG:一時滞在
	PRINTFORMW 因为有来这里过夜的角色、所以无法搬家
	RETURN
ENDIF
DRAWLINE
;Color map
IF RECOLOREDMAPS
	CALL DRAW_COLOREDMAP(ANIMATION_SEED, "HIKKOSHI")
ELSE
	CALL DRAW_MAP("HIKKOSHI")
ENDIF
DRAWLINE
PRINTFORML ■%GET_MAPNAME(MAIN_MAP)%
CALL 物件データ(MAIN_MAP)
PRINTFORM 採集可能地点：
CALL ShowGatheringSpots(MAIN_MAP)
PRINTFORML
FONTITALIC
CALLFORM Map紹介_{MAIN_MAP}
FONTREGULAR
PRINTL
DRAWLINEFORM =
PRINT 可以对自宅进行設定。要住在哪里呢？
PRINTBUTTON @"[预览图切换({TFLAG:地圖切換})]", 1997
PRINTL
FOR LOCAL, 0, 11
	SIF MAIN_MAP == LOCAL
		SETCOLOR C_AQUA
	SIF GET_EXISTMAP(LOCAL)
		PRINTFORM [{LOCAL + 2000}] %GET_MAPNAME(LOCAL), 16, LEFT%
	SIF GROUPMATCH(LOCAL, 4, 9, 10)
		PRINTL
	RESETCOLOR
NEXT
PRINT [1999] 決定　　　　　　
SIF !ゲーム開始処理中 && !ゲーム继承処理中
	PRINT [1998] 中止
PRINTL
;Map Animation Code
IF ANIMATERECOLOREDMAPS && !FLAG:70
	REDRAW PREV_REDRAW
	TINPUT ANIMATERECOLOREDMAPS,99999,0,""
ELSE
	INPUT
ENDIF
;拠点切り替え
IF RESULT == 99999
	PREV_REDRAW = CURRENTREDRAW()
	REDRAW 0
	CLEARLINE LINECOUNT - LINESTART
	GOTO MAPANIMATION
ELSEIF RESULT >= 2000 && GET_EXISTMAP(RESULT - 2000)
	MAIN_MAP = RESULT - 2000
	CALLFORM ROOMSETTING_{MAIN_MAP}
	CALLFORM DEFAULT_OWNER_{MAIN_MAP}
	TFLAG:地圖切換 = 1
	RESTART
;プレビュー切り替え
ELSEIF RESULT == 1997
	TFLAG:地圖切換 += 1
	SIF TFLAG:地圖切換 > MAP_PAGES
		TFLAG:地圖切換 = 1
	RESTART
;中止
ELSEIF RESULT == 1998 && !ゲーム開始処理中 && !ゲーム继承処理中
	引っ越し = 0
	MAIN_MAP = 設定前MAIN_MAP
	拠点_酒虫 = 設定前酒虫
	默认初期位置 = 設定前初期位置
	拠点_家主 = 設定前大家
	TFLAG:地圖切換 = 0
	CALLFORM ROOMSETTING_{MAIN_MAP}
	RETURN
;決定
ELSEIF RESULT == 1999
	SIF 設定前MAIN_MAP == MAIN_MAP
	CALLFORM DEFAULT_OWNER_{MAIN_MAP}
ELSE
	RESTART
ENDIF
;決定後
FLAG:神簽契約 = 0
TFLAG:地圖切換 = 0
CALL SET_ENKAI_LOCATIONDISPLAY
CALL MAP_DEFAULTRESIDENT(MAIN_MAP)
;施錠
VARSET LOCK

FOR LOCAL,1,CHARANUM
	CALL CHARA_SLEEP, LOCAL, 0
NEXT
CHK_TRAINING = 0
CHK_GARDENING = 0
FOR LOCAL,1,100
	SIF ROOMDATA:LOCAL & 場所_訓練
		CHK_TRAINING = 1
	SIF ROOMDATA:LOCAL & 場所_菜園
		CHK_GARDENING = 1
NEXT
SIF !CHK_TRAINING
	CALL COLORMESSAGE(@"这个地图无法进行[戦闘訓練]",C_YELLOW,2)
SIF !CHK_GARDENING
	CALL COLORMESSAGE(@"这个地图无法进行[家庭菜園]",C_YELLOW,2)
SIF FLAG:扮演 && CFLAG:(FLAG:扮演):出禁 && MAIN_MAP == GET_MAPID(CSVCFLAG(FLAG:扮演,311))
	CFLAG:MASTER:初期位置 = CSVCFLAG(FLAG:扮演,311)
;罠ポイントの張替え
CALL TRAP_MOVING

@HAS_DUPLICATE_KEY, ARG
#FUNCTION
FOR LOCAL, 1, CHARANUM
	IF CFLAG:LOCAL:311 == ARG
		SIF TALENT:LOCAL:恋慕
			RETURNF 1
	ENDIF
NEXT

@MAP_DEFAULTRESIDENT(ARG, UP约会中)
#DIM UP约会中
;ARG=Map番号
SIF !UP约会中
	FLAG:住宿人物 = 0
MAIN_MAP = ARG
FOR LOCAL, 1, CHARANUM
	IF MAP_住人(MAIN_MAP, LOCAL)
		CFLAG:LOCAL:初期位置 = CSVCFLAG (LOCAL, GETNUM(CFLAG, "初期位置"))
		CFLAG:LOCAL:神社在住 = CFLAG:LOCAL:初期位置
		SIF !UP约会中
			DEBUGPRINTFORML 在%CALLNAME:LOCAL%住下来了
	ELSE
		PRIVATEROOM:(CFLAG:LOCAL:初期位置 % 100) = 0
		CFLAG:LOCAL:初期位置 = SUKIMA()
		CFLAG:LOCAL:神社在住 = 0
	ENDIF
	CFLAG:LOCAL:自宅位置 = CSVCFLAG (LOCAL, GETNUM(CFLAG, "自宅位置"))
	CFLAG:LOCAL:現在位置 = CFLAG:LOCAL:初期位置
	CFLAG:LOCAL:约会中 = MAIN_MAP
NEXT
SIF !UP约会中
	PRINTFORMW %GET_MAPNAME(ARG)%設定完毕
CALL ROOMSETTING
IF !UP约会中
	CFLAG:MASTER:初期位置 = 默认初期位置
	CFLAG:MASTER:現在位置 = CFLAG:MASTER:初期位置
	引っ越し = 0
ENDIF

@ROOMSETTING
#DIM R_ID
#DIM 私室候補
#DIM CHARA
私室候補 = 0
SUMIKOMI_ROOM = 0
;拠点住人は初期位置をPRIVATEROOMに設定,施錠の都合で先に処理
VARSET PRIVATEROOM
FOR LOCAL, 1, CHARANUM
	IF CFLAG:LOCAL:神社在住 && !CFLAG:LOCAL:出禁
		;PRIVATEROOM変数に、そこ的家主の番号を保存
		PRIVATEROOM:(CFLAG:LOCAL:初期位置 % 100) = LOCAL
	 	SIF CFLAG:LOCAL:大家候補
			私室候補 ++
	ENDIF
NEXT
TRYCCALLFORM ROOMSETTING_{MAIN_MAP}
	PRINTFORMW %GET_MAPNAME(MAIN_MAP)%的房间情報設定完毕
	;住宿人物用のワンルーム
	IF FLAG:住宿人物 && SUMIKOMI_ROOM
		R_ID = SUMIKOMI_ROOM % 100
		ROOMDATA:R_ID |= 場所_ワンルーム
	ENDIF
	FOR LOCAL, 1, 100
		;補完設定
		;MASTERの初期位置は最低限の機能を備える,飲食はできなくなる
		IF CFLAG:MASTER:初期位置 % 100 == LOCAL
			ROOMDATA:LOCAL |= 場所_被窝
			ROOMDATA:LOCAL |= 場所_学习
			ROOMDATA:LOCAL |= 場所_プライベート
			ROOMDATA:LOCAL &= ~場所_飲食
		ENDIF
		;ワンルーム
		IF ROOMDATA:LOCAL & 場所_ワンルーム
			ROOMDATA:LOCAL |= 場所_被窝
			ROOMDATA:LOCAL |= 場所_屋内
			ROOMDATA:LOCAL |= 場所_厨房
			ROOMDATA:LOCAL |= 場所_学习
			ROOMDATA:LOCAL |= 場所_休憩
			ROOMDATA:LOCAL |= 場所_飲食
			ROOMDATA:LOCAL |= 場所_プライベート
			ROOMDATA:LOCAL |= 場所_炬燵
			ROOMDATA:LOCAL &= ~場所_過疎
		ENDIF
		;被窝
		IF ROOMDATA:LOCAL & 場所_被窝
			ROOMDATA:LOCAL |= 場所_休憩
		ENDIF
		;洋室
		IF ROOMDATA:LOCAL & 場所_洋室
			ROOMDATA:LOCAL |= 場所_屋内
		ENDIF
		;厨房
		IF ROOMDATA:LOCAL & 場所_厨房
			ROOMDATA:LOCAL |= 場所_つまみ食い
		ENDIF
		;施錠チェック,初期位置が変わった場合、施錠を解錠する
		R_ID = LOCAL + MAIN_MAP * 100
		SIF LOCK:R_ID && !PRIVATEROOM:LOCAL
			LOCK:R_ID = 0
	NEXT
CATCH
	PRINTFORMW 房间情報的設定失敗了
ENDCATCH
IF (ゲーム開始処理中 || ゲーム继承処理中) || !默认初期位置 || 引っ越し
	IF 私室候補
		CALL 引っ越し初期位置設定
	ELSE
		CALL 默认引っ越し
	ENDIF
ENDIF

@默认引っ越し
TRYCCALLFORM DEFAULT_OWNER_{MAIN_MAP}
CATCH
	PRINTFORMW 房间情報的設定失敗了
ENDCATCH
PRINTFORMW 将%NAME_FROM_PLACE(默认初期位置)%設定为了%CALLNAME:MASTER%的私室

@引っ越し初期位置設定
#DIM CHARA
#DIM ANIMATION_SEED
#DIM PREV_REDRAW
ANIMATION_SEED = 0
PREV_REDRAW = CURRENTREDRAW()

;Map Animation Code
$MAPANIMATION
LINESTART = LINECOUNT
SIF ANIMATERECOLOREDMAPS && !FLAG:70
	ANIMATION_SEED ++
DRAWLINE
IF RECOLOREDMAPS
	CALL DRAW_COLOREDMAP(ANIMATION_SEED)
ELSE
	CALL DRAW_MAP
ENDIF
DRAWLINE
PRINTFORML %PRINT_COLOR("这个地图有多个", C_WHITE) + PRINT_COLOR("私室候補", C_ORANGE) + PRINT_COLOR("。", C_WHITE)%
SIF DAY <= 1
	PRINTFORML 第一天限定选项：无视必要信赖选择住的地点。
PRINTBUTTON @"[预览图切换({TFLAG:地圖切換})]", 1997
PRINTL
FOR CHARA, 1, CHARANUM
	IF CFLAG:CHARA:神社在住 && CFLAG:CHARA:大家候補
		SIF CFLAG:CHARA:居住必要信賴度 > CFLAG:CHARA:信賴度 && DAY > 1
			SETCOLOR C_L_GRAY
		PRINTBUTTON @"[{CFLAG:CHARA:大家候補 % 100, 3}] - %NAME_FROM_PLACE(CFLAG:CHARA:大家候補), 20, LEFT%　家主:%CALLNAME:CHARA, 15, LEFT%　必要信賴度:{CFLAG:CHARA:居住必要信賴度}", CHARA
		PRINTL
		RESETCOLOR
	ENDIF
NEXT
PRINTFORML [  0] - ({默认初期位置 % 100, 2})%NAME_FROM_PLACE(默认初期位置)%（默认設定）
;Map Animation Code
IF ANIMATERECOLOREDMAPS && !FLAG:70
	REDRAW PREV_REDRAW
	TINPUT ANIMATERECOLOREDMAPS,1234,0,""
ELSE
	INPUT
ENDIF

IF RESULT == 0
	CALL 默认引っ越し
ELSEIF RESULT == 1234
	PREV_REDRAW = CURRENTREDRAW()
	REDRAW 0
	CLEARLINE LINECOUNT - LINESTART
	GOTO MAPANIMATION
ELSEIF RESULT < CHARANUM && CFLAG:RESULT:神社在住 && CFLAG:RESULT:大家候補
	CHARA = RESULT
	IF CHARA != 0 && CFLAG:CHARA:居住必要信賴度 > CFLAG:CHARA:信賴度 && DAY > 1
		PRINTFORMW %CALLNAME:CHARA%还没有信頼%CALLNAME:MASTER%到这种程度…
		RESTART
	ELSE
		拠点_家主 = CHARA
		默认初期位置 = CFLAG:CHARA:大家候補
		ROOMDATA:(默认初期位置 % 100) |= 場所_被窝
		ROOMDATA:(默认初期位置 % 100) |= 場所_プライベート
		PRINTFORMW 将%NAME_FROM_PLACE(CFLAG:CHARA:大家候補)%設定为了%CALLNAME:MASTER%的私室
	ENDIF
;プレビュー切り替え
ELSEIF RESULT == 1997
	TFLAG:地圖切換 += 1
	SIF TFLAG:地圖切換 > MAP_PAGES
		TFLAG:地圖切換 = 1
	RESTART
ELSE
	RESTART
ENDIF

;-------------------------------------------------
;引っ越し下見時にROOMDATAを参照して施設の有無を表示する関数
;ARG	MAP_ID
;-------------------------------------------------
@物件データ(ARG)
#DIM MAPDATA
VARSET MAPDATA
VARSET LOCAL
PRINT 施設：
;Mapデータの参照
FOR LOCAL, 1, 100
	SIF (ROOMDATA:LOCAL & 場所_風呂) != 0
		MAPDATA |= 場所_風呂
	SIF (ROOMDATA:LOCAL & 場所_厨房) != 0
		MAPDATA |= 場所_厨房
	SIF (ROOMDATA:LOCAL & 場所_訓練) != 0
		MAPDATA |= 場所_訓練
	SIF (ROOMDATA:LOCAL & 場所_学习) != 0
		MAPDATA |= 場所_学习
	SIF (ROOMDATA:LOCAL & 場所_菜園) != 0
		MAPDATA |= 場所_菜園
	SIF (ROOMDATA:LOCAL & 場所_天之声) != 0
		MAPDATA |= 場所_天之声
NEXT
FONTBOLD
CALL COLORMESSAGE(@"【風呂】", (MAPDATA & 場所_風呂) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【厨房】", (MAPDATA & 場所_厨房) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【訓練】", (MAPDATA & 場所_訓練) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【学习】", (MAPDATA & 場所_学习) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【菜園】", (MAPDATA & 場所_菜園) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【天之声】", (MAPDATA & 場所_天之声) ? C_WHITE # C_GRAY, 0)
FONTREGULAR

;地点の参照
FOR LOCAL, MINROOM(), MAXROOM
	SIF FISHING_SPOT(LOCAL)
		LOCAL:1 = 1
	SIF IS_LIBRARY(LOCAL, 1)
		LOCAL:2 = 1
	SIF GATHERING_SPOT(LOCAL)
		LOCAL:3 = 1
	SIF PHARMACY_SPOT(LOCAL) == 2
		LOCAL:4 = 1
	SIF STALL_SPOT(LOCAL)
		LOCAL:5 = 1
	SIF PLACE_SENTO(LOCAL)
		LOCAL:6 = 1
	SIF OMIKUJI_SPOT(LOCAL)
		LOCAL:7 = 1
	SIF MORIYA_OMIKUJI(LOCAL)
		LOCAL:8 = 1
	SIF KAIDASHI_SHOP(LOCAL)
		LOCAL:9 = 1
	SIF FLOWER_SHOP(LOCAL)
		LOCAL:10 = 1
	SIF SAKE_SHOP(LOCAL)
		LOCAL:11 = 1
	SIF BOOK_RENTAL(LOCAL)
		LOCAL:12 = 1
	SIF YASAI_SHOP(LOCAL)
		LOCAL:13 = 1
	SIF CASINO_SPOT(LOCAL)
		LOCAL:14 = 1
	SIF HUNTING_SPOT(LOCAL)
		LOCAL:15 = 1
NEXT
FOR LOCAL, 1, 20
	IF LOCAL:LOCAL
		SELECTCASE LOCAL
		CASE 1
			PRINTFORM 【釣魚】
		CASE 2
			PRINTFORM 【読書】
		CASE 3
			PRINTFORM 【採集】
		CASE 4
			PRINTFORM 【調合】
		CASE 5
			PRINTFORM 【露店】
		CASE 6
			PRINTFORM 【銭湯】
		CASE 7
			PRINTFORM 【神簽】
		CASE 8
			PRINTFORM 【守矢神簽】
		CASE 9
			PRINTFORM 【食料品店】
		CASE 10
			PRINTFORM 【花屋】
		CASE 11
			PRINTFORM 【酒屋】
		CASE 12
			PRINTFORM 【借书屋】
		CASE 13
			PRINTFORM 【八百屋】
		CASE 14
			PRINTFORM 【赌场】
		CASE 15
			PRINTFORM 【罠猟】
		ENDSELECT
	ENDIF
NEXT
PRINTL
PRINT 住人：
;角色表示
FOR LOCAL, 1, CHARANUM
	IF MAP_住人(MAIN_MAP, LOCAL)
		SIF LOCAL:21
			PRINTFORM 、
		RESULT = 0
		TRYCALLFORM M_KOJO_K{LOCAL}
		PRINTFORM \@ LOCAL:20 ? 、 # \@\@ RESULT ? * # \@%CALLNAME:LOCAL%
		LOCAL:21 ++
	ENDIF
NEXT
PRINTL

;Map内の採集可能地点を表示する関数
@ShowGatheringSpots(ARG)
#DIM cStart
#DIM cEnd
#DIM SpotID
#DIM Num
#DIM Wordcount
#DIM pCount

cStart = 1 + ARG * 100
cEnd = 99 + ARG * 100
Num = 0
pCount = 0
FOR LOCAL, cStart, cEND
	SpotID = GATHERING_SPOT(LOCAL, 1)
	IF SpotID
		pCount ++
		STRLENFORM %ForagePlaceName(SpotID)%
		Wordcount = RESULT:0
		Num += Wordcount
		IF Num > 110
			PRINTL
			PRINTFORM 　　　　　　　
			Num = Wordcount
		ENDIF
		PRINTFORM %ForagePlaceName(SpotID)%
		SIF Num <= 106
			PRINTFORM 　　
	ENDIF
NEXT
SIF !pCount
	PRINT 無
PRINTFORML
