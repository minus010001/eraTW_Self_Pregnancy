;FileName_MOVEMENT.ERB ----------------------------- Rev1.00
;時間経過処理
;CALL		USER
;ARG		経過時間前
;RETURN		時間経過量
;COMMENT	
;-----------------------------------------------------------
@TIME_PROGRESS(ARG)
#FUNCTION
SIF TIME + DAY * 1440 < ARG
	RETURNF 0
RETURNF TIME + DAY * 1440 - ARG

;FileName_MOVEMENT.ERB ----------------------------- Rev1.01
;角色帰宅処理
;CALL		USER
;ARG		0:角色NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@KITAKU(ARG)
#DIMS 帰宅理由
;午前3時～午前7時
IF INRANGE(TIME, 180, 420)
	帰宅理由 = 天亮了、
;午前7時～午後3時
ELSEIF INRANGE(TIME, 421, 899)
	帰宅理由 = 差不多到時間了、
;午後3時～午後7時
ELSEIF INRANGE(TIME, 900, 1140)
	帰宅理由 = 太阳下山了、
;午後7時～午前3時
ELSE
	帰宅理由 = 已经是深夜了、
ENDIF

IF CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置 && !CFLAG:MASTER:睡眠 && CFLAG:ARG:現在位置 != CFLAG:ARG:初期位置
	CALL OneNightStay(ARG)
	SIF CFLAG:ARG:神社在住
		RETURN
	IF 睡眠時間(ARG)
		PRINTFORMW %CALLNAME:ARG%去睡眠了。
	;约会中
	ELSEIF CHK_DATENOW(CFLAG:(ARG):约会中)
		;诶嘿嘿中は非表示
		SIF !CFLAG:ARG:诶嘿嘿
			PRINTFORMW %帰宅理由%%CALLNAME:ARG%和你的约会到此为止。
		;自宅にいる時,待客室にいる時は诶嘿嘿していない時
		IF CFLAG:ARG:現在位置 == CFLAG:ARG:自宅位置 || (CFLAG:ARG:現在位置 == OMANEKIBEYA() && !CFLAG:ARG:诶嘿嘿)
			;帰宅口上,3,玄関先で見送り
			CALL KOJO_MESSAGE_SEND("EVENT", 20, ARG, 3)
		;通常の约会中
		ELSE
			;お誘い
			IF (TALENT:ARG:恋慕 || TALENT:ARG:炮友 == 2) && CFLAG:ARG:积攒度 >= (700 + TALENT:ARG:羞恥心 * 150) && !CFLAG:ARG:神社在住 && !TCVAR:ARG:连住禁止 && CFLAG:(ARG):現在位置 != OMANEKIBEYA()
				PRINTFORML 被%CALLNAME:ARG%邀請去房間
				;帰宅口上,4,部屋に来ないかと誘う
				CALL KOJO_MESSAGE_SEND("EVENT", 20, ARG, 4)
				CALL ASK_YN("去","不去")
				IF !RESULT
					IF CFLAG:ARG:自宅位置
						PRINTFORML %CALLNAME:ARG%的邀请下、%CALLNAME:MASTER%走向了%CALLNAME:ARG%的房间……
						PRINTFORMW +{TIME_REQUIRED(CFLAG:ARG:現在位置 / 100, CFLAG:ARG:自宅位置 / 100)}分
						TIME += TIME_REQUIRED(CFLAG:ARG:現在位置 / 100, CFLAG:ARG:自宅位置 / 100)
					ELSE
						PRINTFORML 应%CALLNAME:ARG%的邀请、%CALLNAME:MASTER%走向了不知具体位置的%CALLNAME:ARG%的房间……
						PRINTFORMW +30分
						TIME += 30
					ENDIF
					;帰宅口上,5,誘われるまま行く
					CALL KOJO_MESSAGE_SEND("EVENT", 20, ARG, 5, 0)
					CALL OMANEKI_ENTER(ARG)
					CALL TARGETSET_CHACK
				ELSE
					PRINTFORMW %CALLNAME:ARG%好象很寂寞似的……
					;帰宅口上,6,誘いを拒绝
					CALL KOJO_MESSAGE_SEND("EVENT", 20, ARG, 6)
					SIF (CFLAG:MASTER:現在位置 % 100) % 10 && CHK_DATENOW(CFLAG:MASTER:约会中) && FLAG:约会的对象 == ARG && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
						CFLAG:MASTER:現在位置 = CFLAG:MASTER:约会中 * 100
				ENDIF
			ELSE
				;帰宅口上,2,约会中帰宅
				CALL KOJO_MESSAGE_SEND("EVENT", 20, ARG, 2)
				SIF (CFLAG:MASTER:現在位置 % 100) % 10 && CHK_DATENOW(CFLAG:MASTER:约会中) && FLAG:约会的对象 == ARG && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
					CFLAG:MASTER:現在位置 = CFLAG:MASTER:约会中 * 100
			ENDIF
		ENDIF
		;多人约会 - 目标1回家 继续和目标2约会,同时记录好感度,下面删除了一段判定
		IF 同行人数() > 1
		;#;PRINTFORMW 多人约会判定  1 - 开始
			FOR LOCAL,1,CHARANUM
				SIF LOCAL == ARG
					CONTINUE
				IF CHK_DATENOW(CFLAG:(LOCAL):约会中)
					FLAG:约会的对象 = LOCAL
					TFLAG:约会前好感度 = CFLAG:LOCAL:好感度
					CFLAG:MASTER:约会中 = CFLAG:LOCAL:约会中
					CFLAG:MASTER:現在位置 = CFLAG:LOCAL:現在位置
				ENDIF
			NEXT
		;#;PRINTFORMW 多人约会判定  1 - 结束
		ENDIF
	;神社内にいるか自宅でも待客室でもない場所
	ELSEIF AT_HOME(ARG) || (CFLAG:(ARG):現在位置 != CFLAG:(ARG):自宅位置 && CFLAG:(ARG):現在位置 != OMANEKIBEYA()) && CFLAG:ARG:行動 != 5
		;帰宅口上,1,通常帰宅
		CALL KOJO_MESSAGE_SEND("EVENT", 20, ARG, 1)
		PRINTFORMW %帰宅理由%%CALLNAME:ARG%回去了。
		CALL SET_TOGETHER(0,ARG,1)
		SIF (CFLAG:MASTER:現在位置 % 100) % 10 && CHK_DATENOW(CFLAG:MASTER:约会中) && FLAG:约会的对象 == ARG && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
			CFLAG:MASTER:現在位置 = CFLAG:MASTER:约会中 * 100
	ENDIF
	IF CFLAG:(ARG):诶嘿嘿 > 0 || CHK_DATENOW(CFLAG:(ARG):约会中)
		;修改了大量判定
		CFLAG:ARG:约会中 = MAIN_MAP
			SIF CFLAG:MASTER:招待 != ARG && CFLAG:ARG:诶嘿嘿
				CALL ENDUFUFU(ARG)
			FLAG:约会的对象 = 0
			TFLAG:约会前好感度 = 0
		;招待诶嘿嘿中は継続
		SIF !CFLAG:MASTER:招待
			CFLAG:MASTER:诶嘿嘿 = 0
		CFLAG:MASTER:同行 = 0
		;多人约会 - 单一目标困了回家bug
		;#;PRINTFORMW 多人约会判定  2 - 开始
		FOR LOCAL,1,CHARANUM
			SIF LOCAL == ARG
				CONTINUE
			IF CHK_DATENOW(CFLAG:(LOCAL):约会中)
				FLAG:约会的对象 = LOCAL
				TFLAG:约会前好感度 = CFLAG:LOCAL:好感度
				CFLAG:MASTER:约会中 = CFLAG:LOCAL:约会中
				CFLAG:MASTER:現在位置 = CFLAG:LOCAL:現在位置
				PRINTFORML 目送%CALLNAME:ARG%远去,%CALLNAME:MASTER%决定继续和%CALLNAME:LOCAL%约会...
				CFLAG:MASTER:同行 = 180
				CFLAG:LOCAL:同行 = 180
			ENDIF
		NEXT
		;#;PRINTFORMW 多人约会判定  2 - 结束
	ENDIF
ENDIF
CFLAG:ARG:同行 = 0

IF !TCVAR:ARG:休憩中 && !CFLAG:ARG:招待
	;拠点から返回なら今日はもう来訪しない
	SIF IN_HOME(CFLAG:ARG:現在位置) && !CFLAG:MASTER:睡眠
		CFLAG:ARG:拠点来訪 = 0
	;活動時間外は自宅が那个ば寝るまで自宅にいる
	IF CFLAG:ARG:自宅位置 && !IN_HOME(CFLAG:ARG:自宅位置) && !睡眠時間(ARG)
		CFLAG:ARG:現在位置 = CFLAG:ARG:自宅位置
	;自宅がないか寝ると消える
	ELSE
		CFLAG:ARG:現在位置 = SUKIMA()
	ENDIF
ENDIF


@被从寝室赶了出来(ARG)
SIF !AT_HOME(MASTER)
	RETURN
SIF !ARG
	RETURN
SIF CFLAG:ARG:現在位置 != CFLAG:MASTER:現在位置
	RETURN
SIF !睡眠時間(ARG)
	RETURN
SIF CFLAG:ARG:現在位置 != CFLAG:ARG:初期位置
	RETURN
SIF !SHIRAHU(ARG)
	RETURN
;忍び込み中
SIF CFLAG:ARG:隠密中
	RETURN
SIF 寝室許可(ARG, 1)
	RETURN

;なんのためのCLEARLINE？
;CLEARLINE 1
PRINTFORMW 被犯困的%CALLNAME:ARG%赶出来了…
;おやすみ口上,7-1,部屋から逐出て寝た
CALL KOJO_MESSAGE_SEND("EVENT", 3, ARG, 7, 1)
;忍び込みOFF
CALL SET_SNEAK(0,ARG,2)
;恶作剧OFF
CALL SET_PRANK(0,ARG,2)
;诶嘿嘿リセット
CALL ENDUFUFU_ALL
;夜這い侵入成功フラグクリア
CLEARBIT CFLAG:ARG:夜這い, 8
;移動可能な場所へ移動
CALL GETOUT(MASTER)


@風呂から逐出(ARG)
SETCOLOR 150,150,250
PRINTL 【浴場的闖入者】
RESETCOLOR
IF TALENT:ARG:羞恥心 < 0
	IF ABL:ARG:親密 < 5
		PRINTFORML 我現在在裡面、請你給我出去、%CALLNAME:ARG%泰然自若地如此說道。
		CFLAG:ARG:好感度 -= (20 - ABL:ARG:親密 * 2)
		CFLAG:ARG:信賴度 -= (20 - ABL:ARG:親密 * 2)
		SOURCE:ARG:反感 += 300
		CFLAG:MASTER:現在位置 = KICKOUT()
	;ELSE
	;	PRINTFORML %CALLNAME:ARG%坦然的问要不要一起洗
	ENDIF
ELSE
	PRINTFORML %CALLNAME:ARG%察觉了自己所面对的状況、发出尖叫声飞奔出了浴場
	CFLAG:ARG:好感度 -= (50 - ABL:ARG:親密 * 2)
	CFLAG:ARG:信賴度 -= (50 - ABL:ARG:親密 * 2)
	SOURCE:ARG:反感 += 1000
	BASE:ARG:憤怒 += (1500 - ABL:ARG:親密 * 100)
	CFLAG:ARG:現在位置 = KICKOUT()
	;角色を動かしたのでTARGET再設定
ENDIF
CFLAG:ARG:好感度 = MAX(CFLAG:ARG:好感度,0)
CFLAG:ARG:信賴度 = MAX(CFLAG:ARG:信賴度,0)
CALL TARGETSET_CHACK
CFLAG:ARG:同室 = 0
RETURN 1


@待客室処理(ARG)
SIF FLAG:時間停止 || !CFLAG:ARG:招待
	RETURN

;待客室から出たら日常に返回
;泊まる前か後かは连住禁止フラグを参照してます
IF CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	CALL 待客室から出る(ARG)

;翌日仕事に行く場合
ELSEIF 仕事中チェック(ARG) && TCVAR:ARG:连住禁止 && CFLAG:ARG:诶嘿嘿 != 1
	CFLAG:MASTER:現在位置 = CFLAG:ARG:自宅位置
	IF CFLAG:ARG:诶嘿嘿
		PRINTFORMW %CALLNAME:ARG%好像要開始工作了…
		CALL ENDUFUFU(LOCAL)
		PRINTFORMW %CALLNAME:ARG%雖然依依不捨但還是離開了
	ELSE
		PRINTFORMW %CALLNAME:ARG%因為工作而離開了
	ENDIF
	CALL 待客室から出る(ARG)

;约会中
ELSEIF CHK_DATENOW(CFLAG:ARG:约会中)
	IF CFLAG:ARG:神社在住
		;住宿人物は睡眠時間になったら约会終了
		IF 睡眠時間(ARG)
			CFLAG:ARG:约会中 = MAIN_MAP
			FLAG:约会的对象 = 0
		ENDIF
	ELSE
		;约会中に返回時間が来たらメッセージ＆约会だけ消す
		IF !VISIT(ARG)
			CALL KITAKU(ARG)
		ENDIF
	ENDIF
ENDIF


@待客室から出る(ARG)
;遭遇フラグを自宅位置に設定（連動してると他の角色が駆けつけてくるのは仕様）
CFLAG:ARG:遭遇位置 = CFLAG:ARG:自宅位置
;初期位置を元に戻す（参道的另一边や0もいるが99でもおｋ）
IF CFLAG:ARG:神社在住
	CFLAG:ARG:初期位置 = CFLAG:ARG:神社在住
ELSE
	CFLAG:ARG:初期位置 = SUKIMA()
ENDIF
;活動時間ならそのまま约会。住宿人物は起きていれば约会
;の予定だったが蔷薇ンスブレイカーな気がする＆朝帰りは一人の方が雰囲気でるので住宿人物だけ
;活動前も配置するようになったので起床時間なら外出ように
IF CFLAG:ARG:神社在住
	CFLAG:ARG:現在位置 = 97 + MAIN_MAP * 100
	CFLAG:ARG:约会中 = MAIN_MAP
	FLAG:约会的对象 = 0
	TFLAG:约会前好感度 = 0
ELSE
	IF !睡眠時間(ARG) && !CFLAG:ARG:睡眠
		;自宅位置に移動
		CFLAG:ARG:現在位置 = CFLAG:ARG:遭遇位置
		;泊まる前or住宿人物なら约会継続,ワープ移動してたらダメ
		IF !TCVAR:ARG:连住禁止 && CHK_DATENOW(CFLAG:ARG:约会中) && !AT_HOME(MASTER)
			CFLAG:ARG:约会中 = (CFLAG:ARG:自宅位置) / 100
			CFLAG:ARG:同行 = 60
		ENDIF
	;寝ている場合は見えないところに移動
	;住宿人物は識別するために現在位置97を使ってるので要注意（MOVEMENT2のSLEEPへ続く）
	ELSE
		CFLAG:ARG:現在位置 = SUKIMA()
		CFLAG:ARG:约会中 = MAIN_MAP
		FLAG:约会的对象 = 0
		TFLAG:约会前好感度 = 0
	ENDIF
ENDIF

;招待フラグを消して日常に戻す
CFLAG:ARG:招待 = 0
CFLAG:MASTER:招待 = 0
LOCK:(OMANEKIBEYA()) = 0


@OneNightStay(ARG)
#DIM YANUSHI
#DIM REQUIRED_DESIRE
YANUSHI = PRIVATEROOM:(CFLAG:MASTER:初期位置 % 100)
SIF CFLAG:ARG:积攒度 < 800
	RETURN
SIF CFLAG:ARG:現在位置 != CFLAG:MASTER:現在位置
	RETURN
SIF FLAG:一時滞在
	RETURN
SIF CFLAG:ARG:神社在住
	RETURN
SIF !AT_HOME(MASTER)
	RETURN
REQUIRED_DESIRE = 10
SIF TALENT:ARG:恋慕 || TALENT:ARG:炮友
	REQUIRED_DESIRE -= 5
SIF TALENT:ARG:思慕
	REQUIRED_DESIRE -= 2
IF !CFLAG:ARG:既成事實 & 合意_诶嘿嘿
	REQUIRED_DESIRE += TALENT:ARG:難以越過的底線 * 2
	REQUIRED_DESIRE += TALENT:ARG:貞操 * 2
ENDIF
SIF TALENT:ARG:風騷
	REQUIRED_DESIRE = 0
SIF ABL:ARG:欲望 < REQUIRED_DESIRE
	RETURN

PRINTFORML %CALLNAME:ARG%说今天晚上不想回去…
;帰宅口上,7,今夜は帰りたくない
CALL KOJO_MESSAGE_SEND("EVENT", 20, ARG, 7)
CALL ASK_YN("同意留宿","不同意留宿")
IF !RESULT
	IF YANUSHI
		PRINTFORMW 虽然非常想让对方留下了、但是有点害怕%CALLNAME:YANUSHI%…
		GOTO REFUSAL
	ENDIF
	FLAG:一時滞在 = ARG
	CFLAG:ARG:本来の初期位置 = CFLAG:ARG:初期位置
	CFLAG:ARG:初期位置 = CFLAG:MASTER:初期位置
	CFLAG:ARG:神社在住 = CFLAG:ARG:初期位置
	PRINTFORMW %CALLNAME:ARG%在%NAME_FROM_PLACE(CFLAG:MASTER:初期位置)%留宿了
	;帰宅口上,8,泊まっていくことになる
	CALL KOJO_MESSAGE_SEND("EVENT", 20, ARG, 8)
ELSE
	$REFUSAL
	PRINTFORMW %CALLNAME:MASTER%郑重地拒绝了
	;帰宅口上,9,断った
	CALL KOJO_MESSAGE_SEND("EVENT", 20, ARG, 9)
ENDIF

@SET_OneNightStay()
