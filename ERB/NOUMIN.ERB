[SKIPSTART]
小作人用各種設定関数

カネはくれるが拘束時間で割ればすこぶる薄給、つまりフレーバーです（でも貰えると嬉しいよね）
標準の報酬係数を400として格差あり、東方くわしくないので違和感あったら変えてくらさい
450は商売やってるようなひとびと

思いつきをありったけ積んだ
よくわかんなかった人々は実装してない
追加・訂正求む

ボツネタ
・華扇の屋敷の裏山に果樹（栗、柿、梅など）
なんとなくイメージでありそう
いろいろ曖昧すぎるのでとりあえず保留
・哆来咪さんが砂糖を生産
とにかく蘇伊特なものをと思った
サトウキビか甜菜だがなんだかシュールなので保留
養蜂農家で蜂蜜を作る手もあるか
後に甜菜は琪露諾の作物となった
・妹紅or輝夜で竹林でタケノコ農家
タケノコは採れそうだがこの２人が作物を徴収しに来るイメージがしない
地主という形で他人と関わり合いになるだろうか？？？と思ってやめ
・蓮子の蓮根畑、鈴湖の林檎園
梅莉とせーらんの作物を思いつかなかった
かたっぽだけ地主設定してると違和感
・正邪でミラクルフルーツ畑
苦味＆酸味を甜と感じさせる＝ひっくり返す
大量生産するようなもんでもないようなと思ってやめ
・純狐さんの生姜畑
すごい形相で叫びながら齧ったりするの？あまりにも一発ネタすぎるので却下
・魅魔様の牧場
でも（ｒｙ


果樹では柿や蜜柑なんかがあると和風感が増すが任せられる人が思いつかなかった
このほか繊維系で綿花、麻、染料系で茜などを挙げてみる


2018/05/19
・各種拠点Mapの実装に伴ってバグってた番号を修正
・農民系のデータの記述は各地主の角色データ関数側に移動させました
　小作人パッチの構想段階で角色データ関数的な技術は考えてましたが
　関数群が中々に巨大なので小作人パッチだけのために実装するのはなぁと思ってやめてました
　作っちゃったから角色データ関数側に移動
・@STR_NOUMIN()の仕様変更
　１．STR_NOUMIN/INT_NOUMINの二種類に分けてTOINTの必要を無くした
　２．角色データ関数で管理するようになったので第一引数で渡す内容をCALLNAMEから登録番号に変更
　３．STR/INT_NOUMINの第二引数は従来街道、STR_NOUMIN(1, "説明")等で動きます
　４．角色データ関数側の記述で使ってる"農民："の部分はSTR/INT_NOUMINで勝手に付与します

[SKIPEND]
@INT_NOUMIN(C_ID, TYPE)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM  C_ID;地主の登録番号
#DIMS TYPE;取得情報の種類
RETURNF GET_INT(, "角色データ", C_ID, "農民：" + TYPE)

@STR_NOUMIN(C_ID, TYPE)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM  C_ID;地主の登録番号
#DIMS TYPE;取得情報の種類
RETURNF GET_STR(, "角色データ", C_ID, "農民：" + TYPE)



;農民初期設定関数
@SET_NOUMIN
#LOCALSIZE 1000
#LOCALSSIZE 1
#DIM LCOUNT
#DIM C_ID
#DIM LIST_ID
#DIM CHARA_COUNT
#DIM LIST_COUNT
#DIM PAGE
#DIMS NAME_地主
PRINTFORML 你是農民嗎？
PRINTFORML [1] - 是
PRINTFORML [0] - 不是
DO
	INPUT
	SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
	CASEELSE
		REUSELASTLINE 无效输入
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1

PAGE = 0
CHARA_COUNT = 0
;地主一覧の取得
FOR C_ID, 1, CHARANUM
	SIF C_ID == FLAG:扮演
		CONTINUE
	SIF !STRLENS(STR_NOUMIN(C_ID, "説明"))
		CONTINUE
	LOCAL:CHARA_COUNT = C_ID
	CHARA_COUNT ++
NEXT

FLAG:地主 = 0
FLAG:田地工作的成果 = 0
FLAG:返済延滞回数 = 0
FLAG:収穫納入回数 = 0
FLAG:累積収穫評価 = 0
FLAG:前回収穫評価 = 0


DO
	LIST_COUNT = 0
	PRINTFORML 請選擇雇用你的地主
	PRINTFORML 
	FOR LIST_ID, (PAGE * 20), CHARA_COUNT
		C_ID = LOCAL:LIST_ID
		PRINTFORM [{C_ID ,3}] - 地主：%CALLNAME:C_ID, 10%　　
		PRINTFORM %STR_NOUMIN(C_ID, "説明"), 14%　　
		PRINTFORML %STR_NOUMIN(C_ID, "田的种类")%的位置在%STR_NOUMIN(C_ID, "畑の所在地")%的附近
		LIST_COUNT ++
		SIF LIST_COUNT > 19
			BREAK
	NEXT
	PRINTFORML 
	PRINTFORML [901] - 上一頁
	PRINTFORML [902] - 下一頁
	PRINTFORML [  0] - 還是算了
	DO
		INPUT
		SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 901
			SIF PAGE > 0
				PAGE --
		CASE 902
			SIF (PAGE + 1) * 20 < CHARA_COUNT
				PAGE ++
		CASE 1 TO CHARANUM - 1
			SIF !STRLENS(STR_NOUMIN(RESULT, "説明"))
				GOTO RETRY
			FLAG:地主 = RESULT
		CASEELSE
			$RETRY
			REUSELASTLINE 无效输入
			CONTINUE
		ENDSELECT
		BREAK
	LOOP 1
LOOP !(FLAG:地主)

NAME_地主 = %CALLNAME:(FLAG:地主)%
PRINTFORML 
PRINTFORML 你成為了為%NAME_地主%種植%STR_NOUMIN(FLAG:地主, "作物")%的佃戶
PRINTFORML 你租借的%STR_NOUMIN(FLAG:地主, "田的种类")%是在%STR_NOUMIN(FLAG:地主, "畑の所在地")%的附近
PRINTFORMW 請不要偷懶每天都照料它吧

CALL MODE_NOUMIN
CALL LOAN_NOUMIN

[IF_DEBUG]
;挙動、れいあうとの確認用
;MONEY = 100000
;DO
;	SIF FLAG:農民難易度
;		CALL COST_NOUMIN
;	CALL REPAY_NOUMIN
;	WAIT
;LOOP 1
[ENDIF]

@MODE_NOUMIN
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS NAME_地主
NAME_地主 = %CALLNAME:(FLAG:地主)%
PRINTFORML 請設定農民生活的難易度
PRINTFORML （根據選擇的不同可能會導致角色形象的崩壞）
PRINTFORML 
PRINTFORML [1] - 普通農民　    　　　　%NAME_地主%是個好心的地主
PRINTFORML [2] - 硬核農民　      　　　%NAME_地主%是個貪婪的地主
PRINTFORML [3] - 極限農民          　　%NAME_地主%是個施行暴政的蛮横地主
DO
	INPUT
	IF INRANGE(RESULT, 1, 3)
		FLAG:農民難易度 = RESULT
		BREAK
	ELSE
		REUSELASTLINE 无效输入
		CONTINUE
	ENDIF
LOOP 1
SELECTCASE FLAG:農民難易度
CASE 1
	PRINTFORML 你的地主是個非常善良的好人
	FLAG:畑拡張 = 1
CASE 2
	PRINTFORML 你成爲了受地主压迫的農民
	FLAG:畑拡張 = 2
CASE 3
	PRINTFORML 你成爲了一个被地主彻底剝削的農民
	FLAG:畑拡張 = 3
ENDSELECT
PRINTFORMW （農民難易度可以在設置里変更）
SETBIT FLAG:1000, 2



;%TOSTR(LOCAL, "#,###")%
;報酬400 * 1000 = 40万円（標準金額）
@LOAN_NOUMIN
#DIMS NAME_地主
NAME_地主 = %CALLNAME:(FLAG:地主)%
LOCAL = INT_NOUMIN(FLAG:地主, "報酬") * 1000
PRINTFORML 開始农业的%CALLNAME:MASTER%收到了初期投資費用和%金額表示(LOCAL)%的借入金
FLAG:地主貸付金 = LOCAL
FLAG:年始貸付金 = LOCAL
SELECTCASE FLAG:農民難易度
CASE 1
	PRINTFORML %NAME_地主%說这是無息借款所以耐心地還就好
	IF !ITEM:園芸用品一式
		PRINTFORML %NAME_地主%还建议先去人間之里的花店買些必要的園芸用品
		PRINTFORML 看来地主不会承担购买农具的费用
	ENDIF
CASE 2, 3
	PRINTFORML 这笔贷款好好像有{INT_NOUMIN(FLAG:地主, "金利")}％的利息
	PRINTFORML %NAME_地主%还提出了借出農具
	PRINTFORML ……好像没有拒绝的权利
	;たぶんこの他に現地に無駄に色々な道具が山ほどある
ENDSELECT
WAIT

@RESET_NOUMIN
FLAG:地主 = 0
FLAG:田地工作的成果 = 0
FLAG:返済延滞回数 = 0
FLAG:収穫納入回数 = 0
FLAG:累積収穫評価 = 0
FLAG:前回収穫評価 = 0
FLAG:農民難易度 = 0
FLAG:畑拡張 = 0
FLAG:地主貸付金 = 0
FLAG:年始貸付金 = 0

@SET_APPLY_NOUMIN
SIF FLAG:農民難易度 >= 2 && !ITEM:園芸用品一式
	ITEM:園芸用品一式 ++

;農民情報表示関数
@SHOW_NOUMIN
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS NAME_地主
#DIM HATAKE
NAME_地主 = %CALLNAME:(FLAG:地主)%
HATAKE = INT_NOUMIN(FLAG:地主, "場所ID")
SIF GET_MAPID(HATAKE) != MAIN_MAP
	HATAKE = 6000 + (GET_MAP_REPLACEMENT(HATAKE) / 10)

PRINTFORML %NAME_地主%的佃户（在%STR_NOUMIN(FLAG:地主, "説明")%種植%STR_NOUMIN(FLAG:地主, "作物")%）
PRINTFORML %STR_NOUMIN(FLAG:地主, "田的种类")%在%STR_NOUMIN(FLAG:地主, "畑の所在地")%的%NAME_FROM_PLACE(HATAKE)%附近
;いいひとでも作物は全部もらいます
SELECTCASE FLAG:農民難易度
CASE 1
	PRINTFORML 地主是個非常親切的人
	SIF FLAG:地主貸付金
		PRINTFORML 借了%金額表示(FLAG:地主貸付金)%無息借款
CASE 2
	PRINTFORML 被地主欺压
	SIF FLAG:地主貸付金
		PRINTFORML %金額表示(FLAG:地主貸付金)%借款的利率是{INT_NOUMIN(FLAG:地主, "金利")}％（年利）
CASE 3
	PRINTFORML 被地主徹底壓榨
	SIF FLAG:地主貸付金
		PRINTFORML %金額表示(FLAG:地主貸付金)%借款的利率是{INT_NOUMIN(FLAG:地主, "金利")}％（月利）
ENDSELECT
PRINTFORML 


;取り立て関数
;本当は年貢でもなんでもないけどわかりやすさ優先
;口上対応とかご褒美えっちとかは仕様がわからなくて実装できない
@NENGU_NOUMIN
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 年貢の評価
PRINTFORML %CALLNAME:(FLAG:地主)%來征收本月的成果
PRINTFORMW （今月的収穫量：{FLAG:田地工作的成果}）
年貢の評価 = FLAG:田地工作的成果 / 20
LOCAL = (年貢の評価 - 3) * 5
SIF LOCAL < 0
	LOCAL = LOCAL - SQRT(MAX(1, CFLAG:(FLAG:地主):信賴度))
;(天候パッチ)寒気による作物被害が20を超えたら20毎に冷害判定
SIF AMOUNT_COLD_CROP_DAMAGE >= 20
	IS_COLD_CROP_DAMAGE ++
SIF AMOUNT_COLD_CROP_DAMAGE >= 40
	IS_COLD_CROP_DAMAGE ++
SIF AMOUNT_COLD_CROP_DAMAGE >= 60
	IS_COLD_CROP_DAMAGE ++
SELECTCASE 年貢の評価
	;(天候パッチ)冷害・風害発生時は態度が軟化
	CASE IS <= 0
		IF IS_COLD_CROP_DAMAGE > 0 || IS_STORM_CROP_DAMAGE > 0 || IS_FLOOD_CROP_DAMAGE > 0
			PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的収穫量几乎为零
			PRINTFORML %CALLNAME:(FLAG:地主)%看著惡劣天候所造成的破壞呆然了
		ELSE
			PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的収穫量几乎为零
			PRINTFORML %CALLNAME:(FLAG:地主)%用可怕的表情瞪著這邊
		ENDIF
	CASE 1
		IF IS_COLD_CROP_DAMAGE > 0 || IS_STORM_CROP_DAMAGE > 0 || IS_FLOOD_CROP_DAMAGE > 0
			PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的収穫相当少
			PRINTFORML 由於惡劣天氣所造成的損害、%CALLNAME:(FLAG:地主)%垂頭喪氣的樣子
		ELSE
			PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的収穫相当少
			PRINTFORML %CALLNAME:(FLAG:地主)%因爲收穫量太少而明顯失望的樣子
		ENDIF
	CASE 2
		IF IS_COLD_CROP_DAMAGE > 0 || IS_STORM_CROP_DAMAGE > 0 || IS_FLOOD_CROP_DAMAGE > 0
			PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的収穫比较少
			PRINTFORML 由於惡劣天氣所造成的損害、%CALLNAME:(FLAG:地主)%一臉失望的樣子
		ELSE
			PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的収穫比较少
			PRINTFORML %CALLNAME:(FLAG:地主)%因爲收穫量少而不滿意的樣子
		ENDIF
	CASE 3
		PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的収穫不够多
		PRINTFORML %CALLNAME:(FLAG:地主)%對收穫量多多少少感到不滿
	CASE 4
		PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的収穫还行
		PRINTFORML %CALLNAME:(FLAG:地主)%對收穫量感到滿意
	CASE 5
		PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的収穫比较多
		PRINTFORML %CALLNAME:(FLAG:地主)%的多出的收穫量很高興的樣子
	CASE 6
		PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的収穫相当多
		PRINTFORML %CALLNAME:(FLAG:地主)%因爲預期以上的收穫量露出了微笑
	CASEELSE
		PRINTFORML 这个月的%STR_NOUMIN(FLAG:地主, "作物")%的収穫多的吓人！
		PRINTFORML %CALLNAME:(FLAG:地主)%因爲超乎想象的收穫量折服了
ENDSELECT
WAIT

;(天候パッチ)冷害・風害発生時は怒らない
IF LOCAL < 0 && IS_COLD_CROP_DAMAGE <= 0 && IS_STORM_CROP_DAMAGE <= 0 && IS_FLOOD_CROP_DAMAGE <= 0
	PRINTFORMW %CALLNAME:(FLAG:地主)%對你抱着不信任感
	CFLAG:(FLAG:地主):信賴度 = MAX(0, CFLAG:(FLAG:地主):信賴度 + LOCAL)
	IF 年貢の評価 <= 0
		PRINTFORMW ……好像把%CALLNAME:(FLAG:地主)%惹火了
		CFLAG:(FLAG:地主):心情不快 = 1
		CFLAG:(FLAG:地主):ブチギレ = 1
		SIF MARK:(FLAG:地主):反発刻印 < 3
			MARK:(FLAG:地主):反発刻印 ++
	ENDIF
ELSEIF LOCAL > 0
	;(天候パッチ)冷害・風害発生時はより評価される
	IF IS_COLD_CROP_DAMAGE > 0 || IS_STORM_CROP_DAMAGE > 0 || IS_FLOOD_CROP_DAMAGE > 0
		PRINTFORM 儘管天氣不順但仍是豐收
		PRINTFORMW %CALLNAME:(FLAG:地主)%的信賴度大幅上升了
		LOCAL += ((IS_COLD_CROP_DAMAGE + IS_STORM_CROP_DAMAGE + (IS_FLOOD_CROP_DAMAGE * 4)) * 2)
	ELSE
		PRINTFORMW %CALLNAME:(FLAG:地主)%的信賴度上升了
	ENDIF
	CFLAG:(FLAG:地主):信賴度 += LOCAL
	IF !MARK:(FLAG:地主):反発刻印
		LOCAL = 年貢の評価 * INT_NOUMIN(FLAG:地主, "報酬")
		PRINTFORMW %CALLNAME:(FLAG:地主)%給了%金額表示(LOCAL)%的零用錢
		MONEY += LOCAL
	ENDIF
ENDIF
FLAG:田地工作的成果 = 0
FLAG:年貢 = 0
FLAG:収穫納入回数 ++
FLAG:累積収穫評価 += 年貢の評価
FLAG:前回収穫評価 = 年貢の評価
SIF FLAG:野菜娘デイリー == 2
	FLAG:野菜娘デイリー = 1
CALL REPAY_NOUMIN
SIF FLAG:農民難易度 > 1
	CALL COST_NOUMIN

;(天候パッチ)冷害発生回数と風害発生回数をリセット
IS_COLD_CROP_DAMAGE = 0
IS_STORM_CROP_DAMAGE = 0
IS_FLOOD_CROP_DAMAGE = 0
AMOUNT_COLD_CROP_DAMAGE = 0


@REPAY_NOUMIN
#DIM HENSAI
#DIM MIN_HENSAI
#DIMS NAME_地主
NAME_地主 = %CALLNAME:(FLAG:地主)%
SIF !FLAG:地主貸付金
	GOTO KANRYOU
SELECTCASE FLAG:農民難易度
CASE 1
	MIN_HENSAI = 100
CASE 2
	MIN_HENSAI = INT_NOUMIN(FLAG:地主, "報酬") * 5
CASE 3
	MIN_HENSAI = INT_NOUMIN(FLAG:地主, "報酬") * (10 + FLAG:返済延滞回数)
ENDSELECT
MIN_HENSAI = MIN(MIN_HENSAI, FLAG:地主貸付金)

PRINTFORML %NAME_地主%要求償還债务
PRINTFORM 要還多少錢？（借金額：
PRINTBUTTON 金額表示(FLAG:地主貸付金), FLAG:地主貸付金
PRINTFORM 、所持金：
PRINTBUTTON 金額表示(MONEY), MONEY
PRINTFORM 、返済額在
PRINTBUTTON 金額表示(MIN_HENSAI), MIN_HENSAI
PRINTFORML 以下時延期還款）
DO
	INPUT
	HENSAI = MIN(RESULT, FLAG:地主貸付金)
	SELECTCASE HENSAI
	CASE IS < MIN_HENSAI
		PRINTFORML %CALLNAME:MASTER%拜托%NAME_地主%可以延期还款
		IF FLAG:農民難易度 == 1
			IF MONEY < MIN_HENSAI
			;本気で金が無い時
				PRINTDATAL
					DATAFORM %NAME_地主%對這邊的生活情況真心的擔憂
					DATAFORM %NAME_地主%對這邊的生活情況真心的擔憂
					DATAFORM %NAME_地主%鼓勵着%CALLNAME:MASTER%鼓勵着
					DATAFORM %NAME_地主%安慰著%CALLNAME:MASTER%安慰起來
					DATAFORM %NAME_地主%不知道該說什麼的樣子
					DATAFORM %NAME_地主%那憐憫的視線扎了過來
					DATAFORM %NAME_地主%的表情陰沉
					DATAFORM %NAME_地主%感到為難
				ENDDATA
			ELSE
			;等一下！
				PRINTDATAL
					DATAFORM %NAME_地主%深深嘆了口氣
					DATAFORM %NAME_地主%揉着太陽穴
					DATAFORM %NAME_地主%用蔑視的眼神盯著
					DATAFORM %NAME_地主%抱着胳膊繃緊了臉
					DATAFORM %NAME_地主%那憐憫的視線扎了過來
					DATAFORM %NAME_地主%臉色僵硬
					DATAFORM %NAME_地主%皺起了眉頭
					DATAFORM %NAME_地主%的表情陰沉
					DATAFORM %NAME_地主%感到為難
				ENDDATA
				PRINTFORML 
				PRINTFORML ……看來總算是同意了
			ENDIF
			FLAG:返済延滞回数 ++
		ELSEIF MONEY >= MIN_HENSAI
			PRINTFORML %NAME_地主%打断了%CALLNAME:MASTER%的話從錢包中強行奪走了%金額表示(MIN_HENSAI)%
			MONEY -= MIN_HENSAI
		ELSE
			PRINTFORML %NAME_地主%砸了砸嘴用冷漠的眼神瞪視這邊
			FLAG:返済延滞回数 ++
		ENDIF
	CASE IS > MONEY
		$RETRY
		REUSELASTLINE 无效输入
		CONTINUE
	CASEELSE
		PRINTFORML %CALLNAME:MASTER%償還了%金額表示(HENSAI)%的借款
		MONEY -= HENSAI
		FLAG:返済延滞回数 = 0
		FLAG:地主貸付金 -= HENSAI
		IF !FLAG:地主貸付金
			IF FLAG:農民難易度 == 1
				PRINTFORML %CALLNAME:MASTER%終於還清了債
				PRINTFORML %NAME_地主%也一起感到高興
			ELSE
				PRINTFORML %CALLNAME:MASTER%還清了債
				PRINTFORML ……雖然很快就被以其他的理由強行借了钱
			ENDIF
		ELSE
			PRINTFORML 剩下的借款還有%金額表示(FLAG:地主貸付金)%……
		ENDIF
		$KANRYOU
		PRINTFORML 
		IF FLAG:農民難易度 == 1
			PRINTDATAL
				DATAFORM 在%NAME_地主%的関照下越說越投機
				DATAFORM %NAME_地主%津津有味的聽著田地工作的報告
				DATAFORM %NAME_地主%有些擔心是不是有感到困擾的事
				DATAFORM 一邊吃著%NAME_地主%帯來的點心一邊暢談
				DATAFORM %NAME_地主%認真傾聽著農田改造的請求
				DATAFORM %NAME_地主%用溫和的視線看著這邊
				DATAFORM %NAME_地主%的笑容很耀眼
			ENDDATA
		ELSE
			PRINTDATAL
				DATAFORM %NAME_地主%只有找到空隙就向著%CALLNAME:MASTER%強行推銷
				DATAFORM %NAME_地主%敷衍的聽著田地工作的報告
				DATAFORM %NAME_地主%在尋找著有沒有為錢所困的人
				DATAFORM %NAME_地主%以改善農田為名建議投資設備……沒有那些錢
				DATAFORM %NAME_地主%的視線里可以察覺到不好的氣息
				DATAFORM %NAME_地主%的笑容裏好像藏着什麼
				DATAFORM %NAME_地主%的笑容不知爲何看着心裏発慫
			ENDDATA
		ENDIF
	ENDSELECT
	BREAK
LOOP 1

IF FLAG:返済延滞回数
	LOCAL = POWER(FLAG:返済延滞回数, FLAG:農民難易度 + 1)
	LOCAL = MIN(LOCAL, CFLAG:(FLAG:地主):信賴度)
	IF LOCAL
		PRINTFORMW %CALLNAME:(FLAG:地主)%對你抱着不信任感
		CFLAG:(FLAG:地主):信賴度 -= LOCAL
	ENDIF
ENDIF



;なんだかんだ理由つけて差っ引かれる
;標準レート400で金利・返済延滞によるペナルティを除くと
;ハードコアで1万、エクストリームで3万の経費が毎月発生する
@COST_NOUMIN
#LOCALSIZE 9
#LOCALSSIZE 9
#DIMS NAME_地主
#DIMS PREV_地主
#DIM COST_RATE
#DIM LCOUNT
NAME_地主 = %CALLNAME:(FLAG:地主)%
COST_RATE = INT_NOUMIN(FLAG:地主, "報酬")
LOCAL:0 = 0
LOCAL:8 = 0
PRINTFORML 根據本月份的如下各種經費追加了借款


;金利計算
;ハードコアなら年始借入金準拠で年利計算
;エクストリームだと年始借入金・今月借入金のうち大きい方で月利計算
IF FLAG:地主貸付金 < 10000000
	SELECTCASE FLAG:農民難易度
	CASE 2
		LOCAL:1 = FLAG:年始貸付金 * INT_NOUMIN(FLAG:地主, "金利") / 1200
	CASE 3
		LOCAL:1 = MAX(FLAG:地主貸付金, FLAG:年始貸付金) * INT_NOUMIN(FLAG:地主, "金利") / 100
	ENDSELECT
ENDIF

SELECTCASE FLAG:農民難易度
CASE 2
	LOCAL:2 = FLAG:返済延滞回数 * COST_RATE
CASE 3
	LOCAL:2 = FLAG:返済延滞回数 * COST_RATE * 10
ENDSELECT

;固定値が大半なんで毎回再計算する必要はあまりない
;地主の切り替わり（と、再起動）のタイミングで判定するように
IF NAME_地主 != PREV_地主
	LOCALS:0 = 経費総額
	LOCALS:1 = 利子
	LOCALS:2 = 返済延滞費
	LOCALS:3 = 農具利用費
	LOCALS:8 = 借入金総額
	SELECTCASE FLAG:農民難易度
	CASE 2
		LOCAL:3 = COST_RATE * 5
	CASE 3
		LOCAL:3 = COST_RATE * 10
	ENDSELECT

	LOCALS:4 = 肥料購入費
	SELECTCASE FLAG:農民難易度
	CASE 2
		LOCAL:4 = COST_RATE * 5
	CASE 3
		LOCAL:4 = COST_RATE * 10
	ENDSELECT

	LOCALS:5 = 作物出荷費
	SELECTCASE FLAG:農民難易度
	CASE 2
		LOCAL:5 = COST_RATE * 5
	CASE 3
		LOCAL:5 = COST_RATE * 15
	ENDSELECT

	LOCALS:6 = 水場利用費
	SELECTCASE FLAG:農民難易度
	CASE 2
		LOCAL:6 = COST_RATE * 5
	CASE 3
		LOCAL:6 = COST_RATE * 20
	ENDSELECT

	IF STR_NOUMIN(FLAG:地主, "作物") == "蚕丝"
		LOCALS:7 = 次期卵費
	ELSE
		LOCALS:7 = 次期種籾費
	ENDIF
	
	SELECTCASE FLAG:農民難易度
	CASE 2
		LOCAL:7 = COST_RATE * 5
	CASE 3
		LOCAL:7 = COST_RATE * 20
	ENDSELECT
	PREV_地主 = %NAME_地主%
ENDIF

LOCAL:0 = SUMARRAY(LOCAL)
LOCAL:8 = LIMIT(FLAG:地主貸付金 + LOCAL:0,  0, 10000000)
FLAG:地主貸付金 = LOCAL:8
PRINTFORML ――――――――――――――――――――――――――――――――
PRINTFORML 　諸経費
PRINTFORML ――――――――――――――――――――――――――――――――
FOR LCOUNT, 1, 8
	SIF LOCAL:LCOUNT
		PRINTFORML 　　%LOCALS:LCOUNT, 12%｜　　　\\%三桁区切(LOCAL:LCOUNT),20%
NEXT
PRINTFORML ――――――――｜―――――――――――――――――――――――
PRINTFORML 　　%LOCALS:0, 12%｜　　　\\%三桁区切(LOCAL:0),20%
PRINTFORML ――――――――｜―――――――――――――――――――――――
PRINTFORM 　　%LOCALS:8, 12%｜　　　\\%三桁区切(LOCAL:8),20%
IF FLAG:地主貸付金 == 10000000
	PRINTFORML 　！　上限　！
ELSE
	PRINTFORML 
ENDIF
PRINTFORMW ――――――――――――――――――――――――――――――――


;CONFIGからの農民再設定（試作）
@RESTART_NOUMIN
#DIM LOCAL_地主
#DIM LOCAL_田地工作的成果
#DIM LOCAL_返済延滞回数
#DIM LOCAL_収穫納入回数
#DIM LOCAL_累積収穫評価
#DIM LOCAL_前回収穫評価
#DIM LOCAL_農民難易度
#DIM LOCAL_畑拡張
#DIM LOCAL_地主貸付金
#DIM LOCAL_年始貸付金
#DIMS LOCALS_元地主
#DIMS LOCALS_新地主
#DIMS LOCALS_元難易度
#DIMS LOCALS_新難易度

LOCAL_地主 = FLAG:地主
LOCAL_田地工作的成果 = FLAG:田地工作的成果
LOCAL_返済延滞回数 = FLAG:返済延滞回数
LOCAL_収穫納入回数 = FLAG:収穫納入回数
LOCAL_累積収穫評価 = FLAG:累積収穫評価
LOCAL_前回収穫評価 = FLAG:前回収穫評価
LOCAL_農民難易度 = FLAG:農民難易度
LOCAL_畑拡張 = FLAG:畑拡張
LOCAL_地主貸付金 = FLAG:地主貸付金
LOCAL_年始貸付金 = FLAG:年始貸付金
LOCALS_元地主 = %CALLNAME:(FLAG:地主)%

IF FLAG:地主
	PRINTFORML %CALLNAME:MASTER%是%LOCALS_元地主%手下的打工農民
	SIF FLAG:地主貸付金 > 0
		PRINTFORML 残余的借金是\\{FLAG:地主貸付金}
	SELECTCASE FLAG:農民難易度
	CASE 1
		LOCALS_元難易度 = 普通
	CASE 2
		LOCALS_元難易度 = 硬核
	CASE 3
		LOCALS_元難易度 = 極限
	ENDSELECT
ELSE
	PRINTFORML %CALLNAME:MASTER%是无债一身轻的自由人
ENDIF

PRINTFORML 要変更農家的設定吗？
PRINTFORML [1] - 是
PRINTFORML [0] - 不是
DO
	INPUT
	SELECTCASE RESULT
	CASE 0
		PRINTFORML 
		RETURN
	CASE 1
	CASEELSE
		REUSELASTLINE 无效输入
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1

DO
	CALL RESET_NOUMIN
	CALL SET_NOUMIN
	PRINTFORML 

	LOCALS_新地主 = %CALLNAME:(FLAG:地主)%
	IF LOCAL_地主 && FLAG:地主 && LOCAL_地主 != FLAG:地主
		PRINTFORML %CALLNAME:MASTER%从%LOCALS_元地主%手下離開了、今後在%LOCALS_新地主%手下打工
		IF LOCAL_地主貸付金 > 0
			PRINTFORML %LOCALS_元地主%的借款\\{LOCAL_地主貸付金}被%LOCALS_新地主%接管了
			FLAG:地主貸付金 += LOCAL_地主貸付金
		ENDIF
		PRINTFORML 向%LOCALS_新地主%借款合計为\\{FLAG:地主貸付金}

	ELSEIF LOCAL_地主 && FLAG:地主 && LOCAL_地主 == FLAG:地主
		; 雇い主の変更がないので元に戻す
		SETCOLOR C_YELLOW
		PRINTFORML 因为雇主没有変更、上述借款没有発生
		SIF LOCAL_地主貸付金 > 0
			PRINTFORML %LOCALS_新地主%的借款现在仍是\\{LOCAL_地主貸付金}
		RESETCOLOR
		FLAG:田地工作的成果 = LOCAL_田地工作的成果
		FLAG:返済延滞回数 = LOCAL_返済延滞回数
		FLAG:収穫納入回数 = LOCAL_収穫納入回数
		FLAG:累積収穫評価 = LOCAL_累積収穫評価
		FLAG:前回収穫評価 = LOCAL_前回収穫評価
		FLAG:地主貸付金 = LOCAL_地主貸付金
		FLAG:年始貸付金 = LOCAL_年始貸付金

		; 難易度が変更されたか
		IF LOCAL_農民難易度 != FLAG:農民難易度
			SELECTCASE FLAG:農民難易度
			CASE 1
				LOCALS_新難易度 = 普通
			CASE 2
				LOCALS_新難易度 = 硬核
			CASE 3
				LOCALS_新難易度 = 極限
			ENDSELECT
			PRINTFORML 難易度从[%LOCALS_元難易度%]変更为了[%LOCALS_新難易度%]
		ELSE
			PRINTFORML 難易度的設定没有変更
		ENDIF

	ELSEIF !LOCAL_地主 && !FLAG:地主
		PRINTFORML 設定保持不変
	ELSEIF LOCAL_地主 && !FLAG:地主
		IF LOCAL_地主貸付金 > 0
			PRINTFORML 没有还清债务的时候不能辞掉农民的工作
			CONTINUE
		ENDIF
		PRINTFORML 辞去农民的工作变成了自由人
	ENDIF

	PRINTFORML 
	PRINTFORML 是否更改設定？
	PRINTFORML [1] - 是
	PRINTFORML [2] - 重新决定
	PRINTFORML [0] - 不更改
	DO
		INPUT
		SELECTCASE RESULT
		CASE 0
			FLAG:地主 = LOCAL_地主
			FLAG:田地工作的成果 = LOCAL_田地工作的成果
			FLAG:返済延滞回数 = LOCAL_返済延滞回数
			FLAG:収穫納入回数 = LOCAL_収穫納入回数
			FLAG:累積収穫評価 = LOCAL_累積収穫評価
			FLAG:前回収穫評価 = LOCAL_前回収穫評価
			FLAG:農民難易度 = LOCAL_農民難易度
			FLAG:畑拡張 = LOCAL_畑拡張
			FLAG:地主貸付金 = LOCAL_地主貸付金
			FLAG:年始貸付金 = LOCAL_年始貸付金
			PRINTFORML 取消更改设定
			PRINTFORML 
			RETURN
		CASE 1
			PRINTFORML 設定已更改
			PRINTFORML 
			RETURN
		CASE 2
		CASEELSE
			REUSELASTLINE 无效输入
			CONTINUE
		ENDSELECT
		BREAK
	LOOP 1
LOOP 1

