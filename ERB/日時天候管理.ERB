;FileName_COMMON.ERB ------------------------------- Rev1.00
;季節管理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHANGE_MONTH
#DIM 神簽代
;日期が月を跨いだら月変更
IF DAY:3 > 31
	DAY:3 = 1
	DAY:2 ++
	;冬の次は春
	IF DAY:2 > 4
		DAY:2 = 1
		FLAG:年始貸付金 = FLAG:地主貸付金
		;(天候パッチ)長期予報を行う
		CALL WEATHER_FORECAST_1YEAR
	ENDIF
	;夏になったらバレンタインフラグリセット
	SIF DAY:2 == 2
		CVARSET CFLAG, 383
	;徴収発生
	SIF FLAG:地主
		FLAG:年貢 = 1
	IF FLAG:澆水代行
		FLAG:澆水代行 = 0
		PRINTFORMW 澆水代理的期限到期了。
	ENDIF
	IF FLAG:新聞購読
		IF MONEY < 3000 && MONEY:2 < 10
			PRINTFORML 因爲缺錢所以文文。新聞的訂閱停止了。
		ELSE
			PRINTL
			PRINTFORML ★繼續訂閱文文。新聞嗎？（月額￥1500 or 5籌碼）
			CALL ASK_M("解約",1,"更新（￥1500）",MONEY >= 1500,"更新（5筹码）",MONEY:2 >= 5)
			SELECTCASE RESULT
				CASE 0
					PRINTFORML 停止了訂閱。
					FLAG:新聞購読 = 0
				CASE 1
					MONEY -= 1500
					PRINTFORML 本月繼續訂閱。
				CASE 2
					MONEY:2 -= 5
					PRINTFORML 本月繼續訂閱。
			ENDSELECT
		ENDIF
	ENDIF
	IF FLAG:神簽契約
		神簽代 = 50
		SIF !MAIN_MAP
			神簽代 = 30
		IF MONEY:2 < 神簽代
			PRINTFORML 因為籌碼不足、所以神签契約被中止了
			FLAG:神簽契約 = 0
		ELSE
			PRINTFORML 要繼續神签契約吗？
			CALL ASK_YN
			IF !RESULT
				PRINTFORML 更新了契約
				MONEY:2 -= 神簽代
			ELSE
				FLAG:神簽契約 = 0
				PRINTFORML 結束了契約
			ENDIF
		ENDIF
	ENDIF
	IF FLAG:蟲の採集サービス
		PRINTFORMDL
		PRINTFORMW 虫采集服务的合同到期了。
		PRINTFORMW 更新申请吗?
		PRINTFORML 所持金:%金額表示(MONEY)%
		LOCAL = CALCULATE_IFS_PRICE()
		CALL ASK_M("不更新",1,@"更新 (￥{LOCAL})",MONEY >= LOCAL)
		IF RESULT > 0
			MONEY -= LOCAL
			PRINTFORMW 契約更新
			PRINTFORMDL
			PRINTFORML 现在的素材收集地点是%GET_MAPNAME(FLAG:蟲の採集サービス - 1)%。
			PRINTFORMW 如果想要改变的话，就和%CALLNAME:[[莉格露]]%打招呼吧。
		ELSE
			FLAG:蟲の採集サービス = 0
		ENDIF
		PRINTFORMDL
	ENDIF
	SIF ENCOUNTDLC
		CALL SP_ENCOUNT
ENDIF
;異常値を固定修正
SIF DAY:3 < 1
	DAY:3 = 1
SIF DAY:2 < 1 || DAY:2 > 4
	DAY:2 = 1

;FileName_COMMON.ERB ------------------------------- Rev1.00
;時間帯管理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHANGE_TIMEZONE
#DIM 現在時間帯

;SELECTCASE TIME
;	CASE 240 TO 599
;		LOCAL = 1
;	CASE 600 TO 1139
;		LOCAL = 2
;	CASE 1140 TO 1440 , 0 TO 239
;		LOCAL = 3
;ENDSELECT

SELECTCASE TIME
	;AM4~6　早朝
	CASE 240 TO 359
		LOCAL = 1
	;AM6~12　正午前
	CASE 360 TO 719
		LOCAL = 2
	;PM12~3　昼
	CASE 720 TO 899
		LOCAL = 3
	;PM3~6　昼下がり
	CASE 900 TO 1079
		LOCAL = 4
	;PM6~8　夕
	CASE 1080 TO 1199
		LOCAL = 5
	;PM8~11　夜
	CASE 1200 TO 1379
		LOCAL = 6
	;PM11~AM4　深夜
	CASE 0 TO 239, IS >= 1380
		LOCAL = 7
ENDSELECT

;(天候パッチ)天候システム上の時間帯管理
SELECTCASE TIME
	;AM0~3　未明
	CASE 0 TO 179
		現在時間帯 = 1
	;AM3~6　明け方
	CASE 180 TO 359
		現在時間帯 = 2
	;AM6~9　朝
	CASE 360 TO 539
		現在時間帯 = 3
	;AM9~12　昼前
	CASE 540 TO 719
		現在時間帯 = 4
	;PM0~3　昼過ぎ
	CASE 720 TO 899
		現在時間帯 = 5
	;PM3~6　夕方
	CASE 900 TO 1079
		現在時間帯 = 6
	;PM6~9　夜のはじめ頃
	CASE 1080 TO 1259
		現在時間帯 = 7
	;PM9~12　夜遅く
	CASE IS >= 1260
		現在時間帯 = 8
ENDSELECT

;(天候パッチ)現在的天候傾向を判定
CALL CHANGE_WEATHER_SYSTEM

IF LOCAL != TIME:2
	TIME:2 = LOCAL
	TRYCALLFORM MOB_PLACE_{MAIN_MAP}
	CALL TIMEZONE_MOVE
ENDIF

;(天候パッチ)天候システム上の時間帯変動・天候変化
IF WEATHER_TIMEZOME != 現在時間帯
	WEATHER_TIMEZOME = 現在時間帯
	CALL CHANGE_WEATHER
ENDIF
;(天候パッチ)時刻・天候に応じて背景色を変更
CALL SET_MAP_WEATHER_BGCOLOR

;FileName_COMMON.ERB ------------------------------- Rev1.00
;季節名取得
;CALL		USER
;ARG		引数がない場合DAY:2
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@GET_MONTH(ARG)
#FUNCTIONS
SIF !ARG
	ARG = DAY:2
SIF !STRLENS(LOCALS)
	SPLIT "無之月/春之月/夏之月/秋之月/冬之月", "/", LOCALS
RETURNF LOCALS:(ARG)

;FileName_COMMON.ERB ------------------------------- Rev1.00
;天気名取得
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@GET_WEATHER
#FUNCTIONS
#DIMS 表示_異常気象,15
;(天候パッチ)台風・猛吹雪は表示しない
IF WEATHERDLC == 1
	IF FLAG:異常気象
		SPLIT " /Ｃ敏感/Ｖ敏感/Ａ敏感/Ｂ敏感/Ｍ敏感/裙子/胖次/桃色の風/台風/猛吹雪/钻石尘/黄沙/烈日/极光", "/", 表示_異常気象
		RETURNF 表示_異常気象:(FLAG:異常気象)
	ENDIF
	SIF !STRLENS(LOCALS)
		SPLIT "晴天/晴朗/多雲/陰天/雨/大雨/霧雨/霧/雪/吹雪/細雪/霧雪/雨雪交加/冰雹/豪雨/ひょう/太阳雨/疏雨/风雨/晴岚/川雾/无风/梅雨", "/", LOCALS
ELSE
	IF FLAG:異常気象 && FLAG:異常気象 != 9 && FLAG:異常気象 != 10
		SPLIT " /Ｃ敏感/Ｖ敏感/Ａ敏感/Ｂ敏感/Ｍ敏感/裙子/胖次/桃色の風/台風/猛吹雪", "/", 表示_異常気象
		RETURNF 表示_異常気象:(FLAG:異常気象)
	ENDIF
	;(天候パッチ)豪雨・ひょうを追加
	SIF !STRLENS(LOCALS)
		SPLIT "晴天/晴朗/多雲/陰天/雨/大雨/霧雨/霧/雪/大雪/細雪/霧雪/雨雪交加/冰雹/豪雨/ひょう", "/", LOCALS
ENDIF
RETURNF LOCALS:(TIME:5)

;-------------------------------------------------
;平日判定(ARG)
;DAYを渡して月～金曜日なら1
;-------------------------------------------------
@IS_WEEKDAY(ARG)
#FUNCTION
SIF !ARG
	ARG = DAY
RETURNF INRANGE(ARG % 7, 1, 5)

;-------------------------------------------------
;週末判定(ARG)
;DAYを渡して土日なら1
;-------------------------------------------------
@IS_WEEKEND(ARG)
#FUNCTION
SIF !ARG
	ARG = DAY
RETURNF !INRANGE(ARG % 7, 1, 5)

;-------------------------------------------------
;曜日一致判定(ARG)
;DAYと文字列を渡し、一致していれば1
;判定用文字列は日月火水木金土で各曜日を表す（＝複数指定可能）
;-------------------------------------------------
@YOUBI_MATCH(ARG, CHECK_STR)
#FUNCTION
#DIMS  CHECK_STR
SIF !ARG
	ARG = DAY
SIF STRCOUNT(CHECK_STR, GET_DAY(ARG))
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;関数名:GET_DAY
;概　要:曜日を表す文字列を取得する関数
;引　数:引数がない場合DAY
;戻り値:曜日を表す文字列(日/月/火/水/木/金/土)
;備　考:文中関数
;-------------------------------------------------
@GET_DAY(ARG)
#FUNCTIONS
#DIMS CONST YOUBI = "日", "月", "火", "水", "木", "金", "土"
SIF !ARG
	ARG = DAY
RETURNF YOUBI:(ARG % 7)

;FileName_COMMON.ERB ------------------------------- Rev1.00
;天気管理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHANGE_WEATHER
#DIM 地底
#DIM 入手胖次
#DIM 持ち主
#DIM ループ開始
#DIM ループ終了
#DIM ループ加算
DEBUGPRINTL 【CHANGE_WEATHER】CALL CHANGE_WATHER
;天候管理にメッセージ表示処理させてすみましぇん＞＜；
;诶嘿嘿中は読取周圍環境程度的能力
;天候変化しなくても虹は消える
地底 = 0
IF CFLAG:MASTER:约会中 == 9
	地底 = 1
ELSEIF CFLAG:MASTER:現在位置 == OMANEKIBEYA() && GROUPMATCH(CFLAG:MASTER:招待, 36, 37, 38, 49, 60, 64, 83, 84)
	地底 = 1
ENDIF
TIME:7 = 0
IF !地底
	SELECTCASE FLAG:異常気象
		CASE 7
			;こうでもしないとまた没穿内裤が飛んできてしまうと思うの
			DO
				CALL RAND_CHARASELECT("主人公以外的角色列表")
				持ち主 = RESULT
				入手胖次 = 今天穿的内裤(持ち主)
			LOOP !入手胖次
			SIF CFLAG:持ち主:(入手胖次 + 胖次管理) == 0
				SETCOLOR C_PINK
			PRINTFORML 强风把%CALLNAME:持ち主%的%PANTSNAME(入手胖次,持ち主)%吹过来了
			RESETCOLOR
			CALL ASK_YN("撿起","不撿")
			IF !RESULT
				PRINTFORMW %CALLNAME:MASTER%撿到了%CALLNAME:持ち主%的%PANTSNAME(入手胖次,持ち主)%
				CALL PANTS_GETTING(持ち主, 入手胖次)
			ELSE
				PRINTFORML %PANTSNAME(入手胖次,持ち主)%就这様飞到不知道哪里去了
			ENDIF
		CASE 8
			SETCOLOR 255,100,255
			FONTBOLD
			PRINTFORMW 幻想乡吹起了桃色之风！
			FOR LOCAL,1,CHARANUM
				IF !RAND:8 && !TALENT:LOCAL:妊娠
					PRINTFORML 沐浴在桃色之风的%CALLNAME:LOCAL%产生了奇怪的感觉
					CFLAG:LOCAL:积攒度 += LIMIT((10 + RAND:20 + RAND:20 + RAND:20) * ABL:LOCAL:欲望,200,1000)
					SIF !TALENT:LOCAL:人形
						TCVAR:LOCAL:発情 = 1
				ENDIF
			NEXT
			RESETCOLOR
			FONTREGULAR
	ENDSELECT
ENDIF

SIF FLAG:異常気象 >= 1 && FLAG:異常気象 <= 8
	RETURN

SIF TIME:5 > 15
	TIME:5 = 0
	
;(天候パッチ)天候変更禁止ならば処理終了
SIF FORBIDDEN_CHANGE_WEATHER == 1
	RETURN
	
;(天候パッチ)新しい天候管理システムへ
TRYCALLFORM CHANGE_WEATHER_SYSTEM_{NOW_WEATHER_CONTROL}

[SKIPSTART]
@現在気温設定
;高低差を５分割
SELECTCASE TIME
	;PM11~AM4
	CASE 0 TO 299, IS >= 1380
		LOCAL = FLAG:62
	;AM5~7
	CASE 300 TO 479
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5)
	;AM8~9
	CASE 480 TO 599
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 2)
	;AM10~11
	CASE 600 TO 719
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 3)
	;PM12~2
	CASE 720 TO 899
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 4)
	;PM3~5
	CASE 900 TO 1079
		LOCAL = FLAG:61
	;PM6~7
	CASE 1080 TO 1199
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 4)
	;PM8~9
	CASE 1200 TO 1259
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 2)
	;PM9~10
	CASE 1260 TO 1379
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5)
ENDSELECT
;日が出ていないと温度低下
SELECTCASE TIME
	;AM5~PM7
	CASE 300 TO 1199
		SIF TIME:5 > 1
			LOCAL -= ((FLAG:61 - FLAG:62) / 5)
ENDSELECT
;----------------------------------------------------------------
@年間基礎最高気温(ARG, ARG:1)
#FUNCTION
;ARG   指定の月
;ARG:1 指定の日
SELECTCASE ARG
	CASE 1
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 17
			CASE 4, 5
				RETURNF 18
			CASE 6 TO 8
				RETURNF 19
			CASE 9 TO 11
				RETURNF 20
			CASE 12 TO 14
				RETURNF 21
			CASE 15 TO 18
				RETURNF 22
			CASE 19 TO 22
				RETURNF 23
			CASE 23 TO 27
				RETURNF 24
			CASE 28 TO 30
				RETURNF 25
		ENDSELECT
	CASE 2
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 25
			CASE 3 TO 7
				RETURNF 26
			CASE 8 TO 13
				RETURNF 27
			CASE 14 TO 17
				RETURNF 28
			CASE 18 TO 22
				RETURNF 29
			CASE 23 TO 28
				RETURNF 30
			CASE 29, 30
				RETURNF 31
		ENDSELECT
	CASE 3
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 31
			CASE 3, 4
				RETURNF 30
			CASE 5 TO 7
				RETURNF 29
			CASE 8 TO 10
				RETURNF 28
			CASE 11, 12
				RETURNF 27
			CASE 13, 14
				RETURNF 26
			CASE 15, 16
				RETURNF 25
			CASE 17 TO 19
				RETURNF 24
			CASE 20 TO 22
				RETURNF 23
			CASE 23, 24
				RETURNF 22
			CASE 25 TO 27
				RETURNF 21
			CASE 28, 29
				RETURNF 20
			CASE 30
				RETURNF 19
		ENDSELECT
	CASE 4
		SELECTCASE ARG:1
			CASE 1 TO 4
				RETURNF 19
			CASE 5 TO 7
				RETURNF 18
			CASE 8 TO 10
				RETURNF 17
			CASE 11, 12
				RETURNF 16
			CASE 13, 14
				RETURNF 15
			CASE 15, 16
				RETURNF 14
			CASE 17, 18
				RETURNF 13
			CASE 19, 20
				RETURNF 12
			CASE 21, 22
				RETURNF 11
			CASE 23 TO 25
				RETURNF 10
			CASE 26, 27
				RETURNF 9
			CASE 28, 29
				RETURNF 8
			CASE 30
				RETURNF 7
		ENDSELECT
	CASE 5
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 7
			CASE 4 TO 9
				RETURNF 6
			CASE 10 TO 14
				RETURNF 5
			CASE 15 TO 21
				RETURNF 4
			CASE 22 TO 28
				RETURNF 5
			CASE 29, 30
				RETURNF 6
		ENDSELECT
	CASE 6
		SELECTCASE ARG:1
			CASE 1 TO 6
				RETURNF 6
			CASE 7 TO 12
				RETURNF 7
			CASE 13, 14
				RETURNF 8
			CASE 15, 16
				RETURNF 9
			CASE 17, 18
				RETURNF 10
			CASE 19, 20
				RETURNF 11
			CASE 21, 22
				RETURNF 12
			CASE 23, 24
				RETURNF 13
			CASE 25, 26
				RETURNF 14
			CASE 27, 28
				RETURNF 15
			CASE 29
				RETURNF 16
			CASE 30
				RETURNF 17
		ENDSELECT
ENDSELECT

;----------------------------------------------------------------
@年間基礎最低気温(ARG, ARG:1)
#FUNCTION
;ARG   指定の月
;ARG:1 指定の日
SELECTCASE ARG
	CASE 1
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 11
			CASE 4 TO 6
				RETURNF 12
			CASE 7, 8
				RETURNF 13
			CASE 9 TO 11
				RETURNF 14
			CASE 12 TO 14
				RETURNF 15
			CASE 15 TO 17
				RETURNF 16
			CASE 18 TO 20
				RETURNF 17
			CASE 21 TO 23
				RETURNF 18
			CASE 24 TO 27
				RETURNF 19
			CASE 28 TO 30
				RETURNF 20
		ENDSELECT
	CASE 2
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 20
			CASE 4 TO 7
				RETURNF 21
			CASE 8 TO 10
				RETURNF 22
			CASE 11 TO 14
				RETURNF 23
			CASE 15 TO 22
				RETURNF 24
			CASE 23 TO 30
				RETURNF 25
		ENDSELECT
	CASE 3
		SELECTCASE ARG:1
			CASE 1 TO 5
				RETURNF 25
			CASE 6 TO 9
				RETURNF 24
			CASE 10 TO 12
				RETURNF 23
			CASE 13, 14
				RETURNF 22
			CASE 15 TO 17
				RETURNF 21
			CASE 18 TO 20
				RETURNF 20
			CASE 21 TO 23
				RETURNF 19
			CASE 24, 25
				RETURNF 18
			CASE 26
				RETURNF 17
			CASE 27, 28
				RETURNF 16
			CASE 29
				RETURNF 15
			CASE 30
				RETURNF 14
		ENDSELECT
	CASE 4
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 14
			CASE 4, 5
				RETURNF 13
			CASE 6, 7
				RETURNF 12
			CASE 8, 9
				RETURNF 11
			CASE 10, 11
				RETURNF 10
			CASE 12 TO 14
				RETURNF 9
			CASE 15 TO 17
				RETURNF 8
			CASE 18 TO 20
				RETURNF 7
			CASE 21 TO 23
				RETURNF 6
			CASE 24 TO 26
				RETURNF 5
			CASE 27 TO 29
				RETURNF 4
			CASE 30
				RETURNF 3
		ENDSELECT
	CASE 5
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 3
			CASE 3 TO 6
				RETURNF 2
			CASE 7 TO 10
				RETURNF 1
			CASE 11 TO 13
				RETURNF 0
			CASE 14 TO 21
				RETURNF -1
			CASE 22 TO 30
				RETURNF 0
		ENDSELECT
	CASE 6
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 0
			CASE 4 TO 6
				RETURNF 1
			CASE 7, 8
				RETURNF 2
			CASE 9, 10
				RETURNF 3
			CASE 11 TO 14
				RETURNF 4
			CASE 15, 16
				RETURNF 5
			CASE 17, 18
				RETURNF 6
			CASE 19, 20
				RETURNF 7
			CASE 21 TO 23
				RETURNF 8
			CASE 24, 25
				RETURNF 9
			CASE 26 TO 28
				RETURNF 10
			CASE 29, 30
				RETURNF 11
		ENDSELECT
ENDSELECT
[SKIPEND]

;定時遭遇位置再設定
@TIMEZONE_MOVE
#DIMS 移動前
#DIMS 移動後
FOR LOCAL,1,CHARANUM
	SIF 出現状態(LOCAL) != 1
		CONTINUE
	SIF !CHARA_HOLIDAY(LOCAL)
		CONTINUE
	SIF CFLAG:LOCAL:招待
		CONTINUE
	SIF CFLAG:LOCAL:同行 || CHK_DATENOW(CFLAG:LOCAL:约会中)
		CONTINUE
	SIF !CFLAG:LOCAL:前一次的位置
		CONTINUE
	SIF CFLAG:LOCAL:睡眠
		CONTINUE
	;你と同じ場所にいるときは確率が下がる
	IF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:現在位置
		IF TALENT:LOCAL:恋慕
			;恋慕は5分の1
			SIF RAND:5
				CONTINUE
		ELSEIF TALENT:LOCAL:思慕
			;思慕は4分の1
			SIF RAND:4
				CONTINUE
		ELSE
			;それ以外は3分の1
			SIF RAND:3
				CONTINUE
		ENDIF
	ELSE
		;見てない場所なら2分の1
		SIF RAND:2
			CONTINUE
	ENDIF
	CALL 遭遇フラグ決定(LOCAL)
NEXT
;全角色決定後、連動角色を設定する
FOR LOCAL, 1, CHARANUM
	VARSET RESULT
	CALLFORM CHARAMOVE_DATA_{LOCAL}(1)
	;依存連動
	IF RESULT
		CFLAG:LOCAL:遭遇位置 = CFLAG:RESULT:遭遇位置
	;対等連動
	ELSEIF RESULT:1
		;連動設定する
		CFLAG:LOCAL:遭遇位置 = CFLAG:連動角色設定(RESULT:1, RESULT:2, RESULT:3):遭遇位置
	ENDIF
NEXT
