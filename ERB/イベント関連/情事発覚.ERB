;FileName_情事発覚.ERB --------------------------------- Rev1.00
;情事の発覚
;CALL		USER
;ARG		0:入室角色NO, 1:発覚状況(0:同室, 1:覗き)
;RETURN		0:同室, 1:立ち去る
;COMMENT	
;-----------------------------------------------------------
@AFFAIR_DISCLOSURE(ARG, NOZOKI)
#DIM PREV_TARGET
#DIM IRAI_ABLE
#DIM NOZOKI
#DIM FORCE_STOP
#DIM FORCE_GETOUT
#DIM WITNESSED
#DIM HAPPENING_TYPE

VARSET LOCAL
FORCE_STOP = 0
FORCE_GETOUT = 0
HAPPENING_TYPE = 0
WITNESSED = TFLAG:現在的TARGET
SIF CFLAG:MASTER:恶作剧
	WITNESSED = TARGET
IF WITNESSED && (CFLAG:(WITNESSED):诶嘿嘿 || CFLAG:(WITNESSED):恶作剧) && !GETBIT(FLAG:潜伏,0)
	DRAWLINE
	IF CFLAG:(WITNESSED):恶作剧
		PRINTFORMW 対睡在地上的%CALLNAME:ARG%惡作劇的時候被%CALLNAME:(WITNESSED)%看到了！
		IF FLAG:催眠強度 >= 75
			PRINTFORMW %CALLNAME:MASTER%一边说着这是很正常的按摩行为一边发动催眠。
			PRINTFORMW %CALLNAME:ARG%呆愣了一会，有些犹豫的接受了%CALLNAME:MASTER%的解释。
			PRINTL
			PRINTFORMW 真刺激
			PRINTL
		ENDIF
		IF FLAG:催眠強度 > 0
			IF !CFLAG:ARG:掌握把柄 && CFLAG:(WITNESSED):恶作剧 == 2 && FLAG:催眠強度 <= 74
				PRINTFORMW %CALLNAME:MASTER%慌张的一边表示这是很正常的按摩行为，一边发动催眠。
				PRINTFORMW %CALLNAME:ARG%不信！被%CALLNAME:ARG%掌握住了把柄！
				PRINTL
				PRINTFORMW 可恶，催眠深度不够！
				PRINTL
			ELSE
				PRINTFORMW %CALLNAME:ARG%露出了呆愣的表情
			ENDIF
		ELSE
			IF !CFLAG:ARG:掌握把柄 && CFLAG:(WITNESSED):恶作剧 == 2 
				PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
			ELSE
				PRINTFORMW %CALLNAME:ARG%露出了呆愣的表情
			ENDIF
		ENDIF
		CALL TOUCH_SET(0,0,0,1)
		DRAWLINE
		FORCE_STOP = 1
		SIF FLAG:催眠強度 <= 74
		CFLAG:ARG:掌握把柄 += 10
	ELSEIF TFLAG:102 == 3 && TFLAG:抱住模式 
		PRINTFORMW 和%CALLNAME:(TFLAG:現在的TARGET)%调情的时候被%CALLNAME:ARG%看到了！
		PRINTFORMW %CALLNAME:(TFLAG:現在的TARGET)%遺憾的從%CALLNAME:MASTER%身上離開…
		CALL UWAKI_RECORD(ARG,0,TFLAG:現在的TARGET)
		TFLAG:抱住模式 = 0
		TFLAG:102 = 1
		CALL 被逆推終了	
	ELSEIF FLAG:催眠強度 >= 5
			PRINTFORMW %CALLNAME:ARG%看到了和%CALLNAME:(TFLAG:現在的TARGET)%的情事現場！
			PRINTFORMW 由于%CALLNAME:ARG%发动了催眠术，所以并不十分慌张…
			PRINTFORMW %CALLNAME:ARG%要怎么解决目前的局面？
			IF !NOZOKI
				PRINTL [0] 一并推倒
				PRINTL [1] 认真解释
				$INPUT_EMBRACE
				INPUT
				IF RESULT == 0
					IF FLAG:催眠強度 >= 95
						PRINTFORMW %CALLNAME:MASTER%告诉%CALLNAME:ARG%这是最新的放松按摩方法。
						PRINTFORMW %CALLNAME:ARG%露出了恍然大悟的神情，好奇的靠了过来
						CFLAG:ARG:诶嘿嘿 = CFLAG:(WITNESSED):诶嘿嘿
						CFLAG:ARG:同室 = 1
						CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,4)
						CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,4)
					ELSEIF FLAG:催眠強度 >= 75
						PRINTFORMW %CALLNAME:MASTER%邀请%CALLNAME:ARG%体验能让她特别舒服的按摩手法
						PRINTFORMW %CALLNAME:ARG%红着脸看了一会就离开了現場
						CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
						CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
						IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
							CALL GETOUT(ARG)
						ELSE
							CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
						ENDIF
						;角色を動かしたのでTARGET再設定
						CALL TARGETSET_CHACK
						CFLAG:ARG:同室 = 0
						RETURN 1
					ELSE
						PRINTFORMW %CALLNAME:MASTER%邀请%CALLNAME:ARG%参加能让她特别快乐的游戏
						PRINTFORMW 犹豫的%CALLNAME:ARG%看到%CALLNAME:MASTER%下流的样子察觉出不对劲
						PRINTFORMW %CALLNAME:ARG%憤怒的把%CALLNAME:MASTER%們趕走了
						PRINTFORMW 被%CALLNAME:ARG%抓到了把柄
						CALL GET_ANGRY(ARG)
						FORCE_STOP = 1
						FORCE_GETOUT = 1
						CFLAG:ARG:掌握把柄 += 20
						DRAWLINE
					ENDIF
					
				ELSEIF RESULT == 1
					IF FLAG:催眠強度 >= 75
						PRINTFORMW %CALLNAME:MASTER%解释这是新版的符卡战斗规则。
						PRINTFORMW %CALLNAME:ARG%露出了恍然大悟的神情，红着脸看了一会就离开了現場
						CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
						CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
						IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
							CALL GETOUT(ARG)
						ELSE
							CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
						ENDIF
						;角色を動かしたのでTARGET再設定
						CALL TARGETSET_CHACK
						CFLAG:ARG:同室 = 0
						RETURN 1
					ELSEIF FLAG:催眠強度 >= 50
						PRINTFORMW %CALLNAME:MASTER%对%CALLNAME:ARG%说这是高级的人体炼成术
						PRINTFORMW %CALLNAME:ARG%说着“那就小点声”然后离开了
						CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
						CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
						CALL GET_ANGRY(ARG)
						IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
							CALL GETOUT(ARG)
						ELSE
							CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
						ENDIF
						;角色を動かしたのでTARGET再設定
						CALL TARGETSET_CHACK
						CFLAG:ARG:同室 = 0
						RETURN 1
					ELSE
						PRINTFORMW %CALLNAME:MASTER%对%CALLNAME:ARG%解释这是正常的造人课程
						PRINTFORMW %CALLNAME:ARG%憤怒的把%CALLNAME:MASTER%們趕走了
						PRINTFORMW 被%CALLNAME:ARG%抓到了把柄
						CALL GET_ANGRY(ARG)
						FORCE_STOP = 1
						FORCE_GETOUT = 1
						CFLAG:ARG:掌握把柄 += 20
						DRAWLINE
					ENDIF
				
				ELSE
				CLEARLINE 1
				GOTO INPUT_EMBRACE
				ENDIF
			
			ELSE
				IF FLAG:催眠強度 >= 75
					PRINTFORMW %CALLNAME:MASTER%说着这是最新的符卡战斗修行方式
					PRINTFORMW %CALLNAME:ARG%從%NAME_FROM_PLACE(CFLAG:ARG:現在位置)%\@ TALENT:ARG:羞恥心 > 0 ? 滿臉通紅、目不轉睛的# 饒有興趣的\@眺望着%CALLNAME:MASTER%們的行為……
				ELSEIF FLAG:催眠強度 >= 50
					PRINTFORML %CALLNAME:MASTER%发动催眠术邀请%CALLNAME:ARG%参加能让她特别舒服的游戏
					PRINTFORML %CALLNAME:ARG%有些犹豫、\@ TALENT:ARG:羞恥心 > 0 ? 滿臉通紅的 # 露出很感興趣的樣子\@打聽着%CALLNAME:MASTER%們到底在做什麼
					PRINTFORMW 邀请失败的%CALLNAME:MASTER%慌忙把%CALLNAME:ARG%趕走了
					CALL GETOUT(ARG)
					CALL TARGETSET_CHACK
					CFLAG:ARG:同室 = 0
				ELSE
					PRINTFORMW %CALLNAME:MASTER%对%CALLNAME:ARG%解释这是学造人的课程
					PRINTFORMW %CALLNAME:ARG%害羞的看了一会后突然发现自己被愚弄了
					PRINTFORMW %CALLNAME:ARG%憤怒了
					CALL GET_ANGRY(ARG)
					SIF !CFLAG:ARG:掌握把柄
						PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
					
					IF TALENT:ARG:恋慕 && !CFLAG:ARG:情事目撃
						IF TCVAR:ARG:亲热 < 3
						TALENT:ARG:心情 = MIN(TALENT:ARG:心情, 0)
						BASE:ARG:憤怒 += MAXBASE:ARG:憤怒 / 4
						ENDIF
					CFLAG:ARG:情事目撃 = TARGET
					ENDIF
				ENDIF
			ENDIF
	ELSE
		IF NOZOKI
			PRINTFORMW 在【%NAME_FROM_PLACE(CFLAG:ARG:現在位置)%】被%CALLNAME:ARG%撞见了与%CALLNAME:(TFLAG:現在的TARGET)%的情事！
		ELSE
			PRINTFORMW %CALLNAME:ARG%闖入了和%CALLNAME:(TFLAG:現在的TARGET)%的情事現場！
		ENDIF
		CALL UWAKI_RECORD(ARG)
		;恋人同士（もしくは恋人を含む）場合
		IF TALENT:MASTER:恋人 && (TALENT:(TFLAG:現在的TARGET):恋人 || WITH_恋人())
			;出くわした
			IF !NOZOKI
				[SKIPSTART]
				;恋慕かある程度仲が良い
				IF (CFLAG:ARG:6 > 1 || TALENT:ARG:恋慕)
					PRINTFORMW %CALLNAME:ARG%面色不虞的様子…
					PRINTL [0] 一并推倒
					PRINTL [1] 还是算了
					$INPUT_EMBRACE
					INPUT
					IF RESULT == 0
						IF !NOZOKI
							PRINTFORMW %CALLNAME:MASTER%一招手示意%CALLNAME:ARG%就好像很開心的依偎了过来
							CFLAG:ARG:诶嘿嘿 = CFLAG:(WITNESSED):诶嘿嘿
							CFLAG:ARG:同室 = 1
							;見た人
							CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,4)
							;見られた人（最も地位の高いTARGETで代表）
							CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,4)
						ENDIF
					ELSEIF RESULT == 1
						PRINTFORMW %CALLNAME:ARG%和%CALLNAME:MASTER%一对上视线就慌慌张张地离開了现场…
						IF !NOZOKI
							;見た人
							CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
							;見られた人（最も地位の高いTARGETで代表）
							CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
						ENDIF
						IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
							CALL GETOUT(ARG)
						ELSE
							CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
						ENDIF
						;角色を動かしたのでTARGET再設定
						CALL TARGETSET_CHACK
						CFLAG:ARG:同室 = 0
						RETURN 1
					ELSE
						CLEARLINE 1
						GOTO INPUT_EMBRACE
					ENDIF
				;参戦するほど仲は良くない
				ELSE
				[SKIPEND]
				;入ってきた方が恋人だった場合、割り込んでくる
;				IF TALENT:MASTER:恋人 && TALENT:MASTER:恋人 == TALENT:ARG:恋人
				IF TALENT:ARG:恋人
					PRINTFORMW %CALLNAME:ARG%屈服了并依偎到了%CALLNAME:MASTER%的身边
					CFLAG:ARG:诶嘿嘿 = CFLAG:(WITNESSED):诶嘿嘿
					;恋人が他の女といちゃついてたら、理性吹っ飛んでもおかしくないし、貂ションマックスって意味で情緒が上がってもいいんじゃないかなぁと愚考する次第
					;BASE:ARG:情緒 = MAXBASE:ARG:情緒
					;BASE:ARG:理性 = 0
					;上記がコメントアウトされてるけど、それが原因のバグが出てるっぽいので追記
					;まあ、マックス＆0にするのはさすがにやり過ぎだった気がしないでもない
					SIF BASE:ARG:情緒 < 300
						BASE:ARG:情緒 = 300
					SIF BASE:ARG:理性 > 300
						BASE:ARG:理性 = 300
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,2)
					CALL UWAKI_RECORD(ARG,1)
					;見られた人（最も地位の高いTARGETで代表）
					;CALL KOJO_EVENT(4,WITNESSED,ARG)
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,2)
					CALL UWAKI_RECORD(WITNESSED, 1)
					DRAWLINE
					RETURN 0
				;してる方が恋人だった場合、そそくさと立ち去る（恋人が混じっている多人数プレイかつTARGETが恋人でない場合も立ち去る
				ELSE
					PRINTFORMW %CALLNAME:ARG%和%CALLNAME:MASTER%一对上视线就慌慌张张地离開了现场…
					IF !NOZOKI
						;見た人
						CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
						;見られた人（最も地位の高いTARGETで代表）
						CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
					ENDIF
					IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
						CALL GETOUT(ARG)
					ELSE
						CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
					ENDIF
					;角色を動かしたのでTARGET再設定
					CALL TARGETSET_CHACK
					CFLAG:ARG:同室 = 0
					RETURN 1
				ENDIF
			;覗きみた
			ELSE
				PRINTFORMW %CALLNAME:ARG%看上去面色不虞背过脸去慌慌张张地离開了现场…
				IF !NOZOKI
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
					;見られた人（最も地位の高いTARGETで代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
				ENDIF
				IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
					CALL GETOUT(ARG)
				ELSE
					CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
				ENDIF
				;角色を動かしたのでTARGET再設定… まぁ、覗きなんで大丈夫なはず。
				CALL TARGETSET_CHACK
				CFLAG:ARG:同室 = 0
				RETURN 1
			ENDIF
		;ここから恋人が含まれていない場合
		ELSEIF (TALENT:ARG:淫乱 || TALENT:ARG:風騷)
			PRINTFORMW %CALLNAME:MASTER%一招手示意%CALLNAME:ARG%就好像很開心的依偎了过来
			CFLAG:ARG:诶嘿嘿 = CFLAG:(WITNESSED):诶嘿嘿
			CFLAG:ARG:同室 = 1
			;見た人
			CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,4)
			;見られた人（最も地位の高いTARGETで代表）
			CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,4)
		ELSEIF TALENT:ARG:無知 && !(CFLAG:ARG:既成事實 & 合意_诶嘿嘿)
		;無知かつ诶嘿嘿未経験
			IF NOZOKI
				PRINTFORMW %CALLNAME:ARG%從%NAME_FROM_PLACE(CFLAG:ARG:現在位置)%\@ TALENT:ARG:羞恥心 > 0 ? 滿臉通紅、目不轉睛的# 饒有興趣的\@眺望着%CALLNAME:MASTER%們的行為……
			ELSE
				PRINTFORML %CALLNAME:ARG%、\@ TALENT:ARG:羞恥心 > 0 ? 滿臉通紅的 # 露出很感興趣的樣子\@打聽着%CALLNAME:MASTER%們到底在做什麼
				PRINTFORMW %CALLNAME:MASTER%們慌忙的把%CALLNAME:ARG%趕走了
				CALL GETOUT(ARG)
				CALL TARGETSET_CHACK
				CFLAG:ARG:同室 = 0
			ENDIF
		;その角色の私室でヤってた場合
		ELSEIF CFLAG:ARG:初期位置 == CFLAG:MASTER:現在位置
			PRINTFORML 発現自己的房間被當做偷情地點的%CALLNAME:ARG%、憤怒的把%CALLNAME:MASTER%們趕出去了
			PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
			CALL GET_ANGRY(ARG)
			FORCE_STOP = 1
			FORCE_GETOUT = 1
			CFLAG:ARG:掌握把柄 += 20
			DRAWLINE
		ELSEIF CFLAG:ARG:行動 == 5 && CFLAG:MASTER:現在位置 == CFLAG:ARG:職場
			PRINTFORML 発現工作場所被當做偷情地點的%CALLNAME:ARG%、憤怒的把%CALLNAME:MASTER%們趕出去了
			PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
			CALL GET_ANGRY(ARG)
			FORCE_STOP = 1
			FORCE_GETOUT = 1
			CFLAG:ARG:掌握把柄 += 20
			DRAWLINE
		ELSEIF CFLAG:ARG:態度 < 3 && PRIVATEROOM:(CFLAG:MASTER:現在位置 % 100) && PRIVATEROOM:(CFLAG:MASTER:現在位置 % 100) != TARGET
			SIF !CFLAG:ARG:掌握把柄
				PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
			IF !NOZOKI
				;見た人
				;CALL KOJO_EVENT(1,ARG,WITNESSED)
				CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,1)
				;見られた人（最も地位の高いTARGETで代表）
				;CALL KOJO_EVENT(2,WITNESSED,ARG)
				CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,1)
			ENDIF
			CALL TOUCH_SET(0,0,0,1)
			DRAWLINE
			FORCE_STOP = 1
			CFLAG:ARG:掌握把柄 += 10
			;謹慎処分とか把柄とかの処理を入れる予定
		ELSE
			IF !NOZOKI
				;態度だけだと既成事實とか見てないことに気づく、ので合意がある場合に迫ってくるように
				IF TCVAR:MASTER:亲热 >= 3 && (GETBIT(CFLAG:ARG:既成事實 , 1) || TALENT:ARG:合意誤認)
					PRINTFORMW %CALLNAME:ARG%屈服了并依偎到了%CALLNAME:MASTER%的身边
					CFLAG:ARG:诶嘿嘿 = CFLAG:(WITNESSED):诶嘿嘿
					;一応、推倒可能な情緒に
					;理性は推倒に関係してないけど、理性的なら混ざろうとしないだろうと
					BASE:ARG:情緒 = MAX(BASE:ARG:情緒, 300)
					BASE:ARG:理性 = MIN(BASE:ARG:理性, 300)
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,2)
					CALL UWAKI_RECORD(ARG,1)
					;見られた人（最も地位の高いTARGETで代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,2)
					CALL UWAKI_RECORD(WITNESSED,1)
					DRAWLINE
					RETURN 0
				ELSE
					PRINTFORMW %CALLNAME:ARG%\@TALENT:ARG:恋慕 ?滿臉失望的 #不高興的轉過了臉、匆忙 \@離開了…
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
					;見られた人（最も地位の高いTARGETで代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
					IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
						CALL GETOUT(ARG)
					ELSE
						CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
					ENDIF
					CALL TARGETSET_CHACK
					CFLAG:ARG:同室 = 0
					IF TALENT:ARG:恋慕
						TALENT:ARG:心情 = MIN(TALENT:ARG:心情, 0)
						BASE:ARG:憤怒 += MAXBASE:ARG:憤怒 / 2
						CFLAG:ARG:情事目撃 = TARGET
					ENDIF
					RETURN 1
				ENDIF
			;覗かれた場合
			ELSE
				PRINTFORMW %CALLNAME:ARG%\@TALENT:ARG:恋慕 ?滿臉失望 #不高興的轉過了臉 \@
				IF TALENT:ARG:恋慕 && !CFLAG:ARG:情事目撃
					IF TCVAR:ARG:亲热 < 3
						TALENT:ARG:心情 = MIN(TALENT:ARG:心情, 0)
						BASE:ARG:憤怒 += MAXBASE:ARG:憤怒 / 4
					ENDIF
					CFLAG:ARG:情事目撃 = TARGET
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF
IF FORCE_STOP
	SIF CFLAG:MASTER:恶作剧
		CALL SET_PRANK(0,TARGET,2)
	FOR LOCAL,0,CHARANUM
		CALL ENDUFUFU(LOCAL)
		SIF LOCAL == WITNESSED || LOCAL == ARG
			CONTINUE
		SIF CFLAG:LOCAL:睡眠
			CONTINUE
		CALL GETOUT(LOCAL)
	NEXT
	TFLAG:106 = 0
	RETURN 0
ENDIF
;通常遭遇
IF !CFLAG:ARG:睡眠 && (!NOZOKI || CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置) && !FLAG:時間停止 && !CHK_DATENOW(CFLAG:ARG:约会中) && (TIME != TIME:3 || CFLAG:MASTER:現在位置 != CFLAG:MASTER:初期位置 || CFLAG:MASTER:あなた前ターン位置 != CFLAG:MASTER:初期位置) && !GETBIT(FLAG:潜伏,0)
	;约会中に偶然遭遇
	IF CHK_DATENOW(CFLAG:MASTER:约会中)
		CALL PRINT_FACE, ARG, "通常"
		IF !CFLAG:(NO:ARG):面識 && TALENT:ARG:路人
			PRINTFORMW %CALLNAME:MASTER%向%CALLNAME:ARG%打了声招呼
		ELSE
			IF !TCVAR:ARG:今天有否见面
				PRINTFORMW 在目的地遇到了%CALLNAME:ARG%
			ELSE
				PRINTFORMW 出门在外又遇到了%CALLNAME:ARG%
			ENDIF
		ENDIF
		IF CFLAG:(NO:ARG):面識 == 0
			CALL SYOTAIMEN(ARG)
		ELSE
			FOR LOCAL:4,1,CHARANUM
				IF CHK_DATENOW(CFLAG:(LOCAL:4):约会中)
					IF !TCVAR:ARG:今天有否见面
						CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,9,LOCAL:4)
					ENDIF
					CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,7,LOCAL:4)
					BREAK
				ELSEIF LOCAL:4 == CHARANUM - 1
					IF !TCVAR:ARG:今天有否见面
						CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,9,0)
					ENDIF
					CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,6,0)
				ENDIF
			NEXT
		ENDIF
	;さっきから居るのは分かっていたか否か
	ELSEIF CFLAG:ARG:前一次的位置 == CFLAG:ARG:現在位置
		CALL PRINT_FACE, ARG
		PRINTFORM %CALLNAME:ARG%が
		PRINTFORM %NAME_FROM_PLACE(CFLAG:ARG:現在位置)%
		PRINTFORMW 那里\@CAN_MOVE(CFLAG:MASTER:前一次的位置, CFLAG:ARG:現在位置) == 2 ? 曾经呆着 # 呆着\@
		SIF !TCVAR:ARG:今天有否见面
			PRINTFORML 看到%CALLNAME:ARG%的%CALLNAME:MASTER%、意识到今天还没打招呼呢
		HAPPENING_TYPE = 1
	;風呂に入ってきた場合
	ELSEIF CAN_MOVE(CFLAG:MASTER:前一次的位置, CFLAG:ARG:前一次的位置) < 2 && CFLAG:ARG:前一次的位置 != CFLAG:MASTER:前一次的位置 && BATHCHECK(CFLAG:MASTER:現在位置)
		CALL PRINT_FACE, ARG, "通常", "裸", "", ""
		PRINTFORMW %CALLNAME:ARG%进入了浴場…
		IF 風呂逐出条件(ARG)
			IF CFLAG:ARG:掌握把柄 && HETEROSEX(MASTER,ARG) && FLAG:催眠強度 < 50
				PRINTFORMW %CALLNAME:MASTER%没法违抗%CALLNAME:ARG%被从浴場里赶了出来
				CFLAG:MASTER:現在位置 = KICKOUT()
				HAPPENING_TYPE = 5
				CALL TARGETSET_CHACK
				CFLAG:ARG:同室 = 0
			ELSE
				PRINTFORMW %CALLNAME:ARG%和%CALLNAME:MASTER%一对上视线就慌慌张张地离開了现场…
				CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
				CALL TARGETSET_CHACK
				CFLAG:ARG:同室 = 0
				HAPPENING_TYPE = 4
			ENDIF
		ELSE
			PRINTFORMW %CALLNAME:ARG%好像想和%CALLNAME:MASTER%一起进来的様子
			SIF !TCVAR:ARG:今天有否见面
				PRINTFORML %CALLNAME:MASTER%注意到他今天还没有向%CALLNAME:ARG%打招呼呢
			HAPPENING_TYPE = 3
		ENDIF
	;廁所に入ってきた場合
	ELSEIF CAN_MOVE(CFLAG:MASTER:前一次的位置, CFLAG:ARG:前一次的位置) < 2 && CFLAG:ARG:前一次的位置 != CFLAG:MASTER:前一次的位置 && IN_TOILET(CFLAG:MASTER:現在位置)
		PRINTW 有人在敲门
		$TOILET_INPUT_LOOP
		PRINTL [0]里面有人ー　[1]请进
		INPUT
		IF RESULT == 1
			PRINTFORM %CALLNAME:ARG%が
			PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:現在位置)
			PRINTFORMW 进来了
			HAPPENING_TYPE = 2
		ELSEIF RESULT == 0
			PRINTW 脚步声远去了
			CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
			CALL TARGETSET_CHACK
			CFLAG:ARG:同室 = 0
			HAPPENING_TYPE = -1
		ELSE
			GOTO TOILET_INPUT_LOOP
		ENDIF
	;ELSEIF CAN_MOVE(CFLAG:MASTER:前一次的位置, CFLAG:ARG:前一次的位置) < 2 && CFLAG:ARG:前一次的位置 != CFLAG:MASTER:前一次的位置
	;↑の条件では来訪時に初対面口上が出ない場合があるので変更
	ELSEIF CFLAG:ARG:前一次的位置 != CFLAG:MASTER:前一次的位置
		CALL PRINT_FACE, ARG, "通常", "服", "", ""
		IF CFLAG:ARG:前一次的位置 != MAXROOM && CFLAG:ARG:前一次的位置 != OMANEKIBEYA()
			PRINTFORMW %CALLNAME:ARG%來到了%NAME_FROM_PLACE(CFLAG:ARG:現在位置)%
		ENDIF
		SIF !TCVAR:ARG:今天有否见面
			PRINTFORML %CALLNAME:MASTER%看到%CALLNAME:ARG%了、回想起来今天还没有打招呼呢
		HAPPENING_TYPE = 2
	ENDIF
	IF !FLAG:70 && CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
		IF !NOZOKI && HAPPENING_TYPE >= 0
			;初対面
			IF CFLAG:(NO:ARG):面識 == 0
				CALL SYOTAIMEN(ARG)
			ELSEIF CFLAG:ARG:情事目撃 && CFLAG:(CFLAG:ARG:情事目撃):現在位置 != CFLAG:ARG:現在位置
				PRINTFORMW 被%CALLNAME:ARG%追问了刚才和%CALLNAME:(CFLAG:ARG:情事目撃)%在一起的事情…
				;CALL KOJO_EVENT(0, ARG, 2)
				CALL KOJO_MESSAGE_SEND("EVENT",8,ARG,1,0)
				CFLAG:ARG:情事目撃 = 0
			ELSEIF CFLAG:ARG:情事目撃 && CFLAG:(CFLAG:ARG:情事目撃):現在位置 == CFLAG:ARG:現在位置
				PRINTFORMW %CALLNAME:ARG%就刚才的事情对%CALLNAME:MASTER%和%CALLNAME:(CFLAG:ARG:情事目撃)%进行了説教…
				CALL KOJO_MESSAGE_SEND("EVENT",8,ARG,2,0)
				CFLAG:ARG:情事目撃 = 0
			ELSE
				IF !TCVAR:ARG:今天有否见面
					CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,8,0)
				ENDIF
				;通常遭遇口上
				CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,HAPPENING_TYPE,0)
			ENDIF
		ENDIF
		CALL FirstMeetToday(ARG)
	ENDIF
ENDIF

@OSASOI(ARG)
#DIM 目的地
#DIM 自宅地域
#DIM 現在地域

自宅地域 = CFLAG:ARG:自宅位置 / 100
現在地域 = GET_MAPID(CFLAG:MASTER:現在位置)
;仕事がある日は誘わない
SIF !CHARA_HOLIDAY(ARG)
	RETURN
SIF GROUPMATCH(TIME:5, 5, 9)
	RETURN
SIF !AT_HOME(MASTER)
	RETURN
IF (TALENT:ARG:恋慕 || TALENT:ARG:思慕) && CFLAG:ARG:宴会参加 && TFLAG:宴会場 / 100 != MAIN_MAP && TFLAG:宴会場 / 100 != 現在地域 && (FLAG:開始日 - DAY) == 0
	目的地 = TFLAG:宴会場 / 100
	PRINTFORML %CALLNAME:ARG%邀请你一起去今天有宴会的%GET_MAPNAME(目的地)%
	GOTO INVITE_ENKAI
ENDIF
IF TALENT:ARG:恋慕 && !RAND:5 && !CFLAG:ARG:宴会参加
	IF CFLAG:ARG:积攒度 >= (700 + TALENT:ARG:羞恥心 * 150) && !CFLAG:ARG:神社在住 && !CFLAG:ARG:自宅位置
		PRINTFORML 被%CALLNAME:ARG%邀請去房間
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,3,自宅地域)
		CALL ASK_YN("去","不去")
		IF !RESULT
			PRINTFORML 接受了%CALLNAME:ARG%的邀請、%CALLNAME:MASTER%走向了她的房間……
			CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,4,自宅地域)
			PRINTFORMW +{TIME_REQUIRED(現在地域, 自宅地域)}分
			TIME += TIME_REQUIRED(現在地域, 自宅地域)
			CFLAG:ARG:约会中 = 自宅地域
			CFLAG:MASTER:约会中 = 自宅地域
			FLAG:约会的对象 = ARG
			CFLAG:MASTER:同行 = 60
			CFLAG:ARG:同行 = 60
			TFLAG:约会前好感度 = CFLAG:ARG:2
			TARGET = ARG
			CALL OMANEKI_ENTER(ARG)
			CALL TARGETSET_CHACK
			;お誘いに乗ったか断ったかをRETURNで返すようにしました
			RETURN 1
		ELSE
			PRINTFORMW %CALLNAME:ARG%內心非常遺憾
			CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,5,自宅地域)
			RETURN 0
		ENDIF
	ELSEIF !RAND:4 && CFLAG:ARG:經常去的地方 >= 0 && CFLAG:ARG:經常去的地方 != MAIN_MAP
		目的地 = CFLAG:ARG:經常去的地方
	ELSE
		目的地 = RAND:10
		SIF 目的地 == MAIN_MAP
			RETURN
	ENDIF
	;要求移動時間が60以上だと不発
	IF TIME_REQUIRED(現在地域, 目的地) >= 60
		RETURN 0
	ENDIF
	PRINTFORML %CALLNAME:ARG%邀请去%GET_MAPNAME(目的地)%
	$INVITE_ENKAI
	CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,0,目的地)
	CALL ASK_YN("去","不去")
	IF !RESULT
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,1,目的地)
		CFLAG:MASTER:同行 = 60
		CFLAG:ARG:同行 = 60
		TFLAG:约会前好感度 = CFLAG:ARG:2
		CALL SET_DATE(目的地, ARG, TIME_REQUIRED(CFLAG:MASTER:约会中, 目的地) - 1000)
		CALL TARGETSET_CHACK
		RETURN 1
	ELSE
		PRINTFORMW %CALLNAME:ARG%表示遺憾
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,2,目的地)
		RETURN 0
	ENDIF
ENDIF

RETURN 0



@UWAKI_RECORD(ARG,公認,浮気相手)
#DIM 浮気相手
#DIM 公認

SIF UWAKI_KNOWN:ARG:0 == ""
	UWAKI_KNOWN:ARG:0 = /
SIF UWAKI_ACCEPTED:ARG:0 == ""
	UWAKI_ACCEPTED:ARG:0 = /


IF 浮気相手
	LOCALS = %CALLNAME:浮気相手%
	SIF !STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
		UWAKI_KNOWN:ARG:0 = %UWAKI_KNOWN:ARG:0%%LOCALS%/
	SIF !STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS) && 公認
		UWAKI_ACCEPTED:ARG:0 = %UWAKI_ACCEPTED:ARG:0%%LOCALS%/
ELSE
	;第三引数が0の場合、你と同じ場所にいる角色全員記録
	FOR LOCAL,1,CHARANUM
		SIF LOCAL == ARG
			CONTINUE
		SIF CFLAG:LOCAL:現在位置 != CFLAG:MASTER:現在位置
			CONTINUE
		;迫る予定の無い人を弾く応急処置。
		SIF !(TCVAR:MASTER:亲热 >= 3 && GETBIT(CFLAG:LOCAL:既成事實, 1))
			CONTINUE
		LOCALS = %CALLNAME:LOCAL%
		SIF !STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
			UWAKI_KNOWN:ARG:0 = %UWAKI_KNOWN:ARG:0%%LOCALS%/
		SIF !STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS) && 公認
			UWAKI_ACCEPTED:ARG:0 = %UWAKI_ACCEPTED:ARG:0%%LOCALS%/
	NEXT
ENDIF

@GET_UWAKI_HISTORY(ARG,浮気相手)
;公認だと2を返す
#FUNCTION
#DIM 浮気相手
LOCALS = /%CALLNAME:浮気相手%/

IF STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS)
	RETURNF 2
ELSEIF STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@SYOTAIMEN(ARG)
SIF CFLAG:ARG:面識
	RETURN
IF TALENT:ARG:路人 == 1
	IF TALENT:MASTER:謎之魅力
		PRINTFORMW %CALLNAME:ARG%感到%CALLNAME:MASTER%的【謎之魅力】、胸口剧烈的跳动
		CALL COLORMESSAGE(@"好感和親密提升了",C_YELLOW,2,1)
		ABL:ARG:親密 += 2
		CFLAG:ARG:好感度 += 300
	ENDIF
	IF TALENT:MASTER:魅惑
		PRINTFORMW %CALLNAME:ARG%被%CALLNAME:MASTER%【魅惑】了
		CALL COLORMESSAGE(@"好感和親密提升了",C_YELLOW,2,1)
		ABL:ARG:親密 += 2
		CFLAG:ARG:好感度 += 300
	ENDIF
	IF TALENT:MASTER:形状 > 0
		IF TALENT:MASTER:禁断的知識 == 1
			IF TALENT:MASTER:形状 < 3
				PRINTFORMW %CALLNAME:ARG%注意到%CALLNAME:MASTER%下体微小的鼓包
				PRINTFORMW 露出了很失望的样子
				PRINTFORMW %CALLNAME:MASTER%让小兄弟旋转了起来
				PRINTFORMW %CALLNAME:ARG%惊奇不已！
				CALL COLORMESSAGE(@"好感和親密上升了",C_YELLOW,2,1)
				ABL:ARG:親密 += 2
				CFLAG:ARG:好感度 += 300
			ELSEIF TALENT:MASTER:形状 == 4
				PRINTFORMW %CALLNAME:ARG%看到%CALLNAME:MASTER%的下体在发光
				PRINTFORMW 认为%CALLNAME:MASTER%是个奇怪的人
				PRINTFORMW %CALLNAME:MASTER%让小兄弟不断闪烁五颜六色的光
				PRINTFORMW %CALLNAME:ARG%被迷惑了！
				CALL COLORMESSAGE(@"好感和親密上升了",C_YELLOW,2,1)
				ABL:ARG:親密 += 2
				CFLAG:ARG:好感度 += 300
			ELSEIF TALENT:MASTER:形状 == 5
				PRINTFORMW %CALLNAME:ARG%注意到%CALLNAME:MASTER%下体的鼓包奇形怪状
				PRINTFORMW 对%CALLNAME:MASTER%感到很害怕
				PRINTFORMW %CALLNAME:MASTER%让小兄弟像震动棒一样震动
				PRINTFORMW %CALLNAME:ARG%被吸引了！
				CALL COLORMESSAGE(@"好感和親密上升了",C_YELLOW,2,1)
				ABL:ARG:親密 += 2
				CFLAG:ARG:好感度 += 300
			ELSEIF TALENT:MASTER:形状 == 6
				PRINTFORMW %CALLNAME:ARG%注意到%CALLNAME:MASTER%下体巨大的鼓包
				PRINTFORMW 认为%CALLNAME:MASTER%在兴奋着而感到厌恶
				PRINTFORMW %CALLNAME:MASTER%让小兄弟挺立起来
				PRINTFORMW %CALLNAME:ARG%没想到竟如此巨大！
				CALL COLORMESSAGE(@"好感和親密上升了",C_YELLOW,2,1)
				ABL:ARG:親密 += 2
				CFLAG:ARG:好感度 += 300
			ELSEIF TALENT:MASTER:形状 == 10
				PRINTFORMW ！！！
				PRINTFORMW %CALLNAME:ARG%的目光被%CALLNAME:MASTER%馬一样的阴茎撑起的鼓包吸引
				PRINTFORMW 被吸引的目光无法移开
				CALL COLORMESSAGE(@"好感和親密上升了",C_YELLOW,2,1)
				ABL:ARG:親密 += 2
				CFLAG:ARG:好感度 += 300
			ENDIF
		ELSE
			IF TALENT:MASTER:形状 < 3
				PRINTFORMW %CALLNAME:ARG%注意到%CALLNAME:MASTER%下体微小的鼓包
				PRINTFORMW 露出了很失望的样子
				CALL COLORMESSAGE(@"好感和親密下降了",C_RED,2,1)
				ABL:ARG:親密 -= 2
				CFLAG:ARG:好感度 -= 300
			ELSEIF TALENT:MASTER:形状 == 4
				PRINTFORMW %CALLNAME:ARG%看到%CALLNAME:MASTER%的下体在发光
				PRINTFORMW 认为%CALLNAME:MASTER%是个奇怪的人
				CALL COLORMESSAGE(@"好感和親密下降了",C_RED,2,1)
				ABL:ARG:親密 -= 2
				CFLAG:ARG:好感度 -= 300
			ELSEIF TALENT:MASTER:形状 == 5
				PRINTFORMW %CALLNAME:ARG%注意到%CALLNAME:MASTER%下体的鼓包奇形怪状
				PRINTFORMW 对%CALLNAME:MASTER%感到很害怕
				CALL COLORMESSAGE(@"好感和親密下降了",C_RED,2,1)
				ABL:ARG:親密 -= 2
				CFLAG:ARG:好感度 -= 300
			ELSEIF TALENT:MASTER:形状 == 6
				PRINTFORMW %CALLNAME:ARG%注意到%CALLNAME:MASTER%下体巨大的鼓包
				PRINTFORMW 认为%CALLNAME:MASTER%在兴奋着而感到厌恶
				CALL COLORMESSAGE(@"好感和親密下降了",C_RED,2,1)
				ABL:ARG:親密 -= 2
				CFLAG:ARG:好感度 -= 300
			ELSEIF TALENT:MASTER:形状 == 10
				PRINTFORMW %CALLNAME:ARG%盯着%CALLNAME:MASTER%馬一样的阴茎撑起的巨大鼓包
				PRINTFORMW 认为%CALLNAME:MASTER%是个伪装成人类的馬妖怪
				CALL COLORMESSAGE(@"好感和親密下降了",C_RED,2,1)
				ABL:ARG:親密 -= 2
				CFLAG:ARG:好感度 -= 300
			ENDIF
		ENDIF
	ENDIF
ENDIF
IF !FLAG:扮演
	SETCOLOR 0xc53d43
	SIF NO:ARG != 拠点_家主
		PRINTFORMW %CALLNAME:ARG%向初次见面的%CALLNAME:MASTER%作了问候
	RESETCOLOR
	CALL KOJO_MESSAGE_SEND("ENCOUNTER",0,ARG,1,0)
ENDIF
CFLAG:ARG:面識 = 1

@FirstMeetToday(ARG)
#DIM PREV_TARGET
#DIM IRAI_ABLE
SIF CFLAG:ARG:現在位置 != CFLAG:MASTER:現在位置
	RETURN
SIF TCVAR:ARG:今天有否见面
	RETURN

TCVAR:ARG:今天有否见面 = 1
CFLAG:ARG:最後に会った日 = DAY
SIF TCVAR:ARG:另一种便服
	PRINTFORMW 今天的%CALLNAME:ARG%打扮得跟往常不一様
IF CFLAG:ARG:オナバレ
	;陥落素質あり、【接受快感】の時、わりと坦率な反応に
	IF CFLAG:ARG:オナバレ == 1
		IF (IS_LOVER(ARG) || TALENT:ARG:炮友) && TALENT:ARG:快感応答 == 1
			PRINTFORMW 前日被目撃了自慰現場的%CALLNAME:ARG%%TEXTR("一副若无其事的表情…/好像有什麼想說似得看著這邊…/努力表现得坦然…")%
		ELSE
			PRINTFORMW 前日被目撃了自慰現場的%CALLNAME:ARG%很難為情…
		ENDIF
	ELSEIF CFLAG:ARG:オナバレ == 2
		IF (IS_LOVER(ARG) || TALENT:ARG:炮友) && TALENT:ARG:快感応答 == 1
			PRINTFORMW 之前看到了%CALLNAME:ARG%的自慰現場的時候、順便給搞了一發的%CALLNAME:ARG%%TEXTR("嫵媚地向這邊送著秋波…/看著這邊壞壞地笑著…/臉上浮現出回味的笑容…")%
		ELSE
			PRINTFORMW 前日被目撃了自慰現場的、於是身體沒有得到滿足的%CALLNAME:ARG%很難為情…
		ENDIF
	ELSEIF CFLAG:ARG:オナバレ == 3
		IF (IS_LOVER(ARG) || TALENT:ARG:炮友) && TALENT:ARG:快感応答 == 1
			PRINTFORMW 之前看到了%CALLNAME:ARG%的自慰現場的時候、展露了激烈高潮臉的%CALLNAME:ARG%%TEXTR("往这边抛了媚眼…/時不時向這邊偷看…/看起來很不好意思的樣子…")%
		ELSE
			PRINTFORMW 前日被目撃了自慰現場的、而且還在別人眼前激烈高潮的%CALLNAME:ARG%很難為情…
		ENDIF
	ENDIF
	CFLAG:ARG:オナバレ = 0
ELSEIF TALENT:ARG:心情 == 1
	PRINTFORMW 不知是不是发生了什么好事呢、今天的%CALLNAME:ARG%心情很好的様子
ELSEIF TALENT:ARG:心情 == -1
	PRINTFORMW 总觉的今天的%CALLNAME:ARG%似乎心情不好的様子…
ENDIF
IF CFLAG:ARG:积攒度 >= 400 && TALENT:MASTER:劣情察知
	SETCOLOR 255,100,255
	FONTBOLD
	IF CFLAG:ARG:积攒度 >= 1000
		IF TALENT:MASTER:形状 == 4
		PRINTFORMW 阴茎探测到了什么发出五彩光芒！
		PRINTFORMW %CALLNAME:MASTER%得知了%CALLNAME:ARG%欲火焚身了……！
		ELSE
		PRINTFORMW 发现！
		PRINTFORMW %CALLNAME:MASTER%发现%CALLNAME:ARG%欲火焚身了……！
		ENDIF
	ELSEIF CFLAG:ARG:积攒度 >= 700
		IF TALENT:MASTER:形状 == 4
		PRINTFORMW 阴茎探测到了什么发出强光！
		PRINTFORMW %CALLNAME:MASTER%得知了%CALLNAME:ARG%饥渴难耐中……！
		ELSE
		PRINTFORMW 发现！
		PRINTFORMW %CALLNAME:MASTER%发现%CALLNAME:ARG%饥渴难耐中……！
		ENDIF
	ELSE
		IF TALENT:MASTER:形状 == 4
		PRINTFORMW 阴茎探测到了什么在一闪一闪发光！
		PRINTFORMW %CALLNAME:MASTER%得知了%CALLNAME:ARG%欲求不满中……！
		ELSE
		PRINTFORMW 发现！
		PRINTFORMW %CALLNAME:MASTER%发现%CALLNAME:ARG%欲求不满中……！
		ENDIF
	ENDIF
	RESETCOLOR
	FONTREGULAR
ENDIF
CALL SEASONAL(ARG)

PREV_TARGET = TARGET
TARGET = ARG
CALL CAN_IRAI_START
IRAI_ABLE = RESULT

IF IRAI_ABLE && IRAI_CHARA_TO_PROGRESS(ARG) == "依頼未受託" && !FLAG:依頼スルー
	;CALL先の判定でTARGET==CLIENT前提の処理が多々あるので一時変更、そのまま戻ってくるので退避しとく
	CALL IRAI_START(ARG)
	TARGET = PREV_TARGET
ELSEIF IRAI_ABLE && GROUPMATCH(IRAI_CHARA_TO_PROGRESS(ARG), "期限切れ", "報告忘れ")
	PRINTFORML %CALLNAME:ARG%責怪%CALLNAME:MASTER%沒有按照要求履行委託
	IF TALENT:MASTER:形状 == 4
		PRINTFORMW 发光的阴茎羞愧的低下头
	ELSE
		PRINTFORMW 多少感到有些对不起对方
	ENDIF
	;TFLAG:104の使い方がよくわからん
	;TFLAG:104 = ARG
	CALL IRAI_REPORT(ARG)
	TARGET = PREV_TARGET
ELSEIF TALENT:ARG:心情 >= 0 && BASE:MASTER:気力 > 0 && BASE:ARG:気力 > 0 && !CFLAG:MASTER:同行 && NEMUKE() < 720 && !睡眠時間(ARG)
	CALL OSASOI(ARG)
	SIF !RESULT
		TARGET = PREV_TARGET
ELSE
	TARGET = PREV_TARGET
ENDIF
