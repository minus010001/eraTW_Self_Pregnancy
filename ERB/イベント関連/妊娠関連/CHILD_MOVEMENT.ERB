;-------------------------------------------------
;ターン毎に兒童の描写をする関数
;従来はCOMENDで行っていた処理
;-------------------------------------------------
@CHILD_DESCRIPTION
#DIM CHILDNUM
#DIMS 兒童の名前
#DIM 自立後の兒童描写
SIF !FLAG:兒童描写
	RETURN

TFLAG:描写中の兒童 = 0
FOR LOCAL, 0, CHARANUM
	;自立した兒童の判定
	IF CFLAG:LOCAL:兒童人数 - CFLAG:LOCAL:育児人数 > 0
		CALL CHILD_MOVEMENT_2(LOCAL)
		IF RESULT
			;同じ描写はしない
			IF TFLAG:描写中の兒童 != 自立後の兒童描写
				CHILDNUM = TFLAG:描写中の兒童 / 1000
				兒童の名前 = %CHILDNAME:LOCAL:CHILDNUM%
				SELECTCASE CHILD_ACT:LOCAL:CHILDNUM
					CASE "仕事"
						PRINTFORMDW 看到了工作中的%兒童の名前%
					CASE "散歩"
						PRINTFORMDW 遇到了%兒童の名前%
				ENDSELECT
			ENDIF
			自立後の兒童描写 = TFLAG:描写中の兒童
		ELSE
			自立後の兒童描写 = 0
		ENDIF
	ENDIF
	;育児中の兒童の判定
	SIF !TALENT:LOCAL:育児中
		CONTINUE
	;欲求不満の抑制
	SIF CFLAG:LOCAL:积攒度 > 300
		CFLAG:LOCAL:积攒度 = 300
	;兒童の行動はここで決定
	CALL CHILD_MOVEMENT(LOCAL, TALENT:LOCAL:育児中)
	;描写しない場合はRESULT=0
	SIF !RESULT
		CONTINUE
	CHILDNUM = TALENT:LOCAL:育児中
	兒童の名前 = %CHILDNAME:LOCAL:CHILDNUM%
	;幼児期（0日～45日）
	IF CFLAG:LOCAL:出産経過日 < CHILD_語彙
		LOCALS = 幼児期
	;幼少期（46日～89日）
	ELSEIF CFLAG:LOCAL:出産経過日 < CHILD_自立前
		LOCALS = 幼少期
	;自立前（90日～119日）
	ELSE
		LOCALS = 自立前
	ENDIF
	SELECTCASE CHILD_ACT:LOCAL:CHILDNUM
		CASE "睡眠"
			IF LOCALS == "幼児期"
				PRINTFORMDW %兒童の名前%入睡了……
			ELSE
				IF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:初期位置
					PRINTFORMDW %兒童の名前%睡着了……
				ELSE
					PRINTFORMDW %兒童の名前%走去了寝室……
				ENDIF
			ENDIF
			CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_SLEEPING", LOCALS)
		CASE "起床"
			PRINTFORMDW %兒童の名前%睡醒了
		CASE "入浴"
			CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_BATH", LOCALS)
		CASE "吃飯"
			IF CFLAG:LOCAL:出産経過日 < CHILD_離乳
				PRINTFORMDW %CALLNAME:LOCAL%正在给%兒童の名前%授乳
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_吃飯", "授乳")
			ELSEIF LOCALS == "幼児期"
				PRINTFORMDW %CALLNAME:LOCAL%在给%兒童の名前%喂婴儿食品
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_吃飯", "離乳食")
			ELSE
				SELECTCASE TIME
				CASE 300 TO 540 
					PRINTFORMDW %兒童の名前%正在吃早餐
				CASE 660 TO 900
					PRINTFORMDW %兒童の名前%正在吃午餐
				CASE 1020 TO 1200
					PRINTFORMDW %兒童の名前%正在吃夕食
				CASE 1200 TO 1440
					PRINTFORMDW %兒童の名前%正在吃晚餐
				CASE 0 TO 180
					PRINTFORMDW %兒童の名前%正在吃夜宵
				CASEELSE
					PRINTFORMDW %兒童の名前%正在吃饭
				ENDSELECT
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_吃飯", LOCALS)
			ENDIF
		CASE "玩具"
			IF LOCALS == "自立前"
				PRINTFORMDL %兒童の名前%怀念地看着%CALLNAME:MASTER%制作的木制玩具……
			ELSE
				PRINTFORMDW %兒童の名前%用%CALLNAME:MASTER%制作的玩具開始了玩耍
			ENDIF
			CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_TOY", LOCALS)
		CASE "お絵かき"
			PRINTFORMDW %兒童の名前%開始画画了
		CASE "玩耍"
			PRINTFORMDW %兒童の名前%跑来跑去地玩耍着
		CASE "読書"
			PRINTFORMDW %兒童の名前%開始了读书
		CASE "料理"
			PRINTFORMDW %兒童の名前%一个人開始做饭了
		CASE "絵描き"
			PRINTFORMDW %兒童の名前%開始画画了
		CASE "歌唱"
			PRINTFORMDW %兒童の名前%開始唱了一首歌
		CASE "掃除"
			PRINTFORMDW %兒童の名前%開始打扫房间了
		CASE "支度"
			PRINTFORMDW %兒童の名前%准备去寺子屋了
		CASE "登校"
			IF (MAIN_MAP != 2 &&CFLAG:MASTER:現在位置 == 寺子屋) || (MAIN_MAP == 2 && CFLAG:MASTER:現在位置 == 222)
				PRINTFORMDW %兒童の名前%到学校了
			ELSE
				PRINTFORMDW %兒童の名前%前往寺子屋了
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_登下校")
			ENDIF
		CASE "授業"
			PRINTFORMDW %兒童の名前%正在上课
		CASE "帰宅"
			IF (MAIN_MAP != 2 &&CFLAG:MASTER:現在位置 == 寺子屋) || (MAIN_MAP == 2 && CFLAG:MASTER:現在位置 == 222)
				PRINTFORMDW %兒童の名前%上完课离開学校了
			ELSE
				PRINTFORMDW %兒童の名前%从寺子屋回来了
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 1, 0, "CHILD_RAISING_登下校")
			ENDIF
		CASEELSE
;				;口上の発生は3分の1,一旦凍結
;				SIF !RAND:3
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_OTHER", LOCALS)
	ENDSELECT
NEXT
RESETCOLOR


;-------------------------------------------------
;兒童の行動を判定する関数
;RETURN 描写するなら1
;-------------------------------------------------
@CHILD_MOVEMENT(CHARA, CHILDNUM)
#DIM CHARA
#DIM CHILDNUM
#DIM TIMESTAMP
#DIM TIMESTAMP_DAY
#DIM 経過時間
VARSET RESULT

;一つ前の行動を保存
LOCALS = %CHILD_ACT:CHARA:CHILDNUM%

;TIME_PROGRESSが使えないので强行算出
SIF TIMESTAMP_DAY != DAY
	TIMESTAMP = 1440 * DAY + TIME
経過時間 = 1440 * DAY + TIME - TIMESTAMP

;10分以上経過してたら骰子を振る
SIF 経過時間 >= 10
	LOCAL = RAND:100

;授業中止
SIF !GO_SCHOOL(CHARA) && GROUPMATCH(CHILD_ACT:CHARA:CHILDNUM, "授業", "支度", "登校")
	CHILD_ACT:CHARA:CHILDNUM = 

;幼児期（0日～45日）
IF CFLAG:CHARA:出産経過日 < CHILD_語彙
	;睡眠中,お腹が空いたら随机で起きる
	IF CHILD_ACT:CHARA:CHILDNUM == "睡眠"
		SIF TCVAR:CHARA:兒童空腹度 >= 180 && LOCAL % 6 == 0
			CHILD_ACT:CHARA:CHILDNUM = 起床
	;起きてる時
	ELSEIF CHILD_ACT:CHARA:CHILDNUM != "睡眠"
		;お風呂
		IF BATHROOM(CFLAG:CHARA:現在位置)
			CHILD_ACT:CHARA:CHILDNUM = 入浴
		;吃飯中
		ELSEIF CHILD_ACT:CHARA:CHILDNUM == "吃飯"
			;満たされたら終了
			SIF TCVAR:CHARA:兒童空腹度 <= 90
				CHILD_ACT:CHARA:CHILDNUM = 
			;20分で満たされる計算
			TCVAR:CHARA:兒童空腹度 -= 経過時間 * 9
		;吃飯,離乳後は吃飯系指令実行時も発生
		ELSEIF TCVAR:CHARA:兒童空腹度 >= 180 || (CFLAG:CHARA:出産経過日 >= CHILD_離乳 && GROUPMATCH(SELECTCOM, 414, 415, 610, 615, 617))
			;授乳
			SIF CFLAG:CHARA:出産経過日 < CHILD_離乳
				EXP:CHARA:噴乳経験 ++
			CHILD_ACT:CHARA:CHILDNUM = 吃飯
		;玩具終了（吃飯と違って必ずしも継続しない）
		ELSEIF CHILD_ACT:CHARA:CHILDNUM == "玩具"
			SIF LOCAL % 7 == 0
				CHILD_ACT:CHARA:CHILDNUM = 
		;玩具
		ELSEIF CFLAG:CHARA:出産経過日 >= CHILD_玩具 && LOCAL % 6 == 0
			CHILD_ACT:CHARA:CHILDNUM = 玩具
		;睡眠
		ELSEIF TCVAR:CHARA:兒童空腹度 < 180 && LOCAL < 5
			CHILD_ACT:CHARA:CHILDNUM = 睡眠
		;お風呂から出る
		ELSEIF CHILD_ACT:CHARA:CHILDNUM == "入浴" && !BATHROOM(CFLAG:CHARA:現在位置)
			CHILD_ACT:CHARA:CHILDNUM = 
		ENDIF
	ENDIF
	;幼児期は全部角色依存
	SIF CFLAG:MASTER:現在位置 == CFLAG:CHARA:現在位置
		RESULT = 1
;通学時（60日～）
ELSEIF GO_SCHOOL(CHARA) && INRANGE(TIME, 360, 900)
	;登校後
	IF CHILD_ACT:CHARA:CHILDNUM == "登校"
		CHILD_ACT:CHARA:CHILDNUM = 授業
	;帰宅後
	ELSEIF  CHILD_ACT:CHARA:CHILDNUM == "帰宅" && INRANGE(TIME, 840, 900)
		CHILD_ACT:CHARA:CHILDNUM = 
	;6時～7時,朝ごはん
	ELSEIF INRANGE(TIME, 360, 420)
		CHILD_ACT:CHARA:CHILDNUM = 吃飯
	;7時～7時半,支度
	ELSEIF INRANGE(TIME, 420, 450)
		CHILD_ACT:CHARA:CHILDNUM = 支度
	;7時半～8時,登校
	ELSEIF INRANGE(TIME, 450, 480)
		CHILD_ACT:CHARA:CHILDNUM = 登校
	;8時～14時,授業
	ELSEIF INRANGE(TIME, 480, 840)
		CHILD_ACT:CHARA:CHILDNUM = 授業
	;14時～15時,下校～帰宅
	ELSEIF INRANGE(TIME, 840, 900) && CHILD_ACT:CHARA:CHILDNUM  == "授業"
		CHILD_ACT:CHARA:CHILDNUM = 帰宅
	ENDIF
	;授業の描写
	IF CHILD_ACT:CHARA:CHILDNUM == "授業"
		IF MAIN_MAP == 2
			SIF CFLAG:MASTER:現在位置 == 222
				RESULT = 1
		ELSE
			SIF CFLAG:MASTER:現在位置 == 寺子屋
				RESULT = 1
		ENDIF
	;登下校描写,寺子屋にいる場合も描写する
	ELSEIF CHILD_ACT:CHARA:CHILDNUM == "登校" || CHILD_ACT:CHARA:CHILDNUM == "帰宅"
		IF MAIN_MAP == 2
			SIF CFLAG:MASTER:現在位置 == 222
				RESULT = 1
			SIF CFLAG:MASTER:現在位置 == CFLAG:CHARA:現在位置
				RESULT = 1
			SIF 自室自宅判定(CFLAG:MASTER:現在位置, CHARA)
				RESULT = 1
		ELSE
			;拠点角色の場合は拠点内でのみ
			IF CFLAG:CHARA:神社在住
				IF AT_HOME(MASTER)
					SIF CFLAG:MASTER:現在位置 == CFLAG:CHARA:現在位置
						RESULT = 1
					SIF 自室自宅判定(CFLAG:MASTER:現在位置, CHARA, CFLAG:CHARA:出産経過日 >= CHILD_自立前)
						RESULT = 1
				ELSE
					SIF CFLAG:MASTER:現在位置 == 寺子屋
						RESULT = 1
				ENDIF
			ELSE
				SIF CFLAG:MASTER:現在位置 == CFLAG:CHARA:現在位置
					RESULT = 1
				SIF 自室自宅判定(CFLAG:MASTER:現在位置, CHARA, CFLAG:CHARA:出産経過日 >= CHILD_自立前)
					RESULT = 1
				SIF CFLAG:MASTER:現在位置 == 寺子屋
					RESULT = 1
			ENDIF
		ENDIF
	;それ以外の描写
	ELSE
		;拠点にいる時に角色依存および角色自室
		IF AT_HOME(MASTER)
			SIF CFLAG:MASTER:現在位置 == CFLAG:CHARA:現在位置
				RESULT = 1
			SIF 自室自宅判定(CFLAG:MASTER:現在位置, CHARA)
				RESULT = 1
		ENDIF
	ENDIF
;幼少期（46日～89日）
ELSEIF CFLAG:CHARA:出産経過日 < CHILD_自立前
	;就寝
	IF 兒童の睡眠時間(CHARA)
		CHILD_ACT:CHARA:CHILDNUM = 睡眠
	;起床
	ELSEIF CHILD_ACT:CHARA:CHILDNUM == "睡眠" && !兒童の睡眠時間(CHARA)
		CHILD_ACT:CHARA:CHILDNUM = 起床
	;お風呂
	ELSEIF BATHROOM(CFLAG:CHARA:現在位置)
		CHILD_ACT:CHARA:CHILDNUM = 入浴
	;吃飯中
	ELSEIF CHILD_ACT:CHARA:CHILDNUM == "吃飯"
		;満たされたら終了
		SIF TCVAR:CHARA:兒童空腹度 <= 120
			CHILD_ACT:CHARA:CHILDNUM = 
		;30分で満たされる計算
		TCVAR:CHARA:兒童空腹度 -= 経過時間 * 12
	;吃飯,角色同室中は吃飯系指令実行時も発生
	ELSEIF TCVAR:CHARA:兒童空腹度 >= 360 || (CFLAG:MASTER:現在位置 == CFLAG:CHARA:現在位置 && GROUPMATCH(SELECTCOM, 414, 415, 610, 615, 617))
		CHILD_ACT:CHARA:CHILDNUM = 吃飯
	;行動の終了
	ELSEIF GROUPMATCH(CHILD_ACT:CHARA:CHILDNUM, "玩具", "お絵かき")
		SIF LOCAL % 8 == 0
			CHILD_ACT:CHARA:CHILDNUM = 
	;起床後はすぐ別行動に移る（他の行動がなければ）
	ELSEIF CHILD_ACT:CHARA:CHILDNUM == "起床"
		CHILD_ACT:CHARA:CHILDNUM = 
	;お風呂から出る
	ELSEIF CHILD_ACT:CHARA:CHILDNUM == "入浴" && !BATHROOM(CFLAG:CHARA:現在位置)
		CHILD_ACT:CHARA:CHILDNUM = 
	ELSE
		;行動
		SELECTCASE LOCAL
		CASE IS < 20
			CHILD_ACT:CHARA:CHILDNUM = 玩具
		CASE IS < 40
			CHILD_ACT:CHARA:CHILDNUM = お絵かき
		ENDSELECT
	ENDIF
	;起床、睡眠は自室で描写する,行動移行したときだけ角色依存でも描写する
	IF GROUPMATCH(CHILD_ACT:CHARA:CHILDNUM, "起床", "睡眠")
		SIF 自室自宅判定(CFLAG:MASTER:現在位置, CHARA)
			RESULT = 1
		SIF CFLAG:MASTER:現在位置 == CFLAG:CHARA:現在位置 && CHILD_ACT:CHARA:CHILDNUM != LOCALS
			RESULT = 1
	;入浴は角色依存で描写する
	ELSEIF GROUPMATCH(CHILD_ACT:CHARA:CHILDNUM, "入浴")
		SIF CFLAG:MASTER:現在位置 == CFLAG:CHARA:現在位置
			RESULT = 1
	;それ以外は角色依存と角色自室で描写する,場合によってはワープするのは仕様
	ELSE
		SIF CFLAG:MASTER:現在位置 == CFLAG:CHARA:現在位置
			RESULT = 1
		SIF 自室自宅判定(CFLAG:MASTER:現在位置, CHARA)
			RESULT = 1
	ENDIF
;自立前（90日～119日）
ELSE
	;就寝（自立日を除く）
	IF 兒童の睡眠時間(CHARA) && CFLAG:CHARA:出産経過日 != CHILD_自立
		CHILD_ACT:CHARA:CHILDNUM = 睡眠
	;起床
	ELSEIF CHILD_ACT:CHARA:CHILDNUM == "睡眠" && !兒童の睡眠時間(CHARA)
		CHILD_ACT:CHARA:CHILDNUM = 起床
	;吃飯中
	ELSEIF CHILD_ACT:CHARA:CHILDNUM == "吃飯"
		;満たされたら終了
		SIF TCVAR:CHARA:兒童空腹度 <= 0
			CHILD_ACT:CHARA:CHILDNUM = 
		;30分で満たされる計算
		TCVAR:CHARA:兒童空腹度 -= 経過時間 * 12
	;吃飯,角色同室中は吃飯系指令実行時も発生
	ELSEIF TCVAR:CHARA:兒童空腹度 >= 360 || (CFLAG:MASTER:現在位置 == CFLAG:CHARA:現在位置 && GROUPMATCH(SELECTCOM, 414, 415, 610, 615, 617))
		CHILD_ACT:CHARA:CHILDNUM = 吃飯
	;玩具終了
	ELSEIF CHILD_ACT:CHARA:CHILDNUM == "玩具"
		SIF LOCAL % 3 == 0
			CHILD_ACT:CHARA:CHILDNUM = 
	;行動の終了（ある程度継続する）
	ELSEIF GROUPMATCH(CHILD_ACT:CHARA:CHILDNUM, "玩遊戲", "勉強", "読書", "料理", "絵描き", "歌唱", "掃除")
		SIF LOCAL % 8 == 0
			CHILD_ACT:CHARA:CHILDNUM = 
	;起床後はすぐ別行動に移る（他の行動がなければ）
	ELSEIF CHILD_ACT:CHARA:CHILDNUM == "起床"
		CHILD_ACT:CHARA:CHILDNUM = 
	ELSE
		;行動
		SELECTCASE LOCAL
		CASE IS < 6
			CHILD_ACT:CHARA:CHILDNUM = 玩耍
		CASE IS < 12
			CHILD_ACT:CHARA:CHILDNUM = 勉強
		CASE IS < 14
			CHILD_ACT:CHARA:CHILDNUM = 読書
		CASE IS < 16
			CHILD_ACT:CHARA:CHILDNUM = 料理
		CASE IS < 18
			CHILD_ACT:CHARA:CHILDNUM = 絵描き
		CASE IS < 20
			CHILD_ACT:CHARA:CHILDNUM = 歌唱
		CASE IS < 22
			CHILD_ACT:CHARA:CHILDNUM = 掃除
		CASE IS < 24
			CHILD_ACT:CHARA:CHILDNUM = 玩具
		ENDSELECT
	ENDIF
	;起床、睡眠は自室で描写する,行動移行したときだけ角色依存でも描写する
	IF GROUPMATCH(CHILD_ACT:CHARA:CHILDNUM, "起床", "睡眠")
		SIF 自室自宅判定(CFLAG:MASTER:現在位置, CHARA, 1)
			RESULT = 1
		SIF CFLAG:MASTER:現在位置 == CFLAG:CHARA:現在位置 && CHILD_ACT:CHARA:CHILDNUM != LOCALS
			RESULT = 1
	;玩具、絵かき、掃除、勉強は角色自室のみ描写する
	ELSEIF GROUPMATCH(CHILD_ACT:CHARA:CHILDNUM, "玩具", "絵描き", "掃除", "勉強")
		SIF 自室自宅判定(CFLAG:MASTER:現在位置, CHARA, 1)
			RESULT = 1
	;玩耍と歌唱は訓練できる場所
	ELSEIF GROUPMATCH(CHILD_ACT:CHARA:CHILDNUM, "玩耍", "歌唱")
		SIF 戦闘訓練可(CFLAG:MASTER:現在位置)
			RESULT = 1
	;読書は読書できる場所か角色自室
	ELSEIF CHILD_ACT:CHARA:CHILDNUM == "読書"
		IF 自室自宅判定(CFLAG:MASTER:現在位置, CHARA, 1)
			RESULT = 1
		ELSEIF IS_LIBRARY(CFLAG:MASTER:現在位置, 1)
			IF CFLAG:CHARA:神社在住 && AT_HOME(MASTER)
				RESULT = 1
			ELSEIF CFLAG:MASTER:現在位置 / 100 == CFLAG:CHARA:自宅位置 / 100
				RESULT = 1
			ENDIF
		ENDIF
	;料理は料理できる場所
	ELSEIF CHILD_ACT:CHARA:CHILDNUM == "料理"
		IF KITCHEN(CFLAG:MASTER:現在位置)
			IF CFLAG:CHARA:神社在住 && AT_HOME(MASTER)
				RESULT = 1
			ELSEIF CFLAG:MASTER:現在位置 / 100 == CFLAG:CHARA:自宅位置 / 100
				RESULT = 1
			ENDIF
		ENDIF
	;それ以外は角色依存と角色自室で描写する
	ELSE
		SIF CFLAG:MASTER:現在位置 == CFLAG:CHARA:現在位置
			RESULT = 1
		SIF 自室自宅判定(CFLAG:MASTER:現在位置, CHARA, 1)
			RESULT = 1
	ENDIF
ENDIF

;吃飯以外ならお腹が減る
SIF CHILD_ACT:CHARA:CHILDNUM != "吃飯"
	TCVAR:CHARA:兒童空腹度 += 経過時間
IF CFLAG:CHARA:出産経過日 < CHILD_語彙
	TCVAR:CHARA:兒童空腹度 = MIN(TCVAR:CHARA:兒童空腹度, 180)
ELSE
	TCVAR:CHARA:兒童空腹度 = MIN(TCVAR:CHARA:兒童空腹度, 480)
ENDIF

TIMESTAMP = 1440 * DAY + TIME
TIMESTAMP_DAY = DAY
;時間停止中は描写しない
SIF FLAG:時間停止
	RESULT = 0
;诶嘿嘿中は描写しない
SIF CFLAG:MASTER:诶嘿嘿
	RESULT = 0
;この時点でRESULT=1なら情景描写はする
SIF RESULT
	TFLAG:描写中の兒童 = CHARA
;行動が継続していたら移行描写はしない
SIF CHILD_ACT:CHARA:CHILDNUM == LOCALS
	RESULT = 0

RETURN RESULT

;-------------------------------------------------
;兒童の描写を判定する際の自室と自宅の判定
;ARG    判定地点（主に現在位置）
;ARG:1  拠点内の默认初期位置を含めるなら1（自立前）
;RETURN 描写するなら1
;-------------------------------------------------
@自室自宅判定(ARG, CHARA, ARG:1)
#FUNCTION
#DIM CHARA
;拠点角色の場合は自室
IF CFLAG:CHARA:神社在住
	SIF ARG == CFLAG:CHARA:初期位置
		RETURNF 1
;外の角色の場合は自宅前と待客室,
ELSE
	SIF ARG == CFLAG:CHARA:自宅位置
		RETURNF 1
	SIF ARG == OMANEKIBEYA() && TARGET == CHARA
		RETURNF 1
	SIF ARG:1 && ARG == 默认初期位置
		RETURNF 1
ENDIF
RETURNF 0

;-------------------------------------------------
;自立後の兒童の行動を判定する関数
;RETURN 描写するなら1
;-------------------------------------------------
@CHILD_MOVEMENT_2(CHARA)
#DIM CHARA
#DIM CHILDNUM
#DIM 描写数
VARSET RESULT
描写数 = MIN(10, CFLAG:CHARA:兒童人数 - CFLAG:CHARA:育児人数) + 1
FOR LOCAL, 1,  描写数
	CHILDNUM = LOCAL
	SIF CHILDNAME:CHARA:CHILDNUM == ""
		BREAK
	;親の睡眠時間を受け継ぐ
	IF !BETWEENTIME(CFLAG:CHARA:起床時間, CFLAG:CHARA:就寝時間)
		CHILD_ACT:CHARA:CHILDNUM = 睡眠
	;8割は表示されない,例外的に末番一致でスキップする（自立先が被った場合の特別処置）
	ELSEIF FLAG:每日变更事件 < 800 && FLAG:每日变更事件 % 10 != CHARA % 10
		CHILD_ACT:CHARA:CHILDNUM = 
	;土日祝日はオフ
	ELSEIF YOUBI_MATCH(DAY, "土日") || FESTIVAL() != ""
		CHILD_ACT:CHARA:CHILDNUM = 散歩
	;活動中は仕事（親の活動時間を受け継ぐ）
	ELSEIF BETWEENTIME(CFLAG:CHARA:来訪時間, CFLAG:CHARA:帰宅時間)
		CHILD_ACT:CHARA:CHILDNUM = 仕事
	;それ以外はフリー
	ELSE
		CHILD_ACT:CHARA:CHILDNUM = 散歩
	ENDIF
	;睡眠,其他は表示されない
	IF GROUPMATCH(CHILD_ACT:CHARA:CHILDNUM, "", "睡眠")
		RESULT = 0
	;それ以外は自立先に居れば表示される,ただし現地エリアに引っ越していると表示されない
	ELSEIF CHILD_AREA:CHARA:CHILDNUM / 100 != MAIN_MAP
		SIF CFLAG:MASTER:現在位置 == CHILD_AREA:CHARA:CHILDNUM
			RESULT = 1
	ENDIF
	
	;時間停止中は描写しない
	SIF FLAG:時間停止
		RESULT = 0
	;诶嘿嘿中は描写しない（念の為）
	SIF CFLAG:MASTER:诶嘿嘿
		RESULT = 0
	;この時点でRESULT=1なら描写する,自立後用の処理
	SIF RESULT
		TFLAG:描写中の兒童 = CHILDNUM * 1000 + CHARA
NEXT
RETURN RESULT
