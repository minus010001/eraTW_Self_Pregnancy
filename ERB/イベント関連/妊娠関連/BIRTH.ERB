@BIRTH(ARG)
#DIM CHILDNUM
DRAWLINE
;妊娠消去、出産経験、性別の決定、誕生日決定
TALENT:ARG:妊娠 = 0
EXP:ARG:出産経験 += 1
CHILDNUM = EXP:ARG:出産経験 % 10
SIF !EXP:ARG:出産経験 % 10
	CHILDNUM = 10
IF FLAG:兒童の性別
	CHILD_SEX:ARG:CHILDNUM = FLAG:兒童の性別
ELSE
	CHILD_SEX:ARG:CHILDNUM = RAND:2 + 1
ENDIF
CHILD_BIRTHDAY:ARG:CHILDNUM = DAY
CHILD_BIRTHDATE:ARG:CHILDNUM = (DAY / 124 + (DAY % 124 ? 1 # 0)) * 1000
CHILD_BIRTHDATE:ARG:CHILDNUM += DAY:2 * 100 + DAY:3
CHILD_WEATHER:ARG:CHILDNUM = %GET_WEATHER()%
IF CFLAG:ARG:無自覚妊娠
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,4,CHILDNUM)
	CHILD_LOVE:ARG:CHILDNUM = -100
ELSEIF TALENT:ARG:恋慕
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,1,CHILDNUM)
	CHILD_LOVE:ARG:CHILDNUM = 300
ELSEIF TALENT:ARG:思慕
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,2,CHILDNUM)
	CHILD_LOVE:ARG:CHILDNUM = 200
ELSE
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,3,CHILDNUM)
	CHILD_LOVE:ARG:CHILDNUM = 100
ENDIF

SIF !CFLAG:ARG:無自覚妊娠 && ITEM:私達だけが知る哀嘆 == 1
	CHILD_LOVE:ARG:CHILDNUM += 150

;あらかじめ自立先を決めちゃう（自立するときにもっかい決める）
CALL 自立先選定(ARG, CHILDNUM)
PRINTFORMW %CALLNAME:ARG%平安无事的生下了%CALLNAME:MASTER%的孩子
PRINTFORMW \@ CHILD_SEX:ARG:CHILDNUM == 1 ? %CALLNAME:ARG%是一個很元気的女孩子 # %CALLNAME:MASTER%是一個很元気的男孩子 \@
PRINTFORMW %CALLNAME:ARG%接过刚刚诞生的小宝宝、温柔的抱住并開始喂起母乳来
SIF TALENT:ARG:母性 == 0
	PRINTFORMW %CALLNAME:ARG%觉醒了[母性]！
PRINTL ******************************************************************
PRINTFORML 第{CHILDNUM}子　母：%CALLNAME:ARG%　父：%CALLNAME:MASTER%
PRINTFORML 　性別：\@ CHILD_SEX:ARG:CHILDNUM == 1 ? 女 # 男 \@
PRINTFORM 誕生日：
CALL PRINT_DATE(DAY)
PRINTFORML  %FESTIVAL()% -%GET_WEATHER()%-
;PRINTFORML 　身高：
;PRINTFORML 　体重：
;PRINTFORML 　種族：
;PRINTFORML 　髪色：
;PRINTFORML 　瞳色：
PRINTL ******************************************************************


;育児中・母性追加
CALL CHILD_NAME(ARG)
CFLAG:ARG:育児人数 += 1
CFLAG:MASTER:育児人数 += 1
CFLAG:ARG:出産経過日 = 1
CFLAG:ARG:妊娠経過日数 = 0
CFLAG:ARG:無自覚妊娠 = 0
CFLAG:ARG:身に覚え_母 = 0
CFLAG:ARG:身に覚え_父 = 0
CFLAG:ARG:妊娠自覚状態 = 0 
TALENT:ARG:母性 = 1

CFLAG:ARG:産休中 = 0

EXP:ARG:愛情経験 += 10
CFLAG:ARG:兒童人数 += 1
CFLAG:MASTER:兒童人数 += 1
IF FLAG:甲斐性無
	PRINTFORML %CALLNAME:MASTER%因為自己孩子的誕生
	CALL ASK_YN("萌生起了作爲父母的責任感","對今後可能發生起的各種各樣的事態感到不安")
	IF !RESULT
		PRINTFORMW %CALLNAME:MASTER%的毫無志氣消失了
	ELSE
		PRINTFORMW 人的性格會這麼簡單就改変的話那麼就沒有人會覺得辛苦了
		PRINTFORMW %CALLNAME:MASTER%還是以前那樣不可靠的性格、幸福的和%CALLNAME:ARG%一起面臨著育児
	ENDIF
	FLAG:甲斐性無 = RESULT
ENDIF
		

@CHILD_NAME(YOME)
#DIM YOME
#DIM CHILDNUM
#DIMS PROPOSED_NAME, 10
CHILDNUM = EXP:YOME:出産経験 % 10
SIF !EXP:YOME:出産経験 % 10
	CHILDNUM = 10
IF CHILD_SEX:YOME:CHILDNUM == 1
	PROPOSED_NAME:0 = %随机角色_命名(YOME)%
ELSE
	PROPOSED_NAME:0 = %男の子_命名(YOME, CHILDNUM)%
ENDIF
$LOOP0
PRINTL 請決定孩子的名字
PRINTFORML 
CALL ASK_M("自己決定",1, "一覧隨機候補",1, @"顺从%CALLNAME:YOME%的提案取名为「%PROPOSED_NAME:0%」",1)
;CALL ASK_M("自己決定",1,"一覧隨機候補",1,"取名為帯著「墨西哥吹來的熱風！」意味的「桑塔納」怎麼樣！",1)
SELECTCASE RESULT
	CASE 0
		$LOOP1
		PRINTL 請輸入孩子的名字
		INPUTS
		IF RESULTS == ""
			GOTO LOOP1
		ELSE
			PROPOSED_NAME:0 = %RESULTS%
			CALL CHK_CHILDNAME_DUPLICATION(PROPOSED_NAME:0)
			PRINTFORML %PROPOSED_NAME:0%可以麼？
			CALL ASK_YN
			IF RESULT
				GOTO LOOP1
			ELSE
				CHILDNAME:YOME:CHILDNUM = %PROPOSED_NAME:0%
			ENDIF
		ENDIF
	CASE 1
		$LOOP2
		FOR LOCAL,1,10
			IF CHILD_SEX:YOME:CHILDNUM == 1
				PROPOSED_NAME:LOCAL = %随机角色_命名(YOME)%
			ELSE
				PROPOSED_NAME:LOCAL = %男の子_命名(YOME, CHILDNUM)%
			ENDIF
			PRINTFORML [{LOCAL}] - %PROPOSED_NAME:LOCAL%
		NEXT
		DRAWLINE
		PRINTFORML [99] - 候補再生成
		PRINTFORML [100] - 返回
		$LOOP3
		INPUT
		SELECTCASE RESULT
			CASE 1 TO 9
				LOCAL = RESULT
				CALL CHK_CHILDNAME_DUPLICATION(PROPOSED_NAME:LOCAL)
				PRINTFORML %PROPOSED_NAME:LOCAL%可以麼？
				CALL ASK_YN
				IF RESULT
					GOTO LOOP0
				ELSE
					CHILDNAME:YOME:CHILDNUM = %PROPOSED_NAME:LOCAL%
				ENDIF
			CASE 99
				GOTO LOOP2
			CASE 100
				GOTO LOOP0
			CASEELSE
				GOTO LOOP3
		ENDSELECT
	CASE 2
		;PRINTFORML 生氣的%CALLNAME:YOME%自己決定好了名字…
		PRINTFORML %CALLNAME:YOME%高兴地呼唤着自己孩子的名字
		CHILDNAME:YOME:CHILDNUM = %PROPOSED_NAME:0%
ENDSELECT

PRINTFORML 取下了%CHILDNAME:YOME:CHILDNUM%的名字
TALENT:YOME:育児中 = CHILDNUM
CALL ASK_DIARY(@"%CALLNAME:YOME%生下了一个\@ CHILD_SEX:YOME:CHILDNUM == 1? 女# 男\@孩子。<br>取名为%CHILDNAME:YOME:CHILDNUM%。")
;----------------------------------------------------
;その角色に特定の年齢の兒童がいたら返す関数
;----------------------------------------------------
@CHK_HAVE_CHILD(ARG,ARG:1,ARG:2)
#FUNCTION
#DIM 年齢
SIF !CFLAG:ARG:育児人数
	RETURNF 0

FOR LOCAL, 1, 11
	年齢 = DAY - CHILD_BIRTHDAY:ARG:LOCAL
	SIF INRANGE(年齢,ARG:1,ARG:2-1)
		RETURNF LOCAL
NEXT

RETURNF 0
;----------------------------------------------------
;誕生日の兒童がいるか返す関数
;eraTW4.570proto_bugfix_m05での仕様変更
;・REFを使用し、配列を書き換えるF関数にした（引数を全て渡さないとエラーになる）
;・仕様上、配列の想定最大要素数は10*人物数量上限となる（＝RESULTの要素数を突破してエラーが出たので仕様変更）
;・BREAKやCONTINUEを使用して処理を効率化
;----------------------------------------------------
@CHILD_HAPPY_BIRTHDAY(CNT_BIRTHDAY, NAME_CHILD, AGE_CHILD)
#FUNCTION
#DIM CHARA
#DIM CHILDNUM
#DIM  REF CNT_BIRTHDAY
#DIMS REF NAME_CHILD
#DIM  REF AGE_CHILD
FOR CHARA, 1, CHARANUM
	SIF !CFLAG:CHARA:兒童人数
		CONTINUE
	FOR CHILDNUM, 1, MIN(11, CFLAG:CHARA:兒童人数 + 1)
		;名無し≒未登録なら下一位角色へ
		SIF !STRLENS(CHILDNAME:CHARA:CHILDNUM)
			BREAK
		;誕生日以外はスキップ
		SIF (DAY - CHILD_BIRTHDAY:CHARA:CHILDNUM) % 124
			CONTINUE
		CNT_BIRTHDAY ++
		NAME_CHILD:CNT_BIRTHDAY = %CHILDNAME:CHARA:CHILDNUM%
		AGE_CHILD:CNT_BIRTHDAY = (DAY - CHILD_BIRTHDAY:CHARA:CHILDNUM) / 124
	NEXT
NEXT
RETURNF CNT_BIRTHDAY

;兒童の名前被りチェック
@CHK_CHILDNAME_DUPLICATION(ARGS)
#DIM C_ID
FOR C_ID,1,CHARANUM
	;"FOR LOCAL,0,10"から修正
	FOR LOCAL,1,11
		SIF CHILDNAME:C_ID:LOCAL == ARGS
			CALL COLORMESSAGE(@"%CALLNAME:C_ID%已经有一个同名的孩子了",C_YELLOW,1)
	NEXT
NEXT