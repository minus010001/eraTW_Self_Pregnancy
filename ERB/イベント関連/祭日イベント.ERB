;-------------------------------------------------
;関数名:FESTIVAL
;概　要:行事を表す文字列を取得する関数
;引　数:無
;-------------------------------------------------
@FESTIVAL
#FUNCTIONS
IF DAY:2 == 1
	SELECTCASE DAY:3
		CASE 1
			RETURNF "元旦"
		CASE 3
			RETURNF "女兒節"
		CASE 11
			RETURNF "母親節"
		CASE 13
			RETURNF "白色情人節前日"
		CASE 14
			RETURNF "白色情人節"
		CASE 25
			RETURNF "例大祭"
		CASE 28
			RETURNF "兔子之日"
	ENDSELECT
ELSEIF DAY:2 == 2
	SELECTCASE DAY:3
		CASE 7
			RETURNF "七夕"
		CASE 14
			RETURNF "盂蘭盆節"
		CASE 28
			RETURNF "兔子之日"
	ENDSELECT
ELSEIF DAY:2 == 3
	SELECTCASE DAY:3
		CASE 1
			RETURNF "風神「二百十日」"
		CASE 15
			RETURNF "十五夜"
		CASE 27
			RETURNF "収穫祭"
		CASE 28
			RETURNF "兔子之日"
		CASE 30
			RETURNF "萬聖節"
	ENDSELECT
ELSEIF DAY:2 == 4
	SELECTCASE DAY:3
		CASE 3
			RETURNF "節分"
		CASE 14
			RETURNF "情人節"
		CASE 24
			RETURNF "平安夜"
		CASE 25
			RETURNF "聖誕節"
		CASE 28
			RETURNF "兔子之日"
		CASE 31
			RETURNF "大晦日"
	ENDSELECT
ENDIF

;特定の祭日の起床時メッセージ
@SEASONAL_MORNING
#DIM 生活時間補正, 5
SELECTCASE FESTIVAL()
	CASE "元旦"
		SIF DAY != 1
			CALL NEWYEAR
	CASE "情人節"
		PRINTL 会收到巧克力吗…
	CASE "白色情人節前日"
		PRINTFORMW 需要準備好回禮
	CASE "兔子之日"
		PRINTFORMW \@ DAY:2 == 4? 在寒空之下穿着兔女郎服度過艰难的一天…# 大家应该都穿着兔女郎服\@
	CASE "白色情人節"
		FOR LOCAL,1,CHARANUM
			SELECTCASE CFLAG:LOCAL:バレンタイン
				CASE 1
					PRINTFORML %CALLNAME:LOCAL%给了黑雷神巧克力……
					PRINTFORMW 礼物还算合适吧
				CASE 2
					PRINTFORML %CALLNAME:LOCAL%给了普通的巧克力……
					PRINTFORMW 要準備相應的回禮
				CASE 3
					PRINTFORML %CALLNAME:LOCAL%给了高级的巧克力……
					PRINTFORMW 要準備上佳的回禮
				CASE 4
					PRINTFORML %CALLNAME:LOCAL%给了亲手做的巧克力……
					PRINTFORMW 要準備充滿真心的回禮
			ENDSELECT
		NEXT
	CASE "大晦日"
		PRINTL 今天的每个角色都会熬夜
ENDSELECT

@SEASONAL(ARG)
VARSET LOCAL
;出会ったとき
SELECTCASE FESTIVAL()
	CASE "元旦"
		PRINTL
		PRINTFORML ●%CALLNAME:MASTER%向%CALLNAME:ARG%致以新年问候
		CALL OTOSHIDAMA(ARG)
	CASE "情人節"
		SELECTCASE CFLAG:ARG:好感度
			CASE 1000 TO 3000
				PRINTFORMW 从%CALLNAME:ARG%那收到了巧克力
				PRINTW ……是黑雷神（义理）巧克力呢
				ITEM:120 ++
				CFLAG:ARG:バレンタイン = 1
			CASE 3001 TO 5000
				PRINTFORMW 从%CALLNAME:ARG%那收到了巧克力
				ITEM:121 ++
				CFLAG:ARG:バレンタイン = 2
			CASE IS > 5000
				IF EXP:ARG:愛情経験 < 100
					PRINTFORMW 从%CALLNAME:ARG%那收到了好像很贵的巧克力
					ITEM:122 ++
					CFLAG:ARG:バレンタイン = 3
				ELSE
					PRINTFORML 从%CALLNAME:ARG%那收到了巧克力
					PRINTW 是亲手作的！好開心！
					ITEM:123 ++
					CFLAG:ARG:バレンタイン = 4
				ENDIF
		ENDSELECT
ENDSELECT

@FESTIVAL_MENU()
#FUNCTIONS
SELECTCASE DAY:2
	;春
	CASE 1
		SELECTCASE DAY:3
			CASE 1,2
				RETURNF "年节菜"
		ENDSELECT
	;夏
	CASE 2
	;秋
	CASE 3
	;冬
	CASE 4
		SELECTCASE DAY:3
			CASE 3
				RETURNF "江户卷"
			CASE 31
				RETURNF "跨年荞麦麺"
		ENDSELECT
ENDSELECT

@NEWYEAR
#DIM YOME
#DIM ChildCount
#DIM CardCount
PRINTW 新年快乐！
CardCount = 0
FOR LOCAL,1,CHARANUM
	IF ABL:YOME:親密 >= 5 && !CFLAG:YOME:神社在住
		PRINTFORML 收到了来自%CALLNAME:YOME%的新年贺卡
		CardCount ++ 
	ENDIF
	SIF CFLAG:YOME:兒童人数 - CFLAG:YOME:育児人数 <= 0
		CONTINUE
	FOR ChildCount,1,11
		IF DAY - CHILD_BIRTHDAY:YOME:ChildCount < 120
			PRINTFORML 收到了来自%CALLNAME:YOME%的\@ CHILD_SEX:YOME:ChildCount == 1 ? 女儿 # 儿子 \@的%CHILDNAME:YOME:ChildCount%的新年贺卡
			CardCount ++ 
		ENDIF
	NEXT
NEXT
IF CardCount > 0
	PRINTFORMW 総計{CardCount}枚！
ENDIF
PRINTFORML 那么、%CALLNAME:MASTER%今年的抱負是…
PRINTL
PRINTLC [1] 自慰奨励
PRINTLC [2] 同性愛推進
PRINTLC [3] 牝穴開拓
PRINTL
PRINTL
PRINTLC [4] 尻穴開発
PRINTLC [5] 乳挟振興
PRINTLC [6] 子孫繁栄
$LOOP
INPUT
SELECTCASE RESULT
	CASE 1
		LOCALS = 自慰
	CASE 2
		LOCALS = 同性愛
	CASE 3
		LOCALS = 性交
	CASE 4
		LOCALS = 肛交
	CASE 5
		LOCALS = 乳交
	CASE 6
		LOCALS = 子作り
	CASEELSE
		GOTO LOOP
ENDSELECT

PRINTFORML %CALLNAME:MASTER%决定今年专注于%LOCALS%、下定了决心
FLAG:抱負 = RESULT

;全年フラグをリセット
FOR LOCAL,0,CHARANUM
	CFLAG:LOCAL:全年 = 0
NEXT

@OTOSHIDAMA(ARG)
SIF DAY <= 1
	RETURN
SIF !TALENT:ARG:幼稚 && !TALENT:ARG:育児中
	RETURN
IF TALENT:ARG:幼稚
	PRINTFORMW 被%CALLNAME:ARG%要了压岁钱
ELSE
	PRINTFORMW 在%CALLNAME:ARG%的要求下给了%SHOW_CHILDNAME(ARG)%压岁钱
ENDIF

SELECTCASE MONEY
	CASE IS > 500000
		LOCAL = 10000
	CASE IS > 100000
		LOCAL = 5000
	CASE IS > 30000
		LOCAL = 3000
	CASE IS > 3000
		LOCAL = 1000
	CASEELSE
		LOCAL = 0
ENDSELECT
IF !LOCAL
	PRINTFORML 当%CALLNAME:MASTER%展示了空空如也的钱包时、%CALLNAME:ARG%\@ TALENT:ARG:幼稚? 投来了怜悯的眼神 # 露骨地表现出了失望的表情\@
	PRINTFORMW %CALLNAME:MASTER%的心靈受到了傷害
ELSE
	PRINTFORML 被%CALLNAME:ARG%拿走了\\{LOCAL}…
	PRINTFORML %CALLNAME:ARG%很開心
	MONEY -= LOCAL
	TALENT:ARG:心情 = 1
	CFLAG:ARG:好感度 += LOCAL/200
	CFLAG:ARG:信賴度 += LOCAL/200
ENDIF