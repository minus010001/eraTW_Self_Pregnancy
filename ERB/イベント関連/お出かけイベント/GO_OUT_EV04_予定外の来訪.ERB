@GO_OUT_CONDITION_EV4
#DIM NUM 
;OPTION設定次第で発生しない
SIF FLAG:交流来訪模式 == 3
	RETURN 0
CALL FISHER_YATES_SHAFFLE(RANDOM_CHARANUM)
;随机で10人拨片UP
FOR COUNT, 0, 10
	NUM = SHAFFLE_ARRAY:COUNT
	;出禁/拠点住まい/来訪不能角色、你を弾く
	SIF CFLAG:NUM:出禁 || CFLAG:NUM:神社在住 || CFLAG:NUM:来訪不能 || !NUM
		CONTINUE
	;訪問予定/仕事/宴会参加角色を弾く
	SIF CFLAG:NUM:拠点来訪 || 仕事日チェック(NUM) || CFLAG:NUM:宴会参加
		CONTINUE
	;隙間にいない/活動時間外/衰弱中角色を弾く
	SIF CFLAG:NUM:現在位置 != SUKIMA() || !VISIT(NUM) || CFLAG:NUM:衰弱
		CONTINUE
	;来訪フラグ.ERBから流用した角色別設定
	SELECTCASE NUM
		CASE [[妹紅]], [[輝夜]]
			SIF CFLAG:NUM:好感度 <= 500
				CONTINUE
		CASE [[依姫]], [[豊姫]], [[探女]]
			SIF !TALENT:NUM:恋慕 && !TALENT:NUM:思慕
				CONTINUE
		CASE [[若鷺姫]]
			SIF !GETBIT(FLAG:泳池, 3)
				CONTINUE
	ENDSELECT
;ここまで通った角色を拠点に呼ぶ
	CALL GO_OUT_EV4(NUM)
	RETURN 1
NEXT
RETURN 0

@GO_OUT_EV4(ARG)
CFLAG:ARG:拠点来訪 = 1
CFLAG:ARG:現在位置 = ENTRANCE
CFLAG:ARG:最後に来訪した日 = DAY
PRINTFORML %CALLNAME:MASTER%看到一位少女向%NAME_FROM_PLACE(ENTRANCE)%走去
PRINTFORMW 【%CALLNAME:ARG%来訪了】
PRINTFORML 
RETURN 1