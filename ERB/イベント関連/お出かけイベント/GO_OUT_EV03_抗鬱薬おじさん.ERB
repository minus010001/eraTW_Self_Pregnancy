@GO_OUT_CONDITION_EV3
IF !RAND:10 && (MAIN_MAP == 2 || CFLAG:MASTER:约会中 == 2)
	CALL GO_OUT_EV3
ELSE
	RETURN 0
ENDIF
RETURN 1

@GO_OUT_EV3
#DIM PAIR
PAIR = FLAG:约会的对象
SETCOLOR C_L_GRAY
PRINTFORMDW 道中、有一个可疑的男人正在接近…！？
PRINTFORML 
IF PAIR
	PRINTFORML 「你们！　很抱歉打扰了、能稍微听我说说吗？」
ELSE
	PRINTFORML 「那位！　那边那位！　能听我说些話吗？」
ENDIF
PRINTFORMW 「不会、不会花太多時間的。只要听我说说就行了…」
PRINTFORMDL 一个蓬乱長髪、有着胡子的男人过来搭話了
PRINTFORMDL 只有眼睛炯炯有神的表情让人見了感到不安…
RESETCOLOR
CALL ASK_M("無視并帰去", 1, "那就聊聊吧", 1)
SETCOLOR C_L_GRAY
IF !RESULT
	PRINTFORMDL 拒绝了痛苦男人的搭话
	PRINTFORML 「啊啊！　请等一下！　不要無視、救救我啊～」
	PRINTFORMDL %CALLNAME:MASTER%\@ PAIR ? 们 # \@头也不回地踏上了帰路
	PRINTFORMW
	RESETCOLOR
	RETURN 1
ENDIF
PRINTFORML 「哦哦！　谢谢、谢谢！」
PRINTFORML 「刚才、从哪里開始说起呢…」
SELECTCASE DAY:2
	CASE 1, 3
		PRINTFORMDW 明明是这种季節却在出汗的男人開始了叙述…
	CASE 2
		PRINTFORMDW 虽然天气很热但也能看出流汗量不尋常的男人開始了叙述…
	CASE 4
		PRINTFORMDW 尽管很冷却满脸汗水的男人開始了叙述…
ENDSELECT
PRINTDW …………
PRINTDW ……
PRINTDW …
IF CFLAG:MASTER:今日の運勢 / 100 + TFLAG:幸運補正 < 5
	PRINTFORML 「你\@ PAIR ? 们 # \@也注意到了吧？　最近发生的许多異変…」
	PRINTFORML 「为什么这种異常事态一直在发生？」
	PRINTFORML 「没错、世界末日就要到了！」
	PRINTFORML 「知道了这个、就不安得不得了…」
	PRINTFORMW 「…………！　…………！！」
	PRINTFORMDL 男人没完没了地描述着自己的臆想…
	PRINTFORMDL 感到厌烦的%CALLNAME:MASTER%\@ PAIR ? 和%CALLNAME:PAIR% # \@在適当的时候逃走了
	PRINTFORMW 
	IF PAIR
		PRINTFORML 难得的约会被浇了冷水、带着扫兴気氛的二人踏上了帰路…
		CALL RECOVER(MASTER, -300, "気力", "憂鬱的話")
		CALL RECOVER(PAIR, -300, "気力", "憂鬱的話")
		BASE:PAIR:情緒 = BASE:PAIR:情緒 / 3
	ELSE
		PRINTFORMDL 浪费了時間和気力的%CALLNAME:MASTER%厌烦地踏上了帰路…
		CALL RECOVER(MASTER, -300, "気力", "憂鬱的話")
	ENDIF
	PRINTFORMDL +60分
	TIME += 60
ELSE
	PRINTFORML 「世界末日临近的谣言你\@ PAIR ? 们 # \@也是知道的吧？」
	PRINTFORMW 「那个谣言让我非常悩火…」
	PRINTFORML 「从善意的薬商那里買到了薬！」
	PRINTFORML 「多亏了这个…烦悩被彻底解消了！」
	PRINTFORML 「哎呀、这样就算世界末日到了也不怕！」
	PRINTFORMDW 男人无休止地谈论着抗鬱薬的神奇之处…
	PRINTFORML 
	PRINTFORML 「这个可厉害！　虽然贵一点……但在当今世界没有可不行！」
	PRINTFORMW 「嘛、多亏了这个还有剩余一些薬…」
	PRINTFORMW 「好！　作为听我说話的谢礼！　这些就全部给你吧！」
	PRINTFORMDL 男人将成堆的睡眠薬塞给了%CALLNAME:MASTER%
	CALL GET_ITEM("睡眠薬", 9, 2)
	IF PAIR
		PRINTFORMDL …仔细一看还混有其他種類的薬
		CALL GET_ITEM("媚薬", 2, 2)
	ENDIF
	PRINTFORML 
	PRINTFORMDL 烦恼着到底该不该服用这个的%CALLNAME:MASTER%\@ PAIR ? 们 # \@踏上了帰路…
	CALL RECOVER(MASTER, -100, "気力", "疲惫的話")
	SIF PAIR
		CALL RECOVER(PAIR, -100, "気力", "疲惫的話")
	PRINTFORMDL +30分
	TIME += 30
ENDIF
PRINTFORMW 
RESETCOLOR
RETURN 1
