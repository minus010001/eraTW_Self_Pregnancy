@GO_OUT_CONDITION_EV0
#DIM PAIR
PAIR = FLAG:约会的对象
SIF RAND:10 || GROUPMATCH(PAIR, 0, RANDOM_CHARANUM) 
	RETURN 0
SIF !TALENT:PAIR:思慕 && !TALENT:PAIR:恋慕
	RETURN 0
SIF MARK:PAIR :反発刻印 || TALENT:PAIR :心情 < 0
	RETURN 0
CALL GO_OUT_EV0(PAIR)

@GO_OUT_EV0(ARG)
#DIM MULTIPLE

IF TALENT:ARG:幼稚
	PRINTFORMW 在回来的路上%TEXTR(@"和%CALLNAME:ARG%竞走着/和%CALLNAME:ARG%比赛着/扛着%CALLNAME:ARG%")%帰来了
	CALL PRINT_FACE, ARG, "笑顔" ,,, "照れ"
	PRINTFORML %CALLNAME:ARG%看起来很開心…
	MULTIPLE = 12
ELSEIF TALENT:ARG:恋人
	PRINTFORMW 在回来的路上被%CALLNAME:ARG%抱着手臂帰来了
	CALL PRINT_FACE, ARG, "笑顔",,, "照れ"
	PRINTFORML 直到约会的最後都感觉很幸福…
	MULTIPLE = 13
ELSEIF TALENT:ARG:恋慕
	PRINTFORMW 和%CALLNAME:ARG%牵着手帰来了
	CALL PRINT_FACE, ARG, "笑顔",,, "照れ"
	PRINTFORML %CALLNAME:ARG%看起来很開心…
	MULTIPLE = 12
ELSE
	PRINTFORMW 在回来的路上和%CALLNAME:ARG%牵着手帰来了
	CALL PRINT_FACE, ARG, "通常",,, "照れ"
	PRINTFORML 感觉和%CALLNAME:ARG%的距離更近了…
	MULTIPLE = 11
ENDIF
PRINTFORMW 
BASE:ARG:情緒 = MIN(BASE:ARG:情緒 / 10 * MULTIPLE, MAXBASE:ARG:情緒)
SIF !TALENT:ARG:幼稚
	BASE:ARG:理性 = MAX(BASE:ARG:理性 - MULTIPLE * 5, 0)
RETURN 1
