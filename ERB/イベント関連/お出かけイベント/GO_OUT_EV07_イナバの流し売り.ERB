@GO_OUT_CONDITION_EV7
SIF RAND:10 || FLAG:约会的对象
	RETURN 0
CALL GO_OUT_EV7
RETURN 1
@GO_OUT_EV7
#DIM 取引
#DIM 割引
#DIM 値段
#DIM 石販売中

石販売中 = (GETBIT(FLAG_DAILY_EV23, 0) && !ITEM:(GETNUM(ITEM, "奇跡之石")))
割引 = -50
値段 = ITEMPRICE:(GETNUM(ITEM, "奇跡之石")) / 10
SETCOLOR 0XFFFFFF
PRINTFORMDW 道中、突然間被因幡襲擊了…！？
PRINTFORML
PRINTFORML 「啊啊啊！！请不要！！客人！！！啊啊！有麻烦啦！！」
PRINTFORMW 「啊ー！！客人！！我不会玩弾幕游戏的！！请稍等一下！！！」
PRINTFORML 「啊啊！请不要！！请冷静一点！！请听我说！！啊ー！！！」
PRINTFORMDW 背着许多货物的因幡独自一人大声呼喊着… 
CALL PRINT_FACE, 0, "通常", "服", "20"
PRINTFORML 「诶ー、非常抱歉给您带来的不便」
PRINTFORMW 「我现在的呼吸有点慌乱…」
PRINTFORML 「但是、在此相遇一定是有缘」
PRINTFORM 「客人对这边的
CALL COLORMESSAGE("优秀的", C_YELLOW, 0, 1)
PRINTFORMW 商品有兴趣吗？」
RESETCOLOR
CALL ASK_M("無視并离開", 1, "随便看看", 1)
SETCOLOR 0XFFFFFF
IF !RESULT
	PRINTFORMDL 拒绝一切虚假销售
	PRINTFORML 「怎么会ー、至少聊几句嘛ー」
	PRINTFORMDL 無視了仍然在坚持的販売員的%CALLNAME:MASTER%踏上了帰路
	PRINTFORMW
	RESETCOLOR
	RETURN 1
ENDIF
CALL PRINT_FACE, 0, "笑顔", "服", "20"
PRINTFORMW 「非常感谢！」
PRINTFORM 「那么、其他地方见不到的
CALL COLORMESSAGE("秘蔵", C_AQUA, 0, 1)
PRINTFORML 之一！」
PRINTFORM 「虽然
CALL COLORMESSAGE("价格", C_M_GREEN, 0, 1)
PRINTFORM 有点小贵、但是効果绝对
CALL COLORMESSAGE("有保障", C_RED, 0, 1)
PRINTFORMW ！！」
PRINTFORM 「专为客人您这类
CALL COLORMESSAGE("特殊", C_YELLOW, 0, 1)
PRINTFORM 人士所提供的一系列
CALL COLORMESSAGE("特別", C_YELLOW, 0, 1)
PRINTFORML 商品！！」
PRINTFORMW 「请随意阅览吧！！！」
RESETCOLOR
DRAWLINE
$INPUT_LOOP
RESETCOLOR
CALL ITEM_SHOPPING(GETNUM(ITEM, "仙香玉兎"), "", 割引)
CALL ITEM_SHOPPING(GETNUM(ITEM, "国士無双の薬"), "", 割引)
CALL ITEM_SHOPPING(GETNUM(ITEM, "毒气花园"), "", 割引)
CALL ITEM_SHOPPING(GETNUM(ITEM, "劇毒身軀"), "", 割引)
IF 石販売中
	PRINTFORM [{GETNUM(ITEM, "奇跡之石")}] 
	CALL COLORMESSAGE("獨佔愛慾的奇跡之石", C_D_PINK, 1, 1)
	CALL COLORMESSAGE("　　　特別価格90％OFF！", C_RED, 0, 1)
	PRINTFORML %金額表示(値段), 13%
ENDIF
DRAWLINE
SIF 石販売中
	PRINTL [990] 「奇跡之石」是什么？
PRINTFORML [999] 果然还是不買了
PRINTFORML 　　　現在的所持金:%金額表示(MONEY)%
$INPUT_LOOP2
INPUT
取引 = RESULT
SETCOLOR 0XFFFFFF
SELECTCASE 取引
	CASE GETNUM(ITEM, "仙香玉兎") TO GETNUM(ITEM, "劇毒身軀"), GETNUM(ITEM, "奇跡之石")
		IF 取引 == GETNUM(ITEM, "奇跡之石") && !石販売中
			CLEARLINE 1
			GOTO INPUT_LOOP2
		ENDIF
		LOCAL = 取引 == GETNUM(ITEM, "奇跡之石") ? 90 # 割引
		CALL SHOP_ASK(RESULT, LOCAL)
		IF RESULT
			PRINTFORMW 「诶ー、那么其他的物品呢？」
			CLEARLINE 12 + RESULT + (石販売中 * 3)
			GOTO INPUT_LOOP
		ENDIF
		CALL GET_ITEM(ITEMNAME:取引, 1, 2)
	;あのセリフたまに聞きたくなるので
	CASE 990
		IF !石販売中
			CLEARLINE 1
			GOTO INPUT_LOOP2
		ENDIF
		PRINTFORMW 「哦呀？　还以为客人您可能会知道…」
		PRINTFORML 「啊真是好怀念呢」
		PRINTFORML 「在刚開始工作的时候、我拼命去背诵的那个商品説明…」
		PRINTFORML 「那么我確認一下、客人您也再听一遍吧」
		PRINTFORMW 
		CALL DAILY_EV23_SALESTALK(1)
		FONTBOLD
		PRINTFORMW 「但是、现在！」
		PRINTFORM 「这次为你提供了
		CALL COLORMESSAGE(@"\\{値段}", C_RED, 0, 1)
		PRINTFORMW 这样的价格！」
		FONTREGULAR
		PRINTFORML 「不知道什么时候才能入手的希少珍品！！」
		PRINTFORM 「如果错过了这个
		CALL COLORMESSAGE("特別価格", C_RED, 0, 1)
		PRINTFORML …」
		PRINTFORMW 「可能就永远没有机会获得了哦！？」
		PRINTFORML 「那么、客人！」
		PRINTFORM 「保证充実性生活的
		CALL COLORMESSAGE("獨佔愛慾的奇跡之石", C_D_PINK, 0, 1)
		PRINTFORMW ！！！！」
		PRINTFORML 「当然的！　会买下吧！？」
		FONTREGULAR
		CALL ASK_M("买！", MONEY >= 値段, "不买", 1)
		IF !RESULT
			MONEY -= 値段
			CALL GET_ITEM("奇跡之石", 1, 2)
		ELSE
			取引 = 999
		ENDIF
	CASE 999
	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP2
ENDSELECT
IF 取引 < 999
	CALL PRINT_FACE, 0, "笑顔", "服", "20"
	CALL COLORMESSAGE("「非常感谢您的购买！」", 0XFFFFFF, 2, 1)
	PRINTFORMW 「咿呀～客人桑真好骗…」
	CLEARLINE 1
	PRINTFORMW 「非常希望您能再次购买！！」
	PRINTFORML 「那么今天就先告辞了！」
ELSE
	CALL PRINT_FACE, 0, "憤怒", "服", "20"
	CALL COLORMESSAGE("「什么嘛、只是个穷鬼啊…」", 0XFFFFFF, 1, 2)
	CALL COLORMESSAGE("「不妙不妙…浪费这么多时间了啊ー」", 0XFFFFFF, 1, 2)
	PRINTFORMDW 意识到%CALLNAME:MASTER%什么都不打算买的同时、販売員的態度瞬间变坏了…
	CALL COLORMESSAGE(@"「%TEXTR("咱/我/爷/")%可没有时间和一个什么都不买的家伙打交道ー」", 0XFFFFFF, 1, 2)
	CALL COLORMESSAGE("「再ー见ー」", 0XFFFFFF, 1, 2)
ENDIF
PRINTFORMDW 兔子販売員\@ 取引 < 999 ? # 留下几句尖酸的台詞后 \@离開了
RESETCOLOR
PRINTFORML 
RETURN 1
