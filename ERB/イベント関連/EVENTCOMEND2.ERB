@TURN_RESET
;処女喪失
FOR LOCAL, 0, CHARANUM
	SIF !TCVAR:LOCAL:破瓜
		CONTINUE
	IF TCVAR:LOCAL:破瓜 == 1
		IF TALENT:LOCAL:処女 == 1
			TALENT:LOCAL:処女 = 0
			EX:LOCAL:処女喪失记号 = 1
		ENDIF
		IF LOCAL && (FLAG:時間停止 || TCVAR:LOCAL:烂醉 || CFLAG:LOCAL:睡眠)
			TALENT:LOCAL:処女 = -1
			EX:LOCAL:処女喪失记号 = 1
			CFLAG:LOCAL:292 = 1
		ENDIF
	ENDIF
	IF TCVAR:LOCAL:破瓜 == 2 && TALENT:LOCAL:処女 == 2
		TALENT:LOCAL:処女 = 0
		EX:LOCAL:処女喪失记号 = 2
	ENDIF
	IF TCVAR:LOCAL:破瓜 == -1 && TALENT:LOCAL:処女 == -1
		TALENT:LOCAL:処女 = 0
		EX:LOCAL:処女喪失记号 = 1
	ENDIF
NEXT

;童貞喪失
IF HAS_PENIS(MASTER)
	SELECTCASE SELECTCOM
		CASE 60, 61, 65, 67, 68, 320, 322
			TALENT:MASTER:1 |= 1
		CASE 62, 63, 66, 69, 70, 323
			TALENT:MASTER:1 |= 2
	ENDSELECT
ENDIF
IF HAS_PENIS(TARGET)
	SELECTCASE SELECTCOM
		CASE 64
			TALENT:1 |= 1
		CASE 92, 93, 94
			TALENT:1 |= 2
		CASE 71
			IF MASTER_POSE(SET_V, SET_P, 1)
				TALENT:1 |= 2
			ELSE
				TALENT:1 |= 1
			ENDIF
	ENDSELECT
ENDIF

;接吻成功
;合意取得
SIF (SELECTCOM == 312 || SELECTCOM == 602) && TFLAG:193 != 99 && !CFLAG:MASTER:恶作剧
	SETBIT CFLAG:既成事實, 0

;告白成功
;恋人取得
IF SELECTCOM == 352 && TFLAG:193
	SETBIT CFLAG:既成事實, 2
	SETBIT CFLAG:MASTER:既成事實, 2
	TALENT:MASTER:恋人 = NO:TARGET
	TALENT:恋人 = NO:TARGET
	;炮友削除
	IF TALENT:TARGET:炮友 == 1
		TALENT:TARGET:炮友 = 0
	ENDIF
ENDIF

;吃飯
SIF SELECTCOM == 414 || SELECTCOM == 415
	CALL RESET_DISH

;约会道中の進行
IF TFLAG:约会道中 > 1000
	;開始時は+1000して設定値に戻すだけ
	TFLAG:约会道中 -= 1000
ELSEIF TFLAG:约会道中
	SIF !CFLAG:MASTER:诶嘿嘿
		TFLAG:约会道中 = TFLAG:约会道中 - TIME_PROGRESS(TIME:1)
	IF TFLAG:约会道中 <= 0
		PRINTFORMW 到达了%DATENAME_PLACE()%
		TFLAG:约会道中 = 0
		CFLAG:MASTER:現在位置 = GET_ENTRANCE(CFLAG:MASTER:约会中)
		CFLAG:(FLAG:约会的对象):現在位置 = CFLAG:MASTER:現在位置
	ENDIF
ENDIF


;調教履歴を1ターン保存
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:現在位置 == SUKIMA()
		CONTINUE
	TCVAR:LOCAL:110 = TCVAR:LOCAL:10
	TCVAR:LOCAL:112 = TCVAR:LOCAL:12

	SIF TCVAR:LOCAL:102
		TCVAR:LOCAL:102 --
	SIF !TCVAR:LOCAL:102
		TCVAR:LOCAL:103 = -1
	SIF TCVAR:LOCAL:104
		TCVAR:LOCAL:104 --
	SIF !TCVAR:LOCAL:104
		TCVAR:LOCAL:105 = -1
	;体位の保存
	FOR LOCAL:1, 1, 11
		TEQUIP:LOCAL:(110 + LOCAL:1) = TEQUIP:LOCAL:(100 + LOCAL:1)
	NEXT
	;体勢制御
	IF TCVAR:LOCAL:体勢制御
		TCVAR:LOCAL:体勢制御 --
		SIF TCVAR:LOCAL:体勢制御 == 0
			TCVAR:LOCAL:体勢 = 0
	ENDIF
	;位置関係制御
	IF TCVAR:LOCAL:位置関係制御
		TCVAR:LOCAL:位置関係制御 --
		SIF TCVAR:LOCAL:位置関係制御 == 0
			TCVAR:LOCAL:位置関係 = 0
	ENDIF
	;ついでに射精処理用文字列変数の初期化
	CSTR:LOCAL:10 = /
	CSTR:LOCAL:11 = /
	CSTR:LOCAL:汎用エフェクト =
	;服装フラグ
	IF FLAG:時間停止 == 1
	ELSE
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
	ENDIF
	IF BATHROOM(CFLAG:LOCAL:現在位置) && GET_MAPID(CFLAG:LOCAL:現在位置) == MAIN_MAP
		;風呂フラグはMOVEMENT共通処理へ移動しました
;		CFLAG:LOCAL:風呂 = 0
		RESET_STAIN LOCAL
		CFLAG:LOCAL:膣内射精 = 0
		CFLAG:LOCAL:肛門射精 = 0
		CFLAG:LOCAL:口内精液 = 0
		CFLAG:LOCAL:脸上精液 = 0
		CFLAG:LOCAL:手上精液 = 0
	ELSEIF CFLAG:LOCAL:風呂 > 120 && !CFLAG:LOCAL:诶嘿嘿
		STAIN:LOCAL:Ａ |= 汚渍_肛門
	ENDIF
NEXT
;BASEの変動
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:現在位置 == SUKIMA()
		CONTINUE
	;勃起度
	;加算後
	CFLAG:LOCAL:勃起度２ = BASE:LOCAL:勃起
	BASE:LOCAL:勃起 = MIN(MAXBASE:LOCAL:勃起, BASE:LOCAL:勃起)
	IF NOWEX:LOCAL:射精
		IF !MASTER_POSE(SET_V, SET_P) && !MASTER_POSE(SET_A, 1)
			BASE:LOCAL:勃起 = BASE:LOCAL:勃起 * (ABL:LOCAL:欲望 + 1) * (BASE:LOCAL:精力 + 200) / ((MAXBASE:LOCAL:精力 + 200) * (ABL:LOCAL:欲望 + 10))
		ELSE
			BASE:LOCAL:勃起 = 1000
		ENDIF
	ENDIF
NEXT

SIF CFLAG:MASTER:诶嘿嘿
	CALL 诶嘿嘿中特殊事件

;フラグのリセット
TFLAG:192 = 0
TFLAG:193 = 0
TFLAG:194 = 0
TSTR:2 = 


;TCVARのリセット
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:現在位置 == SUKIMA()
		CONTINUE
	FOR LOCAL:1, 0, 100
		TCVAR:LOCAL:(LOCAL:1) = 0
	NEXT
;挿入履歴
	TCVAR:LOCAL:100 = TEQUIP:LOCAL:50
	TCVAR:LOCAL:101 = TEQUIP:LOCAL:51
NEXT

TFLAG:110 = TCVAR:MASTER:亲热

;FIRSTTIMEにSELECTCOMをぶっこむ
CALLF FIRSTTIME(SELECTCOM)
FOR LOCAL, 1, CHARANUM
	SIF EXP_UP(15, LOCAL)
		CALLF FIRSTTIME("精飲", 0, LOCAL)
NEXT
;射精部位
FOR LOCAL:1, 0, 60
	IF GETBIT(TFLAG:1, LOCAL:1)
		LOCALS = "射精"{LOCAL:1}
		CALLF FIRSTTIME(LOCALS, 0, TARGET)
	ENDIF
NEXT
PREVCOM = SELECTCOM
;派生指令の履歴
TFLAG:151 = TFLAG:50
;前回のTARGET
IF TFLAG:前回の指令結果 > 0
	TFLAG:103 = TARGET
	TFLAG:150 = ASSIPLAY
ENDIF

;経験値取表示用
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	;現在位置の保存
	CFLAG:LOCAL:前一次的位置 = CFLAG:LOCAL:現在位置
	FOR LOCAL:1, 0, 100
		TCVAR:LOCAL:(400 + LOCAL:1) = EXP:LOCAL:(LOCAL:1)
	NEXT
NEXT
IF BATHCHECK(CFLAG:MASTER:現在位置) && !FLAG:時間停止 && AT_HOME(MASTER)
	FOR LOCAL,1,CHARANUM
		SIF 風呂逐出条件(LOCAL) && CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
			CALL 風呂から逐出(LOCAL)
		;MASTERが追い出されたならループ終了
		SIF BATHCHECK(CFLAG:MASTER:現在位置) == 0
			BREAK
	NEXT
ENDIF
;TFLAGのリセット
VARSET TFLAG, 0, 0, 100
TFLAG:400 = 0
TFLAG:移動不能メッセージ = 0
;眠いと気力が減少していく
IF !CFLAG:MASTER:睡眠
	IF NEMUKE() >= 960
		BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 5 * TIME_PROGRESS(TIME:1) , 0)
		BASE:MASTER:気力 = MAX(BASE:MASTER:気力 - 10 * TIME_PROGRESS(TIME:1) , 0)
	ELSEIF NEMUKE() >= 840
		BASE:MASTER:気力 = MAX(BASE:MASTER:気力 - 2 * TIME_PROGRESS(TIME:1) , 0)
	ENDIF
ENDIF


@诶嘿嘿中特殊事件
#DIM 奪主導権確率
VARSET LOCAL
;你主導
IF CFLAG:TARGET:诶嘿嘿 == 1 && CFLAG:积攒度 > 300 && !TCVAR:TARGET:强行
;攻守逆転
	IF SHIRAHU(TARGET) && TEQUIP:TARGET:体位 == 3
		;強気なほど確率が高い→分母は小さい
		;1/確率
		奪主導権確率 = 15
		奪主導権確率 -= TALENT:TARGET:胆量 * 2
		奪主導権確率 -= TALENT:TARGET:応答 * 2
		奪主導権確率 -= TALENT:TARGET:自尊心 * 2
		奪主導権確率 += TALENT:TARGET:自制心 * 3
		奪主導権確率 -= TALENT:TARGET:性的兴趣 * 3
		奪主導権確率 += TALENT:TARGET:羞恥心 * 2
		奪主導権確率 -= TALENT:TARGET:施虐狂 * 5
		奪主導権確率 += TALENT:TARGET:受虐狂 * 5

		;RANDに渡すので最低値は1
		SIF 奪主導権確率 < 1
			奪主導権確率 = 1
		IF !RAND(奪主導権確率)
			CFLAG:MASTER:诶嘿嘿 = 2
			TFLAG:COMABLE管理 = 3
			FOR LOCAL,1,CHARANUM
				IF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:現在位置
					CFLAG:LOCAL:诶嘿嘿 = 2
					LOCAL:1 ++
				ENDIF
			NEXT
			PRINTFORML 察覺到的時候、主導權已經被%CALLNAME:TARGET%\@ LOCAL:1 > 1 ? 她們 #  \@奪走了
		ENDIF
	ENDIF
;对方主導
ELSEIF CFLAG:TARGET:诶嘿嘿 == 2
	IF TARGET == RANDOM_CHARANUM && TALENT:TARGET:風俗嬢 && (BASE:気力 <= 0 || CFLAG:积攒度 <= 0)
		PRINTFORML %CALLNAME:TARGET%\@ BASE:気力 <= 0 ? 有些疲憊地 # 満足地 \@譲出了主導権
		CFLAG:TARGET:诶嘿嘿 = 1
		CFLAG:MASTER:诶嘿嘿 = 1
	ELSEIF BASE:気力 <= 0
		PRINTFORML %CALLNAME:TARGET%似乎有点疲劳、放開了%CALLNAME:MASTER%
		CALL 被逆推終了("疲惫")
	ELSEIF CFLAG:积攒度 <= 0 && !TCVAR:TARGET:随便
		PRINTFORML %CALLNAME:TARGET%好像已经满足了
		CALL KIGEN_CHANGE(TARGET,100,1)
		BASE:憤怒 = 0
		CFLAG:心情不快 = 0
		CALL ASK_M("摆脱",1,"我們還沒有滿足",1,"繼續進攻",ABL:施虐属性 >= 3)
		SELECTCASE RESULT
			CASE 0
				PRINTFORML %CALLNAME:TARGET%放開%CALLNAME:MASTER%了
				CALL 被逆推終了("満足")
			CASE 1
				PRINTFORML %CALLNAME:MASTER%推倒了%CALLNAME:TARGET%
				CALL KOJO_MESSAGE_SEND("EVENT",30,TARGET,3)
				FOR LOCAL,0,CHARANUM
					CFLAG:(TARGET:LOCAL):诶嘿嘿 = 1
					IF INROOM(CFLAG:MASTER:現在位置) == 2
						CALL DATUI_SHOES(LOCAL,0)
						CALL DATUI_SHOES(MASTER,0)
					ENDIF
				NEXT
			CASE 2
				PRINTFORML %CALLNAME:TARGET%眯起眼睛、在耳边轻声说一起做到不能做为止
				CALL KOJO_MESSAGE_SEND("EVENT",30,TARGET,4)
				TCVAR:TARGET:随便 = 1
		ENDSELECT
	ENDIF
ENDIF

@風呂逐出条件(ARG)
#FUNCTION
SIF FLAG:70
	RETURNF 0
SIF !GETBIT(TALENT:MASTER:2,1) && HETEROSEX(MASTER,ARG)
	RETURNF 0
SIF TALENT:ARG:羞恥心 < 0 && ABL:ARG:親密 >= 5
	RETURNF 0
SIF TALENT:ARG:無知 && ABL:ARG:親密 >= 5
	RETURNF 0
SIF CFLAG:ARG:诶嘿嘿
	RETURNF 0
SIF FLAG:催眠強度 > 50
	RETURNF 0
SIF CFLAG:ARG:態度 < 2
	RETURNF 1
SIF CFLAG:ARG:既成事實 < 1
	RETURNF 1
SIF !(TALENT:ARG:恋慕 || TALENT:ARG:炮友)
	RETURNF 1

RETURNF 0
