@AFTER_AFFAIR
#DIM SPENT_TIME
SPENT_TIME = (TIME-UFUFU_START_TIME) / 60 
IF SPENT_TIME < 0
	SPENT_TIME += 24
ENDIF
;ブチギレと强行状態とTSP切れと烂酔奸と風呂場プレイと睡眠時間中は表示しない
SIF CFLAG:ブチギレ || TCVAR:强行 || !SHIRAHU(TARGET)
	RETURN 0
	;ほんの稍微の诶嘿嘿では表示されないよう、潤滑、欲情、体力、积攒度、その日の絶頂回数に条件をつける（大した意味はない）
SIF !(PALAM:潤滑 >= PALAMLV:3 && PALAM:欲情 >= PALAMLV:3 && BASE:体力 < MAXBASE:体力 * 9 / 10 && CFLAG:积攒度 < 300 && EX:Ｃ絶頂 + EX:Ｖ絶頂 + EX:Ａ絶頂 + EX:Ｂ絶頂 + EX:Ｍ絶頂 >= 4)
	RETURN 0

PRINTFORMD %CALLNAME:MASTER%和%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1? 们 # \@的
;経過時間表示
IF SPENT_TIME
	PRINTFORMD 长达{SPENT_TIME}小時的
	SIF SPENT_TIME > 5
		PRINTFORMD 非常壮烈的
ENDIF
;ここまで
IF TFLAG:泡風呂
	PRINTFORMD 金錢買來的短暫的歡愉
ELSEIF TALENT:恋慕 || TALENT:恋人
	PRINTFORMD 充满誓约的爱情仪式
ELSEIF TALENT:愛欲
	PRINTFORMD 互相贪恋着对方与自己极度契合的肉体交媾
ELSEIF TALENT:思慕
	PRINTFORMD 互有好感、却朋友以上恋人未满的两人所渴求的密会
ELSEIF TALENT:炮友 == 1
	PRINTFORMD 以快乐为目的苟合
ELSE
	PRINTFORMD 轻率的一夜情
ENDIF
PRINTFORMW 结束了。

SIF BATHROOM()
	RETURN 0
;-------------------------------------------------------------------------------------
;以下、精液漏れ・愛液による濡れの描写（精液漏れの描写が優先）

;膣充填率が水準以上もしくは肛門充填率が水準以上
IF SOAKED_OR_NOT("V_SEMINAL") || SOAKED_OR_NOT("A_SEMINAL")
	PRINTFORMD 大量的%EXPR_VAR("SPERM")%%TEXTR("被灌进/被释放进/被注入进")%%CALLNAME:TARGET%的
	SIF SOAKED_OR_NOT("V_SEMINAL")
		PRINTFORMD %EXPR_VAR("VULVA")%
	SIF SOAKED_OR_NOT("A_SEMINAL")
		PRINTFORMD \@ SOAKED_OR_NOT("V_SEMINAL") ? 和 # \@%EXPR_VAR("ANUS")%
	PRINTFORMDL %TEXTR("眼看着都要溢出来了。/慢慢的漏了出来了。/垂落下来。")%
	IF TALENT:MASTER:濃厚精液
		PRINTFORML 还是半固态的%EXPR_VAR("SPERM")%并没有滴落下来、而是粘住下体吊垂着、甚至在%CALLNAME:TARGET%大腿内侧甩动着。
	ELSE
		PRINTFORMD %CALLNAME:TARGET%用%TEXTR("纸巾/手帕/毛巾")%
		PRINTFORMDL 在%EXPR_VAR("CROTCH")%上擦拭了好一会、但%EXPR_VAR("SPERM")%还是从下面%TEXTR("滴滴答答地滴下来了/滴下来了/流了出来、在大腿上流出了一条淫靡的白线/咕嘟咕嘟地溢出来了")%。
	ENDIF
	;--------------------------------------------
	;垂れ口上呼び出し
	;--------------------------------------------
	CALL WEAREDPANTIES_TRANSPARENCY("SPERM")
	CALL KOJO_MESSAGE_SEND("EVENT",15,TARGET,3,1)
;膣・肛門充填率がともに水準未満だが潤滑が必要LV以上
ELSEIF SOAKED_OR_NOT("SECRETIONAL")
	PRINTFORMD %CALLNAME:TARGET%、将湿漉漉的%EXPR_VAR("CROTCH")%用%TEXTR("纸巾/手帕/毛巾")%
	PRINTFORMDL 擦拭着、蜜液不断从尚未从興奮中冷却的肉体中溢出。
	;--------------------------------------------
	;溢れ口上呼び出し
	;--------------------------------------------
	CALL WEAREDPANTIES_TRANSPARENCY("JUICE")
	CALL KOJO_MESSAGE_SEND("EVENT",15,TARGET,3,2)
ENDIF
;-------------------------------------------------------------------------------------------------------
;以下、事後の情緒の描写
;複数TARGETがいる場合非表示
IF GET_TARGETNUM() == 1
	PRINTFORMD 情事之后收拾完的两人
	IF TALENT:恋慕 || TALENT:恋人
		PRINTFORMDL 即使完事了、但仍然还是躺在一起说着说不完的情话。
	ELSEIF TALENT:愛欲
		PRINTFORMDL 仍然散发着火热的气息、彼此调整着紊乱的呼吸过了好长一段时间。
	ELSEIF TALENT:思慕
		PRINTFORMDL 还带着羞涩、互相注视着对方羞涩的脸。
	ELSEIF TALENT:炮友 == 1
		PRINTFORMDL 在暧昧的气氛中、默默地贴着对方的身体。
	ELSE
		PRINTFORMDL 一段时间内、空中散逸着沉默而尴尬的气氛。
	ENDIF
	PRINTFORMW
ENDIF

CALL KOJO_MESSAGE_SEND("EVENT",15,TARGET,3,3)

;---------------------------------------------------------------------------------------------------------
;以下、事後に服等を着なおす描写		
;上身衣服、連衣裙、和服、外搭のどれかを着たままだったばあい
IF EQUIP:上身衣服１ || EQUIP:上身衣服２ || EQUIP:連衣裙 || EQUIP:和服 || EQUIP:外搭
	PRINTFORMDL 慌乱的穿起衣服的%CALLNAME:TARGET%背向这边、再手忙脚乱整理着凌乱的衣服。
	IF PALAM:欲情 >= PALAMLV:7
		PRINTFORMD 被香汗打湿的白里透红的肌肤
	ELSEIF PALAM:欲情 >= PALAMLV:5
		PRINTFORMD 出汗后、白皙的肌肤泛着粉红色的光泽。
	ELSEIF PALAM:欲情 >= PALAMLV:3
		PRINTFORMD 带着汗水的白皙肌肤
	ELSE
		PRINTFORMD 微微出汗
	ENDIF
	PRINTFORMW 看着让人血压飙升。
;現在上半身が裸でかつ便装の上半身内衣が文胸か微型文胸のばあい
ELSEIF TEQUIP:上半身着衣状況 == 0 && (CFLAG:TARGET:207 == CLOTHES_ID("上半身内衣１", "文胸") || CFLAG:TARGET:208 == CLOTHES_ID("上半身内衣２", "微型文胸"))
	PRINTFORMDL H之后、%CALLNAME:TARGET%背向着%CALLNAME:MASTER%要求%CALLNAME:MASTER%帮她挂上文胸的挂钩。
	IF PALAM:欲情 >= PALAMLV:7
		PRINTFORMD 香汗从泛着粉红色的脖颈滑落，忍不住亲了上去
	ELSEIF PALAM:欲情 >= PALAMLV:5
		PRINTFORMD 从背后欣赏着%CALLNAME:TARGET%带汗水微微泛红的背脊
	ELSEIF PALAM:欲情 >= PALAMLV:3
		PRINTFORMD %CALLNAME:TARGET%微微出汗的肌肤十分诱人
	ELSE
		PRINTFORMW 从背后看过去、%CALLNAME:TARGET%光滑的肩胛骨看上去美极了。
	ENDIF
ENDIF

CALL KOJO_MESSAGE_SEND("EVENT",15,TARGET,3,4)

;----------------------------------------------------------------------------------------------------------
;以下、残り香の描写

;濃厚的残り香の表示に必要な射精数、噴乳数、放尿数の設定
LOCAL:5 = 5 ;必要射精数
LOCAL:6 = 5 ;必要噴乳数
LOCAL:7 = 1 ;必要放尿数

;残り香を表示するのは屋内のときだけ
IF OPENPLACE(CFLAG:MASTER:現在位置) < 2
	;その日のふたりの射精数、噴乳数、放尿数によって分岐
	IF EX:射精 + EX:MASTER:射精 >= LOCAL:5 || EX:噴乳 + EX:MASTER:噴乳 >= LOCAL:6 || EX:放尿 + EX:MASTER:放尿 >= LOCAL:7
		PRINTFORMD 热气腾腾的、
		SIF EX:射精 + EX:MASTER:射精 >= LOCAL:5
			PRINTFORMD 精液独特的味道
		SIF EX:噴乳 + EX:MASTER:噴乳 >= LOCAL:6
			PRINTFORMD \@ EX:射精 + EX:MASTER:射精 >= LOCAL:5 ? と # \@甘甜的母乳香味
		SIF EX:放尿 + EX:MASTER:放尿 >= LOCAL:7
			PRINTFORMD \@ EX:射精 + EX:MASTER:射精 >= LOCAL:5 || EX:噴乳 + EX:MASTER:噴乳 >= LOCAL:6 ? 和 # \@透着尿臭的骚味
		PRINTFORMDL 混杂着淫靡而浓稠的余香、
		PRINTFORMW 明确地表明了在那里面的秘密。
	ELSE
		PRINTFORMW 酸甜的体液的气味和热腾腾的空气、还保留着之前的H的痕迹。
	ENDIF
ENDIF
;----------------------------------------------------------------------------------------------------------


@WEAREDPANTIES_TRANSPARENCY(ARGS)
#DIM ぱんつ状況

;胖次を奪ったorもらったor元から穿いてない場合
IF CFLAG:没穿内裤 || (CFLAG:205 == 0 && CFLAG:206 == 0)
	ぱんつ状況 = 0
;胖次を一旦脱いでいた場合
ELSEIF !EQUIP:下半身内衣１ && !EQUIP:下半身内衣２
	ぱんつ状況 = 1
ELSE
	ぱんつ状況 = 2
ENDIF


IF !ぱんつ状況
	PRINTFORMDL %CALLNAME:TARGET%的下面是真空的。
	IF ARGS == "SPERM"
		PRINTFORMDL 今天、没穿内裤、就滴着%EXPR_VAR("SPERM")%这样度过吧。
	ELSE
		PRINTFORMDL 今天、%EXPR_VAR("CROTCH")%充满着爱液的湿润、没有了内裤的保护、就这様暴露在空气中。
	ENDIF
;胖次がある場合
ELSE
	SIF ぱんつ状況 == 1
		PRINTFORMDL %CALLNAME:TARGET%把脱下的内衣重新穿起来。
	IF ARGS == "SPERM"
		PRINTFORMD 淌出来的%EXPR_VAR("SPERM")%
	ELSE
		PRINTFORMD 不断流出的爱液
	ENDIF
	PRINTFORMD 把\@ ぱんつ状況 == 1? 那条# \@内裤%TEXTR("黏在了一起/浸的湿漉漉的/沾湿透了")%
	;胖次が透けうる場合
	IF PANTIES_TRANSPARENCY(TARGET)
		PRINTFORMDL 、甚至可以看到整个阴唇的形状和阴蒂的凸起。
	ELSE
		PRINTFORMDL 、非常诱人。
	ENDIF
ENDIF


;### びしょ濡れ判定用式中関数 ################
;ARG=角色番号
@SOAKED_OR_NOT(ARGS,ARG)
#FUNCTION
#DIM 必要膣充填率
#DIM 必要肛門充填率
#DIM 必要潤滑程度

SIF ARG == 0
	ARG = TARGET

必要膣充填率 = 30
必要肛門充填率 = 30
必要潤滑程度 = PALAMLV:5

IF ARGS == "V_SEMINAL"
	IF 充填率(ARG,1) >= 必要膣充填率
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ELSEIF ARGS == "A_SEMINAL"
	IF 充填率(ARG,2) >= 必要肛門充填率
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ELSEIF ARGS == "SECRETIONAL"
	IF PALAM:ARG:潤滑 >= 必要潤滑程度
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ENDIF

THROW 错误的文字列引数


;### 表現バリエーション用式中関数 ################
@EXPR_VAR(ARGS)
#FUNCTIONS
VARSET LOCALS

IF ARGS == "SPERM"
	SELECTCASE RAND:4
	CASE 0
		LOCALS = 白濁液
	CASE 1
		LOCALS = 精液
	CASE 2
		LOCALS = 浓精
	CASEELSE
		LOCALS = 子種汁
	ENDSELECT
ELSEIF ARGS == "VULVA"
	SELECTCASE RAND:5
	CASE 0
		LOCALS = 秘処
	CASE 1
		LOCALS = 秘裂
	CASE 2
		LOCALS = 陰門
	CASE 3
		LOCALS = 蜜穴
	CASEELSE
		LOCALS = 女陰
	ENDSELECT
ELSEIF ARGS == "ANUS"
	SELECTCASE RAND:4
	CASE 0
		LOCALS = 尻穴
	CASE 1
		LOCALS = 肛門
	CASE 2
		LOCALS = 后庭
	CASEELSE
		LOCALS = 菊門
	ENDSELECT
ELSEIF ARGS == "CROTCH"
	SELECTCASE RAND:4
	CASE 0
		LOCALS = 股間
	CASE 1
		LOCALS = 局部
	CASE 2
		LOCALS = 私处
	CASEELSE
		LOCALS = 股
	ENDSELECT
ENDIF

RETURNF LOCALS