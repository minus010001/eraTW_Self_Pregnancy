;オプション設定処理
@OPTION
#DIM L_COUNT
#DIM L_FEED
#DIM TEMP
VARSET LOCAL
L_FEED = LINECOUNT
TOOLTIP_SETDELAY 100
TEMP = CURRENTREDRAW()
REDRAW 0
DRAWLINEFORM =
FOR L_COUNT, 0, 91
	CALL OPTION_SETNAME(L_COUNT)
	SIF !RESULT
		CONTINUE
	LOCAL:L_COUNT = 1
	IF RESULTS:2 == "" 
		PRINTFORML [{L_COUNT, 2}] - %RESULTS:0, 30, RIGHT%：%RESULTS:1%
	ELSE
		HTML_PRINT @"<button value='{L_COUNT}' title='%RESULTS:2%'>[{L_COUNT, 2}] - %RESULTS:0, 30, RIGHT%：%RESULTS:1%</button>"
	ENDIF
NEXT
DRAWLINEFORM －
PRINTFORML [91] - 各种补丁的启动设定
PRINTFORML [92] - 自動喘息一括設定
PRINTFORML [93] - 簡易過濾的總設定
PRINTFORML [94] - 画像表示設定
PRINTFORML [95] - 登場人物的切换
PRINTFORML [96] - 指令过滤器
;#;PRINTFORML [97] - カウンター行動フィルタ（デバッグ用）
;PRINTFORML [98] - 料理操作的設定
DRAWLINE

SIF DAY > 124
	HTML_PRINT @"<button value='200' title='【警告】 これより 先は 読んで是けない'>[200] - 败者・食尘</button>"
PRINTFORML [999] - 設定完了
INPUT
SELECTCASE RESULT
CASE 0 TO 90
	SIF LOCAL:RESULT
	CALL OPTION_SETTING(RESULT)
	CLEARLINE LINECOUNT - L_FEED
CASE 91
	CALL OPTION_DLC
CASE 92
	CALL OPTION_自動喘息一括設定
CASE 93
	CALL 簡易过滤器一括設定
CASE  94
	CALL OPTION_画像表示詳細設定
CASE 95
	CALL 登場人物的切换
CASE 96
	CALL OPTION_COMMAND_FILTER
[IF_DEBUG]
CASE 97
	TRYCALL SET_OPTION_COOKING
[ENDIF]
;CASE 98
;	CALL SET_OPTION_COOKING
CASE  200
		TOOLTIP_SETDELAY 500
	IF DAY <= 124
		REDRAW TEMP
		RETURN
	ENDIF
	PRINTFORML 要返回到第一天么？？
	PRINTL 
	PRINTL [0]不要按下「開关」啊ーー
	PRINTL [1]不！到极限了、按下它！就现在！
	INPUT
	IF RESULT == 1
		CALL 強行開始新的游戏
	ELSE
;		PRINTW 虽然没能停止按下“开关”，但幸运的是只回到了选项选择，当然你也可以回头。
	ENDIF
CASEELSE
	TOOLTIP_SETDELAY 500
	REDRAW TEMP
	RETURN
ENDSELECT
RESTART

@OPTION_SETNAME(ARG)
;RESULT:2	ツールチップ表記内容
VARSET RESULTS 
RESULT = 1
SELECTCASE ARG
CASE 0
	RESULTS:0 = 妊娠の有無
	IF FLAG:妊娠開关
		RESULTS:1 = 妊娠する
		RESULTS:2 = ※你の妊娠機能は未実装※
	ELSE
		RESULTS:1 = 不会怀孕
	ENDIF
CASE 1
	RESULTS:0 = 兒童の性別
	SELECTCASE FLAG:兒童の性別
	CASE 0
		RESULTS:1 = 随机
	CASE 1
		RESULTS:1 = 女孩子
	CASE 2
		RESULTS:1 = 男孩子
	ENDSELECT
	SIF !FLAG:妊娠開关
		RESULT = 0
CASE 2
	RESULTS:0 = 兒童の描写 
	IF FLAG:兒童描写
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
	SIF !FLAG:妊娠開关
		RESULT = 0
CASE 3
	RESULTS:0 = 住宿事件
	IF FLAG:住宿事件
		RESULTS:1 = 无
	ELSE
		RESULTS:1 = 有
		RESULTS:2 = 親しい角色が自宅地域に引っ越してきます
	ENDIF
CASE 4
	RESULTS:0 = 宴会の発生
	IF FLAG:宴会的有無
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
CASE 5
	RESULTS:0 = 対方是否会告白
	IF FLAG:告白禁止
		RESULTS:1 = 不会告白
	ELSE
		RESULTS:1 = 会告白
	ENDIF
CASE 6
	RESULTS:0 = 從炮友系陥落向恋慕系陥落変化
	IF FLAG:陥落路線変化
		RESULTS:1 = 会変化
	ELSE
		RESULTS:1 = 不会変化
	ENDIF
CASE 7
	RESULTS:0 = 因事件而[無知]喪失
	IF FLAG:無知自動喪失禁止
		RESULTS:1 = 不会喪失
	ELSE
		RESULTS:1 = 会喪失
	ENDIF
CASE 8
	RESULTS:0 = 文本設定
	IF FLAG:情景文本設定 == 1 && FLAG:口上文本設定 == 2
		RESULTS:1 = 口上＋情景文本
	ELSEIF FLAG:情景文本設定 == 1 && FLAG:口上文本設定 == -1
		RESULTS:1 = 仅情景文本
	ELSEIF FLAG:情景文本設定 == 0 && FLAG:口上文本設定 == -1
		RESULTS:1 = 不显示文本
	ENDIF
CASE 9
	RESULTS:0 = 口上顔色
	IF FLAG:口上色
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
CASE 10
	RESULTS:0 = 汚物程度表示
	IF FLAG:垃圾显示OFF
		RESULTS:1 = 否
	ELSE
		RESULTS:1 = 是
	ENDIF
CASE 11
	RESULTS:0 = counter的地文対素質的改変
	IF FLAG:Counter地文控制
		RESULTS:1 = 会変化
	ELSE
		RESULTS:1 = 不会変化
	ENDIF
CASE 12
	RESULTS:0 = 描写胸部的文本
	IF FLAG:Look表示用乳房 == 1 && FLAG:Look表示用乳頭 == 1
		RESULTS:1 = 全部固定
	ELSEIF FLAG:Look表示用乳房 == 1 && FLAG:Look表示用乳頭 == 0
		RESULTS:1 = 仅固定乳房描写
	ELSEIF FLAG:Look表示用乳房 == 0 && FLAG:Look表示用乳頭 == 1
		RESULTS:1 = 仅固定乳头描写
	ELSEIF FLAG:Look表示用乳房 == 0 && FLAG:Look表示用乳頭 == 0
		RESULTS:1 = 不固定
	ENDIF
;CASE 13
;	RESULTS:0 = 好意/理性之壁の表示
;	IF FLAG:好意理性短縮
;		RESULTS:1 = 縮短
;	ELSE
;		RESULTS:1 = 不縮短
;	ENDIF
;	RESULTS:2 = ※現在未機能※
;CASE 14
;	RESULTS:0 = 能力表示的標記
;	IF FLAG:能力表記
;		RESULTS:1 = 数字標記
;	ELSE
;		RESULTS:1 = 記号標記
;	ENDIF
;	RESULTS:2 = ※現在未機能※
CASE 15
	RESULTS:0 = 毎回合のABLUP
	IF FLAG:ABLUP省略
		RESULTS:1 = 省略
	ELSE
		RESULTS:1 = 不省略
	ENDIF
CASE 16
	RESULTS:0 = 初吻の記録
	SELECTCASE FLAG:ＦＫ判定
	CASE 0
		RESULTS:1 = 僅在親吻時・僅在有意識時
	CASE 1
		RESULTS:1 = 僅在親吻時・無意識時也算
	CASE 2
		RESULTS:1 = 口交時也算・僅在有意識時
	CASE 3
		RESULTS:1 = 口交時也算・無意識時也算
	ENDSELECT
CASE 17
	RESULTS:0 = 是否会在移動時停下
	SELECTCASE FLAG:停下脚步
	CASE 0
		RESULTS:1 = 無条件停下脚步
	CASE 1
		RESULTS:1 = 只有在口上存在的情况下会停下
	CASE 2
		RESULTS:1 = 不会停下脚步
	ENDSELECT
CASE 18
	RESULTS:0 = 遭遇時的依頼提示
	IF FLAG:依頼スルー
		RESULTS:1 = 不显示
	ELSE
		RESULTS:1 = 显示
	ENDIF
CASE 19
	RESULTS:0 = 消耗品自動購入
	IF FLAG:消耗品自動購入
		RESULTS:1 = 是
		RESULTS:2 = 只买潤滑液
	ELSE
		RESULTS:1 = 否
	ENDIF
CASE 20
	RESULTS:0 = 自動時停移動
	SELECTCASE FLAG:瞬間移動
	CASE 0
		RESULTS:1 = ＯＦＦ
	CASE 1
		RESULTS:1 = 仅在外出散步时
	CASE 2
		RESULTS:1 = 仅在拠点内移動時
	CASE 3
		RESULTS:1 = 外出/拠点内均启用
	ENDSELECT
CASE 21
	RESULTS:0 = 進行能力比較來決定會不會被推倒
	IF FLAG:推倒
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
CASE 22
	RESULTS:0 = 警告牌使用
	IF FLAG:警告牌使用
		RESULTS:1 = 有
		RESULTS:2 = 因为过分的性骚扰等而被发行并累积，游戏就会结束。
	ELSE
		RESULTS:1 = 无
	ENDIF
CASE 23
	RESULTS:0 = 来訪模式
	SELECTCASE FLAG:交流来訪模式
	CASE 0
		RESULTS:1 = 正常
	CASE 1
		RESULTS:1 = 好感度50以下不会来访
	CASE 2
		RESULTS:1 = 仅限陥落的角色
	CASE 3
		RESULTS:1 = 不会来訪
	ENDSELECT
	RESULTS:2 = 自宅地域に角色が訪れる条件
CASE 24
	RESULTS:0 = 脱衣指令的選択
	SELECTCASE FLAG:脱衣選択
	CASE 0
		RESULTS:1 = 毎回選択
	CASE 1
		RESULTS:1 = 全裸
	CASE 2
		RESULTS:1 = 半脱
	ENDSELECT
CASE 25
	RESULTS:0 = 時間停止時脱衣指令的選択
	SELECTCASE FLAG:胖次泥棒
	CASE 0
		RESULTS:1 = 毎回選択
	CASE 1
		RESULTS:1 = 总是占为己有
	CASE 2
		RESULTS:1 = 总是单单脱掉
	ENDSELECT
CASE 26
	RESULTS:0 = 一部分人物的長筒襪穿戴
	IF FLAG:長筒襪着用
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
CASE 27
	RESULTS:0 = 增加色色的内衣
	IF FLAG:セクシー胖次追加
		RESULTS:1 = 增加
	ELSE
		RESULTS:1 = 不增加
	ENDIF
CASE 28
	RESULTS:0 = 胖次収集状況表示
	IF FLAG:胖次収集状況タイプB
		RESULTS:1 = 未入手的胖次也会显示
	ELSE
		RESULTS:1 = 仅显示入手的胖次
	ENDIF
CASE 29
	RESULTS:0 = 地図的显示模式
	IF RECOLOREDMAPS
		IF ANIMATERECOLOREDMAPS
			RESULTS:1 = 颜色地图 + 動画
		ELSE
			RESULTS:1 = 颜色地图
		ENDIF
	ELSE
		RESULTS:1 = 经典
	ENDIF
CASE 30
	RESULTS:0 = 角色列表表示順序
	IF FLAG:並び替え
		RESULTS:1 = 原作順
	ELSE
		RESULTS:1 = ＴＷ順
	ENDIF
CASE 31
	RESULTS:0 = 放尿和漏尿癖的有効化
	IF FLAG:おもらし有効
		RESULTS:1 = 有効
	ELSE
		RESULTS:1 = 無効
	ENDIF
CASE 32
	RESULTS:0 = 採集時の再抽選機能
	IF FLAG:採集再抽選
		RESULTS:1 = 有効
	ELSE
		RESULTS:1 = 無効
	ENDIF
CASE 33
	RESULTS:0 = 日時天候による背景色推移
	IF FLAG:背景色推移
		RESULTS:1 = 有効
	ELSE
		RESULTS:1 = 無効
	ENDIF
CASE 34
	RESULTS:0 = カウンターのお仕置き
	SELECTCASE FLAG:カウンター制裁
	CASE 0
		RESULTS:1 = あり
		RESULTS:2 = 一旦被抓住把柄就会很少发生
	CASE 1
		RESULTS:1 = 不采取过激的惩罚
		RESULTS:2 = 他们不会用工具惩罚我们
	CASE 2
		RESULTS:1 = 没有
	ENDSELECT
CASE 35
	RESULTS:0 = 主人公的肛门開発許可
	IF FLAG:アナル開発無効
		RESULTS:1 = 禁止
		RESULTS:2 = あなたに快Ａソースが入るカウンター行動を禁止します
	ELSE
		RESULTS:1 = 許可
	ENDIF
CASE 36
	RESULTS:0 = 日常行动中的暴风现象
	IF FLAG:風チラ発生
		RESULTS:1 = 有効
	ELSE
		RESULTS:1 = 無効
	ENDIF

CASEELSE
	RESULT = 0
ENDSELECT
RETURN RESULT

@OPTION_SETTING(ARG)
CALL OPTION_SETNAME(ARG)
SELECTCASE RESULTS:0
CASE "妊娠の有無"
	FLAG:妊娠開关 = !FLAG:妊娠開关
CASE "文本設定"
	IF FLAG:情景文本設定 == 1 && FLAG:口上文本設定 == 2
		FLAG:情景文本設定 = 1
		FLAG:口上文本設定 = -1
	ELSEIF FLAG:情景文本設定 == 1 && FLAG:口上文本設定 == -1
		FLAG:情景文本設定 = 0
		FLAG:口上文本設定 = -1
	ELSEIF FLAG:情景文本設定 == 0 && FLAG:口上文本設定 == -1
		FLAG:情景文本設定 = 1
		FLAG:口上文本設定 = 2
	ENDIF
CASE "消耗品自動購入"
	FLAG:消耗品自動購入 = !FLAG:消耗品自動購入
CASE "汚物程度表示"
	FLAG:垃圾显示OFF = !FLAG:垃圾显示OFF
CASE "是否会在移動時停下"
	CALL オプション切り替え("停下脚步", 2)
CASE "進行能力比較來決定會不會被推倒"
	FLAG:推倒 = !FLAG:推倒
CASE "自動時停移動"
	CALL オプション切り替え("瞬間移動", 3)
CASE "住宿事件"
	FLAG:住宿事件 = !FLAG:住宿事件
CASE "警告牌使用"
	FLAG:警告牌使用 = !FLAG:警告牌使用
CASE "一部分人物的長筒襪穿戴"
	FLAG:長筒襪着用 = !FLAG:長筒襪着用
CASE "宴会の発生"
	FLAG:宴会的有無 = !FLAG:宴会的有無
CASE "来訪模式"
	CALL オプション切り替え("交流来訪模式", 3)
CASE "脱衣指令的選択"
	CALL オプション切り替え("脱衣選択", 2)
CASE "時間停止時脱衣指令的選択"
	CALL オプション切り替え("胖次泥棒", 2)
CASE "口上顔色"
	FLAG:口上色 = !FLAG:口上色
CASE "描写胸部的文本"
	IF FLAG:Look表示用乳房 == 1 && FLAG:Look表示用乳頭 == 1
		FLAG:Look表示用乳房 = 1
		FLAG:Look表示用乳頭 = 0
	ELSEIF FLAG:Look表示用乳房 == 1 && FLAG:Look表示用乳頭 == 0
		FLAG:Look表示用乳房 = 0
		FLAG:Look表示用乳頭 = 1
	ELSEIF FLAG:Look表示用乳房 == 0 && FLAG:Look表示用乳頭 == 1
		FLAG:Look表示用乳房 = 0
		FLAG:Look表示用乳頭 = 0
	ELSEIF FLAG:Look表示用乳房 == 0 && FLAG:Look表示用乳頭 == 0
		FLAG:Look表示用乳房 = 1
		FLAG:Look表示用乳頭 = 1
	ENDIF
CASE "能力表示的標記"
	FLAG:能力表記 = !FLAG:能力表記
CASE "counter的地文対素質的改変"
	FLAG:Counter地文控制 = !FLAG:Counter地文控制
CASE "增加色色的内衣"
	FLAG:セクシー胖次追加 = !FLAG:セクシー胖次追加
	IF !FLAG:セクシー胖次追加
		PRINTFORMW 席卷幻想郷的性感内衣热潮结束了
		PRINTL 
		PRINTFORML 要消除掉所拥有的性感内衣吗？
		PRINTFORML 削除対象：超短内裤、兜裆布、T型裤、透明女式短内裤、開放型女式短内裤、创可贴、衬裤（開档）
		PRINTL 
		PRINTL [0] 已经不需要了
		PRINTL [1] 珍藏起来
		INPUT
		IF RESULT == 0
			CALL DELETE_SEXYPANTS
			CLEARLINE 10
		ELSE
			CLEARLINE 8
		ENDIF
	ENDIF
CASE "從炮友系陥落向恋慕系陥落変化"
	FLAG:陥落路線変化 = !FLAG:陥落路線変化
CASE "対方是否会告白"
	FLAG:告白禁止 = !FLAG:告白禁止
CASE "毎回合のABLUP"
	FLAG:ABLUP省略 = !FLAG:ABLUP省略
CASE "因事件而[無知]喪失"
	FLAG:無知自動喪失禁止 = !FLAG:無知自動喪失禁止
CASE "好意/理性之壁の表示"
	FLAG:好意理性短縮 = !FLAG:好意理性短縮
CASE "兒童の描写"
	FLAG:兒童描写 = !FLAG:兒童描写
CASE "兒童の性別"
	CALL オプション切り替え("兒童の性別", 2)
CASE "胖次収集状況表示"
	FLAG:胖次収集状況タイプB = !FLAG:胖次収集状況タイプB
CASE "初吻の記録"
	CALL オプション切り替え("ＦＫ判定", 3)
CASE "遭遇時的依頼提示"
	FLAG:依頼スルー = !FLAG:依頼スルー
CASE "地図的显示模式"
	IF RECOLOREDMAPS
		IF ANIMATERECOLOREDMAPS
			RECOLOREDMAPS = 0
			ANIMATERECOLOREDMAPS = 0
		ELSE
			ANIMATERECOLOREDMAPS = 1000
		ENDIF
	ELSE
		RECOLOREDMAPS = 1
	ENDIF
CASE "角色列表表示順序"
	FLAG:並び替え = !FLAG:並び替え
CASE "放尿和漏尿癖的有効化"
	FLAG:おもらし有効 = !FLAG:おもらし有効
CASE "採集時の再抽選機能"
	FLAG:採集再抽選 = !FLAG:採集再抽選
CASE "日時天候による背景色推移"
	FLAG:背景色推移 = !FLAG:背景色推移
CASE "カウンターのお仕置き"
	CALL オプション切り替え("カウンター制裁", 2)
CASE "主人公的肛门開発許可"
	FLAG:アナル開発無効 = !FLAG:アナル開発無効
CASE "日常行动中的暴风现象"
	FLAG:風チラ発生 = !FLAG:風チラ発生
ENDSELECT

@オプション切り替え(ARGS, ARG)
FLAG:ARGS ++
SIF FLAG:ARGS > ARG
	FLAG:ARGS = 0

@OPTION_自動喘息一括設定
CALL ASK_M("全員無効",1,"全員有効（无下流）",1,"全員有効（有下流）",1, "中止",1)
IF INRANGE(RESULT, 0, 2)
	FOR LOCAL,1,CHARANUM
		CFLAG:LOCAL:自動喘息 = RESULT
	NEXT
	PRINTFORMW 统一設定了
ENDIF

@登場人物的切换
PRINTL 　被指定的人物将不会登場。（"*"为有口上的角色）
DRAWLINE
SIF FLAG:並び替え
	CALL CHARASORT_EX
FOR LOCAL:1, 1, EX_CHARANUM
	IF FLAG:並び替え
		LOCAL = RESULT:(LOCAL:1)
		SIF LOCAL < 0
			CONTINUE
	ELSE
		LOCAL = LOCAL:1
		SIF LOCAL >= CHARANUM
			BREAK
	ENDIF
	SIF TALENT:LOCAL:路人
		CONTINUE
	SIF CFLAG:LOCAL:出禁
		SETCOLOR C_D_RED
	RESULT = 0
	TRYCALLFORM M_KOJO_K{NO:LOCAL}
	PRINTFORMLC [{LOCAL, 3}] \@ RESULT ? * # \@%CALLNAME:LOCAL%
	RESETCOLOR
NEXT
PRINTL 　
DRAWLINE
PRINTFORML [999] 設定終了　出禁人物：{FLAG:出禁人数}人　[998]重置
$LOOP
INPUT
IF RESULT >= 1 && RESULT <= CHARANUM
	IF CFLAG:RESULT:出禁 && FLAG:扮演 != RESULT
		CFLAG:RESULT:出禁 = 0
		SIF CFLAG:RESULT:現在位置 == CFLAG:RESULT:初期位置 && CFLAG:RESULT:睡眠
			LOCK:(CFLAG:RESULT:初期位置) = 1
		FLAG:出禁人数 --
	ELSEIF TALENT:RESULT:路人
		PRINTFORMW 这个角色不能解除出禁
		GOTO LOOP
	ELSEIF FLAG:扮演 == RESULT
		IF CFLAG:(FLAG:扮演):出禁 == 1
			PRINTFORML 要扮演現在这个人物的话、将会变成两个%CALLNAME:RESULT%同时存在的情况…
			PRINTL 即使这様也要登場吗？
			PRINTL [0]是 [1]否
			INPUT
			IF RESULT == 0
				CFLAG:(FLAG:扮演):出禁 = 0
				SIF CFLAG:(FLAG:扮演):現在位置 == CFLAG:(FLAG:扮演):初期位置 && CFLAG:(FLAG:扮演):睡眠
					LOCK:(CFLAG:(FLAG:扮演):初期位置) = 1
				FLAG:出禁人数 --
				PRINTFORMW %CALLNAME:(FLAG:扮演)%将登場
			ELSE
				GOTO LOOP
			ENDIF
		ELSE
			CFLAG:(FLAG:扮演):出禁 = 1
			SIF LOCK:(CFLAG:(FLAG:扮演):初期位置) && CFLAG:(FLAG:扮演):初期位置 < 100
				LOCK:(CFLAG:(FLAG:扮演):初期位置) = 0
			FLAG:出禁人数 ++
		ENDIF
	ELSE
		CFLAG:RESULT:出禁 = 1
		SIF LOCK:(CFLAG:RESULT:初期位置) && CFLAG:RESULT:初期位置 < 100
			LOCK:(CFLAG:RESULT:初期位置) = 0
		FLAG:出禁人数 ++
	ENDIF
	RESTART
ELSEIF RESULT == 999
	PRINTW 設定完毕。
ELSEIF RESULT == 998
	FOR LOCAL,1,CHARANUM
		SIF FLAG:扮演 == LOCAL
			CONTINUE
		SIF TALENT:LOCAL:路人
			CONTINUE
		CFLAG:LOCAL:出禁 = 0
	NEXT
	FLAG:出禁人数 = 0
	RESTART
ELSE
	GOTO LOOP
ENDIF

;簡易过滤器制御関数
;仕様上の問題として、設定項目が増えすぎると設計をしなおす必要がある
@簡易过滤器一括設定
#DIM OP_CFLAG_Ｖ過濾器
#DIM OP_CFLAG_Ａ過濾器
#DIM OP_CFLAG_ＳＭ過濾器
#DIM OP_CFLAG_奉仕過濾器
#DIM OP_CFLAG_性騷擾過濾器
#DIM OP_SETTING
IF !LOCAL
	OP_CFLAG_Ｖ過濾器       = GETNUM(CFLAG, "Ｖ過濾器")
	OP_CFLAG_Ａ過濾器       = GETNUM(CFLAG, "Ａ過濾器")
	OP_CFLAG_ＳＭ過濾器     = GETNUM(CFLAG, "ＳＭ過濾器")
	OP_CFLAG_奉仕過濾器     = GETNUM(CFLAG, "奉仕過濾器")
	OP_CFLAG_性騷擾過濾器 = GETNUM(CFLAG, "性騷擾過濾器")
	LOCAL ++
ENDIF
PRINTFORML ～簡易過濾設定～
PRINTFORML ※這裡和單獨個別指令的設定是不一樣的
PRINTFORML 
PRINTFORML [29] 全角色的全簡易過濾OFF
PRINTFORML [28] 開放全角色的[Ｖ]
PRINTFORML [27] 開放全角色的[Ａ]
PRINTFORML [26] 開放全角色的[ＳＭ]
PRINTFORML [25] 開放全角色的[奉仕]
PRINTFORML [24] 開放全角色的[性騷擾]
PRINTFORML [23] 開放[家事]
PRINTFORML 
PRINTFORML [19] 全角色的全簡易過濾ON
PRINTFORML [18] 限制全角色的[Ｖ]
PRINTFORML [17] 限制全角色的[Ａ]
PRINTFORML [16] 限制全角色的[ＳＭ]
PRINTFORML [15] 限制全角色的[奉仕]
PRINTFORML [14] 限制全角色的[性騷擾]
PRINTFORML [13] 限制[家事]
PRINTFORML 
PRINTFORML [99] 設定終了
DO
	INPUT
	OP_SETTING = (RESULT / 10) - 1
	;20で1、10で0
	SELECTCASE RESULT
	CASE 29, 19
		CVARSET CFLAG, OP_CFLAG_Ｖ過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_Ａ過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_ＳＭ過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_奉仕過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_性騷擾過濾器, OP_SETTING
		FLAG:家事過濾器 = OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的所有簡易过滤都设为＜OFF＞了
		ELSE
			PRINTFORMW 全角色的所有簡易过滤都设为＜ON＞了
		ENDIF
	CASE 28, 18
		CVARSET CFLAG, OP_CFLAG_Ｖ過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[Ｖ]指令设为＜開放＞了
		ELSE
			PRINTFORMW 全角色的[Ｖ]指令设为＜限制＞了
		ENDIF
	CASE 27, 17
		CVARSET CFLAG, OP_CFLAG_Ａ過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[Ａ]指令设为＜開放＞了
		ELSE
			PRINTFORMW 全角色的[Ａ]指令设为＜限制＞了
		ENDIF
	CASE 26, 16
		CVARSET CFLAG, OP_CFLAG_ＳＭ過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[ＳＭ]指令设为＜開放＞了
		ELSE
			PRINTFORMW 全角色的[ＳＭ]指令设为＜限制＞了
		ENDIF
	CASE 25, 15
		CVARSET CFLAG, OP_CFLAG_奉仕過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[奉仕]指令设为＜開放＞了
		ELSE
			PRINTFORMW 全角色的[奉仕]指令设为＜限制＞了
		ENDIF
	CASE 24, 14
		CVARSET CFLAG, OP_CFLAG_性騷擾過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[性騷擾]指令设为＜開放＞了
		ELSE
			PRINTFORMW 全角色的[性騷擾]指令设为＜限制＞了
		ENDIF
	CASE 23, 13
		FLAG:家事過濾器 = OP_SETTING
		IF OP_SETTING
			PRINTFORMW 家事指令设为＜開放＞了
		ELSE
			PRINTFORMW 家事指令设为＜限制＞了
		ENDIF
	CASE 99
		PRINTFORMW 簡易过滤的設定完成了
		RETURN
	CASEELSE
		PRINTFORML 無效輸入
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART

;---------------------------------------------------------------
;	画像詳細設定
;	設定が増えたので新設
;---------------------------------------------------------------
@OPTION_画像表示詳細設定
TARGET = [[霊夢]]
FOR LOCAL, 1, 11
	TARGET:LOCAL = LOCAL
NEXT
PRINTL ▼示例▼
CALL PRINT_TARGET_IMAGE(TARGET)
PRINTL 
PRINTFORML [ 0] - 画像\@ FLAG:画像表示 ? 显示 # 不显示 \@
IF FLAG:画像表示
	PRINTFORML [ *]┬ 現在的\@ FLAG:立絵種類 ? 顔絵 # 立絵 \@的顯示人数 {FLAG:画像表示}人
	PRINTFORML [ 1]├ 增加
	PRINTFORML [ 2]└ 减少
	PRINTFORML [ 3] - 角色画像的类型 \@ FLAG:立絵種類 ? [顔] # [立絵] \@
	PRINTFORML [ 4]┬ 現在的画像尺寸[\@ FLAG:多尺寸? 原尺寸 # {默认角色画像横幅} \@]：{100 + isModifier}％
	PRINTFORML [ 5]├ 拡大
	PRINTFORML [ 6]└ 縮小
	PRINTFORML [ 7] - \@ FLAG:立絵種類 ? 顔絵 # 立絵 \@的顯示位置 [\@ FLAG:画像表示位置 ? 左对齐 # 中央对齐 \@]
	PRINTFORML [ 8] - 图像边框显示 \@ FLAG:画像枠表示 ? ON # OFF \@
	PRINTFORML [ 9] - \@ FLAG:巨乳差分表示? 如果角色有巨乳以上的差分画像的话就显示# 不显示巨乳差分画像\@
	PRINTFORML [10] - \@ FLAG:追加エフェクト? 显示追加特效# 不显示追加特效\@
	PRINTFORML [11] - 就寝後的自慰画像\@ FLAG:自慰画像非表示? 不显示# 显示\@
	PRINTFORML [12] - 顔絵附加信息\@ 顔絵付きメッセージ ? 有 # 无\@
	PRINTL 
	PRINTFORML [13]\@ FLAG:挿絵表示 ? ┬ # %" "%- \@ 挿絵を表示する \@ FLAG:挿絵表示 ? ON # OFF \@
	IF FLAG:挿絵表示
		PRINT [14]└ 射精画像的种类
		SELECTCASE FLAG:射精画像種類
			CASE 0
				PRINTL  [断面＋効果音]
			CASE 1
				PRINTL  [只有断面]
			CASE 2
				PRINTL  [只有効果音]
		ENDSELECT
	ELSE
		PRINTL 
	ENDIF
	PRINTFORML [15]\@ FLAG:アニメーション ? ┬ # %" "%- \@動畫設定 \@ FLAG:アニメーション ? 重複 {FLAG:アニメーション}回 # OFF \@
	IF FLAG:アニメーション
		PRINTFORML [16]└挿入中每回合的动画显示：\@FLAG:挿入アニメ过滤器 & アニメ过滤器_挿入 ? [×]# [〇]\@
	ELSE
		PRINTL 
	ENDIF
ELSE
	FOR LOCAL, 1, 13
		PRINTL 
	NEXT
ENDIF
PRINTFORML [999] - 返回
INPUT
IF RESULT == 999
	VARSET TARGET
	RETURN 0
ELSEIF RESULT == 0
	FLAG:画像表示 = !FLAG:画像表示
ELSEIF RESULT == 1
	FLAG:画像表示++
	SIF FLAG:画像表示 > 10
		FLAG:画像表示 = 10
ELSEIF RESULT == 2
	FLAG:画像表示--
	SIF FLAG:画像表示 < 1
		FLAG:画像表示 = 1
ELSEIF RESULT == 3
	FLAG:立絵種類 = !FLAG:立絵種類
ELSEIF RESULT == 4
	IF FLAG:多尺寸
		FLAG:多尺寸 = !FLAG:多尺寸
		默认角色画像横幅 = 180
	ELSEIF 默认角色画像横幅 == 180
		默认角色画像横幅 = 270
	ELSEIF 默认角色画像横幅 == 270
		默认角色画像横幅 = 360
	ELSEIF 默认角色画像横幅 == 360
		默认角色画像横幅 = 540
	ELSEIF 默认角色画像横幅 == 540
		默认角色画像横幅 = 720
	ELSE
		FLAG:多尺寸 = !FLAG:多尺寸
	ENDIF
ELSEIF RESULT == 5
	isModifier = MIN(isModifier + 10, 50)
ELSEIF RESULT == 6
	isModifier = MAX(isModifier - 10, -50)
ELSEIF RESULT == 7
	FLAG:画像表示位置 = !FLAG:画像表示位置
ELSEIF RESULT == 8
	FLAG:画像枠表示 = !FLAG:画像枠表示
ELSEIF RESULT == 9
	FLAG:巨乳差分表示 = !FLAG:巨乳差分表示
ELSEIF RESULT == 10
	FLAG:追加エフェクト = !FLAG:追加エフェクト
ELSEIF RESULT == 11
	FLAG:自慰画像非表示 = !FLAG:自慰画像非表示
ELSEIF RESULT == 12
	INVERTBIT 顔絵付きメッセージ, 0
ELSEIF RESULT == 13
	FLAG:挿絵表示 = !FLAG:挿絵表示
ELSEIF RESULT == 14 && FLAG:挿絵表示
	FLAG:射精画像種類++
	SIF FLAG:射精画像種類 >= 3
		FLAG:射精画像種類 = 0
ELSEIF RESULT == 15
	FLAG:アニメーション++
	SIF FLAG:アニメーション >= 6
		FLAG:アニメーション = 0
ELSEIF RESULT == 16
	INVERTBIT FLAG:挿入アニメ过滤器, 0
	;CALL ASK_M("挿入表示切替",1,"絶頂表示切替",1,"射精表示切替",1,"射精表示切替",1,"返回",1)
	;SIF RESULT == 4
	;	RESTART
	;INVERTBIT FLAG:挿入アニメ过滤器, RESULT
	RESTART
ENDIF
RESTART

;カウンター行動フィルタ
@OPTION_COUNTER_FILTER
#DIM CCOM_ID
#DIM L_FEED
#DIM TEMP
TEMP = CURRENTREDRAW()
REDRAW 0
LOADGLOBAL
CALL COLORMESSAGE, "※ 过滤过度可能会导致不良现象。 ※", C_YELLOW, 2
DRAWLINE
DO
	L_FEED = LINECOUNT
	FOR CCOM_ID, 0, CCOM_MAXNUM
		SIF STR:(4000 + CCOM_ID) == ""
			CONTINUE
		CALL PRINTBUTTON_EXC(@"%STR:(4000 + CCOM_ID)%[{CCOM_ID,3}]", CCOM_ID, CCOM_FILTER:CCOM_ID)
	NEXT
	PRINTL 
	DRAWLINE
	PRINTL [500] - 全部无效
	PRINTL [501] - 复位
	PRINTL 
	PRINTL [999] - 決定
	INPUT
	SELECTCASE RESULT
	CASE 0 TO CCOM_MAXNUM
		SIF STR:(4000 + RESULT) != ""
			CCOM_FILTER:RESULT = !(CCOM_FILTER:RESULT)
	CASE 500, 501
		VARSET CCOM_FILTER, (RESULT == 500)
	CASE 999
		SAVEGLOBAL
		REDRAW TEMP
		RETURN
	ENDSELECT
	CLEARLINE LINECOUNT - L_FEED
LOOP 1
[SKIPSTART]
;;料理オプション設定関数
;;0:手動決定,1:異物不可,2:薬物不可
;@SET_OPTION_COOKING
;#DIM CONST OP_BIT_手動決定 = 0
;#DIM CONST OP_BIT_異物不可 = 1
;#DIM CONST OP_BIT_薬物不可 = 2
;#DIM CONST OP_BIT_打分オン = 3
;PRINTFORML 設定料理関連的選項
;PRINTFORML 現在的設定
;IF GET_OPTION_COOKING("料理内容手動決定")
;	PRINTFORML 　料理内容：手動決定
;ELSE
;	PRINTFORML 　料理内容：自動決定
;ENDIF
;IF GET_OPTION_COOKING("異物混入不可")
;	PRINTFORML 　異物混入：否
;ELSE
;	PRINTFORML 　異物混入：是
;ENDIF
;IF GET_OPTION_COOKING("薬物混入不可")
;	PRINTFORML 　薬物混入：否
;ELSE
;	PRINTFORML 　薬物混入：是
;ENDIF
;SELECTCASE GET_OPTION_COOKING("自動味付け設定")
;CASE 0
;	PRINTFORML 　自動味付：否
;CASE 10
;	PRINTFORML 　自動味付：自動设为普通口味
;CASE 11
;	PRINTFORML 　自動味付：自動设为浓厚口味
;CASE 12
;	PRINTFORML 　自動味付：自動设为清淡口味
;ENDSELECT
;IF GET_OPTION_COOKING("打分機能オフ")
;	PRINTFORML 　打分機能：OFF
;ELSE
;	PRINTFORML 　打分機能：ON
;ENDIF
;DO
;	PRINTFORML [9] 料理内容の設定変更
;	PRINTFORML [8] 異物混入の設定変更
;	PRINTFORML [7] 薬物混入の設定変更
;	PRINTFORML [6] 自動味付の設定変更
;	PRINTFORML [5] 打分機能の設定変更
;	PRINTFORML 
;	PRINTFORML 
;	PRINTFORML [0] 決定
;	INPUT
;	SELECTCASE RESULT
;	CASE 9
;		INVERTBIT FLAG:料理オプション, OP_BIT_手動決定
;	CASE 8
;		INVERTBIT FLAG:料理オプション, OP_BIT_異物不可
;	CASE 7
;		INVERTBIT FLAG:料理オプション, OP_BIT_薬物不可
;	CASE 6
;		PRINTFORML 自動調味的機能設定
;		PRINTFORML [12] 自動調味＜薄味＞
;		PRINTFORML [11] 自動調味＜浓郁＞
;		PRINTFORML [10] 自動調味＜普通の味＞
;		PRINTFORML [ 0] 不使用自動調味
;		DO
;			INPUT
;			SELECTCASE RESULT
;			CASE 10, 11, 12
;				CLEARBIT FLAG:料理オプション, 10, 11, 12
;				SETBIT   FLAG:料理オプション, RESULT
;			CASE 0
;				CLEARBIT FLAG:料理オプション, 10, 11, 12
;			CASEELSE
;			PRINTFORML 無效輸入
;			CONTINUE
;			ENDSELECT
;			BREAK
;		LOOP 1
;	CASE 5
;		INVERTBIT FLAG:料理オプション, OP_BIT_打分オン
;	CASE 0
;		RETURN 
;	CASEELSE
;		PRINTFORML 無效輸入
;		CONTINUE
;	ENDSELECT
;	BREAK
;LOOP 1
;RESTART
;
;
;;料理オプションの内容取得関数
;@GET_OPTION_COOKING(TYPE)
;#FUNCTION
;#DIMS DYNAMIC TYPE
;#DIM CONST OP_BIT_手動決定 = 0
;#DIM CONST OP_BIT_異物混入不可 = 1
;#DIM CONST OP_BIT_薬物混入不可 = 2
;#DIM CONST OP_BIT_打分機能オン = 3
;#DIM CONST OP_BIT_自動味付け_普通 = 10
;#DIM CONST OP_BIT_自動味付け_濃い = 11
;#DIM CONST OP_BIT_自動味付け_单薄的 = 12
;SELECTCASE TYPE
;CASE "料理内容手動決定"
;	SIF GETBIT(FLAG:料理オプション, OP_BIT_手動決定)
;		RETURNF 1
;CASE "料理内容自動決定"
;	RETURNF !GET_OPTION_COOKING("料理内容手動決定")
;
;CASE "異物混入不可"
;	SIF GETBIT(FLAG:料理オプション, OP_BIT_異物混入不可)
;		RETURNF 1
;CASE "異物混入可能"
;	RETURNF !GET_OPTION_COOKING("異物混入不可")
;
;CASE "薬物混入不可"
;	SIF GETBIT(FLAG:料理オプション, OP_BIT_薬物混入不可)
;		RETURNF 1
;CASE "薬物混入可能"
;	RETURNF !GET_OPTION_COOKING("薬物混入不可")
;CASE "自動味付け設定"
;	SIF GETBIT(FLAG:料理オプション, OP_BIT_自動味付け_普通)
;		RETURNF OP_BIT_自動味付け_普通
;	SIF GETBIT(FLAG:料理オプション, OP_BIT_自動味付け_濃い)
;		RETURNF OP_BIT_自動味付け_濃い
;	SIF GETBIT(FLAG:料理オプション, OP_BIT_自動味付け_单薄的)
;		RETURNF OP_BIT_自動味付け_单薄的
;CASE "打分機能オン"
;	SIF GETBIT(FLAG:料理オプション, OP_BIT_打分機能オン)
;		RETURNF 1
;CASE "打分機能オフ"
;	RETURNF !GET_OPTION_COOKING("打分機能オン")
;CASEELSE
;	THROW 不明なTYPE%TYPE%
;ENDSELECT
;RETURNF 0
[SKIPEND]


@SIMATTYAUOJISAN
;TW414_移動パッチにて右クリックやマクロによるスキップもしまっちゃうように修正
;PRINTでのウェイトをなくすかわりにFORCEWAITでスキップ解除するように
;
FORCEWAIT
PRINT 「输入画面上不可能显示的数字的孩子就在这吗？」
FORCEWAIT
PRINT 没收叔叔出現了！
FORCEWAIT
PRINT 「那么、输入画面上不可能显示的数字的孩子的钱、就这様没收了哦」
FORCEWAIT
PRINTFORM 没收叔叔把%CALLNAME:MASTER%的现金没收了
MONEY = 0
FORCEWAIT
PRINT 「输入画面上不可能显示的数字的孩子的道具也、就这様没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%的道具被没收了
FOR LOCAL,0,500
	ITEM:LOCAL = 0
NEXT
FORCEWAIT
PRINT 「输入画面上不可能显示的数字的孩子的技巧也、就这様没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%的技巧被没收了
FORCEWAIT
FOR LOCAL,0,100
	ABL:0:LOCAL = 0
NEXT
PRINT 「输入画面上不可能显示的数字的孩子的経験也、就这様没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%的経験被没收了
FORCEWAIT
FOR LOCAL,0,100
	EXP:0:LOCAL = 0
NEXT
;せっかくだし收藏もしまっちゃいましょう（eraTW4.15胖次対策パッチにて追加）
;
PRINT 「输入画面上不可能显示的数字的孩子的内裤藏品也、就这様没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%殚精竭智収集到的宝贝般的内裤藏品被没收了
FORCEWAIT
FLAG:700 = 0
FOR LOCAL,1,CHARANUM
	FOR LOCAL:1,0,MAXPANTS
		CFLAG:LOCAL:(LOCAL:1 + 胖次管理) = 0
	NEXT
NEXT
PRINT 「输入画面上不可能显示的数字的孩子的信頼也、就这様没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%获得的少女们的信頼被没收了
FOR LOCAL,1,CHARANUM
	CFLAG:LOCAL:信賴度 = 0
NEXT
FORCEWAIT
;
;
;PRINT 「画面上に表示されていない数字を手打ちする子の基礎能力も、どんどんしまっちゃうからね」
;FORCEWAIT
;PRINTFORM %CALLNAME:MASTER%の基礎能力はしまわれてしまった
;FOR LOCAL,0,9
;	MAXBASE:0:LOCAL = 99
;	BASE:0:LOCAL = min(MAXBASE:0:LOCAL,BASE:0:LOCAL)
;NEXT
;さすがにやり過ぎは良くないと思ったのでクリック連打で許してみた
;作者様へ　勝手に当てたパッチですので不愉快でしたらこの部分だけパッチ前に戻してくださって構いません。
;
;
FORCEWAIT
PRINTW 不要再这様啦～～～～


;セクシー胖次削除
@DELETE_SEXYPANTS
#DIM 対象
#DIM 種類
#DIM 所有数

VARSET RESULT

FOR 対象,1,CHARANUM
	FOR 種類,20,26
		所有数 = CFLAG:対象:(種類 + 100)
		IF 所有数 >= 1
			RESULT:0 += (所有数)
			CFLAG:対象:(種類 + 100) = 0
		ENDIF
	NEXT
NEXT

;振動棒胖次削除
@DELETE_VIBEPANTS
#DIM 対象
#DIM 種類
#DIM 所有数

VARSET RESULT

FOR 対象,1,CHARANUM
	FOR 種類,27,30
		所有数 = CFLAG:対象:(種類 + 100)
		IF 所有数 >= 1
			RESULT:0 += (所有数)
			CFLAG:対象:(種類 + 100) = 0
		ENDIF
	NEXT
NEXT


