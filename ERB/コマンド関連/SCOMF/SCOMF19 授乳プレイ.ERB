;-------------------------------------------------
;授乳PLAY
;愛撫系派生指令
;-------------------------------------------------
@SCOM19
;-------------------------------------------------
;実行決定
;-------------------------------------------------
STR:0 = 授乳PLAY

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = TARGET

LOCALS = {TARGET}/
CSTR:PLAYER:10 += LOCALS
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛門 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:PLAYER:2 = 3

;強度
TCVAR:PLAYER:4 = 1000
;奉仕快楽
TCVAR:108 = 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	;角色主体なので時止め中は実行不可能

ELSE
	;手淫によるマ斯塔へのソース追加
	IF ABL:指 < 4 || FLAG:70
		SOURCE:PLAYER:快Ｃ += 80
	ELSE
		SOURCE:PLAYER:快Ｃ += ABL:指 * 20
	ENDIF
	
	IF TALENT:TARGET:胸圍 >= 1
		;基本的に吸乳头と一緒
		DOWNBASE:体力 += 10
		DOWNBASE:気力 += 50
	
		SOURCE:快Ｂ = 20
		SOURCE:情愛 = 100
		SOURCE:露出 = 20
		SOURCE:屈従 = 200
		SOURCE:逸脱 = 100
		SOURCE:反感 = 100
	ELSE
		;胸がちっちゃいと疲れるよ！ 頭しっかり抱き寄せないといけないからね！
		DOWNBASE:体力 += 20
		DOWNBASE:気力 += 80
	
		SOURCE:快Ｂ = 20
		SOURCE:情愛 = 100
		SOURCE:露出 = 20
		SOURCE:屈従 = 200
		SOURCE:逸脱 = 200
		SOURCE:反感 = 100
	ENDIF
		
	;手淫と同じ汚れデータの計算
	SOURCE:不潔 = Y*10 + 60
	
	;吸乳头より角色依存のため、情愛を増やす
	SOURCE:快Ｂ += ABL:PLAYER:舌 * 20
	SOURCE:情愛 += ABL:PLAYER:舌 * 10

	SIF ABL:親密 >= 7
		SOURCE:情愛 += (ABL:親密 * 20)

	;你のTALENT:舌頭靈活をみる
	SIF TALENT:PLAYER:舌頭靈活
		TIMES SOURCE:快Ｂ , 1.50

	;你のTALENT:猫舌をみる
	SIF TALENT:PLAYER:猫舌
		TIMES SOURCE:快Ｂ , 0.75
	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 200
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;角色の指⇔你のＰ的污垢が移動
STAIN:1 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:1

;奴隷のＢ⇔調教者の口的污垢が移動
STAIN:5 |= STAIN:PLAYER:0
STAIN:PLAYER:0 |= STAIN:5
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;手淫経験
SIF !FLAG:70 && !CFLAG:恶作剧
	EXP:手淫経験 ++

;愛情経験
SIF TALENT:恋慕 && !ASSIPLAY
	EXP:愛情経験 += 1
	
;母性持ちなら愛情経験にBOUNS
SIF TALENT:母性 && !ASSIPLAY
	EXP:愛情経験 += 1

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
IF HETEROSEX(TARGET,PLAYER)
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 5
ENDIF
ELSE
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 5
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 5
ENDIF
ENDIF

;你が猫舌持ちの場合は、你に嗜虐快楽経験を追加
SIF TALENT:PLAYER:猫舌
	EXP:PLAYER:嗜虐快楽経験 += 1
EXP:PLAYER:口淫経験 ++
RETURN 1

;-------------------------------------------------
;実行可能判定 難度は乳夾口交よりちょい低いくらい
;-------------------------------------------------
@CAN_SCOM19(ARG)
SKIPDISP ARG
PRINTL 授乳PLAY
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
IF !FLAG:70

	A = 0
	S = 0

	;全部の命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		A += ABL:欲望 * 1
		PRINT 欲望
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		A += ABL:奉仕精神 * 4
		PRINT 奉仕精神
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
		S = 1
	ENDIF
	;ABL:精液中毒
	IF ABL:精液中毒
		SIF S
			PRINT  + 
		A += ABL:精液中毒 * 3
		PRINT 精液中毒
		PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 3,')
		S = 1
	ENDIF

	IF MARK:快楽刻印
		SIF S
			PRINT  + 
		A += MARK:快楽刻印 * 1
		PRINT 快楽刻印
		PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF GETPALAMLV(PALAM:欲情,5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:欲情,5) * 1
		PRINT 欲情
		PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 1,')
		S = 1
	ENDIF
	;害羞
	IF TALENT:羞恥心 > 0
		PRINT  - 
		A -= 3
		PRINT 害羞
		PRINTV '(,3,')
		S = 1
	ENDIF
	;汚臭鈍感
	IF TALENT:汚臭耐性 > 0
		SIF S
			PRINT  + 
		A += 1
		PRINT 汚臭鈍感
		PRINTV '(,1,')
		S = 1
	ENDIF
	;汚臭敏感
	IF TALENT:汚臭耐性 < 0
		PRINT  - 
		A -= 3
		PRINT 汚臭敏感
		PRINTV '(,3,')
		S = 1
	ENDIF
	;献身的
	IF TALENT:献身的
		SIF S
			PRINT  + 
		A += 6
		PRINT 献身的
		PRINTV '(,6,')
		S = 1
	ENDIF
	;否定快感（淫乱、娼婦で無効）
	IF TALENT:快感応答 < 0 && !TALENT:淫乱
		PRINT  - 
		A -= 3
		PRINT 否定快感
		PRINTV '(,3,')
		S = 1
	ENDIF
	;討厭男人（恋慕、親愛で無効）
	IF OTOKOGIRAI(TARGET)
		PRINT  - 
		A -= 12
		PRINT 討厭男人
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 5
		PRINT 恋慕
		PRINTV '(,5,')
		S = 5
	ENDIF
	;親愛
	IF TALENT:恋慕 > 1 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 5
		PRINT 親愛
		PRINTV '(,5,')
		S = 5
	ENDIF

	;調教者が扶她
	IF (TALENT:PLAYER:2 & 3) == 3
		SIF S
			PRINT  + 
		A += 8
		PRINT 扶她
		PRINTV '(,8,')
		S = 1
	ENDIF

	Y = 0
	;愛液的污垢
	SIF STAIN:PLAYER:2 & 1
		Y += 1
	;精液的污垢
	SIF STAIN:PLAYER:2 & 4
		Y += 3
	;肛門的污垢
	SIF STAIN:PLAYER:2 & 8
		Y += 7
	SIF STAIN:PLAYER:2 & 16
		Y += 15

	Y = Y * (2 - TALENT:汚臭耐性) / 2

	;有污垢
	IF Y
		PRINT  - 
		A -= Y
		PRINT 有污垢
		;汚臭鈍感
		IF TALENT:汚臭耐性 > 0
			PRINT 、汚臭鈍感
		ELSEIF TALENT:汚臭耐性 < 0
			PRINT 、汚臭敏感
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF

	;合計を表示(30以上で実行)
	PRINT  = 
	PRINTV A

	V = 32
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V

	WAIT
	SKIPDISP 0
	;実行できない
	SIF A < V
		RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;選択可能判定
;-------------------------------------------------
@COM_ABLE519
;授乳PLAY実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(58)
	RETURN RESULT
;睡眠姦中はダメ
SIF CFLAG:恶作剧 > 1
	RETURN 0
;時止めはダメ
SIF FLAG:70
	RETURN 0
;男人だとダメ
SIF !(TALENT:2 & 1)
	RETURN 0
;上半身着衣だとダメ…というか現状乳頭吸えないので無理…胸だけポロンやりたい所存…
SIF TEQUIP:1 > 1
	RETURN 0
;搾乳器使用中は不可
SIF TEQUIP:17
	RETURN 0
;乳頭夾使用中は不可
SIF TEQUIP:16
	RETURN 0
;乳房電極装着中はダメ
SIF TEQUIP:24
	RETURN 0
;Ｖインサート中はだめ
SIF TEQUIP:体位 > 0
	RETURN 0
RETURN 1

