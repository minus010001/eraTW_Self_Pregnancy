;-------------------------------------------------
;雙重乳交
;助手による指令、及びレズプレイ、レベル3
;-------------------------------------------------
@SCOM11
;-------------------------------------------------
;実行決定
;-------------------------------------------------
STR:0 = 雙重乳交
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = TARGET
;TCVAR:0 射精対象フラグ（ビット0=TARGET 1=PLAYER 2=MASTER 3=ASSI）
;TCVAR:1 予備射精対象フラグ（ビットを使いたくない人用、CFLAG:100に翻訳されます）	（1=TARGET 2=PLAYER 3=MASTER 4=ASSI 5=TARGET+ASSI 6=MASTER+ASSI）
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛門 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手）

;二人目
LOCAL = MASTER_POSE(SET_B,SET_P,1)
LOCALS = {TARGET}/{LOCAL}/
CSTR:PLAYER:10 += LOCALS
TCVAR:MASTER:2 = 5
;強度
TCVAR:MASTER:4 = 2000
;奉仕快楽
TCVAR:108 = 1
TCVAR:LOCAL:108 = 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 10
DOWNBASE:気力 += 100
SOURCE:MASTER:快Ｃ = 150

SOURCE:快Ｂ = 150
SOURCE:情愛 = 200
SOURCE:性行動 = 500
SOURCE:達成 = 250
SOURCE:屈従 = 900
SOURCE:逸脱 = 400
SOURCE:反感 = 400

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*40 + 100

;消費体力、気力
DOWNBASE:LOCAL:体力 += 10
DOWNBASE:LOCAL:気力 += 100

SOURCE:LOCAL:快Ｂ = 150
SOURCE:LOCAL:情愛 = 100
SOURCE:LOCAL:性行動 = 250
SOURCE:LOCAL:達成 = 120
SOURCE:LOCAL:屈従 = 450
SOURCE:LOCAL:逸脱 = 400
SOURCE:LOCAL:反感 = 400

;上のほうで計算した汚れデータ
SOURCE:LOCAL:不潔 = Y*40 + 100
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;圧抑、抵抗持ちには、抑鬱ソース追加
SIF TALENT:自己愛 < 0 || TALENT:抵抗
	SOURCE:鬱屈 += 600
SIF TALENT:LOCAL:自己愛 < 0 || TALENT:LOCAL:抵抗
	SOURCE:LOCAL:鬱屈 += 600

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＢ⇔調教者のＰ的污垢が移動
STAIN:5 |= STAIN:MASTER:2
STAIN:MASTER:2 |= STAIN:5

;助手のＢ⇔調教者のＰ的污垢が移動
STAIN:LOCAL:5 |= STAIN:MASTER:2
STAIN:MASTER:2 |= STAIN:LOCAL:5

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:LOCAL:乳交経験 ++
EXP:乳交経験 ++
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 3
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 3
ENDIF
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,LOCAL)
	EXP:LOCAL:(50 + HETEROSEX(TARGET,LOCAL)) += 3
	EXP:TARGET:(50 + HETEROSEX(TARGET,LOCAL)) += 3
ENDIF
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(LOCAL,PLAYER)
	EXP:LOCAL:(50 + HETEROSEX(LOCAL,PLAYER)) += 3
	EXP:PLAYER:(50 + HETEROSEX(LOCAL,PLAYER)) += 3
ENDIF

RETURN 1

@CAN_SCOM11(ARG)
SKIPDISP ARG
PRINTL 雙重乳交
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
A = 0
S = 0

;全部の命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 1
	PRINT 欲望
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINT 奉仕精神
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF S
		PRINT  + 
	A += ABL:精液中毒 * 3
	PRINT 精液中毒
	PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 3,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 1
	PRINT 快楽刻印
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
	S = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,5)
	SIF S
		PRINT  + 
	A += GETPALAMLV(PALAM:欲情,5) * 1
	PRINT 欲情
	PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 1,')
	S = 1
ENDIF
;害羞
IF TALENT:羞恥心 > 0
	PRINT  - 
	A -= 1
	PRINT 害羞
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF S
		PRINT  + 
	A += 1
	PRINT 汚臭鈍感
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	A -= 3
	PRINT 汚臭敏感
	PRINTV '(,3,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 6
	PRINT 献身的
	PRINTV '(,6,')
	S = 1
ENDIF
;否定快感（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	A -= 1
	PRINT 否定快感
	PRINTV '(,1,')
	S = 1
ENDIF
;討厭男人（恋慕、親愛で無効）
IF OTOKOGIRAI(TARGET) == 1
	PRINT  - 
	A -= 12
	PRINT 討厭男人
	PRINTV '(,12,')
	S = 1
ENDIF
;淫乱
IF TALENT:淫乱 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINT 淫乱
	PRINTV '(,5,')
	S = 5
ENDIF
;娼婦
IF TALENT:淫乱 > 1 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINT 娼婦
	PRINTV '(,5,')
	S = 5
ENDIF

;調教者が扶她
IF (TALENT:PLAYER:2 & 3) == 3
	SIF S
		PRINT  + 
	A += 8
	PRINT 扶她
	PRINTV '(,8,')
	S = 1
ENDIF

Y = 0
;愛液的污垢
SIF STAIN:PLAYER:2 & 1
	Y += 1
;精液的污垢
SIF STAIN:PLAYER:2 & 4
	Y += 3
;肛門的污垢
SIF STAIN:PLAYER:2 & 8
	Y += 7
SIF STAIN:PLAYER:2 & 16
	Y += 15
Y = Y * (2 - TALENT:汚臭耐性) / 2

;有污垢
IF Y
	PRINT  - 
	A -= Y
	PRINT 有污垢
	;汚臭鈍感
	IF TALENT:汚臭耐性 > 0
		PRINT 、汚臭鈍感
	ELSEIF TALENT:汚臭耐性 < 0
		PRINT 、汚臭敏感
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF

;合計を表示(38以上で実行)
PRINT  = 
PRINTV A

V = 38
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT
SKIPDISP 0
;実行できない
SIF A < V
	RETURN 0
RETURN 1

;-------------------------------------------------
;選択可能判定
;-------------------------------------------------
@COM_ABLE511
;雙重乳交
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(511)
	RETURN RESULT
LOCAL = MASTER_POSE(SET_B,SET_P,1)
;主人が男人か半人半妖か扶她じゃないとダメ
SIF !(TALENT:PLAYER:2 & 2)
	RETURN 0
;猫舌の奴隷の場合は技巧Lv2以上必要
SIF TALENT:猫舌 && ABL:技巧 < 2
	RETURN 0
;猫舌の助手の場合は技巧Lv2以上必要
SIF TALENT:LOCAL:猫舌 && ABL:LOCAL:技巧 < 2
	RETURN 0

;助手が奉仕精神ＬＶ３以上、技巧ＬＶ４以上じゃないとダメ
SIF ABL:LOCAL:技巧 < 4 || ABL:LOCAL:奉仕精神 < 3
	RETURN 0
;助手とTARGETの両方に胸がないとダメ
SIF !(TALENT:2 & 1) || !(TALENT:LOCAL:2 & 1)
	RETURN 0
;上半身着衣
SIF TEQUIP:1 > 1
	RETURN 0
;縄使用中は不可
SIF TEQUIP:縄 || TEQUIP:LOCAL:縄
	RETURN 0
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
RETURN 1


