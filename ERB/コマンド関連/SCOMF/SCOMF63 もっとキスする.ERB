@SCOM63
;更多的接吻
SELECTCASE TFLAG:192
	;通常
	CASE 0
		LOCAL = RAND:110
		LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
		SIF LOCAL:1 > 109
			LOCAL:1 = 109
		IF LOCAL <= 9
			;10％で大成功
			TFLAG:193 = 1
			IF CHK_DATENOW(CFLAG:TARGET:约会中)
				EXP:MASTER:約會経験 ++
				EXP:TARGET:約會経験 ++
			ENDIF
		ELSEIF LOCAL >= LOCAL:1
			;10～1％で失敗
			TFLAG:193 = -1
		ELSE
			;残りは成功
			TFLAG:193 = 0
		ENDIF
	;強制成功or大成功
	CASE 1
		IF RAND:110 <= 9
			;10％で大成功
			TFLAG:193 = 1
			IF CHK_DATENOW(CFLAG:TARGET:约会中)
				EXP:MASTER:約會経験 ++
				EXP:TARGET:約會経験 ++
			ENDIF
		ELSE
			;残りは成功
			TFLAG:193 = 0
		ENDIF
	;強制失敗
	CASE -1
		TFLAG:193 = -1
		TFLAG:98 = -1
	;指令終了
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT
;先制発生時は失敗しない
SIF TFLAG:102 == 3
	TFLAG:193 = MAX(TFLAG:193,0)

IF FLAG:70
	BASE:MASTER:TSP -= 30
ELSEIF CFLAG:TARGET:陪睡中
	DOWNBASE:MASTER:体力 += 30
	DOWNBASE:MASTER:気力 += 50
ELSEIF CHK_DATENOW(CFLAG:TARGET:约会中)
	DOWNBASE:体力 += 30
	DOWNBASE:気力 += 50
	DOWNBASE:MASTER:体力 += 10
	DOWNBASE:MASTER:気力 += 70
ELSE
	DOWNBASE:体力 += 50
	DOWNBASE:気力 += 70
	DOWNBASE:MASTER:体力 += 30
	DOWNBASE:MASTER:気力 += 100
ENDIF

;固定で獲得するソース
SOURCE:歓楽 = 200
SOURCE:情愛 = 250
SOURCE:露出 = 80
SOURCE:反感 = 200
SOURCE:受動 = 150 + 300 * ABL:従順
SOURCE:快Ｍ = ABL:PLAYER:舌 * 3 + 20
SOURCE:PLAYER:快Ｍ = ABL:舌 * 3 + 20
EXP:接吻経験 ++
EXP:MASTER:接吻経験 ++

SIF CFLAG:睡眠
	SOURCE:反感 = 150
	
;ABL:親密をみる
IF ABL:親密 <= 1
	SOURCE:情愛 += (ABL:親密 * 30)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 150 + (ABL:親密 * 50)
	SOURCE:情愛 += 200 + (ABL:親密 * 40)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 250 + (ABL:親密 * 70)
	SOURCE:情愛 += 280 + (ABL:親密 * 50)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 350 + (ABL:親密 * 80)
	SOURCE:情愛 += 380 + (ABL:親密 * 50)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 400 + (ABL:親密 * 90)
	SOURCE:情愛 += 500 + (ABL:親密 * 70)
ELSE
	SOURCE:歓楽 += 700 + (ABL:親密 * 75)
	SOURCE:情愛 += 730 + (ABL:親密 * 50)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:情愛 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:情愛 += 400 + (CFLAG:2 - 500) / 4
ELSE
	SOURCE:情愛 += 1200 + (CFLAG:2 - 5000) / 6
ENDIF

SIF SOURCE:情愛 < 0
	SOURCE:情愛 = 0

LOCAL = 0
;好感度が低いと好感度低下
IF CFLAG:2 <= 50
	LOCAL -= 3
ELSEIF CFLAG:2 <= 100
	LOCAL -= 2
ELSEIF CFLAG:2 <= 250
	LOCAL -= 1
ENDIF

SOURCE:受動 = 150 + 300 * ABL:従順

;好感度変化
SIF !CFLAG:睡眠
	TFLAG:97 += LOCAL

IF TFLAG:193 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:情愛 , 0.50
	TIMES SOURCE:受動 , 0.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:受動 , 1.00
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:情愛 , 2.00
	TIMES SOURCE:受動 , 2.00
ENDIF

SIF !FLAG:70
	TIME += 10

IF CHK_DATENOW(CFLAG:TARGET:约会中)
	EXP:MASTER:約會経験 ++
	EXP:TARGET:約會経験 ++
ENDIF
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 2
	EXP:MASTER:百合経験 += 2
ENDIF
RETURN 1

;-------------------------------------------------
;選択可能判定
;-------------------------------------------------
;更多的接吻
@COM_ABLE563
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(563)
	RETURN RESULT
;相手が居ないとダメ
SIF !TARGET
	RETURN 0
SIF TFLAG:103 != TARGET
	RETURN 0
RETURN 1

