;-------------------------------------------------
;Gスポット刺激
;愛撫系派生指令
;-------------------------------------------------
@SCOM3
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛門 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 13
;強度
TCVAR:TARGET:4 = 1300

;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1

;破瓜抑制
TFLAG:2 = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 15

	SOURCE:快Ｖ = 180
	SOURCE:苦痛 = 10
	SOURCE:液体 = 50

	;処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 1
		SOURCE:苦痛 += 1000
	ENDIF

	;再生処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 2
		SOURCE:苦痛 += 250
	ENDIF

ELSE

	DOWNBASE:体力 += 50
	DOWNBASE:気力 += 80

	SOURCE:快Ｖ = 180
	SOURCE:苦痛 = 20
	SOURCE:液体 = 50
	SOURCE:露出 = 80
	SOURCE:逸脱 = 100
	SOURCE:反感 = 100


	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 100

	;処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 1
		SOURCE:苦痛 += 2000
		SOURCE:反感 += 3000
	ENDIF

	;再生処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 2
		SOURCE:苦痛 += 500
		SOURCE:反感 += 1000
	ENDIF
ENDIF
;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_V(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;膣内に破瓜之血的污垢を付加
SIF TALENT:処女 && TALENT:処女 != -1
	STAIN:6 |= 64
;奴隷の膣内⇔調教者の指的污垢が移動
	STAIN:6 |= STAIN:PLAYER:1
	STAIN:PLAYER:1 |= STAIN:6

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;V経験
EXP:Ｖ経験 += 1
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 3
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 3
ENDIF

RETURN 1

;-------------------------------------------------
;選択可能判定
;-------------------------------------------------
@COM_ABLE503
;Gスポット刺激
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(503)
	RETURN RESULT
;男人だとダメ
SIF  !(TALENT:2 & 1)
	RETURN 0
;潤滑不足で、従順3以下・百合属性3以下のASSIの場合指挿入れはできない（施虐狂ならＯＫ）
IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 3 || ABL:ASSI:百合属性 <= 3) && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF

;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;主人の技巧が3以下はだめ
SIF ABL:PLAYER:技巧 < 3
	RETURN 0
RETURN 1

