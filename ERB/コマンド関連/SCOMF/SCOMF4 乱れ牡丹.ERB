;-------------------------------------------------
;乱牡丹
;性交系派生指令
;-------------------------------------------------
@SCOM4
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛門 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手）

LOCALS = {TARGET}/
CSTR:PLAYER:10 += LOCALS
TCVAR:PLAYER:2 = 1
;強度
TCVAR:PLAYER:4 = 2300

;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 60
	SOURCE:快Ｃ = 300
	SOURCE:快Ｖ = 400
	SOURCE:快Ｂ = 300
	SOURCE:苦痛 = 200

	;処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 1
		SOURCE:苦痛 += 1000
	ENDIF

	;再生処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 2
		SOURCE:苦痛 += 250
	ENDIF

ELSE

	DOWNBASE:体力 += 50
	DOWNBASE:気力 += 100

	SOURCE:快Ｃ = 300
	SOURCE:快Ｖ = 400
	SOURCE:快Ｂ = 300
	SOURCE:情愛 = 400
	SOURCE:苦痛 = 400
	SOURCE:露出 = 600
	SOURCE:逸脱 = 700
	SOURCE:反感 = 1200

	;上のほうで計算した汚れデータ
	SOURCE:不潔 = Y*10 + 60


	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 1000

	;贞操重视持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（性交系のみ）
	SIF TALENT:貞操 > 0 && !TALENT:恋慕
		SOURCE:反感 += 1000

	;処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 1
		SOURCE:苦痛 += 2000
		SOURCE:反感 += 3000
	ENDIF

	;再生処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 2
		SOURCE:苦痛 += 500
		SOURCE:反感 += 1000
	ENDIF
ENDIF
;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_V(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;膣内に破瓜之血的污垢を付加
SIF TALENT:処女  &&TALENT:処女 != -1
	STAIN:6 |= 64
;奴隷の膣内⇔調教者のＰ的污垢が移動
STAIN:6 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:6

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;性交経験
EXP:PLAYER:挿入経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
ENDIF

IF FLAG:70 == 1
	EXP:MASTER:時姦経験 += 1
ELSE
	EXP:Ｖ性交経験 += 1
	;愛情経験
	SIF !ASSIPLAY
		EXP:愛情経験 += 2 * TALENT:恋慕
ENDIF
RETURN 1

@CAN_SCOM4(ARG)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
IF FLAG:70 == 1

ELSE

	;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
	IF !TEQUIP:縄
		SKIPDISP ARG
		PRINTL 乱牡丹
		A = 0
		S = 0
		;全部の命令に共通の要素を考慮
		;(従順が高いと命令に従いやすいなど)
		CALL COM_ORDER

		;ABL:欲望
		IF ABL:欲望
			SIF S
				PRINT  + 
			A += ABL:欲望 * 3
			PRINT 欲望
			PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
			S = 1
		ENDIF
		;ABL:Ｖ感覚
		IF ABL:Ｖ感覚
			SIF S
				PRINT  + 
			A += ABL:Ｖ感覚 * 2
			PRINT Ｖ感覚
			PRINTV 'LV,ABL:Ｖ感覚,'(,ABL:Ｖ感覚 * 2,')
			S = 1
		ENDIF
		;快楽刻印
		IF MARK:快楽刻印
			SIF S
				PRINT  + 
			A += MARK:快楽刻印 * 3
			PRINT 快楽刻印
			PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 3,')
			S = 1
		ENDIF
		;PALAM:潤滑（の不足）
		IF PALAM:潤滑 < PALAMLV:3
			PRINT  - 
			A -= 5
			PRINT 潤滑不足
			PRINTV '(,5,')
			S = 1
		ENDIF
		;PALAM:欲情
		IF GETPALAMLV(PALAM:欲情,5)
			SIF S
				PRINT  + 
			A += GETPALAMLV(PALAM:欲情,5) * 3
			PRINT 欲情
			PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 3,')
			S = 1
		ENDIF
		;害羞
		IF TALENT:羞恥心 > 0
			PRINT  - 
			A -= 2
			PRINT 害羞
			PRINTV '(,2,')
			S = 1
		ENDIF
		;否定快感
		IF TALENT:快感応答 < 0 && !TALENT:恋慕
			PRINT  - 
			A -= 5
			PRINT 否定快感
			PRINTV '(,5,')
			S = 1
		ENDIF
		;討厭男人（恋慕、親愛で無効）
		IF OTOKOGIRAI(TARGET)
			PRINT  - 
			A -= 12
			PRINT 討厭男人
			PRINTV '(,12,')
			S = 1
		ENDIF
		;恋慕
		IF TALENT:恋慕 && ASSIPLAY == 0
			SIF S
				PRINT  + 
			A += 15
			PRINT 恋慕
			PRINTV '(,15,')
			S = 15
		ENDIF
		;親愛
		IF TALENT:恋慕 > 1 && ASSIPLAY == 0
			SIF S
				PRINT  + 
			A += 5
			PRINT 親愛
			PRINTV '(,5,')
			S = 5
		ENDIF
		;処女
		LOCAL = MAX(0,40 - EXP:Ｖ経験)
		IF TALENT:処女 && TALENT:処女 != -1 && LOCAL
			PRINT  - 
			A -= LOCAL
			PRINT 処女
			PRINTV '(,LOCAL,')
			S = 1
		;V経験
		ELSEIF EXP:Ｖ経験 < EXPLV:2
			PRINT  - 
			A -= 15
			PRINT Ｖ経験
			PRINT 不足
			PRINTV '(,15,')
			S = 1
		ENDIF

		;調教者が扶她
		IF (TALENT:PLAYER:2 & 3) == 3
			SIF S
				PRINT  + 
			A += 4
			PRINT 扶她
			PRINTV '(,4,')
			S = 1
		ENDIF

		;媚薬
		IF TCVAR:媚薬
			SIF S
				PRINT  + 
			A += 6
			PRINT 媚薬
			PRINTV '(,6,')
			S = 1
		ENDIF

		Y = 0
		;愛液的污垢
		SIF STAIN:PLAYER:2 & 1
			Y += 1
		;精液的污垢
		SIF STAIN:PLAYER:2 & 4
			Y += 3
		;肛門的污垢
		SIF STAIN:PLAYER:2 & 8
			Y += 7
		SIF STAIN:PLAYER:2 & 16
			Y += 15
		Y = Y * (2 - TALENT:汚臭耐性) / 2

		;有污垢
		IF Y
			PRINT  - 
			A -= Y
			PRINT 有污垢
			;汚臭鈍感
			IF TALENT:汚臭耐性 > 0
				PRINT 、汚臭鈍感
			ELSEIF TALENT:汚臭耐性 < 0
				PRINT 、汚臭敏感
			ENDIF
			PRINTV '(,Y,')
			S = 1
		ENDIF

		;合計を表示(47以上で実行)
		PRINT  = 
		PRINTV A

		V = 47
		SIF A < V
			PRINT  < 
		SIF A == V
			PRINT  = 
		SIF A > V
			PRINT  > 
		PRINT 実行値
		PRINTV V

	ENDIF

	WAIT
	SKIPDISP 0
	;実行できない
	SIF A < V
		RETURN 0
ENDIF
RETURN 1


;-------------------------------------------------
;選択可能判定
;-------------------------------------------------
@COM_ABLE504
;乱牡丹実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(504)
	RETURN RESULT
;調教者が男人、扶她の場合はある程度勃起てないとダメ
SIF (TALENT:PLAYER:2 & 2) && BASE:MASTER:勃起 < 1000
	RETURN 0
;羞恥PLAY中じゃないとダメ
SIF TEQUIP:30 == 0
	RETURN 0
;振動棒挿入中はダメ
SIF TEQUIP:13
	RETURN 0
;男人だとダメ
SIF  !(TALENT:2 & 1)
	RETURN 0
;全裸でないとダメ（襪子、特殊は除く）
SIF TEQUIP:0 || TEQUIP:1
	RETURN 0
;調教者が男人、半人半妖、扶她、もしくは穿戴式假陰茎がないとダメ
SIF !(TALENT:PLAYER:2 & 2) && ITEM:PBAND == 0
	RETURN 0
;潤滑不足で、従順3以下・百合属性3以下のASSIの場合乱牡丹はできない（施虐狂ならＯＫ）
IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 3 || ABL:ASSI:百合属性 <= 3) && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF
RETURN 1

