;FileName_USERCOM.ERB ------------------------------ Rev1.00
;指令表示処理
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@SHOW_USERCOM
#DIM DYNAMIC printKojoCom
TFLAG:100 = !TFLAG:101
VARSET LOCAL
;前ターンのMASTERが射精した相手をLOCAL:1へセット
IF TCVAR:MASTER:112 == 1
	;V
	LOCAL:1 = MASTER_POSE(SET_V,SET_P,1)
ELSEIF TCVAR:MASTER:112 == 2
	;A
	LOCAL:1 = MASTER_POSE(SET_A,SET_P,1)
ENDIF

;---------------------------------------
;魔改内容：增加对象剩余工作量查看
;---------------------------------------

IF BASE:TARGET:工作量 && WORKING(TARGET) && (TARGET != 0)
	SETCOLOR 0xFFA488
	PRINTFORML %CALLNAME:TARGET%还剩下大概{BASE:TARGET:工作量}的工作量
	RESETCOLOR
ENDIF
CALL LOOK_AT
PRINTL

CALL 过滤器表示

;指令表示
FOR LOCAL,0,800
	SIF LOCAL == 355 && (!FLAG:70 || CFLAG:TARGET:344 == 1)
		CONTINUE
	SIF LOCAL == 200 && TFLAG:102 == 2
		PRINTL
	SIF LOCAL == 300 && printKojoCom
		PRINTL
	SIF LOCAL == 350 && TFLAG:102 != 2
		PRINTL
	SIF LOCAL == 400 && TFLAG:102 != 2
		PRINTL
	SIF GLOBAL_COMABLE2(LOCAL)
		CONTINUE
	SIF GROUPMATCH(LOCAL,400,405,604)
		CONTINUE
	RESULT = 0
	;現状で対象指令が使用可能かどうか判定
	TRYCALLFORM COM_ABLE{LOCAL}
	IF RESULT
		SIF !QUICKFILTER(LOCAL)
			CONTINUE
		IF (LOCAL == 42 && TEQUIP:11) || (LOCAL == 43 && TEQUIP:12) || (LOCAL == 44 && TEQUIP:13) || (LOCAL == 45 && TEQUIP:14) || (LOCAL == 46 && TEQUIP:15) || (LOCAL == 47 && TEQUIP:16) || (LOCAL == 48 && TEQUIP:17)
			PRINTFORMC %TRAINNAME:LOCAL%解除[{LOCAL,3}]
		ELSEIF LOCAL == 15
			IF CFLAG:MASTER:恶作剧
				PRINTFORMC 愛撫私処[{LOCAL,3}]
			ELSE
				PRINTFORMC 陰蒂愛撫[{LOCAL,3}]
			ENDIF
		ELSEIF LOCAL == 81 && LOCAL:1
			PRINTFORMC 清掃口交[{LOCAL,3}]
		ELSEIF LOCAL == 82 && (TALENT:105 == -2 || TALENT:105 == -1)
			PRINTFORMC 乳交[{LOCAL,3}]
		ELSEIF LOCAL == 60 && (!BEDROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC 対面立位[{LOCAL,3}]
		ELSEIF LOCAL == 61 && (!INROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC 背面立位[{LOCAL,3}]
		ELSEIF LOCAL == 62 && (!BEDROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC 対面立位肛交[{LOCAL,3}]
		ELSEIF LOCAL == 63 && (!INROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC 背面立位肛交[{LOCAL,3}]
		ELSEIF LOCAL == 73 && TCVAR:PLAYER:要去了
			IF TEQUIP:TARGET:体位 == 1 && (BEDROOM(CFLAG:PLAYER:現在位置) || (BATHROOM() && ITEM:20))
				IF CFLAG:TARGET:生理周期 == 7
					PRINTFORMC 菊一文字[{LOCAL,3}]
				ELSE
					PRINTFORMC 松叶立[{LOCAL,3}]
				ENDIF
			ELSEIF TEQUIP:TARGET:体位 == 2
				IF CFLAG:TARGET:生理周期 == 7
					PRINTFORMC 燕返し[{LOCAL,3}]
				ELSE
					PRINTFORMC 望月式[{LOCAL,3}]
				ENDIF
			ELSEIF TEQUIP:TARGET:体位 == 3
				IF CFLAG:TARGET:生理周期 == 7
					PRINTFORMC 撞木反り[{LOCAL,3}]
				ELSE
					PRINTFORMC 时雨茶臼[{LOCAL,3}]
				ENDIF
			ELSEIF TEQUIP:TARGET:体位 == 4
				IF CFLAG:TARGET:生理周期 == 7
					PRINTFORMC 百闭[{LOCAL,3}]
				ELSE
					PRINTFORMC 抱地藏[{LOCAL,3}]
				ENDIF
			ELSEIF TEQUIP:TARGET:体位 == 5
				IF CFLAG:TARGET:生理周期 == 7
					PRINTFORMC 鸣门[{LOCAL,3}]
				ELSE
					PRINTFORMC 牡丹乱[{LOCAL,3}]
				ENDIF
			ELSE
				PRINTFORMC %TRAINNAME:LOCAL%[{LOCAL,3}]
			ENDIF
		ELSEIF LOCAL == 92 && (!BEDROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC 被Ａ対面立位[{LOCAL,3}]
		ELSEIF LOCAL == 93 && (!INROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC 被Ａ背面立位[{LOCAL,3}]
		ELSEIF LOCAL == 100 && TALENT:TARGET:幼稚
			PRINTFORMC 打屁股[{LOCAL,3}]
		ELSEIF LOCAL == 130 && (!BEDROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC 被対面立位[{LOCAL,3}]
		ELSEIF LOCAL == 131 && (!INROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC 被背面立位[{LOCAL,3}]
		ELSEIF LOCAL == 300 && TCVAR:TARGET:302 && ABL:MASTER:話術技能 >= 4
			SETCOLOR C_ORANGE
			PRINTFORMC 会話[{LOCAL,3}]
			RESETCOLOR
		ELSEIF LOCAL == 301 && CFLAG:TARGET:行動 == 5
			PRINTFORMC 敬茶[{LOCAL,3}]
		ELSEIF LOCAL == 301 && GETBIT(TCVAR:MASTER:特別的茶水, 0)
			PRINTFORMC 泡紅茶[{LOCAL,3}]
		ELSEIF LOCAL == 334 && CFLAG:TARGET:350 == 49 && (TALENT:TARGET:酒耐性 == -2 || CFLAG:TARGET:妊娠自覚状態 == 1)
			PRINTFORMC 請吃料理[{LOCAL,3}]
		ELSEIF LOCAL == 304 && CFLAG:TARGET:350 == 46
			PRINTFORMC 参加遊戲[{LOCAL,3}]
		ELSEIF LOCAL == 315
			IF GETBIT(TALENT:TARGET:性別,1)
				PRINTFORMC 竿愛撫[{LOCAL,3}]
			ELSEIF CFLAG:MASTER:恶作剧
				PRINTFORMC 愛撫私処[{LOCAL,3}]
			ELSE
				PRINTFORMC 陰蒂愛撫[{LOCAL,3}]
			ENDIF
		ELSEIF LOCAL == 350
			IF FLAG:70 == 1 && CFLAG:TARGET:344 == 0
				LOCALS = 猥褻
			ELSEIF (CFLAG:現在位置 == 情人用宿屋 || CFLAG:現在位置 == 情人旅館)&& !CFLAG:诶嘿嘿
				LOCALS = 抱到身边
			ELSEIF CFLAG:現在位置 == 寺子屋
				LOCALS = 不対勁的個人授業
			ELSEIF CFLAG:現在位置 == OMANEKIBEYA() && TARGET == 43
				LOCALS = 房中術的修行
			;ベッドがない
			;室外
			ELSEIF !INROOM(CFLAG:PLAYER:現在位置)
				LOCALS = 抱到身边
			ELSE
				LOCALS = 推倒
			ENDIF
			SIF GET_TARGETNUM() > 1
				LOCALS = 一并%LOCALS%
			PRINTFORMC %LOCALS%[{LOCAL,3}]
		ELSEIF LOCAL == 351
			IF CFLAG:TARGET:同行
			ELSEIF COM_351_CONDITION(TARGET) == 1
				SETCOLOR 100, 100, 100
			ELSEIF CAN_IRAI_CHARA_GO_TOGATHER(TARGET)
				SETCOLOR C_GREEN
			ELSE
				SETCOLOR C_AQUA
			ENDIF
			PRINTFORMC %TRAINNAME:LOCAL%[{LOCAL,3}]
			RESETCOLOR
		ELSEIF LOCAL == 491
			SETCOLOR 255, 179, 167
			PRINTFORMC %TRAINNAME:LOCAL%[{LOCAL,3}]
			RESETCOLOR
		ELSEIF LOCAL == 360 && BASE:MASTER:気力 <= 0
			PRINTFORMC 任憑擺佈[{LOCAL,3}]
		ELSEIF LOCAL == 366 && GROUPMATCH(IRAI_CHARA_TO_PROGRESS(TARGET), "依頼達成", "依頼失敗")
			SETCOLOR C_GREEN
			PRINTFORMC 報告委託[{LOCAL,3}]
			RESETCOLOR
		ELSEIF LOCAL == 366 && GROUPMATCH(IRAI_CHARA_TO_PROGRESS(TARGET), "依頼未受託")
			SETCOLOR C_GREEN
			PRINTFORMC 聽取依頼[{LOCAL,3}]
			RESETCOLOR
		ELSEIF LOCAL == 367
			SETCOLOR C_GREEN
			PRINTFORMC %NAME_IRAI_ACT()%[{LOCAL,3}]
			RESETCOLOR
		ELSEIF LOCAL == 368
			;PRINTFORMC %陪同行動_名称(TARGET)%[{LOCAL,3}]
			PRINTFORMC %FreeAct_Name(TARGET)%[{LOCAL,3}]
		ELSEIF LOCAL == 337
			PRINTFORMC %兒童ふれあい名称(TFLAG:描写中の兒童)%[{LOCAL,3}]
		ELSEIF LOCAL == 417 && TIME >= 660 && TIME <= 900
			PRINTFORMC 午睡[{LOCAL,3}]
		ELSEIF LOCAL == 417
			PRINTFORMC 假寐[{LOCAL,3}]
		ELSEIF LOCAL == 420
			SELECTCASE MAIN_MAP
				CASE 0
					LOCALS = 留琴
				CASE 3
					LOCALS = 妖精女仆
				CASE 9
					LOCALS = 僵尸妖精
			ENDSELECT
			PRINTFORMC 和%LOCALS%対話[{LOCAL,3}]
		ELSEIF LOCAL == 422 && CFLAG:MASTER:現在位置 == 103
			PRINTFORMC 聆聴佛音[{LOCAL,3}]
		ELSEIF LOCAL == 431 && (CFLAG:MASTER:現在位置 == 24 || CFLAG:MASTER:現在位置 == 441)
			PRINTFORMC 在温泉里放鬆[{LOCAL,3}]
		ELSEIF LOCAL == 431 && (CFLAG:MASTER:現在位置 == 大衆浴場 || CFLAG:MASTER:現在位置 == 940)
			PRINTFORMC 入浴[{LOCAL,3}]
		ELSEIF LOCAL == 431 && OPENPLACE(CFLAG:MASTER:現在位置) && ITEM:簡易泳池 && !BATHCHECK(CFLAG:MASTER:現在位置)
			PRINTFORMC 泳池里放鬆[{LOCAL,3}]
;		ELSEIF LOCAL == 405 && CFLAG:TARGET:同行 && TARGET
;			PRINTFORMC 約會[{LOCAL,3}]
		ELSEIF LOCAL == 410 && OUTROOF(CFLAG:MASTER:現在位置) && DAY:2 == 4
			PRINTFORMC 扫雪[{LOCAL,3}]
		ELSEIF LOCAL == 414 && TIME >= 300 && TIME <= 540 && (CFLAG:MASTER:現在位置 == 9 || CFLAG:MASTER:現在位置 == OMANEKIBEYA())
			PRINTFORMC 吃早餐[{LOCAL,3}]
		ELSEIF LOCAL == 414 && TIME >= 660 && TIME <= 900 && (CFLAG:MASTER:現在位置 == 9 || CFLAG:MASTER:現在位置 == OMANEKIBEYA())
			PRINTFORMC 吃午餐[{LOCAL,3}]
		ELSEIF LOCAL == 414 && TIME >= 1020 && TIME <= 1200 && (CFLAG:MASTER:現在位置 == 9 || CFLAG:MASTER:現在位置 == OMANEKIBEYA())
			PRINTFORMC 吃下午茶[{LOCAL,3}]
		ELSEIF LOCAL == 414 && TIME >= 1200 && TIME <= 1440 && (CFLAG:MASTER:現在位置 == 9 || CFLAG:MASTER:現在位置 == OMANEKIBEYA())
			PRINTFORMC 吃晩餐[{LOCAL,3}]
		ELSEIF LOCAL == 414 && TIME >= 0 && TIME <= 180 && (CFLAG:MASTER:現在位置 == 9 || CFLAG:MASTER:現在位置 == OMANEKIBEYA())
			PRINTFORMC 吃夜宵[{LOCAL,3}]
		ELSEIF LOCAL == 414 && CFLAG:TARGET:行動 == 5
			PRINTFORMC 吃饭休憩[{LOCAL,3}]
		ELSEIF LOCAL == 443
			PRINTFORMC %NAME_UNIQUE_COM(TARGET)%[{LOCAL,3}]
		ELSEIF LOCAL == 448
			IF HUNTING_SPOT(CFLAG:MASTER:現在位置) && MUSHITORI_SPOT(CFLAG:MASTER:現在位置)
				IF GET_MUSHIDATA(MUSHI_TRAP, "場所") && GROUPMATCH(GET_MUSHIDATA(MUSHI_TRAP, "場所"), TRAP_POINT:1, TRAP_POINT:2, TRAP_POINT:3)
					PRINTFORMC 检查罠的状态[{LOCAL,3}]
				ELSE
					PRINTFORMC %TRAINNAME:LOCAL%[{LOCAL,3}]
				ENDIF
			ELSEIF GROUPMATCH(CFLAG:MASTER:現在位置, TRAP_POINT:1, TRAP_POINT:2, TRAP_POINT:3)
				PRINTFORMC 检查罠的状态[{LOCAL,3}]
			ELSEIF GROUPMATCH(CFLAG:MASTER:現在位置, GET_MUSHIDATA(MUSHI_TRAP, "場所"))
				PRINTFORMC 確認昆虫诱捕装置[{LOCAL,3}]
			ELSEIF MUSHITORI_SPOT(CFLAG:MASTER:現在位置)
				PRINTFORMC 昆虫诱捕[{LOCAL,3}]
			ELSE
				PRINTFORMC %TRAINNAME:LOCAL%[{LOCAL,3}]
			ENDIF
		ELSEIF LOCAL == 464
			SELECTCASE CFLAG:MASTER:現在位置
				CASE 210
					IF MAIN_MAP == 8
						PRINTFORMC 前往索道站[{LOCAL,3}]
					ELSE
						PRINTFORMC 前往絶景之丘[{LOCAL,3}]
					ENDIF
				CASE 820
					IF MAIN_MAP == 2
						PRINTFORMC 前往人里北街[{LOCAL,3}]
					ELSE
						PRINTFORMC 前往広場[{LOCAL,3}]
					ENDIF
				CASE 570
					PRINTFORMC 前往中有之道[{LOCAL,3}]
				CASE 760
					PRINTFORMC 前往灼熱地獄跡[{LOCAL,3}]
				CASE 970
					PRINTFORMC 前往间歇泉地下中心[{LOCAL,3}]
				CASEELSE
					LOCALS = %STR:(6000 + SPECIAL_AREAMOVE() / 10)%
					PRINTFORMC 前往%LOCALS%[{LOCAL,3}]
			ENDSELECT
		ELSEIF LOCAL == 482
			PRINTFORMC 追踪%CALLNAME:STALKED%[{LOCAL,3}]
		ELSEIF LOCAL == 626
			PRINTFORMC %GIFTSHOP_NAME(CFLAG:MASTER:约会中, 1)%[{LOCAL,3}]
		ELSEIF LOCAL == 603 && PREVCOM == 603
			PRINTFORMC 拉手[{LOCAL,3}]
		ELSEIF LOCAL == 698
			PRINTFORMC 去%OMANEKI_PLACE()%[{LOCAL,3}]
		ELSEIF LOCAL >= 200 && LOCAL < 230
			CALL KOJO_MESSAGE_SEND("COM", 0, TARGET, LOCAL - 200)
			PRINTFORMC %RESULTS%[{LOCAL,3}]
			printKojoCom = 1
		ELSEIF STRLENS(TRAINNAME:LOCAL)
			PRINTFORMC %TRAINNAME:LOCAL%[{LOCAL,3}]
		ENDIF
	ENDIF
NEXT
RESULT = 0
IF SWEEPGOD == 1 
	PRINTC 一键扫除[778]
ENDIF
;自動実行中なら下記表示をスキップ
SIF !TFLAG:100
	GOTO SKIP
PRINTL
CALL PRINTBUTTON_EX(TFLAG:USERCOM非表示 ? "▽[＋][Ex_COM] ==" # "▼[－][Ex_COM] ==", 3002, TFLAG:USERCOM非表示)
PRINTFORML %"=" * 100%
SIF TFLAG:USERCOM非表示
	GOTO SKIP
;SIF !GETBIT (FLAG:USERCOM開关, 12)
;	PRINTC 履歴表示[800]
IF GETBIT(FLAG:瞬間移動, 1) && !CFLAG:MASTER:同行
	SETCOLOR 0x5090FF
ELSE
	SETCOLOR C_GREEN
ENDIF
RESULT = 0
CALL COM_ABLE400
SIF RESULT
	PRINTC 移動[400]
RESULT = 0
RESETCOLOR
CALL COM_ABLE405
IF RESULT
	IF CFLAG:TARGET:同行 && TARGET
		PRINTC 约会[405]
	ELSE 
		PRINTC 外出[405]
	ENDIF
ENDIF
RESULT = 0

IF GETBIT(FLAG:瞬間移動, 0) && !FLAG:约会的对象
	SETCOLOR 0x5090FF
ELSE
	SETCOLOR C_GREEN
ENDIF
RESULT = 0
CALL COM_ABLE604
SIF RESULT
	PRINTC 散步[604]
RESETCOLOR
TRYCALLFORM IS_DEBUGGERR
IF RESULT == 1
        SETCOLOR 241,158,194
        PRINTC 幻想乡之主[777]
        RESETCOLOR
ENDIF

SIF !GETBIT (FLAG:USERCOM開关, 0)
	PRINTC 汚渍顯示[801]
SIF !CFLAG:陪睡中 && GET_TARGETNUM() > 1 && !GETBIT (FLAG:USERCOM開关, 1)
	PRINTC 変更对象[802]
SIF !GETBIT (FLAG:USERCOM開关, 2)
	PRINTC 能力表示[803]
SIF !GETBIT (FLAG:USERCOM開关, 3)
	PRINTC 能力表示(主角)[804]
SIF !GETBIT (FLAG:USERCOM開关, 4)
	PRINTC 道具確認[805]
SIF !GETBIT(FLAG:USERCOM開关, 11)
	PRINTC 指令過濾器[809]
SIF FLAG:1005
	PRINTC CONFIG[810]
SIF TALENT:MASTER:居場所察知 && AT_HOME(MASTER) && (GETBIT(TALENT:MASTER:居場所察知, 1) || !(CFLAG:诶嘿嘿 || CFLAG:恶作剧 || CFLAG:陪睡中))
	PRINTC 居場所察知[811]
SIF TALENT:MASTER:汚部屋察知 && AT_HOME(MASTER) && !(CFLAG:诶嘿嘿 || CFLAG:恶作剧 || CFLAG:陪睡中)
	PRINTC 汚部屋察知[812]
SIF GETBIT(TALENT:MASTER:劣情察知, 1) && !CFLAG:诶嘿嘿
	PRINTC 劣情探知[813]
SIF FLAG:70 && CFLAG:TARGET:344 == 0 && !CFLAG:TARGET:睡眠 && AT_HOME(MASTER) == 1 && TARGET > 0 && !CFLAG:诶嘿嘿
	PRINTC 携带[815]
SIF FLAG:70 && TALENT:MASTER:集合 && AT_HOME(MASTER) == 1 && !CFLAG:诶嘿嘿
	PRINTC 集合[817]
SIF GATHERING_SPOT(CFLAG:MASTER:現在位置) != 0
	PRINTC 採集列表[818]
IF CAN_DATE(TARGET)
	SIF TIME_REQUIRED(MAIN_MAP,CFLAG:MASTER:约会中) < 10
		SETCOLOR 100, 250, 140
	PRINTFORMC 邀請去%GET_MAPNAME(MAIN_MAP)%({TIME_REQUIRED(MAIN_MAP,CFLAG:MASTER:约会中)}分)[990]
	RESETCOLOR
ENDIF

SIF CAN_DATE(TARGET)
	PRINTC 邀請去約會[992]
;SIF CFLAG:MASTER:現在位置 == CFLAG:MASTER:初期位置
	PRINTC 日曆[993]
SIF FLAG:胖次輸送
	PRINTC 確認手上的胖次[994]
IF AT_HOME(MASTER) && GETBIT(FLAG:瞬間移動, 1)
	SETCOLOR 0x5090FF
ELSEIF !AT_HOME(MASTER) && GETBIT(FLAG:瞬間移動, 0)
	SETCOLOR 0x5090FF
ENDIF
SIF !GETBIT(FLAG:USERCOM開关, 13)
	PRINTC 自動時停移動[995]
RESETCOLOR

;自動掃除
IF AT_HOME(MASTER) && GETBIT(FLAG:自動掃除, 1)
	SETCOLOR 0x5090FF
ELSEIF !AT_HOME(MASTER) && GETBIT(FLAG:自動掃除, 0)
	SETCOLOR 0x5090FF
ENDIF
SIF !GETBIT(FLAG:USERCOM開关, 14) & AT_HOME(MASTER) && !CFLAG:诶嘿嘿 && !CFLAG:陪睡中
	PRINTC 自動掃除[996]
RESETCOLOR
PRINTL
IF FLAG:70 == 1 && CFLAG:TARGET:344 == 0
	SIF EQUIP:0 && TARGET && CFLAG:诶嘿嘿
		PRINTC 脱衣服[820]
	IF EQUIP:0 && TARGET && EQUIP:TARGET:下半身内衣２ != 0
		IF !PANTS_ALREADY_COLLECTED()
			FONTBOLD
			SETCOLOR COLOR("PINK")
		ENDIF
			PRINTC 偷走胖次[821]
			RESETCOLOR
			FONTREGULAR
	ENDIF
	IF (TEQUIP:5 == 0 || EQUIP:TARGET:上半身内衣１) && CFLAG:诶嘿嘿
		SIF EQUIP:0 && TARGET && EQUIP:TARGET:上半身内衣１ != 0 && (EQUIP:TARGET:和服 != 0 || EQUIP:TARGET:上身衣服２ != 0 || EQUIP:TARGET:連体内衣 == 3)
			PRINTC 脱掉衣服[822]
	ENDIF
	IF TEQUIP:5 == 0 && CFLAG:诶嘿嘿
		SIF EQUIP:0 && TARGET && (EQUIP:TARGET:上半身内衣１ || EQUIP:TARGET:上半身内衣２ || EQUIP:TARGET:下半身内衣１ || EQUIP:TARGET:下半身内衣２ || EQUIP:TARGET:緊身内衣 || EQUIP:TARGET:連体内衣) && (EQUIP:TARGET:上身衣服１ || EQUIP:TARGET:上身衣服２ || EQUIP:TARGET:連衣裙 || EQUIP:TARGET:和服 || EQUIP:TARGET:外搭)
			PRINTC 脱到只剩内衣[823]
	ENDIF
	IF MEASUREMENTCHECK(CFLAG:TARGET:現在位置)
		IF CFLAG:TARGET:身体測定 != 2
			PRINTC 身体測定[824]
		ELSE
			PRINTC 結束身体測定[824]
		ENDIF
	ENDIF
	SIF TFLAG:胖次収奪
		PRINTC 奪取胖次[825]
	IF TARGET
		IF TEQUIP:挑逗モード
			PRINTC 结束挑逗[826]
		ELSE
			PRINTC 挑逗[826]
		ENDIF
	ENDIF
	IF CFLAG:诶嘿嘿
		PRINTC 停止猥褻[999]
	ELSE
		PRINTC 時間停止解除[999]
	ENDIF
ELSEIF FLAG:70 == 1 && CFLAG:TARGET:344 == 1
		PRINTC 時間停止解除[999]
	IF CFLAG:诶嘿嘿
		SIF EQUIP:0 && TARGET
			PRINTC 脱衣服[820]

		IF CFLAG:诶嘿嘿 == 2
			IF TFLAG:抱住模式
				PRINTC 甩脱[850]
			ELSE
				PRINTC 摆脱[850]
			ENDIF
			IF TEQUIP:挑逗モード
				PRINTC 结束挑逗[826]
			ELSE
				PRINTC 挑逗[826]
			ENDIF
		ELSE
			PRINTC 結束幽會[999]
		ENDIF
	ENDIF
ELSEIF CFLAG:诶嘿嘿 && !CFLAG:恶作剧
	SIF EQUIP:0 && TARGET && !(TFLAG:102 == 3 && TFLAG:抱住模式)
		PRINTC 脱衣服[820]
		IF TARGET
			IF TEQUIP:挑逗モード
				PRINTC 结束挑逗[826]
			ELSE
				PRINTC 挑逗[826]
			ENDIF
		ENDIF
	IF FLAG:70 == 1 && CFLAG:TARGET:344 == 0
		PRINTC 時間停止解除[999]
	ELSEIF CFLAG:诶嘿嘿 == 2
		IF TFLAG:抱住模式
			PRINTC 甩脱[850]
		ELSE
			PRINTC 摆脱[850]
		ENDIF
	ELSE
		PRINTC 結束幽會[999]
	ENDIF
ENDIF

IF CFLAG:恶作剧
	SIF CFLAG:恶作剧 < 2 && (EXP:TARGET:睡眠姦経験 > 100 || TCVAR:睡眠薬強度 || TCVAR:睡眠深度 > 2000)
		PRINTC 敞開衣服[830]
	IF CFLAG:恶作剧 < 2
		PRINTC 結束猥褻[999]
		IF EQUIP:TARGET:下半身内衣２
			IF !PANTS_ALREADY_COLLECTED()
				FONTBOLD
				SETCOLOR COLOR("PINK")
			ENDIF
			PRINTC 偷走胖次[821]
			RESETCOLOR
			FONTREGULAR
		ENDIF
	ENDIF
	SIF CFLAG:恶作剧 > 1
	PRINTC 結束睡眠姦[999]
ENDIF
IF CFLAG:陪睡中
	PRINTC 結束陪睡[999]
ENDIF
IF CHK_DATENOW(CFLAG:MASTER:约会中) && (!FLAG:70 || CFLAG:TARGET:344 == 1) && !CFLAG:MASTER:诶嘿嘿 && CFLAG:MASTER:現在位置 != OMANEKIBEYA() && TFLAG:妄想中 != 1
	SIF TIME_REQUIRED(MAIN_MAP,CFLAG:MASTER:约会中) < 10
		SETCOLOR 100, 250, 140
	PRINTFORMC 踏上归途({TIME_REQUIRED(MAIN_MAP,CFLAG:MASTER:约会中)}分)[999]
	RESETCOLOR
ENDIF

PRINTL
SETCOLOR C_L_GREEN
PRINTC 存檔操作[10000]
RESETCOLOR

$SKIP
IF PREVCOM >= 0
	IF TFLAG:151
		STR:0 = %STR:(1500 + TFLAG:151)%
	ELSE
		STR:0 = %TRAINNAME:PREVCOM%
	ENDIF
ENDIF
SIF PREVCOM == 304 && CFLAG:TARGET:350 == 49 && TALENT:TARGET:酒耐性 != -2
	STR:0 = 碰杯
SIF PREVCOM == 304 && CFLAG:TARGET:350 == 49 && (TALENT:TARGET:酒耐性 == -2 || CFLAG:TARGET:妊娠自覚状態 == 1)
	STR:0 = 請吃料理
SIF PREVCOM == 304 && CFLAG:TARGET:350 == 51
	STR:0 = 照料
;口上自定义
IF PREVCOM >= 200 && PREVCOM < 230
	CALL KOJO_MESSAGE_SEND("COM", 0, TARGET, PREVCOM - 200)
	STR:0 '= RESULTS
ENDIF
IF !TFLAG:USERCOM非表示
	IF PREVCOM >= 0 && PREVCOM < 300
		PRINTL 
		PRINT ＜前回調教指令:
		PRINTS STR:0
		SIF TFLAG:150
			PRINT (助手)
		PRINTL ＞
	ELSE
		PRINTL 
		PRINT ＜上回指令:
		PRINTS STR:0
		PRINTL ＞
	ENDIF
ENDIF
