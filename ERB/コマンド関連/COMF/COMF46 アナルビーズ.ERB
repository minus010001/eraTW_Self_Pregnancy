;-------------------------------------------------
;後庭拉珠
;道具使用指令、レベル3
;-------------------------------------------------
@COM46
;着脱の処理
TEQUIP:後庭拉珠 = !TEQUIP:後庭拉珠
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛門 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 12
;強度
TCVAR:TARGET:4 = 800
TCVAR:Ａ喪失発生 = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 150
	SOURCE:快Ａ = 150
	SOURCE:苦痛 = 100

	;装着時は快Aは下がる
	SIF TEQUIP:後庭拉珠
		TIMES SOURCE:快Ａ , 0.80

	;外す時は快Aは上がる
	SIF !TEQUIP:後庭拉珠
		TIMES SOURCE:快Ａ , 3.00

ELSE

	DOWNBASE:体力 += 60
	DOWNBASE:気力 += 150

	SOURCE:快Ａ = 150
	SOURCE:苦痛 = 200
	SOURCE:露出 = 50
	SOURCE:屈従 = 50
	SOURCE:逸脱 = 60
	SOURCE:反感 = 80

	;装着時は快Aは下がる
	SIF TEQUIP:後庭拉珠
		TIMES SOURCE:快Ａ , 0.80

	;外す時は快Aは上がる
	SIF !TEQUIP:後庭拉珠
		TIMES SOURCE:快Ａ , 3.00

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 70
ENDIF
;Aへの苦痛は先に処理しておく
CALL PAIN_CHECK_A(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
STAIN:7 |= STAIN:4
STAIN:4 |= STAIN:7
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SIF !TEQUIP:後庭拉珠
	EXP:Ａ経験 += 2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ELSE
	;同性愛　レズなら戻り値1ゲイなら戻り値2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ENDIF
EXP:PLAYER:道具使用経験 ++

RETURN 1
;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM46(ARG)
;後庭拉珠挿入中

PRINTL ＜後庭拉珠挿入中＞

CSTR:TARGET:10 += @"{PLAYER}/"
TCVAR:TARGET:2 = 12
TCVAR:TARGET:4 = 400

IF FLAG:70 == 1
	BASE:MASTER:TSP -= 150
	SOURCE:ARG:快Ａ += 75

ELSE


	DOWNBASE:ARG:体力 += 60
	DOWNBASE:ARG:気力 += 150

	SOURCE:ARG:快Ａ += 75
	SOURCE:ARG:露出 += 25
	SOURCE:ARG:屈従 += 25
	SOURCE:ARG:逸脱 += 25
	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:ARG:自己愛 < 0 || TALENT:ARG:抵抗
		SOURCE:ARG:鬱屈 += 35

	;Aへの苦痛は先に処理しておく
	;EXP:Ａ経験をみる
	LOCAL = 100
	LOCAL:1 = 25
	IF EXP:ARG:Ａ経験 < EXPLV:1
		TIMES LOCAL , 3.00
		TIMES LOCAL:1 , 3.00
	ELSEIF EXP:ARG:Ａ経験 < EXPLV:2
		TIMES LOCAL , 1.20
		TIMES LOCAL:1 , 2.00
	ELSEIF EXP:ARG:Ａ経験 < EXPLV:3
		TIMES LOCAL , 0.80
		TIMES LOCAL:1 , 1.50
	ELSEIF EXP:ARG:Ａ経験 < EXPLV:4
		TIMES LOCAL , 0.60
		TIMES LOCAL:1 , 1.20
	ELSEIF EXP:ARG:Ａ経験 < EXPLV:5
		TIMES LOCAL , 0.40
	ELSEIF EXP:ARG:Ａ経験 >= EXPLV:5
		TIMES LOCAL , 0.20
	ENDIF

	;PALAM:潤滑をみる
	IF PALAM:ARG:潤滑 < PALAMLV:1
		TIMES LOCAL , 1.20
		TIMES LOCAL:1 , 2.00
	ELSEIF PALAM:ARG:潤滑 < PALAMLV:2
		TIMES LOCAL , 1.00
		TIMES LOCAL:1 , 1.50
	ELSEIF PALAM:ARG:潤滑 < PALAMLV:3
		TIMES LOCAL , 0.60
	ELSEIF PALAM:ARG:潤滑 < PALAMLV:4
		TIMES LOCAL , 0.30
	ELSEIF PALAM:ARG:潤滑 >= PALAMLV:4
		TIMES LOCAL , 0.20
	ENDIF
	SOURCE:ARG:苦痛 += LOCAL
	SOURCE:ARG:反感 += LOCAL:1
ENDIF
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE46
;後庭拉珠実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(46)
	RETURN RESULT
;毒人形だと肛門がついてないのでダメ
SIF TALENT:196 == 99
	RETURN 0
;解除是つでも可能
SIF TEQUIP:15
	RETURN 1
;道具を持っているか、道具無設定でないとダメ
SIF ITEM:6 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
SIF TEQUIP:0 > 1
	RETURN 0
;肛用振動棒使用中は不可
SIF TEQUIP:14
	RETURN 0
;普通の浣腸使用中は不可
SIF TEQUIP:21
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;電極使用中は不可
SIF TEQUIP:23
	RETURN 0
;新妻PLAY中はダメ
SIF TEQUIP:33
	RETURN 0
RETURN 1

