;-------------------------------------------------
;乳頭夾
;道具使用指令、レベル2
;-------------------------------------------------
@COM47
;着脱の処理
TEQUIP:乳頭夾 = !TEQUIP:乳頭夾

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 80
	SOURCE:快Ｂ = 200

ELSE

	DOWNBASE:体力 += 10
	DOWNBASE:気力 += 80

	SOURCE:快Ｂ = 200
	SOURCE:露出 = 20
	SOURCE:逸脱 = 30
	SOURCE:反感 = 30


	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 30
ENDIF
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ENDIF
EXP:PLAYER:道具使用経験 ++

RETURN 1
;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM47(ARG)
;乳頭夾装備中
PRINTL ＜乳頭夾装備中＞
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:ARG:快Ｂ += 100

ELSE

	DOWNBASE:ARG:体力 += 5
	DOWNBASE:ARG:気力 += 20

	SOURCE:ARG:快Ｂ += 100
	SOURCE:ARG:露出 += 10
	SOURCE:ARG:逸脱 += 15
	SOURCE:ARG:反感 += 15
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE47
;乳頭夾実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(47)
	RETURN RESULT
;解除是つでも可能
SIF TEQUIP:16
	RETURN 1
;道具を持っているか、道具無設定でないとダメ
SIF ITEM:10 == 0 && NOITEM == 0
	RETURN 0
;上半身着衣
SIF TEQUIP:1 > 1 && TEQUIP:3 == 0
	RETURN 0
;搾乳器使用中は不可
SIF TEQUIP:17
	RETURN 0
;乳房電極装着中はダメ
SIF TEQUIP:24
	RETURN 0
;ASSIの場合、技巧3以上じゃないと使えない。
IF ASSIPLAY
	SIF ABL:ASSI:技巧 < 3
		RETURN 0
ENDIF
;淋浴中はダメ
SIF TEQUIP:32
	RETURN 0
RETURN 1

