;-------------------------------------------------
;胸愛撫
;愛撫系指令、レベル2
;-------------------------------------------------
@COM6
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽
TCVAR:PLAYER:108 = 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:快Ｂ = 100
	IF ABL:PLAYER:指 >= 4
		SOURCE:快Ｂ += 70
	ELSE
		SOURCE:快Ｂ += ABL:PLAYER:指 * 15
	ENDIF
ELSE

	DOWNBASE:体力 += 5
	DOWNBASE:気力 += 50

	SOURCE:快Ｂ = 100
	SOURCE:情愛 = 50
	SOURCE:性行動 = 60
	SOURCE:露出 = 10
	SOURCE:不潔 = 20
	SOURCE:反感 = 20
	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	IF ABL:PLAYER:指 >= 4
		SOURCE:快Ｂ += 70
	ELSE
		SOURCE:快Ｂ += ABL:PLAYER:指 * 15
	ENDIF
	;圧抑、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 20
	;-------------------------------------------------
	;汚れの処理
	;-------------------------------------------------
	;奴隷の胸的污垢がなければ口をつかう（愛液と母乳よごれはＯＫ、汚臭無視か助手調教時は没問題）
	IF STAIN:5 < 2 || STAIN:5 == 16 || STAIN:5 == 17 || ASSIPLAY == 1 || TALENT:PLAYER:汚臭耐性 == 2
		;調教者のTALENT:舌頭靈活をみる
		TIMES SOURCE:不潔 , 1.40
		SIF TALENT:PLAYER:舌頭靈活
			TIMES SOURCE:快Ｂ , 1.40
		SOURCE:鬱屈 += SOURCE:不潔 / 20
		;奴隷のＢ⇔調教者の口的污垢が移動
		STAIN:5 |= STAIN:PLAYER:0
		STAIN:PLAYER:0 |= STAIN:5
		EXP:PLAYER:口淫経験 ++
	ENDIF
ENDIF
;奴隷のＢ⇔調教者の指的污垢が移動
STAIN:5 |= STAIN:PLAYER:1
STAIN:PLAYER:1 |= STAIN:5

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 5
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 5
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 5
	ENDIF
ENDIF
EXP:PLAYER:手淫経験 ++
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE6
;胸愛撫実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(6)
	RETURN RESULT
RETURN 1

