@COM619
#DIM 店員
#DIMS 表情
#DIMS 台詞

DRAWLINE
店員 = GET_MAPID(CFLAG:PLAYER:現在位置) == 2 ? 19 # 18
表情 = 笑顔
IF FLAG:最後一次訪问酒屋日期 == 0
	台詞 = 「第一次来的客人吗？这是特别赠品哦」
ELSEIF DAY - FLAG:最後一次訪问酒屋日期 > 7
	台詞 = 「好久不见了呢ー、请慢用」
ELSEIF FLAG:最後一次訪问酒屋日期 == DAY
	台詞 = 「欢迎光、诶呀？有事吗？」
ELSEIF FLAG:酒屋里的累積購入額 > 1000000 && FLAG:開始日 != DAY
	台詞 = 「欢迎光临、我可是专门为%CALLNAME:MASTER%准备了好多东西呢」
ELSEIF FLAG:開始日 == DAY
	台詞 = 「真不凑巧、全被刚才的客人买光了」
	表情 = 通常
;ここは甜酒を除く
ELSEIF ITEMSALES:濁酒 + ITEMSALES:清酒 + ITEMSALES:鬼殺酒 > 30
	台詞 = 「欢迎光临、本店有许多美酒哦ー」
ELSE
	台詞 = 「欢迎光临ー」
ENDIF
SIF FLAG:酒屋買取強化
	台詞 = %台詞%/「現在正在舉行推銷活動中。買的越多優惠越多」
CALL SPTALK, 0, 表情, 店員, 台詞, 1 
$INPUT_LOOP2
DRAWLINE
PRINTL [ 1]  - 買酒
PRINTL [ 2]  - 卖酒
PRINTL [ 0] - 返回
$INPUT_LOOP
INPUT

IF RESULT == 1
	CALL 酒購入
	GOTO INPUT_LOOP2
ELSEIF RESULT == 2
	CALL 酒卸売
	GOTO INPUT_LOOP2
ELSEIF RESULT != 0
	GOTO INPUT_LOOP
ENDIF

FLAG:最後一次訪问酒屋日期 = DAY
IF FLAG:酒屋的累積転売利益 > 3000000 && FLAG:酒屋的売却率FLAG == 2
	FLAG:酒屋的売却率FLAG = 3
	PRINTL 
	PRINTFORML 一走出店就被叫住了
	台詞 = 「卖了这么多酒过来真是帮了大忙了呢～、从今以后会提高一点收购价的」
ELSEIF FLAG:酒屋的累積転売利益 > 1000000 && FLAG:酒屋的売却率FLAG == 1
	FLAG:酒屋的売却率FLAG = 2
	PRINTL 
	PRINTFORML 一走出店就被叫住了
	台詞 = 「卖了这么多酒过来真是帮上忙了呢ー、要是提高收购价的话能不能多卖一点？」
ELSEIF FLAG:酒屋的累積転売利益 > 100000 && FLAG:酒屋的売却率FLAG == 0
	FLAG:酒屋的売却率FLAG = 1
	PRINTL 
	PRINTFORML 一走出店就被叫住了
	台詞 = 「一直以来到这卖酒真是太感谢了、作为礼物从下次開始收购价会提高哦ー」
ELSE
	台詞 = 「下次再来哦ー」
ENDIF
CALL SPTALK, 0, "笑顔", 店員, 台詞, 1 
TIME += 10
RETURN -1

@酒購入
#DIM 買うもの
#DIM 割引

DRAWLINE
;通信販売から無くなったので定価になります
;;割引を20％に設定(定価の80％で販売)
;割引 = 20

PRINTL 
IF FLAG:最後一次訪问酒屋日期 == 0
	PRINTFORML 「第一次来吧这是特别赠品ー」
	;値引率を40％に設定(定価の60％で販売)
	割引 = 40
ELSE
	PRINTFORML 「买哪种都很划算哦ー」
	;値引率を20％に設定(定価の80％で販売)
	割引 = 20
ENDIF
$INPUT_LOOP2
DRAWLINE
PRINTFORML 所持金:%金額表示(MONEY)%
DRAWLINE
CALL ITEM_SHOPPING(GETNUM(ITEM, "濁酒"), "微甜的便宜酒", 割引)
CALL ITEM_SHOPPING(GETNUM(ITEM, "清酒"), "標準的日本酒", 割引)
CALL ITEM_SHOPPING(GETNUM(ITEM, "鬼殺酒"), "高度数的烈性酒", 割引)
CALL ITEM_SHOPPING(GETNUM(ITEM, "甜酒"), "像饮料一样容易入口的酒", 割引)
DRAWLINE
PRINTL [999] 返回
INPUT

IF RESULT == 999
	RETURN -1
ELSE
	買うもの = RESULT
	CALL ITEMDATA(買うもの, "SHOP:酒屋")
	IF RESULT
		IF ITEMSALES:買うもの <= 0
			PRINTFORMW 「对不起、卖完了呢」
		ELSEIF ITEM:買うもの >= 所持数制限
			PRINTFORMW 「客人、那個已經拿著很多了吧？　倒不如賣給我一點ー」
		ELSE
			CALL ITEMDATA(買うもの, "購入", 割引)
			IF RESULT
				FLAG:酒屋里的累積購入額 += RESULT
				IF FLAG:酒屋里的累積購入額 > 100000 && !RAND:9
					PRINTL 
					PRINTFORML 「一直光顾本店真是太感谢了、这是赠品」
					PRINTFORMW ―得到了作为赠品的%ITEMNAME:GETNUM(ITEM, "姜黄飲料")%―
					ITEM:姜黄飲料 ++
				ELSE
					PRINTL 
					PRINTFORMW 「多谢惠顾ー」
				ENDIF
				
			ELSE
				RESTART
			ENDIF
		ENDIF
	ENDIF
ENDIF

@酒卸売
#DIM 売却率
#DIM 所持数
#DIM 選択した商品
#DIM 売却個数
#DIM 売却額

SELECTCASE FLAG:酒屋的売却率FLAG
	CASE 0
		;値引率を90％に設定(定価の10％で購入してくれる)
		売却率 = 10
	CASE 1
		;値引率を85％に設定(定価の15％で購入してくれる)
		売却率 = 15
	CASE 2
		;値引率を80％に設定(定価の20％で購入してくれる)
		売却率 = 20
	CASE 3
		;値引率を70％に設定(定価の30％で購入してくれる)
		売却率 = 30
ENDSELECT
SIF FLAG:酒屋買取強化
	売却率 += 20
$INPUT_LOOP2
DRAWLINE
PRINT 「
IF FLAG:酒屋買取強化
	PRINT 因為是推銷活動所以特別的
ELSEIF FLAG:酒屋的売却率FLAG
	PRINT 因为是大客户所以特别
ENDIF
PRINTFORML 优惠原价的{売却率}％」
DRAWLINE
PRINT 要卖什么呢…
PRINTFORML 所持金:%金額表示(MONEY)%
CALL 酒一覧表示(0, "売却", 売却率)
IF RESULT == 0
	RETURN -1
ENDIF
;選んだ番号にしたがって所持数を格納
所持数 = ITEM:RESULT
;所持0の場合
IF 所持数 == 0
	PRINTL 
	PRINTW 因为没有现货、想卖也卖不了
	RETURN -1
ENDIF
;所持0で無い場合個数を尋ねる
;もう一回INPUTを使用ため今の値を退避
選択した商品 = RESULT
売却個数 = 0
$SELL_LOOP
PRINTFORML %ITEMNAME:選択した商品%を要卖多少呢…
PRINTFORML 売る数：{売却個数}
PRINTFORML [-100]　[ -10]　[  -1]　[  +1]　[ +10]　[+100]
PRINTBUTTON "[全部] ", 999
PRINTBUTTON " [复位]", -999
PRINTL 
PRINTBUTTON "[売る]", 0
INPUT
IF RESULT
	IF RESULT == 999
		売却個数 = 所持数
	ELSEIF RESULT == -999
		売却個数 = 0
	ELSE
		売却個数 = LIMIT(売却個数 + RESULT, 0, 所持数)
	ENDIF
	CLEARLINE 6
	GOTO SELL_LOOP
ENDIF
IF 売却個数 <= 0
	PRINTL 
	PRINTL 果然还是不卖了
	GOTO INPUT_LOOP2
ENDIF
IF 売却個数 > 所持数
	PRINTL 
	PRINTL 没有那么多
	GOTO INPUT_LOOP2
ENDIF
	
;一つ中奖了の売却額を算出
売却額 = ITEMPRICE:選択した商品 * 売却率 / 100
ITEM:選択した商品 -= 売却個数
MONEY += 売却額 * 売却個数
;お店は定価の80％で販売
FLAG:酒屋的累積転売利益 += ITEMPRICE:選択した商品 * (80 - 売却率) / 100 * 売却個数
PRINTFORMW 以%金額表示(売却額 * 売却個数)%的价格卖掉了{売却個数}個%ITEMNAME:選択した商品%
;販売個数を増加させる処理
ITEMSALES:選択した商品 += 売却個数
GOTO INPUT_LOOP2

;-------------------------------------------------
;在庫の変動
;SHOP_ITEM.erbにあるEVENTTURNENDから一日一回呼ばれる
;-------------------------------------------------
@酒屋の在庫
#DIM 在庫増加数
;宴会前日は酒が品薄に
IF FLAG:開始日 == DAY + 1
	ITEMSALES:濁酒 += RAND:(2 + FLAG:酒屋的売却率FLAG * 1)
	ITEMSALES:清酒     += RAND:(2 + FLAG:酒屋的売却率FLAG * 1)
	ITEMSALES:鬼殺酒   += RAND:(1 + FLAG:酒屋的売却率FLAG * 1)
	ITEMSALES:甜酒     += RAND:(3 + FLAG:酒屋的売却率FLAG * 2)
ELSE
	ITEMSALES:濁酒 += RAND:(3 + FLAG:酒屋的売却率FLAG * 3)
	ITEMSALES:清酒     += RAND:(3 + FLAG:酒屋的売却率FLAG * 2)
	ITEMSALES:鬼殺酒   += RAND:(2 + FLAG:酒屋的売却率FLAG * 1)
	ITEMSALES:甜酒     += RAND:(4 + FLAG:酒屋的売却率FLAG * 3)
ENDIF
;累積購入額に応じても在庫が増加
IF FLAG:酒屋里的累積購入額
	在庫増加数 = MAX(FLAG:酒屋里的累積購入額 / 100000 - 3, 0) + 1
	ITEMSALES:濁酒 += RAND:在庫増加数
	ITEMSALES:清酒     += RAND:在庫増加数
	ITEMSALES:鬼殺酒   += RAND:在庫増加数
	ITEMSALES:甜酒     += RAND:在庫増加数
ENDIF
;最大数制限
ITEMSALES:濁酒 = MIN(ITEMSALES:濁酒, 32)
ITEMSALES:清酒     = MIN(ITEMSALES:清酒, 24)
ITEMSALES:鬼殺酒   = MIN(ITEMSALES:鬼殺酒, 16)
ITEMSALES:甜酒     = MIN(ITEMSALES:甜酒, 32)
;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;酒屋
@COM_ABLE619
;一括管理
SIF GLOBAL_COMABLE(619)
	RETURN RESULT
;酒屋ではない場所
SIF !SAKE_SHOP(CFLAG:MASTER:現在位置)
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1

;酒屋を判定する関数
@SAKE_SHOP(ARG)
#FUNCTION
;酒屋
SIF ARG == GET_MAP_REPLACEMENT(211)
	RETURNF 1
SIF ARG == GET_MAP_REPLACEMENT(1012)
	RETURNF 1
RETURNF 0
