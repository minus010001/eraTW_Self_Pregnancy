;山の製材所
@COM624
#DIM 売値
#DIM ループ数
#DIM リザルト保管

ループ数 = 0

;開店時間・閉店時間
IF INRANGE(TIME:0, 540, 1080) == 0
	PRINTFORMW 好像是非营业时间
	RETURN -1
ENDIF

;初回イベント
IF FLAG:最後に製材所に訪れた日 == 0
	;初回イベント前は、原木持ちでなければ店にも入れない
	IF ITEM:原木 == 0
		PRINTFORMW 来这里也没什么用…先去伐点木材再来卖吧
		RETURN -1
	ENDIF
	;依頼の報酬など、伐採以外で原木を入手してしまった場合の退避用
	SIF !FLAG:原木種類
		FLAG:原木種類 = MAX(RAND:12, 1)
	
	PRINTFORMW 「初次见面、盟友」
	SIF FLAG:扮演 == 0
		PRINTFORMW 「我听过你的传闻哟ー。什么非常活跃的、奇妙的外来人什么的」
	SIF ABL:[[荷取]]:親密 >= 3
		PRINTFORMW 「好像也有河童的同伴受你照顾了呢。很好嘛……嗯哼嗯哼、善良又值得信頼的家伙」
	PRINTFORMW 「那么、有什么事呢？　诶、想要把原木变成製材、吗。可以哟ー、工钱大概是这样――」
	PRINTFORMW 滋滋、河童把説明用的操纵面板调了出来……
	PRINTFORMW 这个、不是比人间之里的行情便宜了很多吗？
	PRINTFORMW 「那是当然啦。这可是工程機械的全自動製材、总不至于落後于人力吧」
	PRINTFORMW 「……话是这么说、在寺庙的建立和神社的修復完成之后就没什么订单了、好不容易才開发出来的機械也闲置在这里了」
	PRINTFORMW 「对了。如果您今後也能多多光临的话、今天的这第一次就是免费服务了、意下如何？」
	PRINTFORMW …这个、不就是单纯的炫耀自己的機械吗、不过我还是把这句话给咽了回去、决定承蒙这个好意
	PRINTFORMW 「就这么決定了。那么请把原木放在这里把」
	PRINTFORMW 「那么、開关・ON！　呼呼呼、咆哮吧我的机魂！　见识一下河童技術力的精华吧！」
	PRINTFORMW 姑且不提迷之情緒高涨的河童、至少工程機械它是緻密有高速地完成了製材
	PRINTFORMW …………
	PRINTFORMW ……
	CALL LogRefine("製材所")
	PRINTL 
	PRINTFORMW 「完成！　怎么样！」
	PRINTFORMW 看着满脸得意的河童、只好坦率地表达了感謝
	PRINTFORMW 「嘿嘿、果然这个时候配这个眼鏡太好了。那么、今后也请多关照了ー」
	PRINTL 
	ループ数 = 1
ENDIF

$LOOP_SELECT

IF ループ数
	PRINTL 
	PRINTFORMW 「撒撒、下次也要把原木带过来哟」
ELSE
	;平常来店
	PRINTL 
	IF FLAG:最後に製材所に訪れた日 == DAY:0
		PRINTFORMW 「哦哟？　怎么了？」
	ELSE
		PRINTFORM 「呀、
		SELECTCASE TIME:0
			CASE 540 TO 600
				PRINTFORM 早上好
			CASE 601 TO 1020
				PRINTFORM 你好
			CASEELSE
				PRINTFORM 晚上好
		ENDSELECT
		PRINTFORM 盟友。　
		IF DAY:0 - FLAG:最後に製材所に訪れた日 > 28
			PRINTFORMW 最近好久没有过来露个脸了、还有点担心来着」
		ELSEIF DAY:0 - FLAG:最後に製材所に訪れた日 > 14
			PRINTFORMW 好久不见了」
		ELSE
			PRINTFORMW 今天有什么事呢？」
		ENDIF
	ENDIF
ENDIF

ループ数 ++
FLAG:最後に製材所に訪れた日 = DAY:0

PRINTFORML 　　所持金: %TOSTR(MONEY,"#,###")%　所持筹码: %TOSTR(MONEY:2,"#,###")%
DRAWLINE

CALL ASK_M("想要製材", ITEM:原木, "確認交易内容", 1, "从店里离開", 2)
CLEARLINE 7

SELECTCASE RESULT
	;製材してほしい
	CASE 0
		;依頼の報酬など、伐採以外で原木を入手してしまった場合の退避用
		SIF !FLAG:原木種類
			FLAG:原木種類 = MAX(RAND:12, 1)

		SETCOLOR 0x00FF00
		PRINTFORMD 「
		PRINTFORM %WOOD_NAME(FLAG:原木種類)%
		PRINTFORMD 的製材对吧。工钱是
		PRINTFORM \\%TOSTR(LUMBERING(FLAG:原木種類, "製材所"),"#,###")%
		PRINTFORMD 左右吧。啊、
		PRINTFORM %TOSTR(LUMBERING_CMCOST(FLAG:原木種類),"#,###")%筹码
		RESETCOLOR
		PRINTFORMW 的话也可以哟？」
		CALL ASK_M("\\来支付", MONEY >= LUMBERING(FLAG:原木種類, "製材所"), "用筹码来支付", MONEY:2 >= LUMBERING_CMCOST(FLAG:原木種類), "还是算了", 2)

		リザルト保管 = RESULT
		IF リザルト保管 == 0 || リザルト保管 == 1
			IF リザルト保管 == 0
				PRINTFORMW 　所持金: \\%TOSTR(MONEY,"#,###")% → \\%TOSTR(MONEY - LUMBERING(FLAG:原木種類, "製材所"),"#,###")%
				PRINT 　
				CALL PAY_MONEY_YEN(LUMBERING(FLAG:原木種類, "製材所"), 2)
			ELSE
				PRINTFORMW 　所持筹码: %TOSTR(MONEY:2,"#,###")% → %TOSTR(MONEY:2 - LUMBERING_CMCOST(FLAG:原木種類),"#,###")%
				PRINT 　
				CALL PAY_MONEY_CM(LUMBERING_CMCOST(FLAG:原木種類), 2)
			ENDIF
			PRINTL 
			PRINTDATAW
				DATAFORM 「谢谢惠顾。那么、開关・ON！　呼呼呼、咆哮吧我的机魂！　见识一下河童技術力的精华吧！」
				DATAFORM 「谢谢惠顾。把原木放在这里、接下来就是全自動作业了！」
				DATAFORM 「谢谢惠顾。那么、让你见识一下现代化工厂・自動化生产的神髄！」
			ENDDATA
			CALL LogRefine("製材所")
			PRINTL 
		ELSEIF リザルト保管 == 2
			PRINTFORMW 「唔、不接受砍价的交渉的ー」
			PRINTL 
			GOTO LOOP_SELECT
		ENDIF
	;取引内容を確認する
	CASE 1
		PRINTFORMW 「原木的製材、给你便宜点哟ー」
		PRINTFORMW 「想要卖出点原木和製材？　我们山里这边已经物資供給過剩了、去卖到人间之里的话比较好吧？」
		PRINTFORMW 「你问我会不会造家具？　唔ー。我造机器已经造腻了、不想再造个家具机器了」
		PRINTFORMW 「这种复杂的工作、我觉得还是去拜托人类的工匠比较好」
		PRINTFORMW 「如果真让我来做的话、可能会比较随心所欲、做出一个带有各种不需要的奇怪機能的河童式家具出来吧ー」
		PRINTFORMW 「…那也挺有意思的？　嘿嘿、嘛、那就有机会的话ー」
		PRINTL 
	CASE 2
		PRINTFORMW 「那么、回头见」
		PRINTL 
		TIME += ループ数 * 5
		RETURN 1
ENDSELECT
GOTO LOOP_SELECT

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;山の製材所
@COM_ABLE624
;一括管理
SIF GLOBAL_COMABLE(624)
	RETURN RESULT

IF !AT_HOME(MASTER)
	SIF CFLAG:MASTER:現在位置 != 玄武之澤
		RETURN 0
ELSE
	;山麓が拠点の場合は、河童の宣傳站
	SIF CFLAG:MASTER:現在位置 != 704
		RETURN 0
ENDIF
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1


;-------------------------------------------------
;製材コストを筹码に変換する関数
;ARG …FLAG:原木種類
;-------------------------------------------------
@LUMBERING_CMCOST(ARG)
#FUNCTION
#DIM 費用ベース

費用ベース = LUMBERING(ARG, 1, 0)

;元値の幅がそんなに広くないので、一律で
RETURNF 費用ベース / 300

;-------------------------------------------------
;製材結果の表示など
;-----------------------------------------------
@LogRefine(Craft)
#DIMS Craft
LOCAL = LUMBERING(FLAG:原木種類, Craft, "QYT")
SETCOLOR 0x00FF00
PRINTFORMW 　%WOOD_NAME(FLAG:原木種類, 1)%の製材: {LOG_SALES:WOOD_NUM(FLAG:原木種類)} → {LOG_SALES:WOOD_NUM(FLAG:原木種類) + LOCAL}
LOG_SALES:WOOD_NUM(FLAG:原木種類) += LOCAL
ITEM:原木 = 0
FLAG:原木種類 = 0
RESETCOLOR