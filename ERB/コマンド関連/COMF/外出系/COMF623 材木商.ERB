;木材商
@COM623
#DIM 製材判定
#DIM 売値
#DIM ループ数
#DIM リザルト保管
#DIM 候補番号
#DIM 候補個数
#DIM 売却個数
#DIM ProcessingCharge
ループ数 = 0

;開店時間・閉店時間
IF INRANGE(TIME:0, 540, 1080) == 0
	PRINTFORMW 好像是非营业时间
	RETURN -1
ENDIF


;初回イベント
IF FLAG:最後に木材商に訪れた日 == 0
	;初回イベント前は、原木持ちでなければ店にも入れない
	IF ITEM:原木 == 0
		PRINTFORMW 来这里也没什么用…先去伐点木材再来卖吧
		RETURN -1
	ENDIF
	;依頼の報酬など、伐採以外で原木を入手してしまった場合の退避用
	SIF !FLAG:原木種類
		FLAG:原木種類 = MAX(RAND:12, 1)
	PRINTFORMW 「欢迎光临、我是这里的店主。这位客人是来卖原木的吗？」
	PRINTFORMW 「那么、让我看一下您带来的原木吧」
	PRINTFORMW 「………」
	PRINTFORMW 「喔、品质很高啊……」
	PRINTFORMW 「……诶？　如果有这个意思的话、毎天都能带过来？　因为人间之里的外面要多少有多少？」
	PRINTFORMW 「…………」
	PRINTFORMW 「……看来这位客人不是泛泛之辈呀」
	PRINTFORMW 「好吧。那么伐採而来的原木、就由我買下来吧」
	PRINTFORMW 「当然、品质越高的価格就越高、反之则只能低价收购」
	PRINTFORMW 「如果需要制作家具之类的製材的话、也可以把原木代为加工成製材。当然、是要收取人工费的」
	PRINTFORMW 「那么、就让我検查一下这次的木头吧」
	;売値はここで設定、基本的に中途中止はしない
	売値 = WOOD_VALUE(FLAG:原木種類)
	SETCOLOR 0x00FF00
	PRINTFORMD 「
	PRINTFORM %WOOD_NAME(FLAG:原木種類)%
	PRINTFORMD 啊……那就以
	PRINTFORM \\{売値}
	RESETCOLOR
	PRINTFORMW 买下怎么样」
	PRINTFORMW 　　所持金: \\%TOSTR(MONEY,"#,###")% → \\%TOSTR(MONEY + 売値,"#,###")%
	PRINTFORMW 「那么这样就成交了。期待您下次带来的商品」
	PRINTFORMW 「哪有、敢进入妖精和妖怪嚣张跋扈的山林里伐木的勇士、一共也没有几人。我可是很期待的哟？」
	PRINTW 
	MONEY += 売値
	ITEM:原木 = 0
	FLAG:原木種類 = 0
	ループ数 = 1
ENDIF

$LOOP_SELECT

IF ループ数
	PRINTL 
	PRINTFORMW 「那么、还有什么别的事吗？」
ELSE
	;平常来店
	PRINTL 
	IF DAY:0 - FLAG:最後に木材商に訪れた日 > 28
		PRINTFORMW 「喔、是你啊。我还以为你已经从樵夫的工作里金斧洗手了」
	ELSEIF DAY:0 - FLAG:最後に木材商に訪れた日 > 14
		PRINTFORMW 「喔、好久不见了」
	ELSEIF FLAG:最後に木材商に訪れた日 == DAY:0
		PRINTFORMW 「什么、忘了东西？　真是个粗心的家伙啊」
	ELSE
		PRINTFORMW 「喔、今日有何贵干？」
	ENDIF
ENDIF

ループ数 ++
FLAG:最後に木材商に訪れた日 = DAY:0

PRINTFORML 　　所持金: \\%TOSTR(MONEY,"#,###")%
DRAWLINE

CALL ASK_M("原木売却・製材", ITEM:原木, "卖木材", ITEM:木材, "製材", 4, "確認交易内容", 5, "从店里离開", 6)
リザルト保管 = RESULT
ProcessingCharge = LUMBERING(FLAG:原木種類, "木材商")
CLEARLINE 12
SELECTCASE リザルト保管
	;原木を売る　製材してほしい
	CASE 0
		;依頼の報酬など、伐採以外で原木を入手してしまった場合の退避用
		SIF !FLAG:原木種類
			FLAG:原木種類 = MAX(RAND:12, 1)
		SETCOLOR 0x00FF00
		PRINTFORML 「%WOOD_NAME(FLAG:原木種類)%啊……」
		売値 = WOOD_VALUE(FLAG:原木種類)
		PRINTFORMD 「买下来的话价格是
		PRINTFORM \\%TOSTR(売値,"#,###")%
		PRINTFORMD 、製材的话加工费是
		PRINTFORM \\%TOSTR(ProcessingCharge,"#,###")%
		RESETCOLOR
		PRINTFORMW 」
		CALL ASK_M("卖了", 1, "製材", MONEY > ProcessingCharge,"还是算了", 1)
		SELECTCASE RESULT
			CASE 0
				PRINTFORMW 　所持金: \\ → \\%TOSTR(MONEY + 売値,"#,###")%
				PRINTFORMW 「谢谢惠顾。那么、期待您下次带来的商品」
				PRINTL 
				MONEY += 売値
			CASE 1
				PRINTL 
				PRINTFORMW 　所持金: \\%TOSTR(MONEY,"#,###")% → \\%TOSTR(MONEY - ProcessingCharge,"#,###")%
				PRINT 　
				CALL PAY_MONEY_YEN(ProcessingCharge, 2)
				PRINTL 
				PRINTFORMW 「好的。那么这边就開始作業了」
				CALL LogRefine("木材商")
			CASE 2
				PRINTFORMW 「嗯？还有什么别的打算吗？」
				PRINTL 
				GOTO LOOP_SELECT
		ENDSELECT
		ITEM:原木 = 0
		FLAG:原木種類 = 0
	;木材を売る
	CASE 1
		候補個数 = ITEM:木材
		候補番号 = 610
		PRINTFORM 「%ITEMNAME:候補番号%的话　\\%TOSTR(ITEMPRICE:候補番号 / 2,"#,###")%买下、怎么样？」
		CALL COM623_SELL(候補個数)
		SELECTCASE RESULT
			CASE 0
				PRINTL 
				GOTO LOOP_SELECT
			CASE 1 TO 候補個数
				CLEARLINE 2
				売却個数 = RESULT
				PRINTFORML 「那么就%ITEMNAME:候補番号%{売却個数}个、总计\\%TOSTR((ITEMPRICE:(候補番号) / 2 * 売却個数),"#,###")%」
				PRINTFORMW 　所持金: \\%TOSTR(MONEY,"#,###")% → \\%TOSTR(MONEY + (ITEMPRICE:(候補番号) / 2 * 売却個数),"#,###")%
				MONEY += (ITEMPRICE:(候補番号) / 2 * 売却個数)
				ITEM:候補番号 -= 売却個数
				PRINTL 
				GOTO LOOP_SELECT
		ENDSELECT
	;製材を売る
	CASE 2
		製材判定 = 0
		FOR LOCAL, 1, 50
			SIF LOG_SALES:LOCAL
				BREAK
			製材判定 ++
		NEXT
		IF 製材判定 > 48
			PRINTFORMW 未持有製材
			PRINTL 
			GOTO LOOP_SELECT
		ENDIF
		PRINTFORMW 「製材的采购吗。让我看看」
		DRAWLINE
		FOR LOCAL, 1, 50
			SIF LOG_SALES:LOCAL == 0
				CONTINUE
			PRINTFORM 　
			SIF LOCAL < 10
				PRINTFORM  
			PRINTFORM [{LOCAL}] 
			PRINTFORM %WOOD_NAME(LOCAL), 16, LEFT%
			PRINTFORM 製材数： 
			SELECTCASE LOG_SALES:LOCAL
				CASE IS < 10
					PRINTFORM     
				CASE IS < 100
					PRINTFORM    
				CASE IS < 1000
					PRINTFORM   
				CASE IS < 10000
					PRINTFORM  
			ENDSELECT
			PRINTFORML {LOG_SALES:LOCAL}
		NEXT
		PRINTFORML 　 [0] 放弃

		$INPUT_LOOP03
		INPUT
		
		リザルト保管 = RESULT
		IF リザルト保管 && LOG_SALES:リザルト保管
			売値 = WOOD_VALUE(リザルト保管, "製材")
			候補個数 = LOG_SALES:リザルト保管
			SETCOLOR 0x00FF00
			PRINTFORMD 「
			PRINTFORM %WOOD_NAME(リザルト保管)%
			PRINTFORMD 的製材的话、以
			PRINTFORM \\%TOSTR(売値,"#,###")%
			RESETCOLOR
			PRINTFORML 买下、怎么样？」
			CALL COM623_SELL(候補個数)
			SELECTCASE RESULT
				CASE 0
					PRINTL 
					GOTO LOOP_SELECT
				CASE 1 TO 候補個数
					CLEARLINE 2
					売却個数 = RESULT
					PRINTFORML 「那么%WOOD_NAME(リザルト保管)%的製材{売却個数}个、总计\\%TOSTR(売値 * 売却個数,"#,###")%」
					PRINTFORMW 　所持金: \\%TOSTR(MONEY,"#,###")% → \\%TOSTR(MONEY + (売値 * 売却個数),"#,###")%
					MONEY += 売値 * 売却個数
					LOG_SALES:リザルト保管 -= 売却個数
					PRINTL 
			ENDSELECT
		ELSEIF リザルト保管 == 0
			PRINTL 
			GOTO LOOP_SELECT
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP03
		ENDIF
	;取引内容を確認する
	CASE 3
		PRINTFORMW 「正在收购原木、竹材、製材」
		PRINTFORMW 「如果没有的话、端材・木材之類的也会收购。嘛、当然也不值几个钱就是了」
		PRINTFORMW 「如果如果的话我们还承包原木的製材。但是、会根据原木的種類・大小收取加工費」
		PRINTFORMW 「顺便一提、先在这里加工成製材、再在这里把製材卖掉就是纯粹的浪费钱。如果是打算义务捐赠的话倒是非常歓迎」
		PRINTFORMW 「……什么、想要家具？　那不是我们的业务范围。找个家具师傅吧」
		PRINTL 
	CASE 4
		PRINTFORMW 「那么、回头再见」
		PRINTL 
		TIME += ループ数 * 5
		RETURN -1
ENDSELECT
GOTO LOOP_SELECT

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;木材商
@COM_ABLE623
;一括管理
SIF GLOBAL_COMABLE(623)
	RETURN RESULT

IF !AT_HOME(MASTER)
	SIF CFLAG:MASTER:現在位置 != 商家町
		RETURN 0
ELSE
	;西大街
	SIF CFLAG:MASTER:現在位置 != 206
		RETURN 0
ENDIF
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1


;-------------------------------------------------
;原木・製材の売却価格を設定する関数
;何回か見る場合もあるので、その日の中では固定値出力
;ARG:0		FLAG:原木種類
;Type		売却価格  省略時は原木
;-------------------------------------------------
@WOOD_VALUE(ARG:0, Type)
#FUNCTION
#DIM Species
#DIM 売値ベース
#DIM 口八丁補正
#DIMS Type

Species = ARG % 100

IF Type == "製材"
	売値ベース =WoodValue_Plank:Species
ELSE
	売値ベース = WoodValue_Log:Species
ENDIF

;日替わり変動
売値ベース += FLAG:每日变更事件 * 2

;口八丁補正  日期で乱数を固定
RANDOMIZE DAY:0
口八丁補正 = RAND:(1 + ABL:MASTER:話術技能 * 5 + ABL:MASTER:教養)

SELECTCASE 口八丁補正
	CASE 0 TO 10

	CASE 11 TO 20
		TIMES 売値ベース, 1.05
	CASE 21 TO 30
		TIMES 売値ベース, 1.07
	CASEELSE
		TIMES 売値ベース, 1.10
ENDSELECT

;原木の時のみ、巨木・大木補正
IF Type != "製材"
	IF ARG > 200
		売値ベース *= 5
	ELSEIF ARG > 100
		売値ベース *= 3
	ENDIF
ENDIF
RETURNF 売値ベース


;-------------------------------------------------
;FLAG:原木種類から、巨木・大木を無視して木材種番号を取り出す関数
;ARG…FLAG:原木種類
;-------------------------------------------------
@WOOD_NUM(ARG)
#FUNCTION

RETURNF ARG % 100

;-------------------------------------------------
;製材コスト、製材数を割り出す関数
;複数回選べるので、固定値を出す
;ARG	FLAG:原木種類
;Type	価格ないし製材数
;-------------------------------------------------
@LUMBERING(ARG, Craft, Type)
#FUNCTION
#DIM 費用ベース
#DIMS Craft
#DIMS Type

IF Type == "QTY"
	RETURNF 1 + (ARG / 100) * 2
ENDIF

費用ベース = WOOD_VALUE(ARG)

SELECTCASE Craft
	CASE "木材商"
		RETURNF 費用ベース / 2
	CASE "製材所"
		RETURNF 費用ベース / 4
ENDSELECT

RETURNF 0

@COM623_SELL(候補個数)
#DIM 候補個数
SETCOLOR 0x777777
PRINTFORML 　要卖多少個？(输入1～{候補個数}、0为返回)
RESETCOLOR
PRINTL 
PRINT 　
PRINTBUTTON "[0] 放弃", 0
PRINT     
PRINTBUTTON "[1] 1個", 1
PRINT     
IF 候補個数 >= 5
	PRINTBUTTON "[5] 5個", 5
	PRINT     
ENDIF
IF 候補個数 >= 10
	PRINTBUTTON "[10] 10個", 10
	PRINT    
ENDIF
INPUT
SIF RESULT > 候補個数
	RETURN 0
SIF RESULT <= 0
	RETURN 0
RETURN RESULT