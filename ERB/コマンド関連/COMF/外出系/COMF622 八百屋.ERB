@COM622
#DIM 買うもの
;購入条件設定
FOR LOCAL, 0, 1000
	CALL ITEMDATA(LOCAL, "SHOP:八百屋")
	IF RESULT
		CALL ITEMDATA(LOCAL, "SALES")
		ITEMSALES:LOCAL = RESULT
	ENDIF
NEXT
DRAWLINE
CALL LIST_ITEM("野菜")
DRAWLINE
PRINTFORML 请选择要購买的道具（所持金:%金額表示(MONEY)%）
DRAWLINE
CALL SHOW_SHOPPING("八百屋")
DRAWLINE
PRINTL [999] - 返回
INPUT
IF RESULT == 999
	TIME += 10
	RETURN -1
ELSE
	買うもの = RESULT
	CALL ITEMDATA(買うもの, "SHOP:八百屋")
	IF RESULT && ITEMSALES:買うもの >= 1 && ITEM:買うもの < 所持数制限
		CALL ITEMDATA(買うもの, "購入")
		SIF !RESULT
			RESTART
	ELSE
		RESTART
	ENDIF
ENDIF
TIME += 10

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;八百屋
@COM_ABLE622
;一括管理
SIF GLOBAL_COMABLE(622)
	RETURN RESULT
;八百屋ではない場所
SIF !YASAI_SHOP(CFLAG:MASTER:現在位置)
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1

@YASAI_SHOP(ARG)
#FUNCTION
;八百屋
SIF ARG == GET_MAP_REPLACEMENT(227)
	RETURNF 1
SIF ARG == GET_MAP_REPLACEMENT(1013)
	RETURNF 1
RETURNF 0
