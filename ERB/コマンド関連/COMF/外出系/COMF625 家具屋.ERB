;家具屋
@COM625
#DIM 売値
#DIM ループ数
#DIM リザルト
#DIM 資材保管
#DIM 削除行数
#DIM MaterialQTY
#DIMS DiscountedPrice

ループ数 = 0

;開店時間・閉店時間
IF INRANGE(TIME:0, 480, 1020) == 0
	PRINTFORMW 好像是非营业时间
	RETURN -1
ENDIF

;初回イベント
IF FLAG:最後に家具屋に訪れた日 == 0
	PRINTL 
	PRINTFORMW 写着家具屋的看板映入眼帘、进入玄关…
	PRINTFORMW 「……什么啊、你那副憨脸。啊？」
	PRINTFORMW 「啊？　是啊、这里是家具屋。什么？　没有摆出来卖的东西？」
	PRINTFORMW 「蠢货玩意。不管是衣柜还是什么别的东西、我这里都是接到订单了之后再開始干活的。现在就只是单纯地闲着摸鱼而已」
	PRINTFORMW 「……所以呢？　你有什么想要的东西吗」
	PRINTFORMW 「……啊？　看了実物之后再決定？」
	PRINTFORMW 「只是过来口嗨两句的话你还是给我回去吧……虽然想这么说、没办法。我看看啊、上一个工作的账本――」
	PRINTFORMW 　这个男人、砰、这么甩出来的账本上面、用一丝不苟的字体写着订单的事项和費用
	PRINTFORMW 　……呜哇、太贵了吧！？
	PRINTFORMW 「这是专业的实力和严谨的态度保证出来的上品、根本不可能是便宜的妖艳贱货好吧」
	PRINTFORMW 　价格上能不能再有点别的什么让步、这么问了问…
	PRINTFORMW 「你只要坐在金块上面、然后一边数数一边往这扔金条、从一数到十的话我就接受你放的屁」
	PRINTFORMW 「……话是这么说。只要能准备好『製材』的材料的话、我就把相应的原料价格给你扣掉」
	PRINTFORMW 　那样的话、嗯、大概价格会是……呼、这样的话至少还是付得起的水平、总算松了口气
	PRINTFORMW 「哼。嘛、准备好了再过来下订单吧。我会给你做出最好的逸品的」
	PRINTL 
	ループ数 = 1
ENDIF

SIF FLAG:作成中家具
	FLAG:家具作成残り = MAX(FLAG:家具作成残り-((DAY:0 - FLAG:最後に家具屋に訪れた日)*2),0)

$LOOP_SELECT

IF ループ数
	PRINTL 
	IF FLAG:家具作成残り
		PRINTFORMW 「让開让開、爷可是很忙的！」
	ELSE
		PRINTFORMW 「什么啊、还有什么别的事吗」
	ENDIF
ELSE
	;平常来店
	PRINTL 
	;家具発注中
	IF FLAG:家具作成残り >= 14
		PRINTFORMW 「什么嘛、是你啊。猴什么急、图纸都还没開始画呢」
	ELSEIF FLAG:家具作成残り >= 9
		PRINTFORMW 「什么嘛、是你啊。如果是问定的家具的话、还得再摸段时间」
	ELSEIF FLAG:家具作成残り >= 5
		PRINTFORMW 「什么嘛、是你啊。定的家具已经摸了不少了、放心吧」
	ELSEIF FLAG:家具作成残り >= 2
		PRINTFORMW 「喔、定的家具就快做好了！」
	ELSEIF FLAG:作成中家具 && FLAG:家具作成残り == 0
		;完成日
		IF FLAG:作成中家具 == 902
			PRINTFORMW 「噢哟、终于来了。　诶？　明明是在旁边看着風呂完成的、为什么还要特地来店里一趟？」
			PRINTFORMW 「因为要交付确认手续书之类的乱七八糟的东西。这种东西不弄好我不舒服、万一留下什么隐患还非常恶心」
		ELSE
			PRINTFORMW 「哟。久等了。这就是你定的东西」
		ENDIF
		IF FLAG:作成中家具 == 907
			PRINTFORMW 「咿呀……嘛、毕竟我也不是做扫帚的专家、还是稍微费了点功夫的」
		ELSEIF ITEM:(FLAG:作成中家具) == 2
			PRINTFORMW 「咿呀……久违地做了个让自己満意的工作。実在是、尽了職人的冥利啦」
		ELSEIF ITEM:(FLAG:作成中家具) == 1 
			PRINTFORMW 「呼、已经把想要在意的地方全都做好了……嘛、大概就是这个样子了吧」
			PRINTFORMW 「虽然作为制作方来说是很満足了、但是可能和使用方的満足有些微妙的区别、不过两者都能兼顾才叫職人就是了」
		ELSE
			PRINTFORMW 「如果有想要的家具了再叫我。啊、如果对现在正在用的家具不満意的话也叫我」
		ENDIF
		PRINTL 
		SETCOLOR 0x00FF00
		PRINTFORM 获得了%ITEMNAME:(FLAG:作成中家具)%
		IF ITEM:(FLAG:作成中家具) == 0
			PRINTFORMW ！
		ELSE
			PRINTFORMW 的改进版本、用起来更舒服了！
		ENDIF
		RESETCOLOR
		PRINTL 
		ITEM:(FLAG:作成中家具) ++
		FLAG:作成中家具 = 0
	ELSE
		IF DAY:0 - FLAG:最後に家具屋に訪れた日 > 28
			PRINTFORMW 「什么嘛、是你啊。终于打算来下单了？」
		ELSEIF FLAG:最後に家具屋に訪れた日 == DAY:0
			PRINTFORMW 「什么嘛。没有搭理你的功………啊、我是很闲怎么了、有意见吗」
		ELSE
			PRINTFORMW 「什么嘛、是你啊。有想要的家具吗？」
		ENDIF
	ENDIF
ENDIF

ループ数 ++
FLAG:最後に家具屋に訪れた日 = DAY:0

PRINTFORM 　　所持金: %TOSTR(MONEY,"#,###")%
IF FLAG:作成中家具
	PRINTFORML 　　%ITEMNAME:(FLAG:作成中家具)%正在制作中　完工：还有{FLAG:家具作成残り}日
ELSE
	PRINTL 
ENDIF
DRAWLINE

CALL ASK_M("想要制作家具", !FLAG:作成中家具, "確認交易内容", 1, "从店里离開", 2)

SELECTCASE RESULT
	;家具を作ってほしい
	CASE 0
		PRINTFORMW 「如果是说你需要的家具的话……嘛、大概是这样？」

		$INPUT_LOOP01
		;列表の呼び出し
		削除行数 = 0
		FOR LOCAL, 902, 908
			削除行数 ++
			IF ITEM:LOCAL == 3
				PRINTPLAINFORM 　[{LOCAL}]
				LOCALS = %ITEMNAME:LOCAL%Lv{ITEM:LOCAL}
				PRINTPLAINFORM %LOCALS, 21, LEFT%
			ELSE
				PRINTFORM 　
				PRINTFORM [{LOCAL}]
				IF ITEM:LOCAL == 0
					;未所持
					PRINTFORM %ITEMNAME:LOCAL, 21, LEFT%
				ELSE
					;所持
					LOCALS = %ITEMNAME:LOCAL%Lv{ITEM:LOCAL}
					PRINTFORM %LOCALS, 21, LEFT%
				ENDIF
			ENDIF
			CALL FURNITURE_EXPLANATION(LOCAL)
		NEXT
		PRINTFORML 　[999]果然还是算了
		削除行数 ++
		
		INPUT
		
		リザルト = RESULT
		MaterialQTY = FURNITURE(リザルト, "軽減上限", ITEM:リザルト)
		SELECTCASE リザルト
			CASE 902 TO 907
				;Lv3の時は選択できない
				IF ITEM:リザルト == 3
					CLEARLINE 削除行数 + 1
					GOTO INPUT_LOOP01
				ENDIF

				;注文家具の請求を作成
				DRAWLINE
				PRINTFORML %ITEMNAME:リザルト%的\@ ITEM:リザルト == 0? 订购# 改修\@
				;所持製材量での値引きの表記
				;Lv1→2の時は半額
				IF ITEM:リザルト == 1
					PRINTFORM 　費用　　　　：\\%TOSTR((ITEMPRICE:リザルト / 2),"#,###")% → 
				ELSE
					PRINTFORM 　費用　　　　：\\%TOSTR(ITEMPRICE:リザルト,"#,###")%  → 
				ENDIF
				DiscountedPrice = %TOSTR(FURNITURE(リザルト, 0, ITEM:リザルト),"#,###")%
				
				IF FURNITURE(リザルト, 0, ITEM:リザルト) == FURNITURE(リザルト, "最低額", ITEM:リザルト)
					SETCOLOR C_RED
				ELSEIF ITEM:リザルト == 1 && FURNITURE(リザルト, 0, ITEM:リザルト) < ITEMPRICE:リザルト / 2
					SETCOLOR C_YELLOW
				ELSEIF FURNITURE(リザルト, 0, ITEM:リザルト) < ITEMPRICE:リザルト
					SETCOLOR C_YELLOW
				ENDIF
				PRINTFORML \\%DiscountedPrice% \@ FURNITURE(リザルト, 0, ITEM:リザルト) == FURNITURE(リザルト, "最低額", ITEM:リザルト)? 最安値！# \@
				RESETCOLOR

				;価格軽減に必要な資材量と現在所持量の比較
				PRINTFORM 　価格軽減条件：准备%WOOD_NAME(FURNITURE(リザルト, "資材番号"))%的製材
				PRINTFORM 准备好{MaterialQTY}
				SIF LOG_SALES:FURNITURE(リザルト, "資材番号") >= MaterialQTY
					SETCOLOR C_RED
				PRINTFORML （{LOG_SALES:FURNITURE(リザルト, "資材番号")}／{MaterialQTY}）
				RESETCOLOR
			CASE 999
				PRINTL 
				GOTO LOOP_SELECT
			CASEELSE
				CLEARLINE 1
				GOTO INPUT_LOOP01
		ENDSELECT
		PRINTL 
		IF ITEM:リザルト == 0
			CALL ASK_M("做这个家具", MONEY >= FURNITURE(リザルト, 0), "算了", 1)
		ELSE
			CALL ASK_M("改修这个家具", MONEY >= FURNITURE(リザルト, 0, ITEM:リザルト) && ITEM:リザルト <= 2, "算了", 1)
		ENDIF
		
		IF RESULT
			CLEARLINE 9 + 削除行数
			GOTO INPUT_LOOP01
		ELSE
			;費用の支支付
			PRINTFORMW 　所持金: \\%TOSTR(MONEY,"#,###")% → \\%TOSTR(MONEY - FURNITURE(リザルト, 0, ITEM:リザルト),"#,###")%
			PRINTFORM 　
			CALL PAY_MONEY_YEN(FURNITURE(リザルト, 0, ITEM:リザルト))

			;製材等の消費  軽減上限量以上に持っていれば上限量分減らし、上限に達していなければ全部消費
			;現在的製材の量を退避してから計算
			資材保管 = LOG_SALES:FURNITURE(リザルト, "資材番号")
			LOG_SALES:FURNITURE(リザルト, "資材番号") = MAX(0, LOG_SALES:FURNITURE(リザルト, "資材番号") - MaterialQTY)
			;文章部分
			PRINTFORMW 　%WOOD_NAME(FURNITURE(リザルト, "資材番号"))%の製材：{資材保管} → {LOG_SALES:FURNITURE(リザルト, "資材番号")}
			PRINTL 
			IF ITEM:リザルト
				PRINTFORMW 「什么嘛、对之前的家具不満吗？　那正好。{FURNITURE(リザルト, "期間", ITEM:リザルト)}天後再到店里来、会让你大吃一惊的」
			ELSE
				PRINTFORMW 「好、那么契約成立了。完工是在{FURNITURE(リザルト, "期間", ITEM:リザルト)}天後、那时候再到店里来」
			ENDIF
			FLAG:家具作成残り = FURNITURE(リザルト, "期間", ITEM:リザルト)
			FLAG:作成中家具 = リザルト
		ENDIF
	;取引内容を確認する
	CASE 1
		PRINTFORMW 「啊？　想確認一下？」
		PRINTFORMW 「这里是家具屋、除了做家具还能干什么」
		PRINTFORMW 「准备好材料的製材的话、会给你点优惠」
		PRINTFORMW 「虽然是废话、下完订单之后把家具需要一定的時間」
		PRINTFORMW 「顺便一提、在制作期間因为要专心、所以不接受其他的订单」
		PRINTFORMW 「啊、如果对做好的家具不満的话、直接给我说。虽然会收点钱、但是会把它改进得更好的」
		PRINTL 
	CASE 2
		PRINTFORMW 「没事的话就拜拜了您呐」
		PRINTL 
		TIME += ループ数 * 5
		RETURN -1

ENDSELECT
GOTO LOOP_SELECT

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;家具屋
@COM_ABLE625
;一括管理
SIF GLOBAL_COMABLE(625)
	RETURN RESULT

IF !AT_HOME(MASTER)
	SIF CFLAG:MASTER:現在位置 != 長屋街
		RETURN 0
ELSE
	;長屋前
	SIF CFLAG:MASTER:現在位置 != 214
		RETURN 0
ENDIF
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1


;-------------------------------------------------
;注文できる家具列表に付随する説明文
;ARG 家具の道具番号
;-------------------------------------------------
@FURNITURE_EXPLANATION(ARG)

IF ITEM:ARG
	PRINTFORM 設置場所：
	SELECTCASE ARG
		CASE 902
			LOCALS = %NAME_FROM_PLACE(NEAR_BATH(1))%
		CASE 904, 907
			LOCALS = %CALLNAME:MASTER%带在身上
		CASEELSE
			LOCALS = %CALLNAME:MASTER%的私室
	ENDSELECT
	PRINTFORMD %LOCALS, 26, LEFT%
ELSE
	PRINTFORM 　　　　　　　　　　　　　　　　　　
ENDIF

SIF ITEM:ARG == 0
	SETCOLOR C_GRAY

SELECTCASE ARG
	CASE 902
		PRINTFORM …在設置的場所入浴的话可以増加体力・気力回復
	CASE 903
		PRINTFORM …増加睡眠时的体力・気力・精力回復
	CASE 904
		PRINTFORM …能做出更好的料理
	CASE 905
		PRINTFORM …提高在私室里勉強或者調合的効率
	CASE 906
		PRINTFORM …増加私室里休憩的体力・気力・精力回復
	CASE 907
		PRINTFORM …提升掃除的効率
	CASEELSE
		PRINT 
ENDSELECT
IF ITEM:ARG == 3
	SETCOLOR C_ORANGE
	PRINTFORML ▲▲
ELSEIF ITEM:ARG == 2
	SETCOLOR C_YELLOW
	PRINTFORML ▲
ELSE
	PRINTL 
ENDIF
RESETCOLOR

;-------------------------------------------------
;家具注文で使用する各種数値をまとめた関数
;現状は902～907
;F_ID …家具の道具番号
;ARGS 0= 補正価格(省略時)  1= 資材による軽減上限数
;      2= 家具に対応する資材番号  3= 軽減最低額  
;      4= 発注後の作成期間   を返す
;EnhanceTime 0= 初発注(省略時)  1= 強化１  2= 強化２
;        主にITEM:家具番号を入れて利用
;-------------------------------------------------
@FURNITURE(F_ID, ARGS, EnhanceTime = 0)
#FUNCTION
#DIM F_ID
#DIM EnhanceTime
#DIM DiscountRate
#DIM MaxDiscount
#DIM 資材番号
#DIM 軽減上限
#DIM 最低額
#DIM 補正価格
#DIM 期間

;手持製材との調整価格を表記
SELECTCASE F_ID
	;木浴缸
	CASE 902
		資材番号 = 2;ヒノキ
		軽減上限 = 50
		最低額 = 250000
		期間 = 15
	;桐木床
	CASE 903
		資材番号 = 8;キリ
		軽減上限 = 30
		最低額 = 200000
		期間 = 10
	;银杏案板
	CASE 904
		資材番号 = 6;イチョウ
		軽減上限 = 10
		最低額 = 50000
		期間 = 5
	;榉木桌椅
	CASE 905
		資材番号 = 7;ケヤキ
		軽減上限 = 30
		最低額 = 200000
		期間 = 10
	;栎木沙发
	CASE 906
		資材番号 = 9;ナラ
		軽減上限 = 30
		最低額 = 200000
		期間 = 10
	;特制竹箒
	CASE 907
		資材番号 = 12;竹
		軽減上限 = 30
		最低額 = 30000
		期間 = 10
ENDSELECT

;Lv1→Lv2時は半分  Lv2→Lv3時は最初と同じで
MaxDiscount = ITEMPRICE:F_ID - 最低額
IF EnhanceTime == 1
	軽減上限 /= 2
	最低額 /= 2
	期間 /= 2
	MaxDiscount /= 2
ENDIF

SIF ARGS == "資材番号"
	RETURNF 資材番号
SIF ARGS == "軽減上限"
	RETURNF 軽減上限
SIF ARGS == "最低額"
	RETURNF 最低額
SIF ARGS == "期間"
	RETURNF 期間
	
DiscountRate = 100 * MIN(LOG_SALES:資材番号, 軽減上限) / 軽減上限

IF EnhanceTime == 1
	補正価格 = (ITEMPRICE:F_ID / 2) - MaxDiscount * DiscountRate / 100
ELSE
	補正価格 = ITEMPRICE:F_ID - MaxDiscount * DiscountRate / 100
ENDIF
RETURNF 補正価格