;-------------------------------------------------
;吸乳头
;愛撫系指令、レベル1
;-------------------------------------------------
@COM11
;着脱の処理
TEQUIP:吸乳头 = !TEQUIP:吸乳头

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽
TCVAR:PLAYER:108 = 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:快Ｂ += ABL:PLAYER:舌 * 20 + 20

ELSE
	DOWNBASE:体力 += 5
	DOWNBASE:気力 += 50

	SOURCE:快Ｂ = 20
	SOURCE:情愛 = 30
	SOURCE:露出 = 20
	SOURCE:不潔 = 30
	SOURCE:反感 = 100

	SOURCE:快Ｂ += ABL:PLAYER:舌 * 20
	SOURCE:情愛 += ABL:PLAYER:舌 * 5

	SIF ABL:親密 >= 7
		SOURCE:情愛 += (ABL:親密 * 20)

	;調教者のTALENT:舌頭靈活をみる
	SIF TALENT:PLAYER:舌頭靈活
		TIMES SOURCE:快Ｂ , 1.50

	;調教者のTALENT:猫舌をみる
	SIF TALENT:PLAYER:猫舌
		TIMES SOURCE:快Ｂ , 0.75
	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 100
ENDIF
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＢ⇔調教者の口的污垢が移動
STAIN:5 |= STAIN:PLAYER:0
STAIN:PLAYER:0 |= STAIN:5
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 5
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 5
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 5
	ENDIF
ENDIF
;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
SIF TALENT:PLAYER:猫舌
	EXP:PLAYER:嗜虐快楽経験 += 1
EXP:PLAYER:口淫経験 ++
RETURN 1
;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM11(ARG)

SELECTCASE SELECTCOM
	CASE 0 TO 2,4,8,20,47 TO 49,60 TO 66,68,70,71,81 TO 85,91 TO 94,105,120 TO 126,140,410
		PRINTL ＜吸乳头解除＞
		SIF TEQUIP:吸乳头
			TEQUIP:吸乳头 = 0
	CASEELSE
;吸乳头中
PRINTL ＜吸乳头中＞
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:快Ｂ += (ABL:PLAYER:舌 + 1) * 10
ELSE
	DOWNBASE:ARG:体力 += 5
	DOWNBASE:ARG:気力 += 20
	DOWNBASE:PLAYER:体力 += 5
	DOWNBASE:PLAYER:気力 += 10
	SOURCE:快Ｂ += (ABL:PLAYER:舌 + 1) * 10
	SOURCE:露出 = 20
	SOURCE:不潔 = 30
	SOURCE:反感 = 50
	SOURCE:情愛 += ABL:PLAYER:舌 * 5

	SIF ABL:親密 >= 5
		SOURCE:情愛 += (ABL:親密 * 3)
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＢ⇔調教者の口的污垢が移動
STAIN:5 |= STAIN:PLAYER:0
STAIN:PLAYER:0 |= STAIN:5
;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
SIF TALENT:PLAYER:猫舌
	EXP:PLAYER:嗜虐快楽経験 += 1
EXP:PLAYER:口淫経験 ++
;EXP:乳頭 ++
ENDSELECT
RETURN 1

@MESSAGE_EQUIP_COM11

IF TFLAG:50 == 19
	;授乳PLAYの時は表示しない
ELSE
	PRINT 舌和唇上
	SIF CFLAG:PLAYER:现在忙得抽不出手 == 1
		PRINTFORM %CALLNAME:TARGET%の
	IF EX:Ｂ絶頂 * 10000 + PALAM:快Ｂ + PALAM:欲情 >= 10000 && ABL:Ｂ感覚 <= 5
		PRINTFORM %TEXTR("乳辱への期待に打ち震えて/乳嬲りを今か今かと待ち望んで/")%
	ENDIF
	PRINTFORM %NIPPLE_DESCRIPTION(TARGET,1)%
	IF RAND:2
		;欲情のPALAMLV4以上
		IF PALAM:欲情 >= PALAMLV:4 && RAND:3
			PRINT 的咯哩咯哩的
		ELSEIF EX:Ｂ絶頂 * 10000 + PALAM:快Ｂ + PALAM:欲情 >= 10000 && RAND:2
			PRINT 的软糖般的
		ELSE
			PRINT 的柔软的
		ENDIF
		PRINT 触感品味着的同时、
	ELSE
		PRINT 触感反复舔舐的同时、
	ENDIF
ENDIF

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE11
;吸乳头実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(11)
	RETURN RESULT
;上半身着衣
SIF (TEQUIP:1 & 2) && TEQUIP:3 == 0
	RETURN 0
;搾乳器使用中は不可
SIF TEQUIP:17
	RETURN 0
;乳頭夾使用中は不可
SIF TEQUIP:16
	RETURN 0
;乳房電極装着中はダメ
SIF TEQUIP:24
	RETURN 0
SIF TEQUIP:体位 == 5 || TEQUIP:体位 == 2 || TEQUIP:体位 == 3
	RETURN 0
;你の口使用中
SIF MASTER_POSE(0,4) && MASTER_POSE(0,4) != TARGET
	RETURN 0
IF IN_ROOM_NUMBER(CFLAG:PLAYER:現在位置) > 2
	LOCAL:1 = 0
	FOR LOCAL,1,CHARANUM
		SIF TEQUIP:LOCAL:吸乳头 && LOCAL != TARGET
			LOCAL:1 ++
	NEXT
	SIF LOCAL:1
	RETURN 0
ENDIF

RETURN 1