;-------------------------------------------------
;肛門愛撫
;愛撫系指令、レベル2
;-------------------------------------------------
@COM5
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽
TCVAR:PLAYER:108 = 1

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛門 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 12
;強度
TCVAR:TARGET:4 = 700

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 50
	SOURCE:快Ａ = 100
	SOURCE:苦痛 = 50

ELSE

	DOWNBASE:体力 += 40
	DOWNBASE:気力 += 100

	SOURCE:快Ａ = 100
	SOURCE:苦痛 = 100
	SOURCE:屈従 = 100
	SOURCE:逸脱 = 80
	SOURCE:反感 = 60

	SIF TALENT:PLAYER:靈活手指
		SOURCE:快Ａ += 20
	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 100
ENDIF
;Aへの苦痛は先に処理しておく
CALL PAIN_CHECK_A(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＡ⇔調教者の指的污垢が移動
STAIN:PLAYER:1 |= STAIN:4
STAIN:PLAYER:1 |= STAIN:7
STAIN:4 |= STAIN:PLAYER:1
STAIN:7 |= STAIN:PLAYER:1
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 2
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 2
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 2
	ENDIF
ENDIF
EXP:PLAYER:手淫経験 ++
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE5
;肛門愛撫実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(5)
	RETURN RESULT
;毒人形だと肛門がついてないのでダメ
SIF TALENT:196 == 99
	RETURN 0
;時間停止中は許可
IF FLAG:70 != 1
	;潤滑不足で、従順3以下・百合属性3以下のASSIの場合肛門愛撫はできない（施虐狂ならＯＫ）
	IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
		SIF (ABL:ASSI:従順 <= 3 || ABL:ASSI:百合属性 <= 3) && TALENT:ASSI:施虐狂 == 0
			RETURN 0
	ENDIF
ENDIF

;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
RETURN 1

