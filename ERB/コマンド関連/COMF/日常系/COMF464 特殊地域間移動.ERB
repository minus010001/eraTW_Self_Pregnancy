;-------------------------------------------------
;特殊区域移动
;日常系指令、レベル1
;-------------------------------------------------
@COM464
#DIM FEE
#DIM 目的場所
#DIM 所要時間
VARSET LOCAL
FEE = 0
FOR LOCAL,1,CHARANUM
	IF CFLAG:(LOCAL):同行
		LOCAL:1 ++
		SIF CFLAG:LOCAL:衰弱 || BASE:LOCAL:気力 == 0
			LOCAL:2 ++
		SIF !CHARA_HOLIDAY(LOCAL) && BASE:LOCAL:工作量
			LOCAL:3 ++
	ENDIF
NEXT
IF NEMUKE() >= 720
	PRINTL 在睡意来袭时还是别外出比较好吧
	RETURN -1
ELSEIF LOCAL:1 > 1
	PRINTL 不能带二人以上外出
	RETURN -1
ELSEIF LOCAL:2 || BASE:MASTER:気力 == 0
	PRINTFORML 因为身体不适所以无法外出\@ BASE:MASTER:気力 == 0 ? （気力為零） # （同行者身体不适）\@
	RETURN -1
ELSEIF LOCAL:3 && FESTIVAL() == ""
	PRINTL 因为同行者处于工作日,所以请注意分配时间...
;(天候パッチ)悪天候時も外出可能に
;ELSEIF TIME:5 == 9 || TIME:5 == 5
;	PRINTL 因为天气恶劣所以无法外出
;	RETURN -1
ELSEIF TALENT:MASTER:100 < -4 
	PRINTL 用这様的身体外出是很危険的
	RETURN -1
ENDIF
SELECTCASE CFLAG:MASTER:現在位置
	CASE 205, 210, 803, 820
		IF !INRANGE(TIME, 540, 1020)
			PRINTFORML 営業時間外だ
			RETURN -1
		ENDIF
		;(天候パッチ)悪天候時は運行見合わせ
		IF GET_LOCATION_AREA_TYPHOON() == 1
			PRINTFORML 台風接近により運行を見合わせているようだ
			RETURN -1
		ENDIF
		IF WIND_VELOCITY >= 1
			PRINTFORML 強風により運行を見合わせているようだ
			RETURN -1
		ENDIF
		SELECTCASE TIME:5
			CASE 14, 15
				PRINTFORML 大雨により運行を見合わせているようだ
				RETURN -1
			CASE 9
				PRINTFORML 大雪により運行を見合わせているようだ
				RETURN -1
		ENDSELECT
		;守矢角色たちと同行時は敷設者割引運賃
		IF CFLAG:31:同行 || CFLAG:32:同行 || CFLAG:33:同行
			FEE = 1
		ELSEIF CFLAG:MASTER:同行
			FEE = 10
		ELSE
			FEE = 5
		ENDIF
		;相手が鬼か天狗だと愛想笑い／敷設者にも敬意を払う
		IF GROUPMATCH(TARGET, 10, 29, 31, 32, 33, 42, 64, 65) && CFLAG:TARGET:同行
			;ガワを山の河童(16)or里の人类(12)に決める
			LOCAL = 12 + ((CFLAG:MASTER:現在位置 / 100 == 8) * 4)
			CALL SPTALK, 0, "笑顔", LOCAL, @"「これはこれは%CALLNAME:TARGET%さん、ご利用ですか？」",1
			PRINTFORM %CALLNAME:TARGET%の姿を認めた
			;人里側に河童が常駐してるのもアレかなと
			IF CFLAG:MASTER:現在位置 / 100 == 2
				PRINTFORM 受付担当
			ELSE
				PRINTFORM 受付河童
			ENDIF
			PRINTFORML 脸上浮现出亲切的笑容
		ELSE
			;人里側では一応丁寧対応に
			IF CFLAG:MASTER:現在位置 / 100 == 2
				CALL SPTALK, 0, "通常", 12, @"「您要坐索道吗？」/「票价是\\{FEE * 200}。或者{FEE}筹码也可以」",1
			ELSE
				CALL SPTALK, 0, "通常", 16, @"「……何事？要乗坐索道麼？」/「{FEE}籌碼。或\\{FEE * 200}金也行」",1
			ENDIF
		ENDIF
		CALL PAY_YEN_OR_CM(FEE * 200, FEE)
		SELECTCASE RESULT
			CASE 0
				RETURN -1
			CASE 1
				IF CFLAG:31:同行 || CFLAG:32:同行 || CFLAG:33:同行
					PRINTFORML 「便宜点吧~」
				ELSE
					PRINTFORML 「多謝惠顧」
				ENDIF
			CASE 2
				IF CFLAG:MASTER:現在位置 / 100 == 2
					PRINTFORML 「用筹码付款吧...嗯、谢谢」
				ELSE
					PRINTFORML 「誒ー、客人明明是人類居然擁有籌碼麼？真是厲害」
				ENDIF
		ENDSELECT
ENDSELECT
;住宿先からの特殊移動 or 住宿先でない場所への特殊移動は约会扱い
IF AT_HOME(MASTER) || MAIN_MAP != SPECIAL_AREAMOVE() / 100
	LOCAL:2 = 0
	LOCALS = %STR:(6000 + SPECIAL_AREAMOVE() / 10)%
	FOR LOCAL,1,CHARANUM
		IF CFLAG:(LOCAL):同行
			CFLAG:LOCAL:约会中 = SPECIAL_AREAMOVE() / 100
			CFLAG:LOCAL:現在位置 = SPECIAL_AREAMOVE()
			LOCAL:2 = LOCAL
			FLAG:约会的对象 = LOCAL
		ENDIF
	NEXT
	CFLAG:MASTER:约会中 = SPECIAL_AREAMOVE() / 100
	CFLAG:MASTER:現在位置 = SPECIAL_AREAMOVE()

	IF CFLAG:MASTER:约会中 == 2
		CALL EX_COSTUME(FLAG:约会的对象, "人里约会")
		SIF RESULT
			PRINTFORMW %CALLNAME:(FLAG:约会的对象)%换了一件适合在人间之里穿的衣服。
	ENDIF
	PRINTFORML 出门去%LOCALS%
	TIME += SPECIAL_AREAMOVE_TIME()

;住宿先でないところから住宿先への移動は约会終了扱い
ELSEIF !AT_HOME(MASTER) && MAIN_MAP == SPECIAL_AREAMOVE() / 100
	IF CFLAG:败犬 && TALENT:路人 == 1
		PRINTFORML 把%CALLNAME:TARGET%带回去了。
	ELSE
		PRINTFORMW 从\@ CFLAG:MASTER:约会中 != CFLAG:TARGET:约会中 || TARGET == MASTER ? 外出 # 约会 \@回来了
	ENDIF
	;约会終了時は強制的にENTRANCEへ飛ばされるため一時保存
	目的場所 = SPECIAL_AREAMOVE()
	所要時間 = SPECIAL_AREAMOVE_TIME()
	CFLAG:MASTER:現在位置 = SPECIAL_AREAMOVE()
	CALL 守矢神簽约会的对象のリセット()
	FOR LOCAL,1,CHARANUM
		IF CHK_DATENOW(CFLAG:LOCAL:约会中)
			CALL DATE_EVENT(LOCAL)
			CFLAG:LOCAL:現在位置 = 目的場所
			CALL SET_DATE(99,LOCAL)
			;约会的对象にも改めて現在位置渡す
			CFLAG:LOCAL:現在位置 = 目的場所
		ENDIF
	NEXT
	CALL SET_DATE(99,MASTER,0)
	;一時保存情報渡す
	CFLAG:MASTER:現在位置 = 目的場所
	TIME += 所要時間
ENDIF
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE464
;停止中は不可
SIF FLAG:70
	RETURN 0
SIF !TFLAG:100
	RETURN 0
SIF TFLAG:约会道中
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(464)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;陪睡中
SIF CFLAG:陪睡中
	RETURN 0
SIF !SPECIAL_AREAMOVE()
	RETURN 0
SIF CFLAG:诶嘿嘿
	RETURN 0
RETURN 1

;-------------------------------------------------
@SPECIAL_AREAMOVE()
#FUNCTION
SELECTCASE CFLAG:MASTER:現在位置
;博麗神社守矢分社
	CASE 21
	;守矢角色と同行中
		SIF CFLAG:31:同行 || CFLAG:32:同行 || CFLAG:33:同行
			RETURNF 840
;人里北大街
	CASE 205
		RETURNF 820
;広場（ミニMap）
	CASE 210
		IF MAIN_MAP != 2
			IF MAIN_MAP == 8
				RETURNF 803
			ELSE
				RETURNF 820
			ENDIF
		ENDIF
;無縁塚（魔法之森がメインMap）
	CASE 506
		RETURNF 610
;無縁塚（外出先から）
	CASE 570
		;メインMapが三途之川～冥界のときは中有之道へ
		SIF MAIN_MAP == 6
			RETURNF 601
		;今後のことを考えて弾いておく 中有之道(外出)へ
		SIF MAIN_MAP != 5
			RETURNF 610
;間欠泉地下中樞(7がメインMap)
	CASE 712
		RETURNF 970
;間欠泉地下中樞(外出先から)
	CASE 760
		;メインMapが地底のときは灼熱地獄跡へ
		SIF MAIN_MAP == 9
			RETURNF 928
		;今後のことを考えて弾いておく 灼熱地獄跡(外出)へ
		SIF MAIN_MAP != 7
			RETURNF 970
;通向山頂的路
	CASE 732
		RETURNF 810
;索道站
	CASE 803
		RETURNF 210
;絶景之丘
	CASE 820
		;メインMapが人里のときは人里北大街へ
		SIF MAIN_MAP == 2
			RETURNF 205
		;守矢神社・早苗私室(820)を弾き広場へ
		SIF MAIN_MAP != 8
			RETURNF 210
;灼熱地獄跡（9がメインMap）
	CASE 928
		RETURNF 760
;灼熱地獄跡（外出先から）
	CASE 970
		;メインMapが妖怪之山（山麓）のときは間欠泉地下中樞へ
		SIF MAIN_MAP == 7
			RETURNF 712
		;今後のことを考えて弾いておく 灼熱地獄跡(外出)へ
		SIF MAIN_MAP != 9
			RETURNF 760
ENDSELECT
RETURNF 0
;-------------------------------------------------
@SPECIAL_AREAMOVE_TIME()
#FUNCTION
SELECTCASE CFLAG:MASTER:現在位置
;博麗神社守矢分社
	CASE 21
		RETURNF 1
;人里北大街
	CASE 205
		RETURNF 15
;広場
	CASE 210
		RETURNF 15
;無縁塚
	CASE 506, 570
		SIF SHORTCUT(3)
			RETURNF 3
		RETURNF 5
;間欠泉地下中樞
	CASE 712, 760
		RETURNF 10
;通向山頂的路
	CASE 732
		RETURNF 5
;索道站
	CASE 803
		RETURNF 15
;絶景之丘
	CASE 820
		RETURNF 15
;灼熱地獄跡
	CASE 928, 970
		RETURNF 5
ENDSELECT
