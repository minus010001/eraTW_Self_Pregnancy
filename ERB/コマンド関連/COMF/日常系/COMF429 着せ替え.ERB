;-------------------------------------------------
;換装
;日常系指令、レベル1
;-------------------------------------------------
@COM429
#DIM C_ID
#DIM ID_換装対象, 人物数量上限
#DIM 換装可能人数
#DIM L_ID ;使用列表のID
CALL MAKE_CHARA_LIST("可换装的角色列表", L_ID, ID_換装対象, 換装可能人数)
IF 換装可能人数 <= 1
	PRINTFORMW %CALLNAME:(MASTER)%换了衣服
	CALL 服装換装(MASTER)
	CALL CLOTHES_SETTING_TRAIN(MASTER)
	RETURN -1
ENDIF

;着替え対象の選択
;列表作成時に可否判定やってるのでここでは必要ないです
PRINTFORML 可以换衣服了
PRINTFORML 要给谁换衣服呢?
PRINTFORML 
CALL SIMPLE_CHARA_LIST(ID_換装対象, 換装可能人数)
;中止した
SIF RESULT == 999
	RETURN -1

;RESULTはすぐ腐るので
C_ID = ID_換装対象:(RESULT)
SIF FLAG:時間停止 == 1 && !(C_ID == MASTER)
	CFLAG:C_ID:時間停止換装 = 1
CALL 服装換装(C_ID)
CALL CLOTHES_SETTING_TRAIN(C_ID)
RETURN 0




;CLOTHES_SETTING_TRAINに靴脱ぎ処理を移動させたので
;ここでCLOTHES_SETTING_TRAINすると靴を脱いでしまい、靴設定の保存ができません
;呼び出し元でCLOTHES_SETTING_TRAINすること
@服装換装(C_ID)
#DIM C_ID
#DIM LINESTART2

LINESTART = LINECOUNT

CALL SHOW_CLOTHES(C_ID, "現在的衣装")
PRINTFORML %CALLNAME:C_ID%换衣服中
PRINTFORML 
PRINTFORML [6] - 衣装確認
PRINTFORML [5] - 指定喜愛的衣装
PRINTFORML [4] - 睡衣的变更
PRINTFORML [3] - 便装的变更
PRINTFORML [2] - 今天穿的衣服的变更
PRINTFORML [1] - 胖次
PRINTFORML 
PRINTFORML [0] - 返回
DO
	INPUT
	LINESTART2 = LINECOUNT
	SELECTCASE RESULT
	CASE 6
		CALL SHOW_CHARA_CLOTHES(C_ID, 0)
		CLEARLINE LINECOUNT - LINESTART
		RESTART
	CASE 5
		$換装5
		CALL SHOW_CLOTHES(C_ID, "套装")
		PRINTFORML 设定%CALLNAME:C_ID%的喜愛套装中
		PRINTFORML 
		PRINTFORML [4] - 指定现在的服装为套装
		PRINTFORML [3] - 穿设定好的套装
		PRINTFORML [2] - 穿和特定角色同款的衣服
		PRINTFORML [1] - 指定的复位
		PRINTFORML 
		PRINTFORML [0] - 放棄
		DO
			INPUT
			SELECTCASE RESULT
			CASE 4
				CALL DECIDE_CLOTHES(C_ID, "套装", "現在的衣装")
			CASE 3
				CALL COSPLAY_SET(C_ID, "套装")
			CASE 2
				CALL COSPLAY_CHARA(C_ID, "套装")
			CASE 1
				PRINTFORML 真的复位吗？
				PRINTFORML 
				PRINTFORML [1] - 复位
				PRINTFORML [0] - 放棄
				DO
					INPUT
					SELECTCASE RESULT
					CASE 1
						CALL CTRL_CLOTHES_SET(C_ID, "套装のリセット")
					CASE 0
						CLEARLINE LINECOUNT - LINESTART2
						GOTO 換装5
					CASEELSE
						CLEARLINE 1
						CONTINUE
					ENDSELECT
				LOOP 1
			CASE 0
				CLEARLINE LINECOUNT - LINESTART
				RESTART
			CASEELSE
			ENDSELECT
			CLEARLINE LINECOUNT - LINESTART2
			GOTO 換装5
		LOOP 1
	CASE 4
		IF C_ID == MASTER
			CLEARLINE LINECOUNT - LINESTART
			RESTART
		ENDIF
		$換装4
		CALL SHOW_CLOTHES(C_ID, "今天的睡衣")
		PRINTFORML 设定%CALLNAME:C_ID%的睡衣中
		PRINTFORML 
		PRINTFORML [5] - 随你喜欢
		PRINTFORML [4] - 睡觉的服装
		PRINTFORML [3] - 指定睡衣
		PRINTFORML [2] - 裸睡
		PRINTFORML [1] - 只穿内裤就睡觉
		PRINTFORML 
		PRINTFORML [0] - 放棄
		DO
			INPUT
			SELECTCASE RESULT
			CASE 5
				CFLAG:C_ID:睡衣指定 = 0
			CASE 4
				SIF CFLAG:C_ID:套装
					CALL CTRL_CLOTHES_SET(C_ID, "睡衣的变更_套装")
			CASE 3
				CALL COSPLAY_SELECT(C_ID, "睡衣", "睡衣セット")
			CASE 2
				CALL CTRL_CLOTHES_SET(C_ID, "睡衣的变更_全裸")
			CASE 1
				CALL CTRL_CLOTHES_SET(C_ID, "睡衣的变更_针织衫")
			CASE 0
				CLEARLINE LINECOUNT - LINESTART
				RESTART
			CASEELSE
				CLEARLINE 1
				CONTINUE
			ENDSELECT
			CLEARLINE LINECOUNT - LINESTART2
			GOTO 換装4
		LOOP 1
	CASE 3
		$換装3
		CALL SHOW_CLOTHES(C_ID, "便服")
		PRINTFORML 设定%CALLNAME:C_ID%的便装中
		PRINTFORML 
		PRINTFORML [4] - 指定现在的服装（为发生而需要而被重写）
		PRINTFORML [3] - 穿设定好的套装
		PRINTFORML [2] - 穿和特定角色同款的衣服
		PRINTFORML [1] - 由本人决定
		PRINTFORML 
		PRINTFORML [0] - 放棄
		DO
			INPUT
			SELECTCASE RESULT
			CASE 4
				CALL DECIDE_CLOTHES(C_ID, "便装", "現在的衣装")
			CASE 3
				CALL COSPLAY_SET(C_ID, "便装")
			CASE 2
				CALL COSPLAY_CHARA(C_ID, "便装")
			CASE 1
				PRINTFORMW 便服的设定交给本人了
				CFLAG:C_ID:基本服装セット = 0
			CASE 0
				CLEARLINE LINECOUNT - LINESTART
				RESTART
			ENDSELECT
			CLEARLINE LINECOUNT - LINESTART2
			GOTO 換装3
		LOOP 1
	CASE 2
		$換装2
		;#;PRINTFORML CFLAG:C_ID:衣装一時変更{CFLAG:C_ID:衣装一時変更}
		CALL SHOW_CLOTHES(C_ID, "現在的衣装")
		PRINTFORML %CALLNAME:C_ID%的现在穿的衣服设定中
		PRINTFORML 
		PRINTFORML [4] - 把衣服换成服装
		PRINTFORML [3] - 穿设定好的套装
		PRINTFORML [2] - 穿和特定角色同款的衣服
		PRINTFORML [1] - 回到今天的默认
		PRINTFORML 
		PRINTFORML [0] - 放棄
		DO
			INPUT
			SELECTCASE RESULT
			CASE 4
				IF !CFLAG:C_ID:套装
					CLEARLINE LINECOUNT - LINESTART2
					GOTO 換装2
				ENDIF
				CALL DECIDE_CLOTHES(C_ID, "現在的衣装", "套装")
			CASE 3
				CALL COSPLAY_SET(C_ID, "現在的衣装")
			CASE 2
				CALL COSPLAY_CHARA(C_ID, "現在的衣装")
			CASE 1
				PRINTFORMW 把今天的服装改了
				CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の変更_便装")
			CASE 0
				CLEARLINE LINECOUNT - LINESTART
				RESTART
			ENDSELECT
			CLEARLINE LINECOUNT - LINESTART2
			GOTO 換装2
		LOOP 1
	CASE 1
		$換装1
		PRINTFORML 斟酌%CALLNAME:C_ID%的胖次中
		PRINTFORML   現在穿著的胖次：%GET_STR(C_ID, "下半身内衣_ずらし可能", EQUIP:C_ID:下半身内衣２, "名前")%
		SELECTCASE CFLAG:C_ID:指定胖次
		CASE 0
			PRINTFORML 　現在的胖次設定：无指定
		CASE 100
			PRINTFORML 　現在的胖次設定：指定不穿
		CASEELSE
			PRINTFORML 　現在的胖次設定：%GET_STR(C_ID, "下半身内衣_ずらし可能", CFLAG:C_ID:指定胖次, "名前")%
		ENDSELECT
		PRINTFORML 
		PRINTFORML 
		SIF C_ID == MASTER
			PRINTFORML [3] - 着用中の胖次を脱掉
		PRINTFORML [2] - 现在的内裤设定为喜欢的胖次
		PRINTFORML [1] - 解除喜欢的胖次
		PRINTFORML 
		PRINTFORML [0] - 放棄
		DO
			INPUT
			SELECTCASE RESULT
			CASE 3
				PRINTFORMW 現在着用している胖次を脱ぎました
				EQUIP:C_ID:下半身内衣２ = 100
			CASE 2
				PRINTFORMW 指定喜欢的胖次
				CFLAG:C_ID:指定胖次 = EQUIP:C_ID:下半身内衣２
				SIF !CFLAG:C_ID:指定胖次
					CFLAG:C_ID:指定胖次 = 100
			CASE 1
				PRINTFORMW 解除喜欢的胖次了
				CFLAG:C_ID:指定胖次 = 0
			CASE 0
				CLEARLINE LINECOUNT - LINESTART
				RESTART
			ENDSELECT
			CLEARLINE LINECOUNT - LINESTART2
			GOTO 換装1
		LOOP 1
	CASE 0
		RETURN
	CASEELSE
		CLEARLINE 1
		CONTINUE
	ENDSELECT
LOOP 1



;模倣させる対象角色の選択
@COSPLAY_CHARA(C_ID, TYPE)
#DIM  C_ID
#DIMS TYPE
#DIM  COS_CHARA
PRINTFORML %CALLNAME:C_ID%的%TYPE%设定中
PRINTFORML 和谁同款呢？
CALL SHOW_CHARA_LIST("可以穿同款衣服的角色列表", "体力気力")
COS_CHARA = RESULT
IF GROUPMATCH(COS_CHARA, 0, 999)
	PRINTFORMW 服装的设定中断了
	RETURN
ELSEIF COS_CHARA == C_ID
	SELECTCASE TYPE
	CASE "套装"
		PRINTFORMW 服装的设定中断了
	CASE "便装"
		PRINTFORMW 便服的设定交给本人了
		CFLAG:C_ID:基本服装セット = 0
	CASE "現在的衣装"
		PRINTFORMW 把今天的服装改了
		CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の変更_便装")
	ENDSELECT
	RETURN
ENDIF
CALL DECIDE_CLOTHES(C_ID, TYPE, "角色便装", COS_CHARA)
SIF RESULT
	RESTART


;衣装セットのカテゴリ選択
@COSPLAY_SET(C_ID, TYPE)
#DIM  C_ID
#DIM  SET_ID
#DIMS TYPE
#DIMS CATEGORY
CALL SHOW_CLOTHES(C_ID, TYPE)
PRINTFORML %CALLNAME:C_ID%的%TYPE%设定中
PRINTFORML 服装的种类选择
PRINTFORML 
PRINTFORML [3] - 基本套装
PRINTFORML [2] - cosplay服装
PRINTFORML [1] - 角色特殊服装集
PRINTFORML 
PRINTFORML [0] - 放棄
DO
	INPUT
	SELECTCASE RESULT
	CASE 3
		CATEGORY '= "基本套装"
	CASE 2
		CATEGORY '= "cosplay服装"
	CASE 1
		CATEGORY '= "角色特殊服装集"
	CASE 0
		RETURN
	CASEELSE
		REUSELASTLINE 無效輸入
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1 
CALL COSPLAY_SELECT(C_ID, TYPE, CATEGORY)
SIF !RESULT
	RESTART


;個々の衣装セットの選択
@COSPLAY_SELECT(C_ID, TYPE, CATEGORY)
#DIM  C_ID
#DIM  SET_ID
#DIMS TYPE
#DIMS CATEGORY
PRINTFORML %CALLNAME:C_ID%的%TYPE%设定中（%CATEGORY%）
PRINTFORML 选择使用的服装套装
PRINTFORML 
CALL CLASS_LIST("衣装セット", CATEGORY)
PRINTFORML 
PRINTFORML [0] - 放棄
DO
	INPUT
	SET_ID = RESULT
	SELECTCASE SET_ID
	CASE 0
		RETURN
	CASEELSE
		SIF GET_INT(0, "衣装セット" , SET_ID, CATEGORY)
			BREAK
		REUSELASTLINE 無效輸入
		CONTINUE
	ENDSELECT
LOOP 1 
CALL DECIDE_CLOTHES(C_ID, TYPE, "衣装セット", SET_ID)
SIF RESULT
	RESTART



;衣装決定の最終確認
@DECIDE_CLOTHES(C_ID, COS_SAVE, COS_LOAD, SET_ID)
#DIM  C_ID     ;設定する角色
#DIMS COS_SAVE ;衣装の保存先
#DIMS COS_LOAD ;衣装の参照元
#DIM  SET_ID   ;参照衣装等のID
;#; PRINTFORML C_ID{C_ID}, COS_SAVE%COS_SAVE%, COS_LOAD%COS_LOAD%, SET_ID{SET_ID}
PRINTFORMW 最終確認
CALL SHOW_CLOTHES(C_ID, COS_LOAD, SET_ID)
PRINTFORML 
PRINTFORML 这套服装可以麼？
PRINTFORML 
PRINTFORML [1] - 決定
PRINTFORML [0] - 再考虑考虑
PRINTFORML 
PRINTFORML [9] - 放棄
DO
	INPUT
	SELECTCASE RESULT
	CASE 1
		BREAK
	CASE 0
		RETURN 1
	CASE 9
		RETURN
	CASEELSE
		REUSELASTLINE 無效輸入
		CONTINUE
	ENDSELECT
LOOP 1


SELECTCASE COS_LOAD
CASE "現在的衣装"
	SELECTCASE COS_SAVE
	CASE "便装"
		PRINTFORMW 现在穿的服装以后每天都会穿了
		CFLAG:C_ID:基本服装セット = 100
		CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の保存_おきにいり")
	CASE "套装"
		PRINTFORMW 现在穿的衣服放进指定了
		CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の保存_おきにいり")
	ENDSELECT
	
CASE "衣装セット"
	PRINTFORMW %CALLNAME:C_ID%的%COS_SAVE%指定為%GET_STR(C_ID, "衣装セット", SET_ID, "名前")%
	SELECTCASE COS_SAVE
	CASE "現在的衣装"
		CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の変更_衣装セット", SET_ID)
	CASE "便装"
		CALL CTRL_CLOTHES_SET(C_ID, "便装の変更_衣装セット", SET_ID)
	CASE "套装"
		CALL CTRL_CLOTHES_SET(C_ID, "套装の変更_衣装セット", SET_ID)
	CASE "睡衣"
		CALL CTRL_CLOTHES_SET(C_ID, "睡衣的变更_衣装セット", SET_ID)
	ENDSELECT

CASE "角色便装"
	PRINTFORMW %CALLNAME:C_ID%穿上和%CALLNAME:SET_ID%同様的衣服了
	SELECTCASE COS_SAVE
	CASE "現在的衣装"
		CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の変更_角色衣装", SET_ID, "便装")
	CASE "便装"
		CALL CTRL_CLOTHES_SET(C_ID, "便装の変更_角色衣装", SET_ID, "便装")
	CASE "套装"
		CALL CTRL_CLOTHES_SET(C_ID, "套装の変更_角色衣装", SET_ID, "便装")
	ENDSELECT

ENDSELECT



;換装
@COM_ABLE429
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(429)
	RETURN RESULT
;私室設定
SIF (CFLAG:MASTER:現在位置 != CFLAG:MASTER:311) && (CFLAG:現在位置 != CFLAG:311)
	RETURN 0
SIF !GETBIT(CFLAG:TARGET:既成事實, 1) && TARGET
	RETURN 0
SIF CFLAG:诶嘿嘿
	RETURN 0
RETURN 1
