;-------------------------------------------------
;家庭菜園
;日常系指令、レベル1
;-------------------------------------------------
@COM418
;LOCAL:1 = 1 + FLAG:畑拡張
LOCAL:1 = GETUNELENGTH418()

FOR LOCAL,0, LOCAL:1
	CALL SHOW_PLANT(LOCAL)
NEXT
PRINTL [999] 算了
$INPUT_LOOP
INPUT
;現状時間停止中は表示のみ
IF INRANGE(RESULT / 100, 0, LOCAL:1) && FLAG:70 == 0
	CALL FARMING(RESULT / 100, RESULT % 100)
	IF RESULT == 0
		RETURN -1
	ELSEIF FLAG:70 == 0
		TIME += RESULT
	ELSE
		BASE:MASTER:TSP -= RESULT * 3
	ENDIF
	RETURN 0
ELSE
	;TIME += 5
	RETURN -1
ENDIF


@FARMING(ARG, 行動番号)
#DIM 行動番号
#DIM 行動コスト
#DIM 畝尺寸

行動コスト = 0
LOCALS = %HATAKE_NAME:ARG%
IF 行動番号 == 0
	IF PLANT_TYPE:ARG > 0
		PRINTFORML 已經種植了%LOCALS%
		;行動コスト += 5
	ELSE
		PRINTL 要种什么呢？ (苗/作物数)
		CALL INPLANT(ARG)
		IF RESULT
			行動コスト += 10
		ENDIF
	ENDIF
ELSEIF 行動番号 == 1
	IF !PLANT_DRY:ARG
		PRINTL 今天已经浇够水了
	;ELSEIF !TFLAG:打水
	ELSEIF VAR_WATER == ""
		PRINTL 還沒有打水
	ELSE
		PLANT_DRY:ARG = 0
		PRINTFORML 对%LOCALS%浇了水
		SIF ITEM:三粒の天滴
			FLAG:天滴坤神 = 1
		行動コスト += 5
	ENDIF
ELSEIF 行動番号 == 2
	;収穫処理を関数化
	行動コスト = HERVEST(ARG, LOCALS)

;全収穫追加
ELSEIF 行動番号 == 6
	;PRINTFORML 全ての畝を収穫します
	畝サイズ = GETUNELENGTH418()
	FOR LOCAL,0,畝尺寸
		LOCALS = %HATAKE_NAME:LOCAL%
		行動コスト += HERVEST(LOCAL, LOCALS)
	NEXT
ELSEIF 行動番号 == 3
	IF PLANT_DRY:ARG <= 4 && PLANT_GROW:ARG <= 10
		PRINTFORML %LOCALS%要拔掉吗？
		CALL ASK_YN
		IF RESULT
			;行動コスト += 5
			RETURN 0
		ENDIF
	ENDIF
	PLANT_TYPE:ARG = 0
	PLANT_DAYS:ARG = 0
	PLANT_GROW:ARG = 0
	PLANT_DRY:ARG = 0
	HATAKE_NAME:ARG =
	PLANT_FOOD:ARG = 0
	PRINTFORML 拔掉了%LOCALS%
	行動コスト += 10
;畑が拡張されたら全体にします
ELSEIF 行動番号 == 4 && ITEM:簡易泳池 && GETBIT (FLAG:泳池, 2)
	IF !PLANT_DRY:ARG
		PRINTL 今天已经浇够水了
	ELSE
		;PLANT_DRY:ARG = 0
		
		畝尺寸 = GETUNELENGTH418()
		FOR LOCAL,0, 畝尺寸
			PLANT_DRY:LOCAL = 0
		NEXT
		PRINTFORML 对%LOCALS%浇了水
		SIF ITEM:三粒の天滴
			FLAG:天滴坤神 = 1
		行動コスト += 5
	ENDIF
ELSEIF 行動番号 == 5 && ITEM:肥料
	IF PLANT_FOOD:ARG
		PRINTL 肥料太多了也不是好事
	ELSE
		PLANT_FOOD:ARG = 1
		PRINTFORML 对%LOCALS%施了肥料
		行動コスト += 20
		ITEM:肥料 --
	ENDIF
ELSE
	;行動コスト += 5
	RETURN 0
ENDIF
RETURN 行動コスト

@INPLANT(ARG)
FOR LOCAL, 0, 1000
	IF STRCOUNT(ITEMNAME:LOCAL, "苗")
		;苗以外を除外
		SIF ITEM:LOCAL
			PRINTFORML [{LOCAL}]%ITEMNAME:(LOCAL), 14, LEFT%({ITEM:(LOCAL), 2, RIGHT}/{ITEM:(苗対応作物番号取得(LOCAL)), 2, RIGHT})
	ENDIF
NEXT
PRINTL [999] 算了
$INPUT_LOOP
INPUT
IF ITEM:RESULT && STRCOUNT(ITEMNAME:RESULT, "苗")
	PRINTFORML 在畑{ARG}里种上了%ITEMNAME:(RESULT)%
	ITEM:RESULT -= 1
	PLANT_TYPE:ARG = RESULT
	PLANT_DRY:ARG = 1
	;三粒の天滴を持っている場合、植えてすぐに施肥、水撒き完了状態に
	IF ITEM:三粒の天滴
		PLANT_DRY:ARG = 0
		PLANT_FOOD:ARG = 1
		FLAG:天滴坤神 = 1
	ENDIF
	HATAKE_NAME:ARG = %ITEMNAME:(苗対応作物番号取得(PLANT_TYPE:ARG))%
	RETURN 1
ELSE
	RETURN 0
ENDIF

@SHOW_PLANT(ARG)
PRINTFORM 畑{ARG}：
;正常な道具かチェック
IF ITEMNAME:苗対応作物番号取得(PLANT_TYPE:ARG) == ""
	PLANT_TYPE:ARG = 0
	PLANT_DAYS:ARG = 0
	PLANT_GROW:ARG = 0
	PLANT_DRY:ARG = 0
	HATAKE_NAME:ARG =
	PLANT_FOOD:ARG = 0
ENDIF
IF PLANT_TYPE:ARG > 0
	SETCOLOR 0x00FF00
	PRINTFORM %HATAKE_NAME:ARG + "栽培中", 16, LEFT%
	IF PLANT_DRY:ARG > 4 || PLANT_GROW:ARG > 10
		SETCOLOR 0x994C00
		PRINT （枯）
	ELSE
		SELECTCASE PLANT_GROW:ARG
			CASE 10
				SETCOLOR 0xFF0000
				PRINT （熟）
			CASE 8,9
				SETCOLOR 0xFF8000
				PRINT （実）
			CASE 6,7
				SETCOLOR 0xFFFF00
				PRINT （花）
			CASE 4,5
				PRINT （葉）
			CASEELSE
				PRINT （苗）
		ENDSELECT 
	ENDIF
	IF !PLANT_DRY:ARG
		SETCOLOR C_AQUA
		PRINT 　撒水完畢
	ENDIF
	IF PLANT_FOOD:ARG
		SETCOLOR C_CREAM
		IF ITEM:三粒の天滴
			PRINT 　享受着坤神的恩惠
		ELSE
			PRINT 　已施肥
		ENDIF
	ENDIF
	RESETCOLOR
	PRINTL
	;時間停止中は菜園の状態を見るだけ
	SIF FLAG:70 == 1
		RETURN
	;水撒き
	IF PLANT_DRY:ARG
		LOCAL = ARG * 100 + 1
		LOCALS = [%TOSTR(LOCAL, "000")%] - 灑水
		PRINTBUTTONLC LOCALS, LOCAL
	ELSE
		PRINTLC 　
	ENDIF
	;収穫
	IF 収穫可能(ARG)
		LOCAL = ARG * 100 + 2
		LOCALS = [%TOSTR(LOCAL, "000")%] - 収穫
		SIF PLANT_GROW:ARG == 10
			SETCOLOR 0xFF0000
		PRINTBUTTONLC LOCALS, LOCAL
		RESETCOLOR
	ELSE
		PRINTLC 　
	ENDIF
	;全収穫追加
	;全て収穫
	IF 収穫可能(ARG)
		LOCAL = ARG * 100 + 6
		LOCALS = [%TOSTR(LOCAL, "000")%] - 全部收获
		SIF PLANT_GROW:ARG == 10
			SETCOLOR 0xFF0000
		PRINTBUTTONLC LOCALS, LOCAL
		RESETCOLOR
	ELSE
		PRINTLC 　
	ENDIF
	;整地
	LOCAL = ARG * 100 + 3
	LOCALS = [%TOSTR(LOCAL, "000")%] - 抜出
	PRINTBUTTONLC LOCALS, ARG * 100 + 03
	;自動灑水系統
	IF ITEM:簡易泳池 && GETBIT (FLAG:泳池, 2) && PLANT_DRY:ARG
		LOCAL = ARG * 100 + 4
		LOCALS = [%TOSTR(LOCAL, "000")%] - 自動灑水系統
		PRINTBUTTONLC LOCALS, LOCAL
	ELSE
		PRINTLC 　
	ENDIF
	;肥料を撒く
	IF ITEM:肥料 && PLANT_FOOD:ARG == 0
		LOCAL = ARG * 100 + 5
		LOCALS = [%TOSTR(LOCAL, "000")%] - 肥料を撒く
		PRINTBUTTONLC LOCALS, LOCAL
	ELSE
		PRINTLC 　
	ENDIF
ELSE
	PRINTL 空き
	;時間停止中は菜園の状態を見るだけ
	SIF FLAG:70 == 1
		RETURN
	;苗植え
	LOCAL = ARG * 100 + 0
	LOCALS = [%TOSTR(LOCAL, "000")%] - 種苗
	PRINTBUTTONLC LOCALS, LOCAL
ENDIF
PRINTL

@苗対応作物番号取得(ARG)
#FUNCTION
IF STRCOUNT(ITEMNAME:ARG, "苗")
	IF ARG == GETNUM(ITEM, "跳蛋苗")
		LOCAL = GETNUM(ITEM, "跳蛋")
	ELSE
		LOCAL = ARG + 200
	ENDIF
ELSE
	LOCAL = ARG
ENDIF
RETURNF LOCAL

@収穫可能(ARG)
#FUNCTION
RETURNF PLANT_GROW:ARG > 7 && !(PLANT_DRY:ARG > 4 || PLANT_GROW:ARG > 10)

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE418
;停止中は不可
;SIF FLAG:70 == 1
;	RETURN 0
;掃除実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(418)
	RETURN RESULT
;庭
SIF !家庭菜園可(CFLAG:MASTER:現在位置)
	RETURN 0
;家庭菜園セット
SIF !ITEM:150
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF CFLAG:诶嘿嘿 == 2
	RETURN 0
RETURN 1

@GETUNELENGTH418()
#FUNCTION
#DIM 畝尺寸

IF TALENT:MASTER:採集Lv > 5
	畝尺寸 = 1 + 3
ELSEIF TALENT:MASTER:採集Lv > 4
	畝尺寸 = 1 + 2
ELSEIF TALENT:MASTER:採集Lv > 2
	畝尺寸 = 1 + 2
ELSEIF TALENT:MASTER:採集Lv > 0
	畝尺寸 = 1 + 1
ELSE
	畝尺寸 = 1
ENDIF
RETURNF 畝尺寸

;全収穫追加
;----------------------------------
;畝一つの収穫処理
;ARG:苗インデックス, ARGS:苗の名称
;-----------------------------------
@HERVEST(ARG, ARGS)
#FUNCTION
#DIM 行動コスト

	行動コスト = 0

	;八百屋で購入できるようになったため収穫条件の厳格化
	IF 収穫可能(ARG) == 0
		;PRINTL 不是可以收获的状态
		RETURNF 行動コスト
	ENDIF
	SIF PLANT_DAYS:ARG > 9
		PLANT_GROW:ARG += 2
	SIF PLANT_FOOD:ARG
		PLANT_GROW:ARG ++
	SIF FLAG:天滴坤神 && RAND:2 == 1
		PLANT_GROW:ARG += 2
	;大豊作…天滴と肥料補正でないと到達不能
	IF PLANT_GROW:ARG > 12
		PRINTL 大豊作だ！
		IF PLANT_FOOD:ARG
			LOCAL = 20 + RAND:6 + RAND:4
		ELSE
			LOCAL = 20
		ENDIF
	ELSEIF PLANT_GROW:ARG > 10
		PRINTL 丰收了！
		IF PLANT_FOOD:ARG
			LOCAL = 14 + RAND:4 + RAND:3
		ELSE
			LOCAL = 14
		ENDIF
	ELSEIF PLANT_GROW:ARG > 7
		IF PLANT_FOOD:ARG
			LOCAL = 7 + RAND:3 + RAND:2
		ELSE
			LOCAL = 7
		ENDIF
	ELSE
		LOCAL = 5
	ENDIF
	SIF TCVAR:MASTER:豊穣
		LOCAL += LOCAL / 5
	;跳蛋一強を崩すため収穫量ダウン
	IF 苗対応作物番号取得(PLANT_TYPE:ARG) == 0
		LOCAL /= 2
	ENDIF
	
	PRINTFORML {LOCAL}個の%ARGS%を収穫した
	ITEM:(苗対応作物番号取得(PLANT_TYPE:ARG)) += LOCAL
	PLANT_TYPE:ARG = 0
	PLANT_DAYS:ARG = 0
	PLANT_GROW:ARG = 0
	PLANT_DRY:ARG = 0
	HATAKE_NAME:ARG =
	PLANT_FOOD:ARG = 0
	行動コスト += 20
RETURNF 行動コスト
