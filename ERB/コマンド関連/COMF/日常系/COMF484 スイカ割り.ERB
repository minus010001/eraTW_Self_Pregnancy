;打西瓜
@COM484
#DIM CHARA
#DIM 同室人数
#DIM 参加可能人数

;雨天中止
IF GROUPMATCH(天候, 4, 5, 6, 7, 8, 10, 11, 12, 13) || GROUPMATCH(FLAG:異常気象, 9, 10)
	IF OUTROOF(CFLAG:MASTER:現在位置)
		PRINTFORMW 天気太差了、还是算了吧…
		RETURN -1
	ENDIF
;料理がある
ELSEIF DISH_NAME != ""
	PRINTFORMW 先想办法处理掉手上的料理吧…
	RETURN -1
ENDIF

;参加可能人数チェック
参加可能人数 = 0
同室人数 = GET_TARGETNUM()
FOR LOCAL, 1, 同室人数 + 1
	CHARA = TARGET:LOCAL
	SIF !SHIRAHU(CHARA)
		CONTINUE
	SIF CFLAG:CHARA:行動 == 5
		CONTINUE
	SIF BASE:CHARA:気力 < 500 || BASE:CHARA:体力 < 500
		CONTINUE
	参加可能人数 ++
NEXT
;TARGETは実行判定で可能なはずなのでエラーチェック
SIF 参加可能人数 == 0
	THROW
SUIKA_PLAYERNUM = 0
CALL SUIKA_CHARASELECT(同室人数, 参加可能人数)
;中止
SIF RESULT == -1
	RETURN -1

;武器選択
CALL SUIKA_WEAPONSELECT

;開幕地の文
DRAWLINE
IF GROUPMATCH(SUIKA_WEAPON, "萬宝槌", "制御棒")
	PRINTFORML %CALLNAME:MASTER%戴上眼罩、全神贯注地開始在原地旋转起来…
ELSE
	PRINTFORML %CALLNAME:MASTER%戴上眼罩、以插在地上的%SUIKA_WEAPON%为軸全神贯注地開始了旋转…
ENDIF
;セットUP
CALL SUIKA_SETUP
WAIT
;TARGETが参加していない場合はTARGETを変更しておく
IF FLAG:约会的对象
	;参加しなくても最優先,そんなひどいことする？
	TARGET = FLAG:约会的对象
ELSEIF !MATCH(SUIKA_PLAYER, TARGET)
	;一人目をターゲットに変更
	TARGET = SUIKA_PLAYER:1
ENDIF

TARGET = SUIKA_PLAYER:1
;ゲーム開始
CALL SUIKA_SMASHING
TFLAG:193 = RESULT

;ソース獲得
DOWNBASE:MASTER:体力 = 200
DOWNBASE:MASTER:気力 = 300
FOR LOCAL, 1, SUIKA_PLAYERNUM + 1
	CHARA = SUIKA_PLAYER:LOCAL
	DOWNBASE:CHARA:気力 += 100
	DOWNBASE:CHARA:体力 += 50
	SELECTCASE TFLAG:193
	CASE 0
		SOURCE:CHARA:歓楽 = 1500 + ABL:CHARA:親密 * 100
		SOURCE:CHARA:受動 = 100 + 50 * ABL:CHARA:従順
		SOURCE:CHARA:征服 = 200 + 80 * ABL:CHARA:施虐属性
	CASE 1, 2
		SOURCE:CHARA:歓楽 = 2500 + ABL:CHARA:親密 * 100
		SOURCE:CHARA:受動 = 200 + 60 * ABL:CHARA:従順
		SOURCE:CHARA:征服 = 400 + 120 * ABL:CHARA:施虐属性
	CASE -1
		IF SUIKA_LIAR:LOCAL || CHARA == [[正邪]]
			SOURCE:CHARA:歓楽 = 1500 + ABL:CHARA:親密 * 100
			SOURCE:CHARA:征服 = 400 + 120 * ABL:CHARA:施虐属性
		ELSE
			SOURCE:CHARA:歓楽 = 800 + ABL:CHARA:親密 * 100
			SOURCE:CHARA:受動 = 100 + 50 * ABL:CHARA:従順
			SOURCE:CHARA:征服 = 50 + 20 * ABL:CHARA:施虐属性
		ENDIF
	ENDSELECT
	IF TFLAG:193 >= 0
		SIF CHARA == TARGET
			TFLAG:信賴度管理 = 1 + TFLAG:193
		IF TFLAG:193 == 2
			TIMES SOURCE:CHARA:歓楽, 1.20
			TIMES SOURCE:CHARA:受動, 1.20
			TIMES SOURCE:CHARA:征服, 1.20
		ENDIF
	ENDIF
	;口上用に役職情報を渡しておく
	SIF CHARA == TARGET && (SUIKA_LIAR:LOCAL || CHARA == [[正邪]])
		TFLAG:194 = 1
	;◆口上
	CALL KOJO_MESSAGE_SEND("SUIKA", 0, CHARA, SUIKA_LIAR:LOCAL, 0, "結果")
	IF RESULTS != ""
		PRINTL 
		PRINTFORML %CALLNAME:CHARA%
		PRINTFORMW 「%RESULTS%」
		RESETCOLOR
	ENDIF
	;なるべく動かない
	TCVAR:CHARA:なるべく動かない = 1
NEXT

;料理生成
SIF TFLAG:193 >= 0
	CALL 料理生成("切好的西瓜")
	
;結果記録
IF TFLAG:193 > 0
	SUIKA_GREAT ++
ELSEIF TFLAG:193 == 0
	SUIKA_SUCCESS ++
ENDIF

;時間経過
TIME += 60

;フラグをリセットして你のフラグを立てる
VARSET SUIKA_PLAYER
CVARSET TCVAR, GETNUM(TCVAR, "打西瓜参加"), 0
TCVAR:MASTER:打西瓜参加 = 1

RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;打西瓜
@COM_ABLE484
;時間停止中
SIF FLAG:時間停止
	RETURN 0
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(484)
	RETURN RESULT
;西瓜がない
SIF !ITEM:西瓜
	RETURN 0
;眼罩がない
SIF !ITEM:眼罩
	RETURN 0
;夏じゃない
SIF GET_MONTH() != "夏之月"
	RETURN 0
;とりあえず訓練できる場所
SIF !戦闘訓練可(CFLAG:MASTER:現在位置)
	RETURN 0
;一日一回まで
SIF TCVAR:MASTER:打西瓜参加
	RETURN 0
;対象がいない
SIF TARGET <= 0
	RETURN 0
;意識がない
SIF !SHIRAHU(TARGET)
	RETURN 0
;陪睡中
SIF CFLAG:MASTER:陪睡中
	RETURN 0
;诶嘿嘿中
SIF CFLAG:MASTER:诶嘿嘿
	RETURN 0
;仕事中
SIF CFLAG:行動 == 5
	RETURN 0
;体力・気力不足
SIF BASE:MASTER:気力 < 500 || BASE:MASTER:体力 < 500
	RETURN 0
;体力・気力不足
SIF BASE:TARGET:気力 < 500 || BASE:TARGET:体力 < 500
	RETURN 0
RETURN 1

;-------------------------------------------------
;打西瓜：角色セレクト関数
;ARG 同室人数
;-------------------------------------------------
@SUIKA_CHARASELECT(ARG, 参加可能人数)
#DIM CHARA
#DIM 参加可能人数
#DIM LINE
#DIM 参加要求
VARSET LOCAL
LINE = 7
DRAWLINE
;全員参加指示
SIF 参加要求
	SUIKA_PLAYERNUM = MIN(参加可能人数, SUIKA_MAXPLAYER)
PRINTFORML ●请選择参加者（%TOFULL(TOSTR(SUIKA_PLAYERNUM))%／%TOFULL(TOSTR(SUIKA_MAXPLAYER))%）谎言数：{SUIKA_LIARNUM()}
FOR LOCAL, 1, ARG + 1
	CHARA = TARGET:LOCAL
	SIF !SHIRAHU(CHARA)
		CONTINUE
	SIF CFLAG:CHARA:行動 == 5
		CONTINUE
	SIF BASE:CHARA:気力 < 500 || BASE:CHARA:体力 < 500
		CONTINUE
	;全員参加指示
	IF 参加要求
		TCVAR:CHARA:打西瓜参加 = LOCAL:1 + 1
		SUIKA_PLAYER:(LOCAL:1 + 1) = CHARA
	ENDIF
	SIF TCVAR:CHARA:打西瓜参加
		SETCOLOR C_AQUA
	PRINTFORM [{CHARA, 3}] %CALLNAME:CHARA, 18, LEFT%
	LOCAL:1 ++
	IF LOCAL:1 % 3 == 0
		PRINTL 
		LINE ++
	ENDIF
	RESETCOLOR
	SIF 参加要求 && LOCAL:1 == SUIKA_MAXPLAYER
		参加要求 = 0
NEXT
参加要求 = 0
IF LOCAL:1 % 3
	PRINTL 
	LINE ++
ENDIF
IF 参加可能人数 > SUIKA_MAXPLAYER
	PRINTL [902] 以上８人参加
ELSE
	PRINTL [901] 全員参加
ENDIF
DRAWLINE
PRINTL [900] 決定　　　　　　　[903] 规则説明
PRINTL [999] 果然还是算了
INPUT

;放棄
IF RESULT == 999
	;参加フラグをリセットする
	CVARSET TCVAR, GETNUM(TCVAR, "打西瓜参加"), 0
	VARSET SUIKA_PLAYER
	RETURN -1
;決定
ELSEIF RESULT == 900
	IF INRANGE(SUIKA_PLAYERNUM, 1, SUIKA_MAXPLAYER)
		RETURN 1
	ELSE
		PRINTFORMW 只能有１～%TOFULL(TOSTR(SUIKA_MAXPLAYER))%名参加者！
		LINE ++
	ENDIF
;全員参加,上から８人を参加
ELSEIF RESULT == 901 || RESULT == 902
	;一旦リセット
	CVARSET TCVAR, GETNUM(TCVAR, "打西瓜参加"), 0
	SUIKA_PLAYERNUM = 0
	VARSET SUIKA_PLAYER
	参加要求 = 1
;ルール説明
ELSEIF RESULT == 903
	CALL SUIKA_TUTORIAL
	RESTART
;角色選択
ELSEIF INRANGE(RESULT, 1, CHARANUM) && MATCH(TARGET, RESULT)
	;参加エントリー
	IF !TCVAR:RESULT:打西瓜参加
		;上限
		IF SUIKA_PLAYERNUM >= SUIKA_MAXPLAYER
			PRINTW 已经不能再加入了！
			LINE ++
		ELSE
			FOR LOCAL, 1, SUIKA_MAXPLAYER + 1
				;空きに入れる
				IF !SUIKA_PLAYER:LOCAL
					SUIKA_PLAYER:LOCAL = RESULT
					TCVAR:RESULT:打西瓜参加 = LOCAL
					BREAK
				ENDIF
			NEXT
			SUIKA_PLAYERNUM ++
		ENDIF
	;参加中止
	ELSE
		FOR LOCAL, TCVAR:RESULT:打西瓜参加, SUIKA_MAXPLAYER + 1
			;参加者は中止する
			IF SUIKA_PLAYER:LOCAL == RESULT
				SUIKA_PLAYER:LOCAL = 0
			;番号が後の人がいるなら登録番号を一つ下げる
			ELSEIF SUIKA_PLAYER:LOCAL
				TCVAR:(SUIKA_PLAYER:LOCAL):打西瓜参加 --
			ENDIF
		NEXT
		;その後シフトする
		FOR LOCAL, TCVAR:RESULT:打西瓜参加, SUIKA_MAXPLAYER + 1
			SIF !SUIKA_PLAYER:LOCAL
				SWAP SUIKA_PLAYER:LOCAL, SUIKA_PLAYER:(LOCAL + 1)
		NEXT
		;参加状態を戻す
		TCVAR:RESULT:打西瓜参加 = 0
		SUIKA_PLAYERNUM --
	ENDIF
ENDIF
CLEARLINE LINE
RESTART

;-------------------------------------------------
;打西瓜：单独指导
;-------------------------------------------------
@SUIKA_TUTORIAL
#DIM MAXPAGE
#DIM PAGE
MAXPAGE = 4
SIF !PAGE
	PAGE = 1
DRAWLINE
PRINTFORML ●规则説明({PAGE} / {MAXPAGE})
SELECTCASE PAGE
CASE 1
	PRINTFORML 你在被蒙住眼睛的状態下握持打撃武器、依赖幻想少女们的声音
	PRINTFORML 将场地某处的西瓜敲碎的游戏。
	PRINTL 
	PRINTFORML 但是参加者中的一些（根据人数）混杂了担当欺骗役的人。
	PRINTFORML 说谎的角色会在提示时撒谎。
CASE 2
	PRINTFORML 因为击打的次数基本只有１次、所以请一边寻找说谎者、
	PRINTFORML 一边探寻真実的提示、慎重地进行移動。
	PRINTL 
	PRINTFORML 在试图移動到场地外的时候会撞到一名参加者。
	PRINTFORML 这种場合下是不能移動的。
CASE 3
	PRINTFORML 如果能完全捕捉到西瓜的位置就是大成功。
	PRINTFORML 是很美味的成功哦。
	PRINTL 
	PRINTFORML 即使是不好的状態、也会有因身份而提示「就是那里！」的情况。
	PRINTFORML 大成功を狙うならより吟味すると良いでしょう。
CASE 4
	PRINTFORML 当人数是奇数（3人以上）的情况下欺骗役的比例会增多難度也会上升。
	PRINTFORML 感觉太難的话可以和偶数人一起玩。
	PRINTL 
	PRINTFORML 如果能成功地打碎西瓜就能做出＜切好的西瓜＞。
	PRINTFORML 请和参加者们一起享用吧。
ENDSELECT
PRINTL 
PRINTFORML \@ PAGE < MAXPAGE ? [1] 次页 # \@
PRINTFORML \@ PAGE > 1 ? [2] 前页 # \@
PRINTFORML [0] 返回
INPUT
IF RESULT == 1 && PAGE < MAXPAGE
	PAGE ++
ELSEIF RESULT == 2 && PAGE > 1
	PAGE --
ELSE
	PAGE = 0
	RETURN
ENDIF
CLEARLINE 12
RESTART

;-------------------------------------------------
;打西瓜：武器選択関数
;-------------------------------------------------
@SUIKA_WEAPONSELECT
DRAWLINE
PRINTFORML ●请選择要使用的武器
PRINTFORML [1] - 自己的扫箒　　%CALLNAME:MASTER%总是随身携带的扫箒。允許额外出手１回。
PRINTFORML [2] - 驱魔棒　　　帶有少許诱导的不思議的祭具。開始時必定朝向西瓜的方向。
PRINTFORML [3] - 木刀　　　　　具有切断力的简单武器。綺麗地切割后能獲得更多资源。面向上級者。
PRINTFORML [4] - 萬宝槌　寄托着愿望的小槌。即使只是掠过也能綺麗地切割。
PRINTFORML [5] - 制御棒　　　　吸收了核能的重棒。開始時将西瓜吸引过来。
INPUT
IF INRANGE(RESULT, 1, 5)
	SELECTCASE RESULT
	CASE 1
		SUIKA_WEAPON = 自己的扫箒
	CASE 2
		SUIKA_WEAPON = 驱魔棒
	CASE 3
		SUIKA_WEAPON = 木刀
	CASE 4
		SUIKA_WEAPON = 萬宝槌
	CASE 5
		SUIKA_WEAPON = 制御棒
	ENDSELECT
ELSE
	CLEARLINE 8
	RESTART
ENDIF

;-------------------------------------------------
;打西瓜：ウソつきの人数
;-------------------------------------------------
@SUIKA_LIARNUM
#FUNCTION
RETURNF MAX((SUIKA_PLAYERNUM - 1) / 2, 0)

;-------------------------------------------------
;打西瓜：二点間の距離
;ARG:1,座標１ ARG:2,座標２ ARG:3,0=合計距離,1=X軸距離,2=Y軸距離
;-------------------------------------------------
@SUIKA_DISTANCE(ARG:1, ARG:2, ARG:3)
#FUNCTION
LOCAL:1 = ARG:1 / 10
LOCAL:2 = ARG:2 / 10
LOCAL:3 = ARG:1 % 10
LOCAL:4 = ARG:2 % 10
LOCAL:5 = ABS(LOCAL:1 - LOCAL:2)
LOCAL:6 = ABS(LOCAL:3 - LOCAL:4)
IF ARG:3 == 1
	RETURNF LOCAL:5
ELSEIF ARG:3 == 2
	RETURNF LOCAL:6
ELSE
	RETURNF LOCAL:5 + LOCAL:6
ENDIF

;-------------------------------------------------
;打西瓜：打撃する位置（正面位置）
;-------------------------------------------------
@SUIKA_AIMING
#FUNCTION
SELECTCASE SUIKA_DIRECTION 
CASE 2
	RETURNF SUIKA_MASTER_P - 1
CASE 4
	RETURNF SUIKA_MASTER_P - 10
CASE 6
	RETURNF SUIKA_MASTER_P + 10
CASE 8
	RETURNF SUIKA_MASTER_P + 1
ENDSELECT

;-------------------------------------------------
;打西瓜：背面位置
;-------------------------------------------------
@SUIKA_BACK
#FUNCTION
SELECTCASE SUIKA_DIRECTION 
CASE 2
	RETURNF SUIKA_MASTER_P + 1
CASE 4
	RETURNF SUIKA_MASTER_P + 10
CASE 6
	RETURNF SUIKA_MASTER_P - 10
CASE 8
	RETURNF SUIKA_MASTER_P - 1
ENDSELECT

;-------------------------------------------------
;打西瓜：位置が場外に出ている判定
;ARG,座標
;-------------------------------------------------
@SUIKA_OVER(ARG)
#FUNCTION
SIF ARG >= 100 || ARG % 10 == 0 || ARG / 10 == 0
	RETURNF 1

;-------------------------------------------------
;打西瓜：西瓜のある方向（客観方向）
;ARG,0=軸指定無,1=X軸,2=Y軸
;-------------------------------------------------
@SUIKA_RADAR(ARG)
#FUNCTION
IF !ARG
	LOCAL = RAND:2 + 1
ELSE
	LOCAL = ARG
ENDIF

IF LOCAL == 1
	IF SUIKA_MASTER_P / 10 - SUIKA_SUIKA_P / 10 > 0
		RETURNF 4
	ELSEIF SUIKA_MASTER_P / 10 - SUIKA_SUIKA_P / 10 < 0
		RETURNF 6
	ELSE
		RETURNF 5
	ENDIF
ELSEIF LOCAL == 2
	IF SUIKA_MASTER_P % 10 - SUIKA_SUIKA_P % 10 > 0
		RETURNF 2
	ELSEIF SUIKA_MASTER_P % 10 - SUIKA_SUIKA_P % 10 < 0
		RETURNF 8
	ELSE
		RETURNF 5
	ENDIF
ENDIF

;-------------------------------------------------
;打西瓜：セットUP関数
;-------------------------------------------------
@SUIKA_SETUP
#DIM 最大距離
;挑戦回数＋１
SUIKA_CHALLENGE ++
;得点リセット
SUIKA_SCORE = 0
;西瓜を失う
CALL LOST_ITEM("西瓜", 1, 1)
;お手つき許容回数
IF SUIKA_WEAPON == "自己的扫箒"
	SUIKA_OTETSUKI = 1
ELSE
	SUIKA_OTETSUKI = 0
ENDIF
;西瓜の位置
SUIKA_SUIKA_P = (RAND:9 + 1) * 10 + RAND:9 + 1
;你の位置,西瓜の位置と距離が6以上12以内,（制御棒なら距離8以内）
SUIKA_MASTER_P = 0
IF SUIKA_WEAPON == "制御棒"
	PRINTFORML 【制御棒的効果】滋滋滋……　在制御棒的作用下、西瓜被吸引过来了！
	最大距離 = 8
ELSE
	最大距離 = 16
ENDIF
WHILE !SUIKA_MASTER_P || !INRANGE(SUIKA_DISTANCE(SUIKA_SUIKA_P, SUIKA_MASTER_P), 6, 最大距離)
	SUIKA_MASTER_P = (RAND:9 + 1) * 10 + RAND:9 + 1
WEND
;你の向き
SUIKA_DIRECTION = 5
IF SUIKA_WEAPON == "驱魔棒"
	WHILE SUIKA_DIRECTION % 2 != 0
		SUIKA_DIRECTION = SUIKA_RADAR()
	WEND
	PRINTFORML 【驱魔棒的効果】被驱魔棒牵引着、%CALLNAME:MASTER%的身体转向了西瓜的方向！
ELSE
	SUIKA_DIRECTION = RAND:4 * 2 + 2
ENDIF
;ウソつき役の設定
VARSET SUIKA_LIAR
SIF !SUIKA_LIARNUM()
	RETURN
CALL FISHER_YATES_SHAFFLE(SUIKA_PLAYERNUM)
FOR LOCAL:1, 0, SUIKA_LIARNUM()
	LOCAL = SHAFFLE_ARRAY:(LOCAL:1) + 1
	SUIKA_LIAR:LOCAL = 1
NEXT
[IF_DEBUG]
	DRAWLINE
	PRINTFORML SUIKA_SUIKA_P:{SUIKA_SUIKA_P}
	PRINTFORML SUIKA_MASTER_P:{SUIKA_MASTER_P}
	PRINTFORML SUIKA_DISTANCE:{SUIKA_DISTANCE(SUIKA_SUIKA_P, SUIKA_MASTER_P)}, {SUIKA_DISTANCE(SUIKA_SUIKA_P, SUIKA_MASTER_P, 1)}, {SUIKA_DISTANCE(SUIKA_SUIKA_P, SUIKA_MASTER_P, 2)}
	PRINTFORML SUIKA_DIRECTION:{SUIKA_DIRECTION}
	PRINTFORML SUIKA_AIMING:{SUIKA_AIMING()}
	FOR LOCAL, 1, SUIKA_PLAYERNUM + 1
		PRINTFORML SUIKA_PLAYER:{LOCAL} %CALLNAME:(SUIKA_PLAYER:LOCAL)% \@ SUIKA_LIAR:LOCAL ? ● # ○ \@
	NEXT
	PRINTFORML SUIKA_WEAPON:%SUIKA_WEAPON%
[ENDIF]

;-------------------------------------------------
;打西瓜：ゲーム部分関数
;-------------------------------------------------
@SUIKA_SMASHING
#DIM CHARA
#DIM LINENUM
#DIM LINENUM_PLUS
CALL FISHER_YATES_SHAFFLE(8)

LINENUM_PLUS = 0
DRAWLINE
FOR LINENUM, 1, 9
	IF LINENUM % 2 == 1
		LOCAL = SHAFFLE_ARRAY:(LINENUM - 1) + 1
		CHARA = SUIKA_PLAYER:LOCAL
		PRINTPLAINFORM % CHARA ? CALLNAME:CHARA # "" , 30, LEFT%
	ELSE
		LOCAL = SHAFFLE_ARRAY:(LINENUM - 2) + 1
		CHARA = SUIKA_PLAYER:LOCAL
		CALL SUIKA_MESSAGE(CHARA, LOCAL, 1)
	ENDIF
	SELECTCASE LINENUM
	CASE 2
		PRINTFORM 　　　　　[8] 向前一步　　　　　
	CASE 4
		PRINTFORM 　[4] 左转　　　　[6] 右转　
	CASE 6
		PRINTFORM 　　　＜＜[5] 敲下！＞＞　　　　
	CASE 8
		PRINTFORM 　　　　　[2] 向後一步　　　　
	CASEELSE
		PRINTFORM %" " * 32%
	ENDSELECT
	IF LINENUM % 2 == 1
		LOCAL = SHAFFLE_ARRAY:(LINENUM) + 1
		CHARA = SUIKA_PLAYER:LOCAL
		PRINTPLAINFORM % CHARA ? CALLNAME:CHARA # "" , 30, RIGHT%
	ELSE
		LOCAL = SHAFFLE_ARRAY:(LINENUM - 1) + 1
		CHARA = SUIKA_PLAYER:LOCAL
		CALL SUIKA_MESSAGE(CHARA, LOCAL, 2)
	ENDIF
	PRINTL 
NEXT
INPUT
DRAWLINE
SELECTCASE RESULT
CASE 5
	PRINTL 
	PRINTFORMW %CALLNAME:MASTER%用尽全力将%SUIKA_WEAPON%在眼前挥下！
	PRINTL 
	LINENUM_PLUS += 3
	IF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 0
		IF SUIKA_WEAPON == "木刀"
			CALL COLORMESSAGE("SLAAAASH!!", C_ORANGE, 2, 3)
			RETURN 2
		ELSE
			CALL COLORMESSAGE("SMAAAASH!!", C_V_PINK, 2, 3)
			RETURN 1
		ENDIF
	ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 1
		IF SUIKA_WEAPON == "萬宝槌"
			CALL COLORMESSAGE("LUCKY STRIKE!", C_YELLOW, 2, 3)
			RETURN 1
		ELSE
			CALL COLORMESSAGE("HIT!", C_YELLOW, 2, 2)
			RETURN 0
		ENDIF
	ELSE
		CALL COLORMESSAGE("打偏了ー！", C_CREAM, 2, 2)
		IF SUIKA_OTETSUKI
			PRINTL 
			PRINTFORMW …但使用扫箒时是可以允許的
			SUIKA_OTETSUKI --
			LINENUM_PLUS += 4
		ELSE
			RETURN -1
		ENDIF
	ENDIF
CASE 4
	PRINTFORML …………
	PRINTFORMW %CALLNAME:MASTER%转向了左边
	SELECTCASE SUIKA_DIRECTION
	CASE 2
		SUIKA_DIRECTION = 6
	CASE 4
		SUIKA_DIRECTION = 2
	CASE 6
		SUIKA_DIRECTION = 8
	CASE 8
		SUIKA_DIRECTION = 4
	ENDSELECT
	LINENUM_PLUS += 2
CASE 6
	PRINTFORML …………
	PRINTFORMW %CALLNAME:MASTER%转向了右边
	SELECTCASE SUIKA_DIRECTION
	CASE 2
		SUIKA_DIRECTION = 4
	CASE 4
		SUIKA_DIRECTION = 8
	CASE 6
		SUIKA_DIRECTION = 2
	CASE 8
		SUIKA_DIRECTION = 6
	ENDSELECT
	LINENUM_PLUS += 2
CASE 2
	PRINTFORML …………
	IF SUIKA_OVER(SUIKA_BACK())
		CALL SUIKA_COLLISION
		LINENUM_PLUS += RESULT
	ELSE
		PRINTFORMW %CALLNAME:MASTER%向后退了一步
		SUIKA_MASTER_P = SUIKA_BACK()
		LINENUM_PLUS += 2
	ENDIF
CASEELSE
	PRINTFORML …………
	IF SUIKA_OVER(SUIKA_AIMING())
		CALL SUIKA_COLLISION
		LINENUM_PLUS += RESULT
	ELSE
		PRINTFORMW %CALLNAME:MASTER%向前迈了一歩
		SUIKA_MASTER_P = SUIKA_AIMING()
		LINENUM_PLUS += 2
	ENDIF
ENDSELECT
CLEARLINE 11 + LINENUM_PLUS
RESTART

;-------------------------------------------------
;打西瓜：衝突メッセージ
;-------------------------------------------------
@SUIKA_COLLISION
#DIM LINE
LINE = 4
LOCAL = SUIKA_PLAYER:(RAND:SUIKA_PLAYERNUM + 1)
PRINTFORMW 咕咚！
PRINTL 
;◆口上
CALL KOJO_MESSAGE_SEND("SUIKA", 0, LOCAL, 0, 0, "衝突")
IF RESULTS != ""
	PRINTFORMW 「%RESULTS%」
	PRINTL 
	LINE += 2
ENDIF
RESETCOLOR
IF LOCAL == [[萃香]]
	PRINTFORMW 那不是西瓜而是萃香
ELSE
	PRINTFORMW %CALLNAME:MASTER%与%CALLNAME:LOCAL%相撞了……
ENDIF

RETURN LINE

;-------------------------------------------------
;打西瓜：台詞関数１,どんな台詞を言うかの判定
;ARG,プレイヤー番号
;ARG:1,表示位置 1=LEFT,2=RIGHT
;-------------------------------------------------
@SUIKA_MESSAGE(CHARA, ARG, ARG:1)
#DIM CHARA
#DIM 客観方向
#DIMS 主観方向
#DIM ウソつき
#DIMS 正直な指示
#DIMS 指示
;非表示
IF !CHARA
	IF ARG:1 == 1
		PRINTPLAINFORM %"" , 30, LEFT%
	ELSE
		PRINTPLAINFORM %"" , 30, RIGHT%
	ENDIF
	RETURN
ENDIF

;完全一致
IF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 0
	正直な指示 = そこだ！
;1マスずれ（2分の1）
ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 1 && !RAND:2
	正直な指示 = そこだ！
;重なっている（1マスずれの漏れ）
ELSEIF SUIKA_DISTANCE(SUIKA_MASTER_P, SUIKA_SUIKA_P) == 0
	正直な指示 = 少し後ろ
;それ以外
ELSE
	;近さ判定
	;LOCAL:1,距離について言う判定
	;LOCAL:2,「少し」を付けて言う判定
	IF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 1
		LOCAL:1 = !RAND:2
		LOCAL:2 = 1
	ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 2
		LOCAL:1 = !RAND:2
		LOCAL:2 = !RAND:2
	ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 3
		LOCAL:1 = !RAND:3
		LOCAL:2 = !RAND:4
	ELSE
		LOCAL:1 = !RAND:4
		LOCAL:2 = 0
	ENDIF
	;距離のヒント
	IF LOCAL:1
		IF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) <= 3
			正直な指示 = 近い
		ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) >= 8
			正直な指示 = かなり遠い
		ELSE
			正直な指示 = 遠い
		ENDIF
	;方向のヒント
	ELSE
		客観方向 = 5
		WHILE 客観方向 % 2 != 0
			客観方向 = SUIKA_RADAR()
		WEND
		IF 客観方向 == SUIKA_DIRECTION
			主観方向 = 前
		ELSE
			SELECTCASE SUIKA_DIRECTION
			CASE 2
				SELECTCASE 客観方向
				CASE 4
					主観方向 = 右
				CASE 6
					主観方向 = 左
				CASE 8
					主観方向 = 後ろ
				ENDSELECT
			CASE 4
				SELECTCASE 客観方向
				CASE 2
					主観方向 = 左
				CASE 6
					主観方向 = 後ろ
				CASE 8
					主観方向 = 右
				ENDSELECT
			CASE 6
				SELECTCASE 客観方向
				CASE 2
					主観方向 = 右
				CASE 4
					主観方向 = 後ろ
				CASE 8
					主観方向 = 左
				ENDSELECT
			CASE 8
				SELECTCASE 客観方向
				CASE 2
					主観方向 = 後ろ
				CASE 4
					主観方向 = 左
				CASE 6
					主観方向 = 右
				ENDSELECT
			ENDSELECT
		ENDIF
		SIF LOCAL:2
			主観方向 = 少し%主観方向%
		正直な指示 = %主観方向%
	ENDIF
ENDIF
;正邪は役職を無視して嘘をつく
IF SUIKA_LIAR:ARG || CHARA == [[正邪]]
	ウソつき = 1
ELSE
	ウソつき = 0
ENDIF
IF ウソつき
	SELECTCASE 正直な指示
	CASE "そこだ！"
		指示 =%TEXTR("少し後ろ/少し前/少し右/少し左")%
	CASE "近い"
		指示 = 遠い
	CASE "遠い"
		指示 = 近い
	CASE "かなり遠い"
		指示 = 遠い
	CASE "前"
		指示 = 後ろ
	CASE "少し前"
		指示 = %TEXTR("少し後ろ/そこだ！")%
	CASE "後ろ"
		指示 = 前
	CASE "少し後ろ"
		指示 = %TEXTR("少し前/そこだ！")%
	CASE "右"
		指示 = 左
	CASE "少し右"
		指示 = %TEXTR("少し左/そこだ！")%
	CASE "左"
		指示 = 右
	CASE "少し左"
		指示 = %TEXTR("少し右/そこだ！")%
	ENDSELECT
ELSE
	指示 = %正直な指示%
ENDIF
;◆口上
CALL KOJO_MESSAGE_SEND("SUIKA", 0, CHARA, 0, 0, 指示)
IF RESULTS != ""
	IF ARG:1 == 1
		PRINTPLAINFORM %"「" + RESULTS + "」", 30, LEFT%
	ELSE
		PRINTPLAINFORM %"「" + RESULTS + "」", 30, RIGHT%
	ENDIF
ELSE
	IF ARG:1 == 1
		PRINTPLAINFORM %SUIKA_MESSAGE_2(CHARA, ARG, 指示), 30, LEFT%
	ELSE
		PRINTPLAINFORM %SUIKA_MESSAGE_2(CHARA, ARG, 指示), 30, RIGHT%
	ENDIF
ENDIF

;-------------------------------------------------
;打西瓜：台詞関数２,実際に喋る台詞
;ARG,プレイヤー番号
;-------------------------------------------------
@SUIKA_MESSAGE_2(CHARA, ARG, 指示)
#FUNCTIONS
#DIM CHARA
#DIMS 指示
;汎用台詞
SELECTCASE 指示
CASE "そこだ！"
	SELECTCASE CFLAG:CHARA:口調
	;～です系
	CASE 1
		RESULTS = %TEXTR("就是那里！/是现在！")%
	;～だよ系
	CASE 2
		RESULTS = %TEXTR("那里！/现在！")%
	;～だぞ系
	CASE 3
		RESULTS = %TEXTR("是那里哒！/就是现在！")%
	;～のじゃ系
	CASE 4
		RESULTS = %TEXTR("在那里！/现在！")%
	;無口系
	CASE 5
		RESULTS = %TEXTR("那里…！/现在…！")%
	;～のよ系
	CASEELSE
		RESULTS = %TEXTR("就是那边哟！/现在哟！")%
	ENDSELECT
CASE "近い"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("非常接近了/在靠近中/再近一点")%
	CASE 2
		RESULTS = %TEXTR("很近！很近了哟！/在靠近了哟！/再走一点！")%
	CASE 3
		RESULTS = %TEXTR("很近了喔/在靠近了喔/还差一点")%
	CASE 4
		RESULTS = %TEXTR("很近/就在很近的地方/再来一点")%
	CASE 5
		RESULTS = %TEXTR("……相当近/……在靠近了/……还差一点")%
	CASEELSE
		RESULTS = %TEXTR("很近了哇/正在靠近呢/再继续哟")%
	ENDSELECT
CASE "遠い"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("还是远的呢/还是很远")%
	CASE 2
		RESULTS = %TEXTR("还远哟/还不够～")%
	CASE 3
		RESULTS = %TEXTR("还远着呐/还远呢")%
	CASE 4
		RESULTS = %TEXTR("还远哦/还远的呢")%
	CASE 5
		RESULTS = %TEXTR("……远/……还远")%
	CASEELSE
		RESULTS = %TEXTR("还远讷/还远哟")%
	ENDSELECT
CASE "かなり遠い"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("还是很远很远的呢/还有很长的路呢")%
	CASE 2
		RESULTS = %TEXTR("差的太远了哟ー/差的太远太远了ー")%
	CASE 3
		RESULTS = %TEXTR("相当远呢/離得可远了啊")%
	CASE 4
		RESULTS = %TEXTR("还远的呢/还很远很远啊")%
	CASE 5
		RESULTS = %TEXTR("……很遠呢/……距離很远")%
	CASEELSE
		RESULTS = %TEXTR("好远啊/離得好远啊")%
	ENDSELECT
CASE "前"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("就是前面/就是这样/方向是正确的")%
	CASE 2
		RESULTS = %TEXTR("就在前面哟/就这样、就这样/这边这边")%
	CASE 3
		RESULTS = %TEXTR("是前方哒/就这样向前/就在正前方哦")%
	CASE 4
		RESULTS = %TEXTR("在前面呀/继续下去就很好/快、迈步")%
	CASE 5
		RESULTS = %TEXTR("……前/……就这样/……方向正确")%
	CASEELSE
		RESULTS = %TEXTR("在前方呀/就这样走/是那个方向哟")%
	ENDSELECT
CASE "少し前"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("前方一点点/稍微再前点/再向前走一步吗…")%
	CASE 2
		RESULTS = %TEXTR("就在前一点！/前一点/再一歩！")%
	CASE 3
		RESULTS = %TEXTR("前一点点/稍微前点/再前一歩吧")%
	CASE 4
		RESULTS = %TEXTR("前面一点点/稍微前一点/还要前一歩呐")%
	CASE 5
		RESULTS = %TEXTR("……前点/……稍微前些/……再一歩")%
	CASEELSE
		RESULTS = %TEXTR("就在前面一点哟/只是稍微前边呢/只差向前一歩了")%
	ENDSELECT
CASE "後ろ"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("是在後面/是在反方向的/那边可没有啊")%
	CASE 2
		RESULTS = %TEXTR("转向後边！/反了、反了！/不在那边哟ー")%
	CASE 3
		RESULTS = %TEXTR("在後边哦/在後方哒/可不在那边哦")%
	CASE 4
		RESULTS = %TEXTR("後面啊/在相反的方向/不在那边的")%
	CASE 5
		RESULTS = %TEXTR("……後边/……反了/……不在那边")%
	CASEELSE
		RESULTS = %TEXTR("後边哟/反方向哟/不是那个方向啊")%
	ENDSELECT
CASE "少し後ろ"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("就在後边/稍微往後点/再向後走一步吗…")%
	CASE 2
		RESULTS = %TEXTR("就在後边的哟！/後一点点/退一歩！")%
	CASE 3
		RESULTS = %TEXTR("就在後边了呢/稍微往後一点/再退一歩吗")%
	CASE 4
		RESULTS = %TEXTR("後一点点/稍稍後一点/再退一歩吧")%
	CASE 5
		RESULTS = %TEXTR("……後点/……稍微後些/……后退一步")%
	CASEELSE
		RESULTS = %TEXTR("就在後边呢/稍微後面一点哟/只差向後一歩了")%
	ENDSELECT
CASE "右"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("是右边/是右边哟ー/是在右方")%
	CASE 2
		RESULTS = %TEXTR("右的哟/右、右/再右点")%
	CASE 3
		RESULTS = %TEXTR("右边哦/右边呢/在右边的")%
	CASE 4
		RESULTS = %TEXTR("右面啊/右面哦/右边呐")%
	CASE 5
		RESULTS = %TEXTR("右/……右/……右哟")%
	CASEELSE
		RESULTS = %TEXTR("右哟/还在右边呢/右边的吧")%
	ENDSELECT
CASE "少し右"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("是右一点点/稍微往右点吧")%
	CASE 2
		RESULTS = %TEXTR("就往右一点/再右一点")%
	CASE 3
		RESULTS = %TEXTR("稍微右点/再右一点")%
	CASE 4
		RESULTS = %TEXTR("稍微右一下/稍稍右一点点")%
	CASE 5
		RESULTS = %TEXTR("……稍微右/……右一点")%
	CASEELSE
		RESULTS = %TEXTR("右一点呐/稍微往右一点吧")%
	ENDSELECT
CASE "左"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("是左边/是左边哟ー/是在左方")%
	CASE 2
		RESULTS = %TEXTR("左的哟/左、左/再左点")%
	CASE 3
		RESULTS = %TEXTR("左边哦/左边呢/在左边的")%
	CASE 4
		RESULTS = %TEXTR("左面啊/左面哦/左边呐")%
	CASE 5
		RESULTS = %TEXTR("左/……左/……左哟")%
	CASEELSE
		RESULTS = %TEXTR("左哟/还在左边呢/左边的吧")%
	ENDSELECT
CASE "少し左"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("是左一点点/稍微往左点吧")%
	CASE 2
		RESULTS = %TEXTR("就往左一点/再左一点")%
	CASE 3
		RESULTS = %TEXTR("稍微左点/再左一点")%
	CASE 4
		RESULTS = %TEXTR("稍微左一下/稍稍左一点点")%
	CASE 5
		RESULTS = %TEXTR("……稍微左/……左一点")%
	CASEELSE
		RESULTS = %TEXTR("左一点呐/稍微往左一点吧")%
	ENDSELECT
ENDSELECT
RETURNF "「" + RESULTS + "」"


