;-------------------------------------------------
;假寐
;日常系指令、レベル1
;-------------------------------------------------
@COM417
#DIM 回復前体力,2
#DIM 回復前気力,2
#DIM 回復後体力,2
#DIM 回復後気力,2
#DIM 回復率_体力, 4
#DIM 回復率_気力, 4
#DIM 回復率_精力, 4

回復率_体力:0 = 200
回復率_気力:0 = 250
回復率_精力:0 = 100

回復率_体力:1 = 240
回復率_気力:1 = 300
回復率_精力:1 = 120

回復率_体力:2 = 280
回復率_気力:2 = 320
回復率_精力:2 = 140

回復率_体力:3 = 300
回復率_気力:3 = 360
回復率_精力:3 = 160

回復前体力:0 = BASE:MASTER:体力
回復前気力:0 = BASE:MASTER:気力
IF TARGET > 0 && CFLAG:TARGET:6 > 0
	回復前体力:1 = BASE:TARGET:体力
	回復前気力:1 = BASE:TARGET:気力
	CALL RECOVER_PERMIL(TARGET, 回復率_体力:MIN(ITEM:桐木床,3), "体力", 1)
	CALL RECOVER_PERMIL(TARGET, 回復率_気力:MIN(ITEM:桐木床,3), "気力", 1)
	CALL RECOVER_PERMIL(TARGET, 回復率_精力:MIN(ITEM:桐木床,3), "精力", 1)
	回復後体力:1 = BASE:TARGET:体力
	回復後気力:1 = BASE:TARGET:気力
		;ABL:親密をみる
		IF ABL:親密 <= 1
			SOURCE:情愛 += (ABL:親密 * 30)
		ELSEIF ABL:親密 <= 3
			SOURCE:歓楽 += 100 + (ABL:親密 * 40)
			SOURCE:情愛 += 200 + (ABL:親密 * 30)
		ELSEIF ABL:親密 <= 5
			SOURCE:歓楽 += 400 + (ABL:親密 * 50)
			SOURCE:情愛 += 300 + (ABL:親密 * 30)
		ELSEIF ABL:親密 <= 8
			SOURCE:歓楽 += 700 + (ABL:親密 * 60)
			SOURCE:情愛 += 400 + (ABL:親密 * 40)
		ELSEIF ABL:親密 <= 11
			SOURCE:歓楽 += 1200 + (ABL:親密 * 80)
			SOURCE:情愛 += 600 + (ABL:親密 * 40)
		ELSE
			SOURCE:歓楽 += 2000 + (ABL:親密 * 50)
			SOURCE:情愛 += 1500 + (ABL:親密 * 20)
		ENDIF
		;好感度をみる
		IF CFLAG:2 <= 500
			SOURCE:歓楽 += CFLAG:2
		ELSEIF CFLAG:2 <= 5000
			SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
		ELSE
			SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
		ENDIF
		SIF SOURCE:歓楽 < 0
		SOURCE:歓楽 = 0
		CFLAG:333 += 90
		TFLAG:98 += 3
	TFLAG:194 = 1
	PRINTFORML 决定和%CALLNAME:TARGET%一起\@ TIME > 660 && TIME < 900? 午睡# 假寐\@
ELSE
 	PRINTFORML %CALLNAME:MASTER%决定稍微\@ TIME > 660 && TIME < 900? 睡个午觉# 假寐一会\@
 	TFLAG:194 = 2
ENDIF

CALL RECOVER_PERMIL(MASTER, 回復率_体力:MIN(ITEM:桐木床,3), "体力", 1)
CALL RECOVER_PERMIL(MASTER, 回復率_気力:MIN(ITEM:桐木床,3), "気力", 1)
CALL RECOVER_PERMIL(MASTER, 回復率_精力:MIN(ITEM:桐木床,3), "精力", 1)

TIME += 90
TFLAG:午睡 = 90
回復後体力:0 = BASE:MASTER:体力
回復後気力:0 = BASE:MASTER:気力
;DRAWLINE
;PRINTFORML 体力+{ABS(回復後体力 - 回復前体力)}（%CALLNAME:MASTER%）
;PRINTFORML 気力+{ABS(回復後気力 - 回復前気力)}（%CALLNAME:MASTER%）
IF TFLAG:194 == 1
;	PRINTFORML 体力+{ABS(回復後体力:1 - 回復前体力:1)}（%CALLNAME:TARGET%）
;	PRINTFORML 気力+{ABS(回復後気力:1 - 回復前気力:1)}（%CALLNAME:TARGET%）
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE417
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(417)
	RETURN RESULT
;私室設定
SIF CFLAG:MASTER:現在位置 != CFLAG:MASTER:311
	RETURN 0
SIF NEMUKE() < 180
	RETURN 0
SIF NEMUKE() > 720
	RETURN 0
SIF TFLAG:午睡 > 0
	RETURN 0
SIF CFLAG:诶嘿嘿 == 2
	RETURN 0
RETURN 1

