;-------------------------------------------------
;推倒
;日常系指令、レベル1
;-------------------------------------------------
@COM350
#DIM NUM_TARGET
#DIM C_ID
;推倒
NUM_TARGET = GET_TARGETNUM()
IF FLAG:甲斐性無
	DRAWLINE
	PRINTFORMW %CALLNAME:MASTER%沒有那樣的膽量
	RETURN -1
ENDIF
IF TFLAG:パール期限
	IF TARGET == TFLAG:パール入れた人
		PRINTFORML %CALLNAME:TARGET%说着在到时间之前都先暂时保留、拒绝了%CALLNAME:MASTER%
	ELSE
		PRINTFORML 这个状態下无法进行情事
	ENDIF
	RETURN -1
ENDIF
IF FLAG:70
	;時間停止は無条件突破
	FOR LOCAL,1, NUM_TARGET + 1
		CFLAG:(TARGET:LOCAL):诶嘿嘿 = 1
	NEXT
ELSE
	;視線による中断判定
	IF EYEWITNESS() == 1
		TFLAG:194 = 1
		TFLAG:不让推倒 ++
	ENDIF
	;MASTER的に多人数は無理だった
	IF NUM_TARGET > 1 && TCVAR:MASTER:亲热 < 2
		TFLAG:194 = 3
		TFLAG:不让推倒 ++
	ENDIF
	
	FOR LOCAL,1, NUM_TARGET + 1
		C_ID = TARGET:LOCAL
		;烂醉
		IF TCVAR:C_ID:烂醉 == 1
			CFLAG:C_ID:诶嘿嘿 = 1
			SIF CFLAG:C_ID:烂酔奸 < 2
				CFLAG:C_ID:烂酔奸 = 1
			IF INROOM(CFLAG:MASTER:現在位置) == 2
				CALL DATUI_SHOES(LOCAL,0)
				CALL DATUI_SHOES(MASTER,0)
			ENDIF
		;合意未取得
		ELSEIF !GETBIT(CFLAG:C_ID:既成事實 , 1) && !TALENT:C_ID:合意誤認
			;未取得だと多人数は駄目
			IF NUM_TARGET > 1
				TFLAG:194 = 3
				TFLAG:不让推倒 ++
				BREAK
			;口上側による制御
			ELSEIF CFLAG:C_ID:口上内抱き寄せ判定_初回
				CALL KOJO_MESSAGE_SEND("PERMISSION", 1, C_ID)
				SELECTCASE RESULT
					CASE -1
						TCVAR:C_ID:初次拥抱 = 98
						TFLAG:不让推倒 ++
						BREAK
					CASE 0
						CALL 抱き寄せ(C_ID)
					CASE 1
						TCVAR:C_ID:初次拥抱 = 99
						SETBIT CFLAG:C_ID:既成事實 , 1
						CALL COLORMESSAGE(@"获得了和%CALLNAME:C_ID%的合意",C_YELLOW,2)
				ENDSELECT
			ELSE
				CALL 抱き寄せ(C_ID)
			ENDIF
		;合意取得済み
		ELSE
			IF FLAG:守矢神簽约会的对象 == C_ID || TALENT:C_ID:風騷 || CFLAG:C_ID:诶嘿嘿 || FLAG:催眠強度 > 74
				GOTO OK
			ELSEIF (BASE:C_ID:情緒 < 300 || TCVAR:MASTER:亲热 < 2)
				TFLAG:194 = 4
				TFLAG:不让推倒 ++
			;口上側による制御
			ELSEIF CFLAG:C_ID:口上内抱き寄せ判定_通常
				CALL KOJO_MESSAGE_SEND("PERMISSION", 2, C_ID)
				IF RESULT == -1
					TFLAG:194 = 100
					TFLAG:不让推倒 ++
				ELSE
					GOTO OK
				ENDIF
			ELSE
				$OK
				SIF BASE:C_ID:情緒 < 300
					BASE:C_ID:情緒 = 300
				CFLAG:C_ID:诶嘿嘿 = 1
				IF INROOM(CFLAG:MASTER:現在位置) == 2
					CALL DATUI_SHOES(LOCAL,0)
					CALL DATUI_SHOES(MASTER,0)
				ENDIF
			ENDIF
		ENDIF
	NEXT
ENDIF

IF TFLAG:不让推倒
	FOR LOCAL,1, NUM_TARGET + 1
		CALL ENDUFUFU(TARGET:LOCAL)
	NEXT
	DOWNBASE:MASTER:気力 = 50
	BASE:TARGET:情緒 /= 2
ELSE
;攻守交代
;ELSEIF CFLAG:MASTER:诶嘿嘿 == 1 && !FLAG:70
;	FOR LOCAL,0,CHARANUM
;		CFLAG:(TARGET:LOCAL):诶嘿嘿 == 2 
;	NEXT
;	TIME += 1
;	RETURN 1
	IF !FLAG:70
		FOR LOCAL,1, NUM_TARGET + 1
			CALL UWAKI_RECORD(TARGET:LOCAL, 1)
		NEXT
	ENDIF
	CFLAG:MASTER:诶嘿嘿 = 1
	SIF !FLAG:70
		TIME += 1
ENDIF
;経過時間表示用に開始時間を取っておく
IF !UFUFU_START_TIME
	UFUFU_START_TIME = TIME
	UFUFU_START_DAY = DAY
ENDIF
RETURN 1


;情人用宿屋情人旅館以外の出かけ先で诶嘿嘿するための特殊条件
@特殊诶嘿嘿条件(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 67
		SIF TCVAR:ARG:特殊事件 >= 4 && CFLAG:ARG:行動 != 5 && (GETBIT(CFLAG:ARG:既成事實 , 1) || TALENT:ARG:合意誤認) && TCVAR:ARG:亲热 >= 2 && CFLAG:ARG:現在位置 == 寺子屋 && GET_TARGETNUM() < 2
			RETURNF 1
ENDSELECT

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE350
#DIM NUM_TARGET
;妄想なので全通し
SIF TFLAG:妄想中 == 1 && CFLAG:诶嘿嘿 != 1
	RETURN 1
;推倒実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(350)
	RETURN RESULT
;時間停止中はスキップ
IF !FLAG:70
	;多人数
	NUM_TARGET = GET_TARGETNUM()
	SELECTCASE NUM_TARGET
	CASE 0
		RETURN 0
	CASE 1
	CASEELSE
		FOR LOCAL, 1, NUM_TARGET + 1
			;全角色に合意がないとダメ
			SIF !GETBIT(CFLAG:(TARGET:LOCAL):既成事實 , 1) && !TALENT:(TARGET:LOCAL):合意誤認
				RETURN 0
			;寝てる角色が居るとダメ
			SIF CFLAG:(TARGET:LOCAL):睡眠
				RETURN 0
			;酔ってる角色がいると駄目
			SIF TCVAR:(TARGET:LOCAL):烂醉
				RETURN 0
		NEXT
	ENDSELECT
	SIF TFLAG:接吻マーク != TARGET && TFLAG:接吻マーク
		RETURN 0
	;睡眠中
	SIF CFLAG:睡眠
		RETURN 0
	;陪睡中
	SIF CFLAG:陪睡中
		RETURN 0
	;仕事中
	IF JOBFUCKING == 0
		SIF CFLAG:行動 == 5 && !TCVAR:TARGET:烂醉
			RETURN 0
	ENDIF
	SIF TCVAR:随便
		RETURN 0
	;约会中
	SIF (CHK_DATENOW(CFLAG:MASTER:约会中) && CFLAG:現在位置 != 情人用宿屋 && CFLAG:現在位置 != 情人旅館 && CFLAG:現在位置 != OMANEKIBEYA()) && !特殊诶嘿嘿条件(TARGET)
		RETURN 0
	;SIF MONEY < 3000 && (CFLAG:現在位置 == 情人用宿屋 || CFLAG:現在位置 == 情人旅館)
		;RETURN 0
ENDIF
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
RETURN 1

;人目があるかどうか返す式中関数
;見てる角色がいると1､路人だと2
@EYEWITNESS()
#FUNCTION
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
		CONTINUE
	SIF CAN_MOVE(CFLAG:LOCAL:現在位置, CFLAG:MASTER:現在位置) & 2
		RETURNF 1
NEXT
SIF WITH_MOB()
	RETURNF 2
SIF DATE_HITOGOMI()
	RETURNF 2

RETURNF 0
