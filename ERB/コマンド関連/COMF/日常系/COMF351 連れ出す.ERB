;-------------------------------------------------
;帯出去
;日常系指令、レベル2
;-------------------------------------------------
@COM351
#DIM NUM_TARGET
#DIM N_TO_M
NUM_TARGET = GET_TARGETNUM()
N_TO_M = 0
;帯出去
VARSET LOCAL
SIF !NUM_TARGET
	RETURN -1
FOR LOCAL, 1, NUM_TARGET + 1
	IF COM_351_CONDITION(TARGET:LOCAL) >= 2
		LOCAL:97 += 1
	ENDIF
NEXT
IF NUM_TARGET == 1 || LOCAL:97 == 1
	IF COM_351_CONDITION(TARGET) < 1
		RETURN -1
	ELSEIF COM_351_CONDITION(TARGET) < 2
		DRAWLINE
		PRINTFORMW %CALLNAME:(TARGET)%正在忙碌中。
		RETURN -1
	ENDIF
	IF CFLAG:TARGET:同行
		CFLAG:TARGET:同行 = 0
		CFLAG:MASTER:同行 = 0
		PRINTFORMW 同行解除了。
	ELSE
		CFLAG:TARGET:同行 = 60
		;雨＋外＋傘or相手が傘の時は共用雨伞
		SIF TIME:5 > 3 && TIME:5 < 14 && OUTROOF(CFLAG:MASTER:現在位置) && (ITEM:36 || EQUIP:TARGET:飾品 == 6)
			TEQUIP:TARGET:201 = 1
		PRINTFORMW %CALLNAME:TARGET%跟着来了
		CFLAG:MASTER:同行 = 60
	ENDIF
	TIME += 1
	SIF CFLAG:TARGET:同行
		CALL KOJO_MESSAGE_SEND("DIRECT",351,TARGET,-1,-1)
	RETURN 1
ENDIF

DO
	PRINTL 要邀请誰呢？
	FOR LOCAL, 1, NUM_TARGET + 1
		SIF COM_351_CONDITION(TARGET:LOCAL) <= 0
			CONTINUE
		IF CFLAG:(TARGET:LOCAL):同行
			SETCOLOR 255, 200, 100
		ELSEIF CFLAG:(TARGET:LOCAL):同行準備
			SETCOLOR C_AQUA
		ENDIF
		PRINTFORML [{LOCAL}] - %CALLNAME:(TARGET:LOCAL)%
		RESETCOLOR
	NEXT
	PRINTL [97] - 带出去
	PRINTL [98] - 全員带出去
	PRINTL [99] - 同行解除
	PRINTL [100] - 中止

	INPUT
	SELECTCASE RESULT
	CASE 100
		FOR LOCAL, 1, NUM_TARGET + 1
			CFLAG:(TARGET:LOCAL):同行準備 = 0
		NEXT
		RETURN -1
	CASE 99
		FOR LOCAL, 1, NUM_TARGET + 1
			CFLAG:(TARGET:LOCAL):同行 = 0
			CFLAG:(TARGET:LOCAL):同行準備 = 0
			;共用雨伞もなくなる
			TEQUIP:(TARGET:LOCAL):共用雨伞 = 0
		NEXT
		PRINTFORMW 同行解除了
		CFLAG:MASTER:同行 = 0
		RETURN 1
	CASE 98
		FOR LOCAL, 1, NUM_TARGET + 1
			SIF COM_351_CONDITION(TARGET:LOCAL) == 2
				N_TO_M = 1
		NEXT
		IF N_TO_M > 0
			PRINTFORMW 同時に連れ出せない角色がいます
			N_TO_M = 0
			CONTINUE
		ENDIF
		FOR LOCAL, 1, NUM_TARGET + 1
			IF COM_351_CONDITION(TARGET:LOCAL) >= 3
				CFLAG:(TARGET:LOCAL):同行 = 60
				LOCAL:98 += 1
			ENDIF
		NEXT
		PRINTFORMW 把{LOCAL:98}个人一起带出去了
		CFLAG:MASTER:同行 = 60
		TIME += LOCAL:98
		SIF CFLAG:TARGET:同行
			CALL KOJO_MESSAGE_SEND("DIRECT",351,TARGET,-1,-1)
		RETURN 1
	CASE 97
		FOR LOCAL, 1, NUM_TARGET + 1
			IF (COM_351_CONDITION(TARGET:LOCAL) >= 2) && (CFLAG:(TARGET:LOCAL):同行準備 || CFLAG:(TARGET:LOCAL):同行)
				LOCAL:98 += 1
				SIF COM_351_CONDITION(TARGET:LOCAL) == 2
					N_TO_M = 1
			ENDIF
		NEXT
		SIF LOCAL:98 == 0
			CONTINUE
		IF LOCAL:98 == 1
			TIME += 1
			LOCAL = 0
			LOCAL:98 = 0
			FOR LOCAL, 1, NUM_TARGET + 1
				IF (COM_351_CONDITION(TARGET:LOCAL) >= 2) && (CFLAG:(TARGET:LOCAL):同行準備 || CFLAG:(TARGET:LOCAL):同行)
					PRINTFORMW 将%CALLNAME:(TARGET:LOCAL)%带上了
					CFLAG:(TARGET:LOCAL):同行 = 60
					CFLAG:(TARGET:LOCAL):同行準備 = 0
					;雨＋外＋傘or相手が傘の時は共用雨伞
					SIF TIME:5 > 3 && TIME:5 < 14 && OUTROOF(CFLAG:MASTER:現在位置) && (ITEM:36 || EQUIP:(TARGET:LOCAL):飾品 == 6)
						TEQUIP:(TARGET:LOCAL):共用雨伞 = 1
				ENDIF
			NEXT
		ELSE
			IF N_TO_M > 0
				PRINTFORMW 同時に連れ出せない角色がいます
				N_TO_M = 0
				CONTINUE
			ENDIF
			PRINTFORMW 把{LOCAL:98}个人一起带出去了
			TIME += LOCAL:98
			LOCAL = 0
			LOCAL:98 = 0
			FOR LOCAL, 1, NUM_TARGET + 1
				IF (COM_351_CONDITION(TARGET:LOCAL) >= 3) && (CFLAG:(TARGET:LOCAL):同行準備 || CFLAG:(TARGET:LOCAL):同行)
					CFLAG:(TARGET:LOCAL):同行 = 60
					CFLAG:(TARGET:LOCAL):同行準備 = 0
				ENDIF
			NEXT
		ENDIF
		CFLAG:MASTER:同行 = 60
		SIF CFLAG:TARGET:同行
			CALL KOJO_MESSAGE_SEND("DIRECT",351,TARGET,-1,-1)
		RETURN 1
	CASEELSE
		IF !INRANGE(RESULT, 1, NUM_TARGET)
			CLEARLINE 1
			CONTINUE
		ELSEIF COM_351_CONDITION(TARGET:RESULT) < 1
			CLEARLINE 1
			CONTINUE
		ELSEIF COM_351_CONDITION(TARGET:RESULT) < 2
			PRINTFORMW %CALLNAME:(TARGET:RESULT)%正在忙碌中。
			CLEARLINE 2
			CONTINUE
		ENDIF
		CFLAG:(TARGET:RESULT):同行準備 ^= 1
	ENDSELECT
LOOP 1

@EQUIP_COM351(ARG)
;ソースは控えめに適当。愛情入りすぎたので自重
IF FLAG:70 == 1

ELSE
	SOURCE:歓楽 += ABL:親密 * 5
	SOURCE:情愛 += ABL:親密 * 5
	SOURCE:反感 += 100 - MIN(ABL:親密 * 10,100)
	SIF ABL:親密 > 9 && TIME_PROGRESS(TIME:1) > 10
	EXP:ARG:愛情経験 += RAND:2
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE351
;停止中は不可
SIF FLAG:70
	RETURN 0
;帯出去実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(351)
	RETURN RESULT
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF CFLAG:诶嘿嘿
	RETURN 0
;约会中
SIF CHK_DATENOW(CFLAG:MASTER:约会中)
	RETURN 0
SIF IN_HOME(CFLAG:現在位置) == 0
	RETURN 0
SIF COM_351_CONDITION(TARGET) == 0
	RETURN 0
;;TARGETの亲热強度1
;FOR LOCAL,0,CHARANUM
;	;TARGET:1～をさがす
;	SIF !TARGET:LOCAL
;		CONTINUE
;	;陪睡中
;	SIF CFLAG:(TARGET:LOCAL):陪睡中
;		CONTINUE
;	;仕事中は除外
;	SIF CFLAG:(TARGET:LOCAL):行動 == 5 && CFLAG:(TARGET:LOCAL):350 != 49 && CFLAG:(TARGET:LOCAL):350 != 51
;		CONTINUE
;	;可能な角色が居れば1を返す
;	SIF COM_351_CONDITION(TARGET:LOCAL) > 0
;		RETURN 1
;NEXT
RETURN 1



;戻り値
; 3 まとめて連れ出せる
; 2 個別選択で連れ出せる
; 1 連れ出せない（取り込み中メッセージ）
; 0 連れ出せない
@COM_351_CONDITION(CHARA)
#FUNCTION
#DIM CHARA
SIF CHARA == MASTER
	RETURNF 0
SIF CFLAG:CHARA:態度 > 0 && CFLAG:CHARA:睡眠 == 0 && CFLAG:CHARA:行動 != 5
	RETURNF 3

;現在同行中の角色は許可(同行条件を満たさなくなった場合に手動で同行解除できるようにする)
SIF CFLAG:CHARA:同行
	RETURNF 2
SIF CFLAG:CHARA:败犬
	RETURNF 2
SIF CFLAG:CHARA:態度 < 1 && TCVAR:CHARA:烂醉 == 0 && !CAN_IRAI_CHARA_GO_TOGATHER(CHARA)
	RETURNF 0
SIF CFLAG:CHARA:睡眠
	RETURNF 0
SIF CFLAG:CHARA:行動 == 5 && !TCVAR:CHARA:烂醉
	RETURNF 1

RETURNF 2