;採集
@COM445
#DIM 採集物
#DIM 採集パワー
#DIM 採集パワーM
#DIM 採集パワーT
#DIM 土地の恵み
#DIM CHARA
#DIM 採集位置
;あからさまな人の敷地での無断採集は放棄、弾幕勝負で勝ってれば腕づくで
IF FLAG:催眠強度 <= 50
	SELECTCASE CFLAG:MASTER:現在位置
		CASE 太陽花田, 438
			IF ABL:[[幽香]]:親密 < 5 && CFLAG:[[幽香]]:弾幕勝負勝利 == 0
				DRAWLINE
				PRINTFORML 感觉在这里擅自採集的话非常危険…
				RETURN -1
			ENDIF
		;天界、桃園
		CASE 天界, 833
			IF ABL:[[天子]]:親密 < 5 && CFLAG:[[天子]]:弾幕勝負勝利 == 0
				DRAWLINE
				PRINTFORML 感觉在这里擅自採集的话会陷入麻烦…
				RETURN -1
			ENDIF
		;虹龍洞
		CASE 虹龍洞, 855
			IF ABL:[[百百世]]:親密 < 5 && CFLAG:[[百百世]]:弾幕勝負勝利 == 0
				DRAWLINE
				PRINTFORML 感觉在这里擅自採集的话会陷入麻烦…
				RETURN -1
			ENDIF
	ENDSELECT
ENDIF

採集物 = 0
採集パワーM = 0
採集パワーT = 0
LOCAL:4 = 0

;参加者判定
IF FLAG:约会的对象 && !FLAG:70
	CHARA = FLAG:约会的对象
ELSEIF TARGET && ABL:TARGET:親密 >= 5 && SHIRAHU(TARGET) && CFLAG:TARGET:行動 != 5
	CHARA = TARGET
ELSE
	CHARA = 0
ENDIF

;土地の恵み。日替わりで-25～+25の間を随机で浮動。釣魚を参考に。
土地の恵み = CFLAG:MASTER:今日の運勢 / 20 - 25

SIF ITEM:三粒の天滴
	土地の恵み += 5

;採集パワー設定
採集パワーM = 10 + TALENT:MASTER:採集Lv * 10 + ABL:MASTER:教養 * 5 + TFLAG:幸運補正 * 10
SIF ITEM:108
	採集パワーM += 10

SIF CHARA && !FLAG:70
	採集パワーT = 10 + TALENT:CHARA:採集Lv * 10 + ABL:CHARA:教養 * 5
;参加者で最もパワーが高いものを基準に、1/2したもう１人のパワーを足す
IF 採集パワーM >= 採集パワーT
	採集パワー = 採集パワーM + (採集パワーT / 2)
ELSE
	採集パワー = 採集パワーT + (採集パワーM / 2)
ENDIF
採集位置 = GATHERING_SPOT(CFLAG:MASTER:現在位置)
REPEAT 5
	REUSELASTLINE  \@ CHARA ? 和%CALLNAME:CHARA%一起 #  \@採集中.%"." * COUNT%
	TWAIT 150, 1
REND
;デ背包用
;PRINTFORML 採集位置{採集位置}
;PRINTFORML 採集パワー{採集パワー}

$TREASURSARCH

CALL Gathering_Drawing(採集位置, 採集パワー, 土地の恵み)

採集物 = RESULT:0
TFLAG:194 = RESULT:1
VARSET RESULT
;試行回数を１回増やす
LOCAL:4 ++
;採集物が気に入らない場合は引き直し
IF 採集物 != 0
	IF !FLAG:採集再抽選
		CLEARLINE 3
	ELSE
		SETCOLOR 0x00FF00
		PRINTFORM 发现了%ITEMNAME:(採集物)%
		RESETCOLOR
		PRINTFORML (所持数：{ITEM:(採集物)})。要採集吗？
		CALL ASK_YN("採集","再次探索")
		IF !RESULT
			CLEARLINE 4
		ELSE
			CLEARLINE 4
			採集物 = 0
			TFLAG:194 = 0
			REPEAT 5
				REUSELASTLINE  \@ CHARA ? 和%CALLNAME:CHARA%一起 #  \@採集中.%"." * COUNT%
				TWAIT 150, 1
			REND
		ENDIF
	ENDIF
ENDIF

;試行回数３回以下で採集物が確保されていない場合はループ
SIF LOCAL:4 <= 3 && 採集物 == 0
	GOTO TREASURSARCH

;道具入手処理 99個以上にならないようにする
IF 採集物 != 0
	LOCALS = %ITEMNAME:採集物%
	CALL GET_ITEM(LOCALS, TFLAG:194 , 2)
ENDIF

;基本の経験
EXP:MASTER:採集経験 ++
SIF !採集物
	EXP:MASTER:採集経験 += 2
SIF CHARA
	EXP:CHARA:採集経験 ++

SIF ITEM:108 && !RAND:2
	EXP:MASTER:採集経験 ++

SIF ABL:MASTER:教養 <= 2
	EXP:MASTER:学習経験 ++

;能力差で教えてもらう・進行解答
IF 採集パワーM >= 採集パワーT && CHARA
	EXP:CHARA:採集経験 ++
ELSEIF CHARA
	EXP:MASTER:採集経験 ++
ENDIF

;約會経験
IF CHK_DATENOW(CFLAG:CHARA:约会中) && CHARA > 0
	EXP:MASTER:約會経験 ++
	EXP:CHARA:約會経験 ++
ENDIF

;時間停止時はTSP減
IF FLAG:70
	;虹龍洞は採掘困難
	IF 採集位置 == 68
		BASE:MASTER:TSP -= 290
	ELSE
		BASE:MASTER:TSP -= 120
	ENDIF
ELSE
	;時間経過
	TIME += 60
	;虹龍洞は空気が单薄的（無い）ので体力を余計に消耗する　ITEM:空色の勾玉の実装が待たれる
	IF 採集位置 == 68
		DOWNBASE:MASTER:体力 = 320 - (ABL:MASTER:戦闘能力 * 10)
		DOWNBASE:MASTER:気力 = 280
		PRINTFORML 坑内は空気が薄く、ひどく疲惫…
	ELSE
		;体力減少は戦闘能力によって減少抑制
		DOWNBASE:MASTER:体力 = 180 - (ABL:MASTER:戦闘能力 * 15)
		DOWNBASE:MASTER:気力 = 150 
	ENDIF
ENDIF

;参加者のソース,同一指令制限を受けないので控えめに,見つからないと半減
IF CHARA > 0
	;固定で獲得するソース
	SOURCE:CHARA:歓楽 = 300
	SOURCE:CHARA:情愛 = 50
	
	;ABL:従順をみる
	IF ABL:従順 <= 1
		SOURCE:CHARA:歓楽 += (ABL:従順 * 40)
		SOURCE:CHARA:情愛 += (ABL:親密 * 20)
	ELSEIF ABL:従順 <= 3
		SOURCE:CHARA:歓楽 += 200 + (ABL:従順 * 40)
		SOURCE:CHARA:情愛 += 50 + (ABL:親密 * 30)
	ELSEIF ABL:従順 <= 5
		SOURCE:CHARA:歓楽 += 500 + (ABL:従順 * 40)
		SOURCE:CHARA:情愛 += 200 + (ABL:親密 * 30)
	ELSEIF ABL:従順 <= 8
		SOURCE:CHARA:歓楽 += 750 + (ABL:従順 * 60)
		SOURCE:CHARA:情愛 += 300 + (ABL:親密 * 40)
	ELSEIF ABL:従順 <= 11
		SOURCE:CHARA:歓楽 += 1000 + (ABL:従順 * 60)
		SOURCE:CHARA:情愛 += 600 + (ABL:親密 * 40)
	ELSE
		SOURCE:CHARA:歓楽 += 1600 + (ABL:従順 * 30)
		SOURCE:CHARA:情愛 += 1000 + (ABL:親密 * 20)
	ENDIF
	SIF CHARA != FLAG:约会的对象 
		SOURCE:CHARA:情愛 = 0
	
	;好感度はみない
	
	SOURCE:CHARA:受動 = 200 + 100 * ABL:従順
	SOURCE:CHARA:征服 = 200 + 100 * ABL:施虐属性
	
	;採集成果をみる、道具価格に応じてとりあえず設定
	IF 採集物
		SELECTCASE ITEMPRICE:(採集物)
			CASE 1 TO 499
				TIMES SOURCE:CHARA:歓楽 , 1.00
				TIMES SOURCE:CHARA:受動 , 1.00
				TIMES SOURCE:CHARA:征服 , 1.00
				SOURCE:CHARA:達成 = 80
			CASE 500 TO 999
				TIMES SOURCE:CHARA:歓楽 , 1.10
				TIMES SOURCE:CHARA:受動 , 1.10
				TIMES SOURCE:CHARA:征服 , 1.10
				SOURCE:CHARA:達成 = 115
			CASE 1000 TO 2999
				TIMES SOURCE:CHARA:歓楽 , 1.25
				TIMES SOURCE:CHARA:受動 , 1.25
				TIMES SOURCE:CHARA:征服 , 1.15
				SOURCE:CHARA:達成 = 125
			CASE 3000 TO 5999
				TIMES SOURCE:CHARA:歓楽 , 1.50
				TIMES SOURCE:CHARA:受動 , 1.50
				TIMES SOURCE:CHARA:征服 , 1.25
				SOURCE:CHARA:達成 = 150
			CASE 6000 TO 9999
				TIMES SOURCE:CHARA:歓楽 , 1.75
				TIMES SOURCE:CHARA:受動 , 1.75
				TIMES SOURCE:CHARA:征服 , 1.50
				SOURCE:CHARA:達成 = 200
			CASE 10000 TO 14999
				TIMES SOURCE:CHARA:歓楽 , 2.00
				TIMES SOURCE:CHARA:受動 , 2.00
				TIMES SOURCE:CHARA:征服 , 1.75
				SOURCE:CHARA:達成 = 300
			CASEELSE
				TIMES SOURCE:CHARA:歓楽 , 2.25
				TIMES SOURCE:CHARA:受動 , 2.25
				TIMES SOURCE:CHARA:征服 , 2.00
				SOURCE:CHARA:達成 = 350
		ENDSELECT
	;見つからない
	ELSE
		TIMES SOURCE:CHARA:歓楽 , 0.50
		TIMES SOURCE:CHARA:受動 , 0.50
		TIMES SOURCE:CHARA:征服 , 0.50
	ENDIF
	;採集物の角色に応じた固有反応（上書き）
	SELECTCASE 採集物
		;青蛙
		CASE 706
		;琪露諾
			IF CHARA == [[琪露諾]]
				TIMES SOURCE:CHARA:歓楽 , 2.00
				TIMES SOURCE:CHARA:受動 , 2.00
				TIMES SOURCE:CHARA:征服 , 2.00
			;諏訪子
			ELSEIF CHARA == [[諏訪子]]
				TIMES SOURCE:CHARA:歓楽 , 0.50
				TIMES SOURCE:CHARA:受動 , 0.50
				TIMES SOURCE:CHARA:征服 , 0.50
			ELSE
				TIMES SOURCE:CHARA:歓楽 , 1.25
				TIMES SOURCE:CHARA:受動 , 1.00
				TIMES SOURCE:CHARA:征服 , 1.00
			ENDIF
			SOURCE:CHARA:達成 = 110
	ENDSELECT
ENDIF

;フラグ引き渡し
TFLAG:SELECTCOM的分岐 = 採集物

RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;採集
@COM_ABLE445
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(445)
	RETURN RESULT
;採集できる場所である
SIF !GATHERING_SPOT(CFLAG:MASTER:現在位置)
	RETURN 0
;约会中は约会的对象とだけ
SIF FLAG:约会的对象 && TARGET != FLAG:约会的对象
	RETURN 0
;睡眠中
SIF CFLAG:MASTER:睡眠
	RETURN 0
SIF CFLAG:诶嘿嘿
	RETURN 0
;体力・気力不足
SIF BASE:MASTER:気力 < 650 || BASE:MASTER:体力 < 680
	RETURN 0
;時間停止中も可能（仮）
;SIF FLAG:70
;	RETURN 0
RETURN 1

;-----------------------------------------------------------
;現在位置から採集スポットを割り出す関数
;ARG		現在位置
;ARG:1		@ShowGatheringSpotsからの参照用
;RETURN		0 該当無	1～30 森、草原	31～60 水系		61～ 特殊
;COMMENT	場所を追加した場合、CASEを変更の事
;-----------------------------------------------------------
@GATHERING_SPOT(ARG, ARG:1)
#FUNCTION
IF !IN_HOME(ARG) && !ARG:1
;サブMapなどでの判定
	SELECTCASE ARG
		CASE 妖精的大樹
			RETURNF 1
		CASE 斜角的竹林
			RETURNF 2
		CASE 無名之丘
			RETURNF 3
		CASE 太陽花田
			RETURNF 4
		CASE 再思之道
			RETURNF 5
		CASE 古木的大樹
			RETURNF 6
		CASE 迷途之家
			RETURNF 7
		CASE 彼岸
			RETURNF 8
		CASE 山麓樹海
			RETURNF 11
		CASE 絶景之丘
			RETURNF 12
		CASE 天界
			RETURNF 13
		CASE 桃林～砂浜
			RETURNF 14
		CASE 霧之湖
			RETURNF 31
		CASE 三途之川
			RETURNF 32
		CASE 玄武之澤
			RETURNF 33
		CASE 大蛤蟆之池
			RETURNF 34
		CASE 九天瀑布
			RETURNF 35
		CASE 山之湖
			RETURNF 36
		CASE 地獄的深道
			RETURNF 37
		CASE 墓地
			RETURNF 61
		CASE 無縁塚
			RETURNF 62
		CASE 間欠泉地下中樞
			RETURNF 64
		CASE 灼熱地獄跡
			RETURNF 65
		CASE 幻想風穴
			RETURNF 66
		CASE 寧靜海
			RETURNF 67
		CASE 虹龍洞
			RETURNF 68
	ENDSELECT
ELSE
	;メインMapでの判定
	SELECTCASE ARG
		;博霊神社
		CASE P25鎮守之森
			RETURNF 1
		;命蓮寺
		CASE P121墓地
			RETURNF 61
		;紅魔館
		CASE P343湖畔
			RETURNF 31
		;永遠亭
		CASE P433斜角的竹林
			RETURNF 2
		CASE P436無名之丘
			RETURNF 3
		CASE P438太陽花田
			RETURNF 4
		;魔法之森
		CASE P506無縁塚
			RETURNF 62
		CASE P507菌菇群生地
			RETURNF 63
		CASE P509再思之道
			RETURNF 5
		CASE P540古木的大樹
			RETURNF 6
		;冥界
		CASE P604彼岸
			RETURNF 8
		CASE P630八雲邸玄関
			RETURNF 7
		;妖怪之山山麓
		CASE P702山道・通往沼澤之道
			RETURNF 9
		CASE P703玄武之澤
			RETURNF 33
		CASE P705山道・中腹
			RETURNF 10
		CASE P708大蛤蟆之池
			RETURNF 34
		CASE P712間欠泉地下中樞
			RETURNF 64
		;妖怪之山山頂
		CASE P805頂上
			RETURNF 12
		CASE P806瀑布背面
			RETURNF 35
		CASE P814湖畔
			RETURNF 36
		CASE P833桃園
			RETURNF 13
		CASE P855虹龍洞
			RETURNF 68
		;地底
		CASE P928灼熱地獄跡
			RETURNF 65
		CASE P945川岸
			RETURNF 37
		CASE P948縦穴底部
			RETURNF 66
		;月
		CASE P1014桃林, P1015砂浜
			RETURNF 14
		CASE P1016入海口
			RETURNF 67
	ENDSELECT
ENDIF
RETURNF 0

;-------------------------------------------------
;その採集スポットでの採集物の出現判定をする関数
;道具番号ARG:0
;GATHERING_SPOT(ARG)の返り値ARG:1（採集スポット）  の
;出現可否を数字で返す 出現==1 非出現==0
;道具を追加した場合は、GROUPMATCH内にそれぞれ追記
;-------------------------------------------------
@GATHERING_PRODUCT(ARG:0, ARG:1)
#FUNCTION

;(酒造パッチ)山葡萄665・发霉葡萄666を追加
SELECTCASE ARG:1
	;鎮守之森、妖精的大樹
	CASE 1
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 607, 608, 611, 612, 616, 619, 621, 622, 626, 627, 628, 636, 665, 666)
			RETURNF 1
	;斜角的竹林
	CASE 2
		SIF GROUPMATCH(ARG:0, 607, 608, 611, 612, 613, 618, 619, 629, 635, 640)
			RETURNF 1
	;無名之丘
	CASE 3
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 603, 612, 616, 620, 626, 627, 631, 636, 638, 665, 666)
			RETURNF 1
	;太陽花田
	CASE 4
		SIF GROUPMATCH(ARG:0, 612, 613, 614, 615, 617, 618, 619, 620, 623, 624, 626, 627, 665, 666)
			RETURNF 1
	;再思之道
	CASE 5
		SIF GROUPMATCH(ARG:0, 612, 614, 619, 624, 631, 632, 638)
			RETURNF 1
	;古木的大樹
	CASE 6
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 603, 604, 605, 607, 608, 611, 612, 616, 618, 621, 622, 623, 625, 626, 627, 628, 665, 666)
			RETURNF 1
	;迷途之家
	CASE 7
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 603, 607, 608, 611, 612, 616, 617, 619, 621, 622, 626, 627, 628, 665, 666)
			RETURNF 1
	;彼岸
	CASE 8
		SIF GROUPMATCH(ARG:0, 600, 601, 607, 608, 612, 614, 620, 624, 638)
			RETURNF 1
	;山道・通往沼澤之道
	CASE 9
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 607, 608, 611, 612, 616, 617, 621, 622, 626, 627, 628, 636, 665, 666)
			RETURNF 1
	;山道・中腹
	CASE 10
		SIF GROUPMATCH(ARG:0, 611, 613, 616, 619, 621, 622, 623, 633)
			RETURNF 1
	;山麓樹海
	CASE 11
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 607, 608, 611, 612, 613, 616, 617, 619, 621, 622, 623, 626, 627, 628, 633, 665, 666)
			RETURNF 1
	;絶景之丘、頂上
	CASE 12
		SIF GROUPMATCH(ARG:0, 602, 603, 607, 608, 611, 612, 615, 616, 619, 621, 622, 625, 626, 627, 628, 665, 666)
			RETURNF 1
	;天界、桃園
	CASE 13
		SIF GROUPMATCH(ARG:0, 617, 623, 630)
			RETURNF 1
	;桃林～砂浜
	CASE 14
		SIF GROUPMATCH(ARG:0, 607, 608, 613, 617, 618, 624, 625, 630)
			RETURNF 1
	;霧之湖、湖畔(紅魔館)
	CASE 31
		SIF GROUPMATCH(ARG:0, 606, 607, 608, 609, 611, 612, 617, 624, 633, 706)
			RETURNF 1
	;三途之川
	CASE 32
		SIF GROUPMATCH(ARG:0, 607, 608, 612, 614, 631, 638, 706)
			RETURNF 1
	;玄武之澤
	CASE 33
		SIF GROUPMATCH(ARG:0, 606, 607, 609, 611, 612, 618, 635, 706)
			RETURNF 1
	;大蛤蟆之池
	CASE 34
		SIF GROUPMATCH(ARG:0, 604, 612, 620, 631, 636, 706)
			RETURNF 1
	;九天瀑布、瀑布背面
	CASE 35
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 606, 609, 611, 613, 631, 634, 706)
			RETURNF 1
	;山之湖、湖畔（妖怪之山山頂）
	CASE 36
		SIF GROUPMATCH(ARG:0, 606, 607, 609, 611, 612, 618, 632, 706)
			RETURNF 1
	;地獄的深道、川岸
	CASE 37
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 604, 606, 609, 620, 631, 633, 636, 706)
			RETURNF 1
	;墓地
	CASE 61
		SIF GROUPMATCH(ARG:0, 601, 602, 604, 614, 620, 631, 636, 638)
			RETURNF 1
	;無縁塚
	CASE 62
		SIF GROUPMATCH(ARG:0, 604, 606, 609, 631, 634, 635, 636, 638)
			RETURNF 1
	;菌菇群生地
	CASE 63
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 603, 604, 605, 605, 629)
			RETURNF 1
	;間欠泉地下中樞
	CASE 64
		SIF GROUPMATCH(ARG:0, 632, 634, 637)
			RETURNF 1
	;灼熱地獄跡
	CASE 65
		SIF GROUPMATCH(ARG:0, 631, 634, 635, 637, 638)
			RETURNF 1
	;幻想風穴、縦穴底部
	CASE 66
		SIF GROUPMATCH(ARG:0, 602, 604, 631, 632)
			RETURNF 1
	;寧靜海
	CASE 67
		SIF GROUPMATCH(ARG:0, 639)
			RETURNF 1
	;虹龍洞
	CASE 68
		SIF GROUPMATCH(ARG:0, 647)
			RETURNF 1
ENDSELECT
RETURNF 0

;-------------------------------------------------
;採集物の季節ごとの出現判定
;道具番号ARG:0の季節ごとの
;出現可否を数字で返す関数 出現==1 非出現==0
;道具を追加した場合は、CASEに追記
;ARG:1 に採集地点を入れると、以下の列表用の式に変換 省略可
;-------------------------------------------------
@GATHERING_SEASON(ARG:0, ARG:1 = 0)
#FUNCTION

LOCAL = DAY:2
SIF GatheringSeason
	LOCAL = GatheringSeason

;(酒造パッチ)山葡萄665・发霉葡萄666を追加
SELECTCASE LOCAL
	;春:
	CASE 1
		SELECTCASE ARG:0
			CASE 600 TO 602, 604 TO 608, 610 TO 613, 616, 618 TO 627, 629, 631 TO 640, 647, 706
				RETURNF 1
		ENDSELECT
	;夏
	CASE 2
		SELECTCASE ARG:0
			CASE 600 TO 601, 603, 606 TO 608, 612 TO 613, 618 TO 627, 630 TO 640, 647, 665, 706
				RETURNF 1
		ENDSELECT
	;秋
	CASE 3
		SELECTCASE ARG:0
			CASE 600 TO 608, 611 TO 614, 616 TO 623, 625 TO 628, 631 TO 640, 647, 665, 706
				RETURNF 1
		ENDSELECT
		;(酒造パッチ)(天候パッチ)发霉葡萄666は寒気の強さが5の時のみ
		SIF ARG:0 == 666 && POWER_COLD_AIR == 5
			RETURNF 1
	;冬
	CASE 4
		;天界、地底、月と菌菇群生地、虹龍洞は夏とみ無て採集可能
		IF ARG:1
			LOCAL:1 =ARG:1
		ELSE
			LOCAL:1 = GATHERING_SPOT(CFLAG:MASTER:現在位置)
		ENDIF

		IF GROUPMATCH(LOCAL:1, 13, 14, 37, 63, 64, 65, 66, 67, 68)
			SELECTCASE ARG:0
				CASE 600 TO 601, 603, 606 TO 609, 612 TO 613, 618 TO 627, 630 TO 640, 647, 706
					RETURNF 1
			ENDSELECT
		ELSE
			SELECTCASE ARG:0
				CASE 606, 609, 615, 620, 631 TO 635, 637 TO 640
					RETURNF 1
			ENDSELECT
		ENDIF
ENDSELECT
RETURNF 0

@SHOW_GATHERING_LIST
#DIM PLACE
PRINTFORML 採集場所一覧
CALL COLORMESSAGE(@"中級素材　",C_YELLOW)
CALL COLORMESSAGE(@"高級素材　",C_P_GREEN)
CALL COLORMESSAGE(@"最高級素材",C_AQUA)
PRINTFORML 
DRAWLINE
FOR LOCAL,1, 90
	IF ForagePlaceName(LOCAL) != ""
		PRINTFORMLC [{LOCAL}] %ForagePlaceName(LOCAL)%
	ENDIF
NEXT
PRINTFORML 
DRAWLINE
PRINTFORML [99] 返回
INPUT
PLACE = RESULT
IF ForagePlaceName(PLACE) != ""
	CALL SHOW_GATHERING_ITEM(PLACE)
	PRINTFORMW 
	CLEARLINE 1
	RESTART
ELSE	
	RETURN -1
ENDIF

@SHOW_GATHERING_LIST_SELECT(PLACE)
#DIM PLACE
DRAWLINE
PRINTFORML 採集可能物一覧
CALL COLORMESSAGE(@"中級素材　",C_YELLOW)
CALL COLORMESSAGE(@"高級素材　",C_P_GREEN)
CALL COLORMESSAGE(@"最高級素材",C_AQUA)
PRINTFORML 
DRAWLINE

SELECTCASE PLACE
	CASE 99
		RETURN -1
	CASEELSE
		CALL SHOW_GATHERING_ITEM(PLACE)
		PRINTFORML
		DRAWLINE
		PRINTFORMW
ENDSELECT

@SHOW_GATHERING_ITEM(PLACE)
#DIM PLACE

FOR LOCAL,600,710
	IF GATHERING_PRODUCT(LOCAL, PLACE)
		LOCALS = %ITEMNAME:LOCAL%
		IF ITEMPRICE:LOCAL >= 1000
			SETCOLOR C_YELLOW
			SIF ABL:MASTER:教養 < 1
				LOCALS = ？？？
		ENDIF
		IF ITEMPRICE:LOCAL >= 3000
			SETCOLOR C_P_GREEN
			SIF ABL:MASTER:教養 < 2
				LOCALS = ？？？
		ENDIF
		IF ITEMPRICE:LOCAL >= 10000
			SETCOLOR C_AQUA
			SIF ABL:MASTER:教養 < 3
				LOCALS = ？？？
		ENDIF
		;季節条件を満たしていない場合は灰色表記に
		SIF !GATHERING_SEASON(LOCAL, PLACE)
			SETCOLOR 0x606060

		PRINTFORM %LOCALS%　
	ENDIF
	RESETCOLOR
NEXT

;-----------------------------
;引数で指定した価格範囲内の素材の中から随机に入手
;-----------------------------
@GetRandMaterial(QYT, PriceRange:0, PriceRange:1)
#DIM PriceRange, 2
#DIM cArray, 100
#DIM cNumber
#DIM Chosen
#DIM QYT
cNumber = 0
FOR LOCAL, 600, 700
	SIF ITEMNAME:LOCAL == ""
		CONTINUE
	;道具価格上限
	SIF ITEMPRICE:LOCAL > PriceRange:0
		CONTINUE
	;下限は省略可
	SIF ITEMPRICE:LOCAL < PriceRange:1
		CONTINUE
	SIF ITEM:LOCAL >= 99
		CONTINUE
	cArray:cNumber = LOCAL
	cNumber ++
NEXT
SIF !cNumber
	RETURN

FOR LOCAL,0,QYT
	Chosen = RAND:cNumber
	ITEM:(cArray:Chosen) ++
	CALL COLORMESSAGE(@"获得了%ITEMNAME:(cArray:Chosen)%",C_YELLOW,1)
NEXT

@ForagePlaceName(ARG)
#FUNCTIONS
SELECTCASE ARG
CASE 1
	IF MAIN_MAP == 0
		LOCALS = 鎮守之森
	ELSE
		LOCALS = 妖精的大樹
	ENDIF
CASE 2
	LOCALS = 斜角的竹林
CASE 3
	LOCALS = 無名之丘
CASE 4
	LOCALS = 太陽花田
CASE 5
	LOCALS = 再思之道
CASE 6
	LOCALS = 古木的大樹
CASE 7
	IF MAIN_MAP == 6
		LOCALS = 八雲邸玄関
	ELSE
		LOCALS = 迷途之家
	ENDIF
CASE 8
	LOCALS = 彼岸
CASE 9
	LOCALS = 山道・通往沼澤之道
CASE 10
	LOCALS = 山道・中腹
CASE 11
	LOCALS = 山麓樹海
CASE 12
	IF MAIN_MAP == 8
		LOCALS = 頂上
	ELSE
		LOCALS = 絶景之丘
	ENDIF
CASE 13
	IF MAIN_MAP == 8
		LOCALS = 桃園
	ELSE
		LOCALS = 天界
	ENDIF
CASE 14
	LOCALS = 桃林～砂浜
CASE 31
	IF MAIN_MAP == 3
		LOCALS = 湖畔(紅魔館)
	ELSE
		LOCALS = 霧之湖
	ENDIF
CASE 32
	LOCALS = 三途之川
CASE 33
	LOCALS = 玄武之澤
CASE 34
	LOCALS = 大蛤蟆之池
CASE 35
	IF MAIN_MAP == 8
		LOCALS = 瀑布背面
	ELSE
		LOCALS = 九天瀑布
	ENDIF
CASE 36
	IF MAIN_MAP == 8
		LOCALS = 湖畔（妖怪之山山頂）
	ELSE
		LOCALS = 山之湖
	ENDIF
CASE 37
	IF MAIN_MAP == 9
		LOCALS = 川岸（地底）
	ELSE
		LOCALS = 地獄的深道
	ENDIF
CASE 61
	LOCALS = 墓地
CASE 62
	LOCALS = 無縁塚
CASE 63
	LOCALS = 菌菇群生地
CASE 64
	LOCALS = 間欠泉地下中樞
CASE 65
	LOCALS = 灼熱地獄跡
CASE 66
	IF MAIN_MAP == 9
		LOCALS = 縦穴底部
	ELSE
		LOCALS = 幻想風穴
	ENDIF
CASE 67
	LOCALS = 寧靜海
CASE 68
	LOCALS = 虹龍洞
CASEELSE
	LOCALS =
ENDSELECT

RETURNF LOCALS

@Gathering_Drawing(採集位置, 採集パワー, Modification)
;RESULT:0	採集道具
;RESULT:1	採集量
#DIM 採集位置
#DIM 採集パワー
#DIM Chosen
#DIM QTY
#DIM Modification
#DIM start			;抽選開始値
#DIM end			;抽選終了値
#DIM difference		;加算or減算
#DIMS 低気圧位置
#DIMS 積乱雲位置
低気圧位置 = %GET_LOCATION_LOW()%
積乱雲位置 = %GET_LOCATION_CUMULUS()%
Chosen = 0
QTY = 0
;採集物の選出
;随机で検索斯塔ト位置をずらしたり、検索の向きを変えたりする
SELECTCASE RAND:5
	CASE 0
		start = 600 + RAND:40
		end = 707
		difference = 1
	CASE 1
		start = 706
		end = 599
		difference = -1
	CASE 2
		start = 640 - RAND:40
		end = 599
		difference = -1
	CASEELSE
		start = 600 + (5 * (RAND:9))
		end = 707
		difference = 1
ENDSELECT

FOR LOCAL:0, start, end, difference
	;道具が無名でなく、出現条件を満たし、所有数上限でない
	SIF ITEMNAME:(LOCAL:0) == ""
		CONTINUE
	IF ITEM:(LOCAL:0) == 99
		DEBUGPRINTFORML 所持上限のため%ITEMNAME:(LOCAL:0)%をスキップ
		CONTINUE
	ENDIF
	IF !GATHERING_SEASON(LOCAL:0)
		DEBUGPRINTFORML 过季のため%ITEMNAME:(LOCAL:0)%をスキップ
		CONTINUE
	ENDIF
	IF  GATHERING_PRODUCT(LOCAL:0, 採集位置) < 1
		DEBUGPRINTFORML 位置が不適切なため%ITEMNAME:(LOCAL:0)%をスキップ
		CONTINUE
	ENDIF
	LOCAL:5 = ITEMPRICE:(LOCAL:0) + 1000
	;採集パワー＋補正値 が √道具費用+1000 を上回ったら入手
	;(天候パッチ)猛烈な台風通過後・二つ玉低気圧通過後・スーパーセル通過後は、龍之爪・竜血・竜骨・龍珠の入手成功率が上昇
	IF GROUPMATCH(LOCAL:0, 606, 625, 634, 647) && ((低気圧位置 == "台風一過" && IS_TYPHOON:1 >= 5) || 低気圧位置 == "二つ玉低気圧通過" || (積乱雲位置 == "積乱雲通過" && CAN_CUMULUS:0 == 5))
		DEBUGPRINTFORML %ITEMNAME:(LOCAL:0)%入手成功率上昇中
		IF 採集パワー + Modification < SQRT((LOCAL:5)/2)
			DEBUGPRINTFORML 採集パワー不足なため%ITEMNAME:(LOCAL:0)%をスキップ
			CONTINUE
		ENDIF
	;(天候パッチ)寒冷渦通過後・二つ玉低気圧通過後は、氷之鱗の入手成功率が上昇
	ELSEIF LOCAL:0 == 609 && (低気圧位置 == "寒冷渦通過" || 低気圧位置 == "二つ玉低気圧通過")
		DEBUGPRINTFORML %ITEMNAME:(LOCAL:0)%入手成功率上昇中
		IF 採集パワー + Modification < SQRT((LOCAL:5)/2)
			DEBUGPRINTFORML 採集パワー不足なため%ITEMNAME:(LOCAL:0)%をスキップ
			CONTINUE
		ENDIF
	ELSEIF 採集パワー + Modification < SQRT(LOCAL:5)
		DEBUGPRINTFORML 採集パワー不足なため%ITEMNAME:(LOCAL:0)%をスキップ
		CONTINUE
	ENDIF
	DEBUGPRINTFORML %ITEMNAME:(LOCAL:0)%に決定
	Chosen = LOCAL:0
	;価格10000を超える希少道具は１つしか手に入らない、それ以下の価格帯は採集パワーに応じて複数入手が可能に
	SELECTCASE LOCAL:5
		CASE 0 TO 2000
			QTY = MAX((RAND:(SQRT(採集パワー)) - 1), 1)
		CASE 2001 TO 4000
			QTY = MAX((RAND:(SQRT(採集パワー)) - 2), 1)
		CASE 4001 TO 7000
			QTY = MAX((RAND:(SQRT(採集パワー)) - 3), 1)
		CASE 7001 TO 9999
			QTY = MAX((RAND:(SQRT(採集パワー)) - 4), 1)
		CASEELSE
			QTY = 1
	ENDSELECT
	BREAK
NEXT
RETURN Chosen, QTY