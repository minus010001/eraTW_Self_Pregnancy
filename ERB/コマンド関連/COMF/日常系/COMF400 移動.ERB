;-------------------------------------------------
;移動
;日常系指令、レベル1
;-------------------------------------------------
@COM400
#DIM 入浴拒否
#DIM 虫かご
#DIM 忍び込み
#DIM 停下脚步対象
#DIM BUTTON返回
#DIM BUTTON切替左
#DIM BUTTON切替中
#DIM BUTTON切替右
#DIM 进不去
#DIM ANIMATION_SEED
#DIM PREV_REDRAW
#DIM 文字導航
#DIM 文字導航用計數器
BUTTON返回 = MAXROOM + 1
BUTTON切替左 = MAXROOM + 2
BUTTON切替中 = MAXROOM + 4
BUTTON切替右 = MAXROOM + 4
文字導航 = MAXROOM + 5
进不去 = 0
ANIMATION_SEED = 0
PREV_REDRAW = CURRENTREDRAW()

;Map Animation Code
$MAPANIMATION
LINESTART = LINECOUNT
SIF ANIMATERECOLOREDMAPS && !FLAG:70
	ANIMATION_SEED ++

;人数カウントをリセット
VARSET COUNTCHARA
;部屋の人数カウントを設定
FOR LOCAL, 1, CHARANUM
	COUNTCHARA:(CFLAG:LOCAL:現在位置) ++
NEXT
入浴拒否 = 0
CALL TURN_RESET

DRAWLINE
;
;	// 自宅Mapの表示を新描画方式に
;
IF RECOLOREDMAPS
	CALL DRAW_COLOREDMAP(ANIMATION_SEED)
ELSE
	CALL DRAW_MAP
ENDIF
PRINTFORML 現在 {(CFLAG:MASTER:現在位置)}:%NAME_FROM_PLACE(CFLAG:MASTER:現在位置)% ←當前位置

IF FLAG:察知模式 == 1
	PRINT 颜色的说明：
	CALL COLORMESSAGE("逗留中　", C_H_PINK, 0)
	CALL COLORMESSAGE("工作中　", C_D_ORANGE, 0)
	CALL COLORMESSAGE("睡眠中　", C_N_BLUE, 0)
	CALL COLORMESSAGE("路人　", C_D_PINK, 0)
	CALL COLORMESSAGE("自由行動", C_YELLOW, 0)
	PRINTL
ELSEIF FLAG:察知模式 == 2
	PRINT 清潔度：
	CALL COLORMESSAGE("　高　", C_CREAM, 0)
	CALL COLORMESSAGE("　中　", C_YELLOW , 0)
	CALL COLORMESSAGE("　低　", C_V_PINK, 0)
	CALL COLORMESSAGE("垃圾山", C_RED, 0)
	PRINTL
ENDIF

PRINTL [  0] - 返回私室
FOR LOCAL, MINROOM(), MAXROOM
	SIF !(CAN_MOVE(CFLAG:MASTER:現在位置, LOCAL) & 1)
		CONTINUE
	SIF NAME_FROM_PLACE(LOCAL) == ""
		CONTINUE
	SELECTCASE FLAG:移動先表示
		CASE 2
			PRINTFORML [{LOCAL, 3}] - %NAME_FROM_PLACE(LOCAL)%
		CASE 1
			PRINTFORM [{LOCAL, 3}]%NAME_FROM_PLACE(LOCAL),5,LEFT%
		CASE 0
	ENDSELECT
NEXT

IF !CAN_MOVE(CFLAG:MASTER:現在位置, ENTRANCE) & 1 && CFLAG:MASTER:現在位置 != ENTRANCE
	SIF FLAG:移動先表示 == 1
		PRINTL
	PRINTFORML [{ENTRANCE, 3}] - %NAME_FROM_PLACE(ENTRANCE)%
ELSEIF FLAG:移動先表示 == 1
	PRINTL
ENDIF
PRINTFORML [{文字導航, 3}] - 地點導航
PRINTFORM [{BUTTON返回, 3}] - 返回　　　
PRINTBUTTON "＜]", BUTTON切替左
PRINTBUTTON "地圖切換", BUTTON切替中
PRINTFORM ({TFLAG:地圖切換})
PRINTBUTTON "[＞", BUTTON切替右
SIF TALENT:MASTER:居場所察知 || TALENT:MASTER:汚部屋察知
	PRINTBUTTON "　　[Y] - 察知模式", "Y"
IF FLAG:察知模式 == 1
	PRINT (居)　
ELSEIF FLAG:察知模式 == 2
	PRINT (汚)　
ENDIF
SIF TALENT:MASTER:居場所察知 && FLAG:察知模式 == 1
	PRINTBUTTON "[Z] - 居場所察知　 ", "Z"
SIF TALENT:MASTER:汚部屋察知 && FLAG:察知模式 == 2
	PRINTBUTTON "[X] - 汚部屋察知　 ", "X"
PRINTBUTTON "[C] - 移動先表示切替　", "C"
IF TALENT:MASTER:体型 < -4 
;&& !CFLAG:MASTER:同行
	IF CFLAG:MASTER:同行
		PRINTL
		PRINT （搭顺风车真開心）
	ELSE
		PRINTL
		PRINT （走过去都很辛苦…）
	ENDIF
ENDIF

DO
	;Map Animation Code
	IF ANIMATERECOLOREDMAPS && !FLAG:70
		REDRAW PREV_REDRAW
		TINPUTS ANIMATERECOLOREDMAPS,"UPDATE",0,""
	ELSE
		INPUTS
	ENDIF
	;システム系を先に処理
	SELECTCASE RESULTS
	CASE "UPDATE"
		PREV_REDRAW = CURRENTREDRAW()
		REDRAW 0
		CLEARLINE LINECOUNT - LINESTART
		BREAK
	;察知系
	CASE "Z", "z", "X", "x"
		IF TALENT:MASTER:居場所察知 && (RESULTS == "Z" || RESULTS == "z")
			CALL HITOSAGASI()
		ELSEIF TALENT:MASTER:汚部屋察知 && (RESULTS == "X" || RESULTS == "x")
			CALL OHEYA_CHECK
		ENDIF
		$INPUT_LOOP
		INPUT
		IF RESULT == 0
			RESTART
		ELSEIF MAP_CAN_MOVE(RESULT) == 0
			CALL CANNOT_MOVE_MESSAGE(RESULT)
			GOTO INPUT_LOOP
		ELSEIF IN_HOME(RESULT) == 1
			CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
			RETURN
		ENDIF
	;察知切り替え
	CASE "Y", "y"
		IF TALENT:MASTER:居場所察知 && TALENT:MASTER:汚部屋察知
			FLAG:察知模式 += 1
			SIF FLAG:察知模式 >= 3
				FLAG:察知模式 = 0
		ELSEIF TALENT:MASTER:居場所察知
			FLAG:察知模式 = !FLAG:察知模式
		ELSEIF TALENT:MASTER:汚部屋察知
			FLAG:察知模式 = (!FLAG:察知模式) * 2
		ENDIF
		RESTART
	;表示切り替え
	CASE "C", "c"
		FLAG:移動先表示 += 1
		SIF FLAG:移動先表示 > 2
			FLAG:移動先表示 = 0
		RESTART
	CASE ""
		CONTINUE
	ENDSELECT

	;半角数字の場合のみ判定を続行
	IF ISNUMERIC(RESULTS)
		RESULT = TOINT(RESULTS)
	ELSE
		CALL CANNOT_MOVE_MESSAGE(0)
		CONTINUE
	ENDIF
	;システム系を先に処理
	SELECTCASE RESULT
	CASE BUTTON返回, CFLAG:MASTER:現在位置
		RETURN -1
	CASE BUTTON切替左
		TFLAG:地圖切換 -= 1
		SIF TFLAG:地圖切換 <= 0
			TFLAG:地圖切換 = MAP_PAGES
		RESTART
	CASE BUTTON切替右
		TFLAG:地圖切換 += 1
		SIF TFLAG:地圖切換 > MAP_PAGES
			TFLAG:地圖切換 = 1
		RESTART
	;文字導航
	CASE 文字導航
		FOR 文字導航用計數器, 1, (MAXROOM - MAIN_MAP * 100)
			
			SIF GET_PLACENAME(文字導航用計數器 + MAIN_MAP * 100) == ""
				CONTINUE
			PRINTFORMC %GET_PLACENAME(文字導航用計數器 + MAIN_MAP * 100)%[{文字導航用計數器 + MAIN_MAP * 100,3}]
		NEXT
		PRINTL
		PRINTFORML [0] 返回
		$INPUT_LOOP_文字導航
		INPUT
		IF RESULT == 0
			RESTART
		ELSEIF MAP_CAN_MOVE(RESULT) == 0
			CALL CANNOT_MOVE_MESSAGE(RESULT)
			GOTO INPUT_LOOP_文字導航
		ELSEIF IN_HOME(RESULT) == 1
			CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
			RETURN
		ENDIF
	;私室に返回
	CASE 0
		IF IN_HOME(CFLAG:MASTER:初期位置) != 1
			PRINTW 初始位置不正确。改为私人房间。
			CFLAG:MASTER:初期位置 = 默认初期位置
		ENDIF
		CALL SKIP_MOVE(CFLAG:MASTER:現在位置, CFLAG:MASTER:初期位置)
		RETURN
	ENDSELECT

	;残るは通常の場所の選択もしくは誤入力

	;TWの方針として非推奨なんだけど手打ち入力でこれがあると入力文字数減って便利なんだ
	SIF INRANGE(RESULT, 1, 99)
		RESULT += MAIN_MAP * 100
	;同じ場所は中止
	IF RESULT == CFLAG:MASTER:現在位置
		RETURN -1
	;移動不可能なら弾くよ
	ELSEIF MAP_CAN_MOVE(RESULT) == 0
		CALL CANNOT_MOVE_MESSAGE(RESULT)
		CONTINUE
	;移動する
	ELSE
		CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
		RETURN
	ENDIF
LOOP 1
;Map Animation Code
SIF ANIMATERECOLOREDMAPS && !FLAG:時間停止
	GOTO MAPANIMATION

;移動
@COM_ABLE400
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(400)
	RETURN RESULT
SIF CFLAG:诶嘿嘿
	RETURN 0
SIF CFLAG:MASTER:恶作剧
	RETURN 0
SIF CFLAG:MASTER:陪睡中
	RETURN 0
;自宅でない
SIF AT_HOME(MASTER) == 0
	RETURN 0
RETURN 1

@SKIP_MOVE(ARG, 最終目的地)
#DIM 暫定目的地,10
#DIM 暫定目的地決定回数
#DIM 最終目的地
#DIM 直前位置
#DIM 出発地点
#DIM 経過時間
#DIM 中継地点,20
#DIM 中継回数
#DIM RELAY_COUNT
#DIM 直前の目的地
#DIM TSP消費倍率

;ARG:0	出発位置
;ARG:1	最終目的地
VARSET LOCAL
VARSET 中継地点
暫定目的地 = 最終目的地
経過時間 = 0
中継回数 = 0
TFLAG:地圖切換 = 0

出発地点 = CFLAG:MASTER:現在位置

;自動掃除
IF SWEEPGOD == 1
	SWEEPGOD = 2
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	CALL AUTO_SWEEP
	SWEEPGOD = 1
ELSE
	CALL AUTO_SWEEP
ENDIF

IF (CAN_MOVE(CFLAG:MASTER:現在位置, 暫定目的地) & 1)
	直前位置 = ARG
	経過時間 = 1
	CFLAG:MASTER:現在位置 = 最終目的地
	RESULT = 最終目的地
	GOTO STOP
ELSE
	TRYCALLFORM SKIP_MOVE_{MAIN_MAP}(最終目的地)
ENDIF

出発地点 = CFLAG:MASTER:現在位置
直前の目的地 = -1

FOR 中継回数,0,20
	WHILE !(CAN_MOVE(出発地点, 暫定目的地) & 1)
		TRYCALLFORM SKIP_MOVE_{MAIN_MAP}(暫定目的地,出発地点)
		[IF_DEBUG]
		PRINTFORML 因为不能直接去{暫定目的地}%STR:(8000 + 暫定目的地)%所以以{RESULT}%STR:(8000 + RESULT)%为目标
		[ENDIF]
		SIF 直前の目的地 == RESULT
			THROW 产生无限循环。
		直前の目的地 = 暫定目的地
		暫定目的地 = RESULT
	WEND
	[IF_DEBUG]
	SETCOLOR C_YELLOW
	PRINTFORML 将第{中継回数+1}个暂定目的地决定为%NAME_FROM_PLACE(暫定目的地)%
	RESETCOLOR
	[ENDIF]
	直前の目的地 = -1
	経過時間 += 1
	中継地点:中継回数 = 暫定目的地
	SIF CAN_MOVE(暫定目的地,最終目的地) & 1
		BREAK
	出発地点 = 暫定目的地
	暫定目的地 = 最終目的地
NEXT
直前位置 = 暫定目的地

FOR RELAY_COUNT,1,中継回数 + 1
	LOCAL:3 = 0
	FOR LOCAL,1,CHARANUM
		IF CFLAG:LOCAL:現在位置 == 中継地点:(RELAY_COUNT-1)
			LOCAL:3 = LOCAL
			BREAK
		ENDIF
	NEXT
	PRINTFORM 第{RELAY_COUNT}的中繼點的%NAME_FROM_PLACE(中継地点:(RELAY_COUNT-1))%
	IF LOCAL:3
		RESULT = 0
		IF CFLAG:(LOCAL:3):口上セレクタ
			TRYCALLFORM M_KOJO_K{LOCAL:3}_{CFLAG:(LOCAL:3):口上セレクタ}
		ELSE
			TRYCALLFORM M_KOJO_K{LOCAL:3}
		ENDIF
		PRINTFORML 里好像有%CALLNAME:(LOCAL:3)%
		IF FLAG:停下脚步 == 2
			;立ち止まらない
		ELSEIF FLAG:停下脚步 == 1 && !RESULT
			;口上がある場合のみ
		ELSE
			CALL ASK_M("打声招呼后继续移动",!FLAG:70,"無視",1,"停下来",1)
			IF RESULT == 2
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:3,1,0)
				経過時間 = RELAY_COUNT
				CFLAG:MASTER:現在位置 = 中継地点:(RELAY_COUNT - 1)
				RESULT = 中継地点:RELAY_COUNT
				GOTO STOP
			ELSEIF RESULT == 1
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:3,3,0)
			ELSE
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:3,2,0)
			ENDIF
		ENDIF
	ELSE
		PRINTFORML 谁也不在
	ENDIF
NEXT

経過時間 = 中継回数 + 1
CFLAG:MASTER:現在位置 = 最終目的地
RESULT = 最終目的地

$STOP
IF IN_TOILET(RESULT)
	CALL 廁所侵入(RESULT)
	SIF RESULT == -1
		CFLAG:MASTER:現在位置 = 直前位置
ELSEIF BATHCHECK(RESULT)
	LOCAL:1 = IN_ROOM_MEMBER("MIN", RESULT, "CFLAG", 6)
	IF !LOCAL:1 && !CFLAG:MASTER:同行
		PRINTFORMW 进入浴場的%CALLNAME:MASTER%泡在了浴池里
	ELSEIF LOCAL:1 && 風呂逐出条件(LOCAL:1) && !CFLAG:MASTER:同行
		PRINTFORMW 被在浴場里的%CALLNAME:(LOCAL:1)%赶出来了…
		CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,1,0)
		CFLAG:MASTER:現在位置 = 直前位置
	ELSEIF LOCAL:1 && !CFLAG:MASTER:同行
		IF FLAG:70 == 1
			PRINTFORMW %CALLNAME:(LOCAL:1)%在入浴中的様子…
		ELSE
			PRINTFORMW 与在浴場里的%CALLNAME:(LOCAL:1)%对上了视线…
			CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,2,0)
		ENDIF
		CFLAG:MASTER:現在位置 = 最終目的地
	ELSE
		FOR LOCAL, 1, CHARANUM
			IF 風呂逐出条件(LOCAL) && CFLAG:LOCAL:同行
				PRINTFORMW 共同入浴的邀请被%CALLNAME:LOCAL%拒绝了
				CALL KOJO_MESSAGE_SEND("EVENT",23,LOCAL,1,0)
				CFLAG:MASTER:現在位置 = 直前位置
				GOTO END
			ELSEIF LOCAL:1 && 風呂逐出条件(LOCAL:1)
				PRINTFORMW 被在浴場里的%CALLNAME:(LOCAL:1)%赶出来了…
				CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,1,0)
				CFLAG:MASTER:現在位置 = 直前位置
				GOTO END
			ENDIF
		NEXT
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:同行
				PRINTFORMW %CALLNAME:LOCAL%好像想一起进入浴場的様子
				CALL KOJO_MESSAGE_SEND("EVENT",23,LOCAL,2,0)
				BREAK
			ENDIF
		NEXT
		CFLAG:MASTER:現在位置 = 最終目的地
	ENDIF
ELSEIF RESULT == 54
	CALL 虫かご侵入(RESULT)
	SIF RESULT == -1
		CFLAG:MASTER:現在位置 = 直前位置
ELSEIF LOCK:(RESULT)
	LOCAL:4 = RESULT
	IF FLAG:70 == 1
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:311 == RESULT && CFLAG:LOCAL:睡眠 && !CFLAG:LOCAL:出禁
				PRINTFORMW 好像%CALLNAME:LOCAL%正在睡觉的様子
			ENDIF
		NEXT
		;移動成立
		CFLAG:MASTER:現在位置 = 最終目的地
	ELSEIF FLAG:70 == 0
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:311 == RESULT && CFLAG:LOCAL:睡眠 && !CFLAG:LOCAL:出禁
				PRINTFORMW 好像%CALLNAME:LOCAL%正在睡觉的様子
			ELSEIF CFLAG:LOCAL:311 == RESULT && !CFLAG:LOCAL:睡眠 && !CFLAG:LOCAL:出禁
				PRINTFORML 好像%CALLNAME:LOCAL%正在睡觉的様子
				PRINTFORMW 听到了一点动静…
			ENDIF
			IF CFLAG:LOCAL:同行
				LOCAL:2 = 1
			ELSE
				LOCAL:2 = 0
			ENDIF
		NEXT
		IF LOCAL:2
			CFLAG:MASTER:現在位置 = 直前位置
		ELSEIF LOCAL:4 != CFLAG:MASTER:初期位置
			CALL ASK_YN("潜入房间","算了吧")
			IF RESULT == 0
				CALL 潜入房間(LOCAL:4, 直前位置)
				GOTO END
			ELSEIF RESULT == 1
				CFLAG:MASTER:現在位置 = 直前位置
			ENDIF
		ENDIF
	ENDIF
ELSEIF TFLAG:宴会場 == RESULT
	CALL ENKAI_SINKOU()
ENDIF
$END
[IF_DEBUG]
PRINTFORML 移动了{経過時間}次
[ENDIF]

TSP消費倍率 = 5
SIF TALENT:MASTER:100 < -4
	TSP消費倍率 += 15
SIF TFLAG:運搬
	TSP消費倍率 += 3
IF FLAG:70 == 1 || (GETBIT(FLAG:瞬間移動, 1) && !CFLAG:MASTER:同行 && BASE:MASTER:TSP > TSP消費倍率 * 経過時間)
	BASE:MASTER:TSP -= TSP消費倍率 * 経過時間
	SIF TFLAG:運搬
		CFLAG:(TFLAG:運搬):現在位置 = CFLAG:MASTER:現在位置
ELSE
	SIF TALENT:MASTER:100 < -4 && !CFLAG:MASTER:同行
		TIME += 7 * 経過時間
	TIME += 2 * 経過時間
	IF CFLAG:93:同行 && !GETBIT (FLAG:泳池, 0)
		TIME += 2 * 経過時間
		BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - (5 * 経過時間), 500)
	ENDIF
ENDIF
;21/09/01 自由行動に対応するフレーバー追加
IF CFLAG:MASTER:現在位置 == FA16_HOLE:0 && !FA16_HOLE:1
	IF (50 - (ABL:MASTER:戦闘能力 * 7)) < RAND:100
		PRINTFORMW ……脚下有一个落穴、但%CALLNAME:MASTER%毫不费力地避開了
		FA16_HOLE:1 = 1
	ELSE
		PRINTFORMW %CALLNAME:MASTER%掉进了一个落穴中！！
		SELECTCASE RAND(15) + TFLAG:幸運補正
			CASE IS < 2
				PRINTFORML 好痛！！　这么想着的时候屁股已经落地了…
				CALL RECOVER(MASTER, -50, "体力", "屁股痛")
				WAIT
			CASE IS > 12
				MONEY += 100
				PRINTFORML …在穴底發現了100円
				PRINTFORMW 　現在的所持金:%金額表示(MONEY)%
		ENDSELECT
		TIME += 3
		FA16_HOLE:1 = 2
		FA16_HOLE:2 = TIME
	ENDIF
ENDIF

PREVCOM = 400
STR:0 = 移動
;NEXTCOMに何かしらの値が入っている場合はそれを書き換えない
;そうでないばあい-1でリセット
SIF NEXTCOM == 0
	NEXTCOM = -1


;-----------------------------------------------------------
;移動不能時の処理
;ARG=移動しようとした場所ID
;-----------------------------------------------------------
@CANNOT_MOVE_MESSAGE(ARG)
IF !ARG || NAME_FROM_PLACE(ARG) == "" || TFLAG:移動不能メッセージ == 998
	PRINTFORMW 错误的输入
	CLEARLINE 2
	REUSELASTLINE
ELSEIF TFLAG:移動不能メッセージ == 999
	PRINTFORMW 锁上了
	CLEARLINE 2
	REUSELASTLINE
ELSE
	TRYCALLFORM MAP_CANNOT_WORD_{MAIN_MAP}(ARG)
ENDIF
TFLAG:移動不能メッセージ = 0
