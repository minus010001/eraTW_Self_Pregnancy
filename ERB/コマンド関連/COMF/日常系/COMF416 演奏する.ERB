;-------------------------------------------------
;演奏
;日常系指令、レベル1
;-------------------------------------------------
@COM416
#DIM DYNAMIC NOW_TARGET
#DIM DYNAMIC NUM_TARGET
#DIM DYNAMIC MUSIC_BONUS
#DIM DYNAMIC BAND_BONUS
#DIM DYNAMIC DANCE_BONUS
#DIM OHINERI
#DIM INTERRUPTED
#DIM OMAKE, 20 = 40,120,195,212,213,214,215,216,500,510,520,521,600,601,610,611,612,636,642,706
#DIM CHOSEN_OMAKE

TFLAG:使用楽器 = 0
IF 宴会場所在判定(MASTER)
;IF CFLAG:MASTER:現在位置 == TFLAG:宴会場 && MASTER != TARGET && DAY >= FLAG:開始日 && TIME >= FLAG:33 && FLAG:参加人数
	IF ABL:MASTER:音楽技能 < 3
		PRINTL 对现在的音楽技能是否能达到宴会演奏的相応水平还不是很有自信……
		RETURN -1
	ELSEIF ABL:MASTER:音楽技能 <= ABL:TARGET:戦闘能力 - TALENT:TARGET:心情
		PRINTFORML 如果演奏不好的话会被%CALLNAME:TARGET%\@ TALENT:TARGET:心情 < 0 ? 生气地 # \@扔石头的
	ELSEIF ABL:MASTER:音楽技能 < 5
		PRINTL 演奏不好的话大概会有石头飛过来吧
	ELSE
		PRINTL 要用哪种楽器给宴会助兴呢
	ENDIF
ELSE
	PRINTL 要使用什么乐器呢……
ENDIF
{
	CALL ASK_M(
		"返回", 1,
		ITEMNAME:30, MUSIC_ABLE(30),
		ITEMNAME:31, MUSIC_ABLE(31),
		ITEMNAME:32, MUSIC_ABLE(32),
		ITEMNAME:33, MUSIC_ABLE(33),
		ITEMNAME:34, MUSIC_ABLE(34)
	)
}
TFLAG:使用楽器 = RESULT
SIF ITEM:演奏指南書 && !RAND:2
	EXP:MASTER:演奏経験 += 2
SIF !TFLAG:使用楽器
	RETURN -1
INTERRUPTED = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:現在位置 != CFLAG:MASTER:現在位置
		CONTINUE
	SIF CFLAG:LOCAL:行動 != 5
		CONTINUE
	IF !GROUPMATCH(CFLAG:LOCAL:職種, JOB_音楽, JOB_遊び, JOB_宴会参加, JOB_酔いつぶれる)
		PRINTFORML 「吵死了！」
		CALL THROW_STONE(LOCAL)
	ELSEIF CFLAG:LOCAL:職種 == JOB_宴会参加 && !ACCOMPANY_ABLE(LOCAL)
		EXP:MASTER:演奏経験 ++
		SIF ABL:LOCAL:戦闘能力 >= 4
			EXP:MASTER:演奏経験 ++
		IF ABL:MASTER:音楽技能 + TALENT:LOCAL:心情 >= ABL:LOCAL:戦闘能力
			PRINTFORML %CALLNAME:LOCAL%%TEXTR("陶醉于你的演奏/雙眼閃著光/称赞了你的演奏")%
			OHINERI = MAX(RAND:(ABL:LOCAL:戦闘能力 + 1), 1)
			MONEY:2 += OHINERI
			PRINTPLAIN *叮啷*
			CALL COLORMESSAGE(@"被投了{OHINERI}個筹码過來！",C_YELLOW,1)
			IF !RAND:7
				IF !RAND:2
					;落空了くじ
					CHOSEN_OMAKE = 131
				ELSE
					CHOSEN_OMAKE = OMAKE:(RAND:20)
				ENDIF
				CALL COLORMESSAGE(@"%ITEMNAME:(CHOSEN_OMAKE)%被扔了過來！",C_YELLOW,1)
				ITEM:CHOSEN_OMAKE ++
			ENDIF
		ELSE
			PRINTFORML 「%TEXTR("吵死了！/滚蛋！/真爛！")%」
			CALL THROW_STONE(LOCAL)
		ENDIF
	ENDIF
	SIF RESULT == -1
		INTERRUPTED ++
NEXT
IF INTERRUPTED
	PRINTFORML 「真是嚴格的客人啊」
	CALL COLORMESSAGE(@"%CALLNAME:MASTER%中断了演奏",C_YELLOW,1)
	RETURN -1
ENDIF

SELECTCASE TFLAG:192
	;通常
	CASE 0
		LOCAL = RAND:100
		LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
		SIF LOCAL:1 > 99
			LOCAL:1 = 99
		IF LOCAL <= (9 + ABL:MASTER:音楽技能)
			;10％で大成功
			EXP:MASTER:演奏経験 ++
			EXP:MASTER:演奏経験 += 5
			TFLAG:193 = 1
		ELSEIF LOCAL >= LOCAL:1
			;10～1％で失敗
			TFLAG:193 = -1
		ELSE
			;残りは成功
			EXP:MASTER:演奏経験 ++
			TFLAG:193 = 0
		ENDIF
	;強制成功or大成功
	CASE 1
		IF RAND:100 <= (9 + ABL:MASTER:音楽技能)
			;10％で大成功
			EXP:MASTER:演奏経験 ++
			EXP:MASTER:演奏経験 += 5
			TFLAG:193 = 1
		ELSE
			;残りは成功
			EXP:MASTER:演奏経験 ++
			TFLAG:193 = 0
		ENDIF
	;強制失敗
	CASE -1
		TFLAG:193 = -1
	;指令終了
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT


DOWNBASE:MASTER:体力 = 300
DOWNBASE:MASTER:気力 = 300

;その場に居るTARGET全員で回す
NOW_TARGET = TARGET
NUM_TARGET = GET_TARGETNUM()
CVARSET TCVAR, GETNUM(TCVAR, "演奏参加"), 0

FOR LOCAL, 1, NUM_TARGET + 1
	SIF !SHIRAHU(TARGET:LOCAL)
		CONTINUE
	TARGET = TARGET:LOCAL
	CALL COM416_1
	SELECTCASE TCVAR:演奏参加
	CASE 1, 2
		MUSIC_BONUS += ABL:音楽技能 + 2 * TALENT:音感
	CASE 3
		DANCE_BONUS += ABL:音楽技能
	ENDSELECT
NEXT

BAND_BONUS = CMATCH(TCVAR:演奏参加, 1, 1) + CMATCH(TCVAR:演奏参加, 2, 1)
IF TFLAG:193 < 0
	FOR LOCAL, 1, NUM_TARGET + 1
		SOURCE:(TARGET:LOCAL):反感 += MUSIC_BONUS * ABL:(TARGET:LOCAL):音楽技能
		SIF TCVAR:(TARGET:LOCAL):演奏参加
			SOURCE:(TARGET:LOCAL):鬱屈 += 3 * MUSIC_BONUS * ABL:音楽技能
		SOURCE:(TARGET:LOCAL):反感 = SOURCE:(TARGET:LOCAL):反感 * (2 + BAND_BONUS + DANCE_BONUS) / 2
		SOURCE:(TARGET:LOCAL):鬱屈 = SOURCE:(TARGET:LOCAL):鬱屈 * (2 + BAND_BONUS + DANCE_BONUS) / 2
		SOURCE:(TARGET:LOCAL):歓楽 = SOURCE:(TARGET:LOCAL):歓楽 * 2 / (2 + BAND_BONUS + DANCE_BONUS)
		SOURCE:(TARGET:LOCAL):征服 = SOURCE:(TARGET:LOCAL):征服 * 2 / (2 + BAND_BONUS + DANCE_BONUS)
		;この手の個別処理は無いほうが良いが思いついてしまったものは仕方がない
		;露娜薩
		SIF TCVAR:[[露娜薩]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):鬱屈, 1.1
		SIF TCVAR:[[舞]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):鬱屈, 0.9
		SIF TCVAR:[[里乃]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):反感, 0.9
	NEXT
ELSE
	BAND_BONUS += TFLAG:193
	BAND_BONUS += MAX(SQRT(MUSIC_BONUS) - 1, 0)
	FOR LOCAL, 1, NUM_TARGET + 1
		SIF !SHIRAHU(TARGET:LOCAL)
			CONTINUE
		SOURCE:(TARGET:LOCAL):歓楽 += MUSIC_BONUS * 15
		SOURCE:(TARGET:LOCAL):征服 += MUSIC_BONUS * 10
		SOURCE:(TARGET:LOCAL):歓楽 = MAX(0, SOURCE:(TARGET:LOCAL):歓楽 * (2 + BAND_BONUS + DANCE_BONUS) / 2)
		SOURCE:(TARGET:LOCAL):征服 = MAX(0, SOURCE:(TARGET:LOCAL):征服 * (2 + BAND_BONUS + DANCE_BONUS) / 2)
		;梅露蘭
		SIF TCVAR:[[梅露蘭]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):歓楽, 1.1
		;雷鼓
		SIF TCVAR:[[雷鼓]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):征服, 1.1
		;心
		SIF TCVAR:[[心]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):歓楽, 1.2
		SIF TCVAR:[[心]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):征服, 1.2
	NEXT
ENDIF

TFLAG:好感度BOUNS = BAND_BONUS * 50
TARGET = NOW_TARGET

TIME += 30
RETURN 1


@COM416_1

DOWNBASE:気力 += 100

;歓楽強度
SOURCE:歓楽 = 700 + ABL:MASTER:音楽技能 * 300 + ABL:親密 * 50
;征服強度
SOURCE:征服 = 100 * (ABL:MASTER:音楽技能 + 1) + ABL:親密 * 30

IF TFLAG:193 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:征服 , 0.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:征服 , 1.00
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:征服 , 2.00
ENDIF
;音楽技能で歓楽がさらに増減
IF ABL:MASTER:音楽技能 >= 5
	TIMES SOURCE:歓楽 , 2.00
ELSEIF ABL:MASTER:音楽技能 == 4
	TIMES SOURCE:歓楽 , 1.50
ELSEIF ABL:MASTER:音楽技能 == 3
	TIMES SOURCE:歓楽 , 1.00
ELSEIF ABL:MASTER:音楽技能 == 2
	TIMES SOURCE:歓楽 , 0.50
ELSE
	TIMES SOURCE:歓楽 , 0.10
ENDIF
;[音楽知識]を持っている場合、歓楽増加
SIF TALENT:MASTER:音楽知識
	TIMES SOURCE:歓楽 , 1.50
;音感のある無でもう少し増減
IF TALENT:MASTER:音感 == 2
	TIMES SOURCE:歓楽 , 2.00
ELSEIF TALENT:MASTER:音感 == 1
	TIMES SOURCE:歓楽 , 1.50
ELSEIF TALENT:MASTER:音感 == -1
	TIMES SOURCE:歓楽, 0.50
ENDIF

SIF TARGET <= 0
	RETURN 1
SELECTCASE TALENT:TARGET:音楽知識
;楽器持ち角色、プリリバ三姉妹と九十九姉妹と雷鼓がTARGET
CASE 1
	EXP:MASTER:演奏経験 ++
	IF ACCOMPANY_ABLE(TARGET)
		TCVAR:演奏日時 = TIME
		DOWNBASE:気力 += 200
		DOWNBASE:体力 += 100
		TIMES SOURCE:歓楽 , 2.00
		TCVAR:演奏参加 = 1
		EXP:演奏経験 ++
		PRINTFORMW %CALLNAME:TARGET%開始演奏著樂器！
		IF ABL:MASTER:音楽技能 >= ABL:TARGET:音楽技能
			EXP:演奏経験 ++
			TIMES SOURCE:征服 , 1.50
		ENDIF
	ENDIF
;歌唱角色、米斯蒂亜と響子、艾倫がTARGET
CASE 2
	EXP:MASTER:演奏経験 ++
	IF ACCOMPANY_ABLE(TARGET)
		TCVAR:演奏日時 = TIME
		DOWNBASE:気力 += 200
		DOWNBASE:体力 += 100
		TIMES SOURCE:歓楽 , 2.00
		TCVAR:演奏参加 = 2
		EXP:歌唱経験 ++
		PRINTFORMW %CALLNAME:TARGET%配合著%CALLNAME:MASTER%的旋律唱起了歌！
		IF ABL:MASTER:音楽技能 >= ABL:TARGET:音楽技能
			EXP:歌唱経験 ++
			TIMES SOURCE:征服 , 1.50
		ENDIF
	ENDIF
;舞踏角色、心、舞、里乃
CASE 3
	EXP:MASTER:演奏経験 ++
	IF ACCOMPANY_ABLE(TARGET)
		TCVAR:演奏日時 = TIME
		DOWNBASE:気力 += 200
		DOWNBASE:体力 += 100
		TIMES SOURCE:歓楽 , 2.00
		TCVAR:演奏参加 = 3
		EXP:舞踏経験 ++
		PRINTFORMW %CALLNAME:TARGET%配合著%CALLNAME:MASTER%的旋律跳起了舞！
		IF ABL:MASTER:音楽技能 >= ABL:TARGET:音楽技能
			EXP:舞踏経験 ++
			TIMES SOURCE:征服 , 1.50
		ENDIF
	ENDIF
CASEELSE
	SIF ABL:MASTER:音楽技能 <= ABL:TARGET:音楽技能 || TALENT:TARGET:音楽知識
		EXP:MASTER:演奏経験 ++
ENDSELECT
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE416
;演奏実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(416)
	RETURN RESULT
	
;いずれの楽器（30～34）も使えない
LOCAL:1 = 0
FOR LOCAL,30,35
	SIF MUSIC_ABLE(LOCAL)
		LOCAL:1 ++
NEXT
SIF !LOCAL:1
	RETURN 0

;楽器三種のいずれかを持っていても13,24,30風呂と14,40廁所では演奏できない
;SIF CFLAG:MASTER:現在位置 == 13 || CFLAG:MASTER:現在位置 == 14 || CFLAG:MASTER:現在位置 == 24 || CFLAG:MASTER:現在位置 == 30 || CFLAG:MASTER:現在位置 == 39 || CFLAG:MASTER:現在位置 == 40
SIF (IN_TOILET(CFLAG:MASTER:現在位置) || BATHCHECK(CFLAG:MASTER:現在位置)) && GET_MAPID(CFLAG:MASTER:現在位置) == MAIN_MAP
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF CFLAG:诶嘿嘿 == 2
	RETURN 0
;睡眠中
;いっそ强行起床させるという案もあったり
SIF CFLAG:睡眠 && !CFLAG:陪睡中 || CFLAG:陪睡中
	RETURN 0
;仕事中、かつ、宴会参加者ではない
SIF CFLAG:行動 == 5 && CFLAG:職種 != JOB_宴会参加
	RETURN 0
;停止中は不可
SIF FLAG:70
	RETURN 0
RETURN 1

@MUSIC_ABLE(ARG)
#FUNCTION
SIF !ITEM:ARG
	RETURNF 0
SELECTCASE ARG
;電子琴、吉他、ペット
	CASE 30,32,33
		RETURNF 1
;鋼琴
;博麗神社居間、紅魔館の広場と廃洋館、地霊殿の広場、寺子屋に設置しているという形です。
	CASE 31
		SIF GROUPMATCH(CFLAG:MASTER:現在位置,GET_MAP_REPLACEMENT(9),GET_MAP_REPLACEMENT(222),GET_MAP_REPLACEMENT(309),GET_MAP_REPLACEMENT(335))
			RETURNF 1
;バイオリン
	CASE 34
		SIF ABL:MASTER:音楽技能 >= 2
			RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT
RETURNF 0

@ACCOMPANY_ABLE(ARG)
#FUNCTION
SIF BASE:ARG:気力 < 200
	RETURNF 0
SIF TIME_PROGRESS(TCVAR:ARG:演奏日時) < 90
	RETURNF 0
SIF ABL:MASTER:音楽技能 < 3
	RETURNF 0
SIF !SHIRAHU(ARG)
	RETURNF 0
SIF CFLAG:ARG:行動 == 5
	RETURNF 0

RETURNF 1

@THROW_STONE(ARG)
#DIM LISTENER_P
CALL COLORMESSAGE(@"%CALLNAME:ARG%扔了石頭過來",C_YELLOW,1)
LISTENER_P = ABL:ARG:戦闘能力
IF RAND:(ABL:MASTER:戦闘能力 * (2 + CFLAG:ARG:弾幕勝負勝利) + 1) > LISTENER_P
	CALL COLORMESSAGE(@"%CALLNAME:MASTER%华丽地回避了攻击",C_YELLOW,1)
ELSE
	CALL RECOVER(MASTER, (LISTENER_P + 1) * (-150), "体力", @"%CALLNAME:ARG%の投石")
	RETURN -1
ENDIF
@演奏集合
#DIM 集合, 人物数量上限, 3 ;集合:X:Y
#DIM 人数                      ;同一Mapにいる人数、移動できない角色も表示され、人数に加算されている
#DIM 入力値                    ;RESULT取得用
#DIM C_ID
#DIM NOW_TARGET
SIF CFLAG:MASTER:現在位置 == OMANEKIBEYA()
	RETURN
SIF !AT_HOME(MASTER)
	RETURN
SIF CFLAG:MASTER:現在位置 == CFLAG:MASTER:初期位置
	RETURN
SIF PRIVATEROOM:(CFLAG:MASTER:現在位置 % 100)
	RETURN
CALL SYUGOS_GETDATA(人数, 集合)
CALL SYUGOS_COST(人数, 集合)
SIF TARGET
	NOW_TARGET = TARGET
FOR LOCAL, 0, 人数
	C_ID = 集合:LOCAL:0
	SIF GETBIT(集合:LOCAL:1, 0)
		CONTINUE
	;移動回数が多いと除外
	SIF 睡眠時間(C_ID)
		CONTINUE
	SIF 集合:LOCAL:2 > 3
		CONTINUE
	SIF CFLAG:C_ID:ブチギレ
		CONTINUE
	;IF RAND:(7-ABL:MASTER:音楽技能) == 0
	;	CFLAG:C_ID:現在位置 = CFLAG:MASTER:現在位置
	;	CFLAG:C_ID:同室 = 1
	;	PRINTFORML %CALLNAME:C_ID,12%听说%CALLNAME:MASTER%正在演奏乐器,于是赶过来旁听了
	;ENDIF

	CFLAG:C_ID:現在位置 = CFLAG:MASTER:現在位置
	CFLAG:C_ID:同室 = 1
	;自由行動の中断
	Activity_Duration:C_ID = 0
	Activity_Type:C_ID = 0
	;PRINTFORML %CALLNAME:C_ID%过来了
	;PRINTFORM %CALLNAME:C_ID%.
NEXT
CALL TARGETSET_CHACK
;SIF NOW_TARGET
;	TARGET = NOW_TARGET

;PRINTFORML 听说%CALLNAME:MASTER%正在演奏乐器,于是赶过来了...
PRINTFORML 搞不好会吸引到附近的人,so...是时候表演真正的技术了!!!
