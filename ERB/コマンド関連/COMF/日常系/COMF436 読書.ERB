;-------------------------------------------------
;読書
;日常系指令、レベル1
;-------------------------------------------------
@COM436
#DIM READING_EXP
READING_EXP = 1


PRINTL 要读什么？ 
CALL ASK_M("文学", 1, "実用書", 1, "小黄书", 1, "中止", 1)
IF RESULT == 0
	SELECTCASE RAND:10
		;忍殺
		CASE 0
			TFLAG:193 = 1
		;失敗
		CASE 1 TO 3
			TFLAG:193 = 2
		;文学
		CASEELSE
			TFLAG:193 = 3
	ENDSELECT
ELSEIF RESULT == 1
	SELECTCASE RAND:10
		;失敗
		CASE 0 TO 2
			TFLAG:193 = 4
		;実用
		CASEELSE
			TFLAG:193 = 5
	ENDSELECT
ELSEIF RESULT == 2
	SELECTCASE RAND:9
		;ｳｽｲﾀｶｲﾎﾝ
		CASE 0 TO 2
			TFLAG:193 = 6
		;保健体育
		CASE 3 TO 5
			TFLAG:193 = 7
		;カーマスートラ
		CASE 6 TO 8
			TFLAG:193 = 8
		;やばい本
;		CASEELSE
;		IF !TALENT:MASTER:禁断的知識
;			TFLAG:193 = 9
;		ELSE
;			GOTO LOOP
;		ENDIF
	ENDSELECT
ELSEIF RESULT == 3
	RETURN -1
ENDIF

IF TARGET == 54 && CFLAG:325 == 5
	IF TALENT:恋慕
		PRINTFORM 和帕秋莉一起享受着读书的快乐
	ELSE
		PRINTFORM 在帕秋莉停止読書之前你也把时间打发在了読書上
	ENDIF
ENDIF
PRINTFORM %CALLNAME:MASTER%把书架上的
SELECTCASE TFLAG:193
	CASE 1
		PRINT 『忍者杀手』
	CASE 2 TO 3
		PRINT 文芸書
	CASE 4 TO 5
		PRINT 実用書
	CASE 6
		PRINT 又薄又贵的书
	CASE 7
		PRINT 保健体育的书
	CASE 8
		PRINT 『印度爱经』
;	CASE 9
;		PRINT タイトルのない皮装丁の湿り気を帯びた本
ENDSELECT
PRINTFORML 拿下読了起來

;3分の1で追加イベント
SIF !RAND:3 && TARGET == 54
	TFLAG:194 = 1
;TARGETが無知かつＨな本読みかつMASTERがTARGETより教養が高くTARGETの教養が2以上かつ五分の一
SIF !RAND:5 && TALENT:TARGET:無知 && TFLAG:193 >= 6 && TFLAG:193 <= 8 && ABL:MASTER:教養 > ABL:教養 && ABL:教養 >= 2
	TFLAG:194 = 2

SELECTCASE TFLAG:193
	CASE 1
		EXP:MASTER:戦闘経験 += 5
	CASE 3
	;文学
		EXP:MASTER:学習経験 += READING_EXP
	CASE 5
	;実用
		SELECTCASE RAND:7
			CASE 0
				EXP:MASTER:会話経験 += READING_EXP
			CASE 1
				EXP:MASTER:撮影経験 += READING_EXP
			CASE 2
				EXP:MASTER:料理経験 += READING_EXP
			CASE 3
				EXP:MASTER:釣魚経験 += READING_EXP
			CASE 4
				EXP:MASTER:採集経験 += READING_EXP
			CASE 5
				EXP:MASTER:調合経験 += READING_EXP
			CASEELSE
				EXP:MASTER:伐採経験 += READING_EXP
		ENDSELECT
	CASE 6
		CALL RECOVER(MASTER,100,"精力")
		CALL RECOVER(MASTER,100,"勃起")
ENDSELECT

IF TARGET > 0 && CFLAG:行動 != 5
	;固定で獲得するソース
	SOURCE:歓楽 = 200
	;信頼
	TFLAG:98 = 3

	;ABL:従順をみる
	IF ABL:従順 <= 1
		SOURCE:歓楽 += (ABL:従順 * 80)
	ELSEIF ABL:従順 <= 3
		SOURCE:歓楽 += 300 + (ABL:従順 * 80)
	ELSEIF ABL:従順 <= 5
		SOURCE:歓楽 += 700 + (ABL:従順 * 80)
	ELSEIF ABL:従順 <= 8
		SOURCE:歓楽 += 950 + (ABL:従順 * 100)
	ELSEIF ABL:従順 <= 11
		SOURCE:歓楽 += 1200 + (ABL:従順 * 90)
	ELSE
		SOURCE:歓楽 += 2000 + (ABL:従順 * 50)
	ENDIF

	;好感度をみる
	IF CFLAG:2 <= 500
		SOURCE:歓楽 += CFLAG:2 * 2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:歓楽 += 1000 + (CFLAG:2 - 500) / 2
	ELSE
		SOURCE:歓楽 += 5000 + (CFLAG:2 - 5000) / 3
	ENDIF
	SIF SOURCE:歓楽 < 0
		SOURCE:歓楽 = 0
ENDIF

DOWNBASE:MASTER:体力 = 50
DOWNBASE:MASTER:気力 = 200
;賢者タイムは読書がはかどる
IF IS_WISEMAN()
	PRINTFORML 由于煩悩消失了、学習的効率提高了
	TIME += 45
ELSE
	TIME += 60
ENDIF
RETURN 1


;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;読書
@COM_ABLE436
;実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(436)
	RETURN RESULT
;约会道中
SIF TFLAG:约会道中
	RETURN 0
;陪睡中
SIF CFLAG:MASTER:陪睡中
	RETURN 0
;诶嘿嘿中もだめ
SIF CFLAG:MASTER:诶嘿嘿
	RETURN 0
;気力0では不可
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;図書館
SIF !IS_LIBRARY(CFLAG:MASTER:現在位置)
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1

;蔵書閲覧判定・改＠下見パッチ
;戻り値はPLACE_IDにすること
;MASTERの現在位置に関係なく判定可能に
;PERMISSIONが真なら許可フリー
@IS_LIBRARY(PLACE_ID, PERMISSION)
#FUNCTION
#DIM PLACE_ID
#DIM PERMISSION
;外にいる場合、拠点用のPLACE_IDに置換
IF !IN_HOME(PLACE_ID)
	SELECTCASE PLACE_ID
		CASE 夢幻遺跡 
			PLACE_ID = P49夢幻遺跡・研究室
		CASE 命蓮寺境内
			PLACE_ID = P105経蔵
		CASE 神霊廟道場
			PLACE_ID = P126道場
		CASE 商家町
			PLACE_ID = P213鈴奈庵
		CASE 寺子屋
			PLACE_ID = P222寺子屋
		CASE 長屋街
			PLACE_ID = P221稗田邸
		CASE 大図書館
			PLACE_ID = P310図書館
		CASE 永遠亭的内院
			PLACE_ID = P414輝夜私室
		CASE 霧雨魔法店
			PLACE_ID = P513魔理沙宅・研究部屋
		CASE 人偶的洋館
			PLACE_ID = P524人偶洋館・研究室
		CASE 迷途之家
			PLACE_ID = P633紫私室
		CASE 白玉楼
			PLACE_ID = P623幽幽子私室
		CASE 仙人的宅邸
			PLACE_ID = P724仙人的宅邸・書斎
		CASE 天界
			PLACE_ID = P842比那名居邸庫房
		CASE 地霊殿
			PLACE_ID = P920書斎
		CASE 綿月亭
			PLACE_ID = P1043書斎
		CASE 寧靜海
			PLACE_ID = P1065庫房
		CASEELSE
			RETURNF 0
	ENDSELECT
ENDIF
;PLACE_IDを返す
SELECTCASE PLACE_ID
	CASE P48夢幻遺跡・第一資料室, P49夢幻遺跡・研究室, P50夢幻遺跡・第二資料室
		RETURNF PLACE_ID
	CASE P105経蔵, P126道場
		RETURNF PLACE_ID
	CASE P213鈴奈庵, P222寺子屋
		RETURNF PLACE_ID
	CASE P221稗田邸
		SIF PERMISSION
			RETURNF PLACE_ID
		;扮演/思慕/恋慕で開放
		SIF FLAG:扮演 == [[阿求]]
			RETURNF PLACE_ID
		SIF TALENT:[[阿求]]:恋慕 || TALENT:[[阿求]]:思慕
			RETURNF PLACE_ID
	CASE P224慧音宅
		SIF PERMISSION
			RETURNF PLACE_ID
		;扮演/信賴度30以上同室で許可
		SIF FLAG:扮演 == [[慧音]]
			RETURNF PLACE_ID
		SIF CFLAG:[[慧音]]:同室 && CFLAG:[[慧音]]:信賴度 >= 30
			RETURNF PLACE_ID
	CASE P310図書館
		RETURNF PLACE_ID
	CASE P414輝夜私室
		SIF PERMISSION
			RETURNF PLACE_ID
		;扮演/思慕以上同室で許可
		SIF GROUPMATCH(FLAG:扮演, [[鈴仙]], [[帝]], [[輝夜]], [[永琳]])
			RETURNF PLACE_ID
		SIF CFLAG:[[輝夜]]:同室 && (TALENT:[[輝夜]]:恋慕 || TALENT:[[輝夜]]:思慕)
			RETURNF PLACE_ID
	CASE P424庫房, P427倉庫
		SIF PERMISSION
			RETURNF PLACE_ID
		;扮演/同室で許可
		SIF GROUPMATCH(FLAG:扮演, [[鈴仙]], [[帝]], [[輝夜]], [[永琳]])
			RETURNF PLACE_ID
		SIF CFLAG:[[鈴仙]]:同室 || CFLAG:[[帝]]:同室 || CFLAG:[[輝夜]]:同室 || CFLAG:[[永琳]]:同室
			RETURNF PLACE_ID
	CASE P513魔理沙宅・研究部屋
		SIF PERMISSION
			RETURNF PLACE_ID
		;扮演/信賴度30以上同室で許可
		SIF FLAG:扮演 == [[魔理沙]]
			RETURNF PLACE_ID
		SIF CFLAG:[[魔理沙]]:同室 && CFLAG:[[魔理沙]]:信賴度 >= 30
			RETURNF PLACE_ID
	CASE P524人偶洋館・研究室
		SIF PERMISSION
			RETURNF PLACE_ID
		;扮演/住んでる/思慕以上同室で許可
		SIF GROUPMATCH(FLAG:扮演, [[愛麗絲]], [[神綺]])
			RETURNF PLACE_ID
		SIF INRANGE(CFLAG:MASTER:初期位置, 521, 531)
			RETURNF PLACE_ID
		SIF (CFLAG:[[愛麗絲]]:同室 || CFLAG:[[神綺]]:同室) && (TALENT:[[愛麗絲]]:恋慕 || TALENT:[[愛麗絲]]:思慕 || TALENT:[[神綺]]:恋慕 || TALENT:[[神綺]]:思慕)
			RETURNF PLACE_ID
	CASE P623幽幽子私室
		SIF PERMISSION
			RETURNF PLACE_ID
		;扮演/思慕以上同室で許可
		SIF GROUPMATCH(FLAG:扮演, [[妖夢]], [[幽幽子]])
			RETURNF PLACE_ID
		SIF CFLAG:[[幽幽子]]:同室 && (TALENT:[[幽幽子]]:恋慕 || TALENT:[[幽幽子]]:思慕)
			RETURNF PLACE_ID
	CASE P633紫私室
		SIF PERMISSION
			RETURNF PLACE_ID
		;扮演/思慕以上同室で許可
		SIF GROUPMATCH(FLAG:扮演, [[橙]], [[藍]], [[紫]])
			RETURNF PLACE_ID
		SIF CFLAG:[[紫]]:同室 && (TALENT:[[紫]]:恋慕 || TALENT:[[紫]]:思慕)
			RETURNF PLACE_ID
	CASE P724仙人的宅邸・書斎
		SIF PERMISSION
			RETURNF PLACE_ID
		;扮演/住んでる/思慕以上同室で許可
		SIF FLAG:扮演 == [[華扇]]
			RETURNF PLACE_ID
		SIF INRANGE(CFLAG:MASTER:初期位置, 720, 730)
			RETURNF PLACE_ID
		SIF CFLAG:[[華扇]]:同室 && (TALENT:[[華扇]]:恋慕 || TALENT:[[華扇]]:思慕)
			RETURNF PLACE_ID
	CASE P842比那名居邸庫房
		SIF PERMISSION
			RETURNF PLACE_ID
		;扮演/住んでる/同室で許可
		SIF GROUPMATCH(FLAG:扮演, [[天子]], [[衣玖]])
			RETURNF PLACE_ID
		SIF INRANGE(CFLAG:MASTER:初期位置, 834, 842)
			RETURNF PLACE_ID
		SIF CFLAG:[[天子]]:同室 || CFLAG:[[衣玖]]:同室
			RETURNF PLACE_ID
	CASE P920書斎
		SIF PERMISSION
			RETURNF PLACE_ID
		;扮演/住んでる/思慕以上同室で許可,ただし信賴度50未満はダメ
		SIF FLAG:扮演 == [[覚]]
			RETURNF PLACE_ID
		SIF CFLAG:[[覚]]:信賴度 < 50
			RETURNF 0
		SIF INRANGE(CFLAG:MASTER:初期位置, 902, 926)
			RETURNF PLACE_ID
		SIF CFLAG:[[覚]]:同室 && (TALENT:[[覚]]:恋慕 || TALENT:[[覚]]:思慕)
			RETURNF PLACE_ID
	;綿月亭書斎
	CASE P1043書斎
		SIF PERMISSION
			RETURNF PLACE_ID
		;扮演/住んでるで許可
		SIF GROUPMATCH(FLAG:扮演, [[鈴仙二號]], [[依姫]], [[豊姫]])
			RETURNF PLACE_ID
		SIF INRANGE(CFLAG:MASTER:初期位置, 1020, 1049)
			RETURNF PLACE_ID
	;純狐邸庫房
	CASE P1050哆来咪宅・居間
		SIF PERMISSION
			RETURNF PLACE_ID
		;扮演/住んでる/思慕以上同室で許可,ただし信賴度50未満はダメ
		SIF FLAG:扮演 == [[純狐]]
			RETURNF PLACE_ID
		SIF CFLAG:[[純狐]]:信賴度 < 50
			RETURNF 0
		SIF INRANGE(CFLAG:MASTER:初期位置, 1060, 1072)
			RETURNF PLACE_ID
		SIF CFLAG:[[純狐]]:同室 && (TALENT:[[純狐]]:恋慕 || TALENT:[[純狐]]:思慕)
			RETURNF PLACE_ID
ENDSELECT
RETURNF 0

;旧関数も一応残しておく
[SKIPSTART]

;蔵書閲覧判定
;
;拠点/散步MAP両方使えます
;MASTERの現在位置を放り込むと資料を見せてくれるかどうかを判定
;戻り値は0もしくは「拠点MAPの場所ID」です
;散步Mapでどこそこで資料見たよってやる用
;PLACE_IDとMASTERの現在位置が違う場合は想定していません
;つまり、博麗神社にいる時に阿求のお屋敷の判定しちゃダメ
@IS_LIBRARY(PLACE_ID)
#FUNCTION
#DIM PLACE_ID              ;かならずMASTERの現在位置を渡すこと
#DIM DYNAMIC PLACE_LIBRARY
IF GET_MAPID(PLACE_ID) == MAIN_MAP
	SELECTCASE GET_MAPNAME(GET_MAPID(PLACE_ID))
	CASE "博麗神社"
	;MAP_博麗神社　研究助手を毎日やってたせいで誰でも資料閲覧できそうなイメージ
		SIF GROUPMATCH(NAME_FROM_PLACE(PLACE_ID), "夢幻遺跡・第一資料室", "夢幻遺跡・研究室", "夢幻遺跡・第二資料室")
			PLACE_LIBRARY = PLACE_ID
	CASE "命連寺"
	;MAP_命連寺、寺/道場ともに頼めば資料を見せてくれそう
		SIF GROUPMATCH(NAME_FROM_PLACE(PLACE_ID), "経蔵", "道場")
			PLACE_LIBRARY = PLACE_ID
	CASE "人間之里"
	;MAP_人里、借书屋と個人宅
		SIF GROUPMATCH(NAME_FROM_PLACE(PLACE_ID), "鈴奈庵", "寺子屋", "慧音宅", "稗田邸")
			PLACE_LIBRARY = PLACE_ID
	CASE "霧之湖～紅魔館"
	;MAP_紅魔館、とりあえず使用条件とかないので素通し
		SIF NAME_FROM_PLACE(PLACE_ID) == "図書館"
			PLACE_LIBRARY = PLACE_ID
	CASE "迷途竹林"
	;MAP_迷途竹林、このMap特に図書室的なものが無い
		;永遠亭はたくさん蔵書がありそうなイメージ
		SIF GROUPMATCH(NAME_FROM_PLACE(PLACE_ID), "輝夜私室", "庫房", "倉庫")
			PLACE_LIBRARY = PLACE_ID
	CASE "魔法之森"
	;MAP_魔法之森、魔法使たちは資料持ってるでしょう
		SIF GROUPMATCH(NAME_FROM_PLACE(PLACE_ID), "魔理沙宅・研究部屋", "人偶洋館・研究室")
			PLACE_LIBRARY = PLACE_ID
	CASE "妖怪之山 (山麓)"
	;MAP_魔法之森、華扇屋敷のみ
		SIF NAME_FROM_PLACE(PLACE_ID) == "仙人的宅邸・書斎"
			PLACE_LIBRARY = PLACE_ID
	CASE "地底"
	;MAP_地底、覚の書斎のみ
		SIF NAME_FROM_PLACE(PLACE_ID) == "書斎"
			PLACE_LIBRARY = PLACE_ID
	ENDSELECT
ELSE
	SELECTCASE GET_MAPNAME(GET_MAPID(PLACE_ID))
	CASE "博麗神社"
		SIF STR:(6000 + PLACE_ID / 10) == "夢幻遺跡"
			PLACE_LIBRARY = 49
	CASE "命蓮寺"
		SIF STR:(6000 + PLACE_ID / 10) == "命蓮寺境内"
			PLACE_LIBRARY = 105
		SIF STR:(6000 + PLACE_ID / 10) == "神霊廟道場"
			PLACE_LIBRARY = 126
	CASE "人間之里"
		SIF STR:(6000 + PLACE_ID / 10) == "商家町"
			PLACE_LIBRARY = 213
		SIF STR:(6000 + PLACE_ID / 10) == "寺子屋"
			PLACE_LIBRARY = 222
		SIF STR:(6000 + PLACE_ID / 10) == "長屋街"
			PLACE_LIBRARY = 221
	CASE "霧之湖～紅魔館"
		SIF STR:(6000 + PLACE_ID / 10) == "大図書館"
			PLACE_LIBRARY = 310
	CASE "迷途竹林"
		SIF STR:(6000 + PLACE_ID / 10) == "永遠亭的内院"
			PLACE_LIBRARY = 427
	CASE "魔法之森"
		SIF STR:(6000 + PLACE_ID / 10) == "霧雨魔法店"
			PLACE_LIBRARY = 513
		SIF STR:(6000 + PLACE_ID / 10) == "人偶的洋館"
			PLACE_LIBRARY = 524
	CASE "三途之川～冥界"
;		[SKIPSTART]
;		IF STR:(6000 + PLACE_ID / 10) == "迷途之家"
;			PLACE_LIBRARY = 630
;		ENDIF
;		IF STR:(6000 + PLACE_ID / 10) == "白玉楼"
;			PLACE_LIBRARY = 690
;		ENDIF
;		[SKIPEND]
	CASE "妖怪之山 (山麓)"
		IF STR:(6000 + PLACE_ID / 10) == "仙人的宅邸"
			PLACE_LIBRARY = 724
		ENDIF
	CASE "妖怪之山 (山頂)"
;		[SKIPSTART]
;		IF STR:(6000 + PLACE_ID / 10) == "天界"
;			PLACE_LIBRARY = 890
;		ENDIF
;		[SKIPEND]
	CASE "地底"
		IF STR:(6000 + PLACE_ID / 10) == "地霊殿"
			PLACE_LIBRARY = 920
		ENDIF
	CASE "夢之世界～月"
;		[SKIPSTART]
;		IF STR:(6000 + PLACE_ID / 10) == "綿月亭"
;			PLACE_LIBRARY = 1060
;		ENDIF
;		[SKIPEND]
	ENDSELECT
ENDIF
RETURNF CAN_READ_BOOK_COLLECTION(PLACE_LIBRARY)




;蔵書閲覧許諾判定
;・かならず拠点Mapの場所ID（詳細な地点の方）を渡すこと
;・MASTERがその場所にいるのが前提で処理を組んでます
@CAN_READ_BOOK_COLLECTION(PLACE_ID)
#FUNCTION
#DIM PLACE_ID
SELECTCASE PLACE_ID
;夢幻遺跡は素通し
CASE 48, 49, 50
	RETURNF PLACE_ID
;寺と神霊廟道場は素通し
CASE 105, 126
	RETURNF PLACE_ID
;鈴奈庵と寺子屋は素通し
CASE 213, 222
	RETURNF PLACE_ID
;稗田邸
;思慕/恋慕で解放
CASE 221
	SIF !TALENT:80:恋慕 && !TALENT:80:思慕
		RETURNF 0
	RETURNF PLACE_ID
;慧音宅
CASE 224
	SIF FLAG:扮演 == 67
		RETURNF PLACE_ID
	SIF CFLAG:67:信賴度 < 30
		RETURNF 0
	SIF CFLAG:67:現在位置 == CFLAG:MASTER:現在位置
		RETURNF PLACE_ID
;図書館は素通し
CASE 310
	RETURNF PLACE_ID
;輝夜私室
CASE 414
	SIF GROUPMATCH(FLAG:扮演, 52, 53, 62, 72)
		RETURNF PLACE_ID
	SIF !TALENT:62:恋慕 && !TALENT:62:思慕
		RETURNF 0
	SIF CFLAG:62:現在位置 == CFLAG:MASTER:現在位置
		RETURNF PLACE_ID
;庫房/倉庫の捜索はゆるく
;誰か一人同席してればよし
CASE 424, 427
	SIF GROUPMATCH(FLAG:扮演, 52, 53, 62, 72)
		RETURNF PLACE_ID
	SIF GROUPMATCH(CFLAG:MASTER:現在位置, CFLAG:52:現在位置, CFLAG:53:現在位置, CFLAG:62:現在位置, CFLAG:72:現在位置)
		RETURNF PLACE_ID
;魔理沙宅の研究室
;多少の信賴度でおｋ
CASE 513
	SIF FLAG:扮演 == 11
		RETURNF PLACE_ID
	SIF CFLAG:11:信賴度 < 30
		RETURNF 0
	SIF CFLAG:11:現在位置 == CFLAG:MASTER:現在位置
		RETURNF PLACE_ID
;人形館の研究室
CASE 524
	SIF GROUPMATCH(FLAG:扮演, 17, 102)
		RETURNF PLACE_ID
	SIF INRANGE(CFLAG:MASTER:初期位置, 521, 531)
		RETURNF PLACE_ID
	SIF !TALENT:17:恋慕 && !TALENT:17:思慕 && !TALENT:102:恋慕 && !TALENT:102:思慕
		RETURNF 0
	SIF GROUPMATCH(CFLAG:MASTER:現在位置, CFLAG:17:現在位置, CFLAG:102:現在位置)
		RETURNF PLACE_ID
;仙人屋敷の書斎
CASE 724
	SIF FLAG:扮演 == 43
		RETURNF PLACE_ID
	SIF INRANGE(CFLAG:MASTER:初期位置, 720, 730)
		RETURNF PLACE_ID
	SIF !TALENT:43:恋慕 && !TALENT:43:思慕
		RETURNF 0
	SIF CFLAG:43:現在位置 == CFLAG:MASTER:現在位置
		RETURNF PLACE_ID
;覚の書斎
;同居してても信賴度50必要
CASE 920
	SIF FLAG:扮演 == 49
		RETURNF PLACE_ID
	SIF CFLAG:49:信賴度 < 50
		RETURNF 0
	SIF INRANGE(CFLAG:MASTER:初期位置, 902, 926)
		RETURNF PLACE_ID
	SIF !TALENT:49:恋慕 && !TALENT:49:思慕
		RETURNF 0
	SIF CFLAG:49:現在位置 == CFLAG:MASTER:現在位置
		RETURNF PLACE_ID
ENDSELECT
[SKIPEND]