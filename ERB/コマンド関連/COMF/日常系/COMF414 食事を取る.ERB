;-------------------------------------------------
;吃飯
;日常系指令、レベル1
;-------------------------------------------------
@COM414
#DIM NOW_TARGET
#DIM EAT_TIME
CALL AssembleForMeal
CVARSET TCVAR, GETNUM(TCVAR, "料理評価値"), 0

IF DISH_TYPE == "主食"
	EAT_TIME = 40
ELSE
	EAT_TIME = 20
ENDIF

;特殊料理判定
IF DISH_NAME == "涮涮锅" && TARGET
	FOR LOCAL, 1, GET_TARGETNUM() + 1
		SIF EQUIP:(TARGET:LOCAL):下半身内衣２ > 0
			CONTINUE
		TFLAG:194 = 1
		DISH_TASTE += "精力/"
		BREAK
	NEXT
ELSEIF STRCOUNT(DISH_TASTE, "跳蛋/")
	TFLAG:194 = 2
ELSEIF DISH_NAME == "奶酪番茄沙拉"
	TFLAG:194 = 30
ELSEIF DISH_NAME == "興奮劑高湯"
	TFLAG:194 = 31
ELSEIF DISH_NAME == "银吉獣料理"
	TFLAG:194 = 34
ENDIF

;まずMASTER以外から
IF TARGET
	;口上側による制御
	CALL KOJO_MESSAGE_SEND("SUCCESS",-1,TARGET,-1,-1)
	SELECTCASE TFLAG:192
	CASE 1
		IF RAND:(100 - ABL:MASTER:料理技能 * 2) <= 9
			TCVAR:TARGET:料理評価値 = 700
		ELSE
			TCVAR:TARGET:料理評価値 = 500
		ENDIF
	CASE -1
		TCVAR:TARGET:料理評価値 = 400
	CASE -2
		TFLAG:193 = -2
		RETURN
	CASEELSE
		TCVAR:TARGET:料理評価値 = 0
	ENDSELECT
	NOW_TARGET = TARGET
	;まず現在的TARGET以外が食べる
	FOR LOCAL, 1, GET_TARGETNUM() + 1
		TARGET = TARGET:LOCAL
		SIF TARGET == NOW_TARGET
			CONTINUE
		;口上側による制御
		CALL KOJO_MESSAGE_SEND("SUCCESS",-1,TARGET,-1,-1)
		SELECTCASE TFLAG:192
		CASE 1
			IF RAND:(100 - ABL:MASTER:料理技能 * 2) <= 9
				TCVAR:TARGET:料理評価値 = 700
			ELSE
				TCVAR:TARGET:料理評価値 = 500
			ENDIF
		CASE -1
			TCVAR:TARGET:料理評価値 = 300
		CASE -2
			CONTINUE
		CASEELSE
			TCVAR:TARGET:料理評価値 = 0
		ENDSELECT
		;実食
		CALL COM414_実食(TARGET, EAT_TIME, TARGET == DISH_ASSI || CFLAG:TARGET:行動 == 5)
	NEXT
	;その後にTARGETが食べる
	TARGET = NOW_TARGET
	;現在的TARGETはBOUNS確定
	CALL COM414_実食(TARGET, EAT_TIME, 1)
	;信賴度,やや大きめ,平均3
	TFLAG:98 = (TCVAR:TARGET:料理評価値 / 100) / 2 + 1
ENDIF

CALL 吃飯(MASTER)
TIME += EAT_TIME
SIF CFLAG:MASTER:同行
	CFLAG:MASTER:同行 += EAT_TIME
RETURN 1

;-------------------------------------------------
;食べる処理と獲得ソースの計算
;CHARA     食べる人
;EAT_TIME  食べる時間
;BONUS     DISH_ASSIと現在的TARGETと仕事中の角色はBOUNS2倍
;-------------------------------------------------
@COM414_実食(CHARA, EAT_TIME, BONUS)
#DIM CHARA
#DIM EAT_TIME
#DIM BONUS
#DIM 空腹
#DIM 評価点係数
#DIM 好感度係数
#DIM 従順係数
#DIM 時間係数

#DIM DISH_ITAZURA
空腹 = 0
;食べられない状況
SIF CFLAG:CHARA:睡眠
	RETURN
SIF CFLAG:CHARA:衰弱
	RETURN
;満腹なので食べない
IF DISH_TYPE == "甜点"
	SIF TIME_PROGRESS(TCVAR:CHARA:甜点空腹時刻)
		空腹 = 1
ELSE
	SIF TIME_PROGRESS(TCVAR:CHARA:空腹時刻)
		空腹 = 1
ENDIF
SIF !空腹
	RETURN
;食べる
CALL 吃飯(CHARA)

;係数設定
IF ABL:CHARA:従順 <= 1
	従順係数 = 0
ELSEIF ABL:CHARA:従順 <= 3
	従順係数 = 1
ELSEIF ABL:CHARA:従順 <= 5
	従順係数 = 2
ELSEIF ABL:CHARA:従順 <= 8
	従順係数 = 3
ELSEIF ABL:CHARA:従順 <= 11
	従順係数 = 4
ELSEIF ABL:CHARA:従順 <= 14
	従順係数 = 5
ELSE
	従順係数 = 6
ENDIF
IF CFLAG:CHARA:好感度 < 100
	好感度係数 = 0
ELSEIF CFLAG:CHARA:好感度 < 500
	好感度係数 = 1
ELSEIF CFLAG:CHARA:好感度 < 1000
	好感度係数 = 2
ELSEIF CFLAG:CHARA:好感度 < 5000
	好感度係数 = 4
ELSEIF CFLAG:CHARA:好感度 < 10000
	好感度係数 = 6
ELSEIF CFLAG:CHARA:好感度 < 30000
	好感度係数 = 8
ELSE
	好感度係数 = 10
ENDIF
;1～3？,軽食&甜点1,主食2
時間係数 = EAT_TIME / 20
;1～9
IF TCVAR:CHARA:料理評価値 < 300
	評価点係数 = 0
ELSEIF TCVAR:CHARA:料理評価値 < 400
	評価点係数 = 1
ELSEIF TCVAR:CHARA:料理評価値 < 500
	評価点係数 = 3
ELSEIF TCVAR:CHARA:料理評価値 < 600
	評価点係数 = 12
ELSEIF TCVAR:CHARA:料理評価値 < 650
	評価点係数 = 16
ELSEIF TCVAR:CHARA:料理評価値 < 700
	評価点係数 = 20
ELSEIF TCVAR:CHARA:料理評価値 < 750
	評価点係数 = 24
ELSEIF TCVAR:CHARA:料理評価値 < 800
	評価点係数 = 30
ELSEIF TCVAR:CHARA:料理評価値 < 850
	評価点係数 = 36
ELSEIF TCVAR:CHARA:料理評価値 < 900
	評価点係数 = 40
ELSE
	評価点係数 = 45
ENDIF

;評価値の影響強め
SOURCE:CHARA:歓楽 = (100 * 評価点係数 + 500 * 好感度係数 + 30 * ABL:CHARA:親密) * 時間係数
SOURCE:CHARA:受動 = (20 * 評価点係数 + 300 * 従順係数 + 30 * ABL:CHARA:従順) * 時間係数
SIF CHARA == DISH_ASSI
	SOURCE:CHARA:征服 = (10 * 評価点係数 + ABL:CHARA:施虐属性 * 30) * 時間係数
SOURCE:CHARA:不潔 = STRCOUNT(DISH_TASTE, "劣化/") * (100 - TCVAR:CHARA:料理評価値 / 10) * 2
SOURCE:CHARA:反感 = STRCOUNT(DISH_TASTE, "劣化/") * (100 - TCVAR:CHARA:料理評価値 / 10) * 2
;没穿内裤涮涮锅
SIF TFLAG:194 == 1 && ! EQUIP:CHARA:下半身内衣２
	SOURCE:CHARA:露出 = 1000

SIF TALENT:CHARA:大胃王
	TIMES SOURCE:歓楽, 1.50
IF BONUS
	TIMES SOURCE:歓楽, 1.50
	TIMES SOURCE:受動, 1.50
	TIMES SOURCE:征服, 1.50
ENDIF

IF TCVAR:CHARA:料理評価値 >= 750
	IF TALENT:CHARA:心情 < 0
		CALL KIGEN_CHANGE(CHARA, 100, 1)
	ELSE
		CALL KIGEN_CHANGE(CHARA, 50, 1)
	ENDIF
	TFLAG:193 = 1
ELSEIF TCVAR:CHARA:料理評価値 >= 400
	CALL KIGEN_CHANGE(CHARA, 30, 1)
	TFLAG:193 = 0
ELSE
	SIF TALENT:CHARA:心情 >= 0
		CALL KIGEN_CHANGE(CHARA, 100, -1)
	TFLAG:193 = -1
ENDIF

SIF CFLAG:CHARA:同行
	CFLAG:CHARA:同行 += EAT_TIME
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE414
;吃飯実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(414)
	RETURN RESULT
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;料理がない
SIF DISH_NAME == ""
	RETURN 0
;主食は屋内じゃなきゃ無理
SIF DISH_TYPE == "主食" && !INROOM(CFLAG:MASTER:現在位置)
	RETURN 0
;食べたばかり
IF DISH_TYPE == "甜点"
	SIF !TIME_PROGRESS(TCVAR:MASTER:甜点空腹時刻)
		RETURN 0
ELSE
	SIF !TIME_PROGRESS(TCVAR:MASTER:空腹時刻)
		RETURN 0
ENDIF
;風呂場飯禁止
SIF BATHCHECK(CFLAG:MASTER:現在位置)
	RETURN 0
SIF CFLAG:MASTER:诶嘿嘿
	RETURN 0
RETURN 1

;-------------------------------------------------
;実際に料理を食べる処理
;-------------------------------------------------
@吃飯(ARG)
;#DIM 精力回復量
;#DIM 気力回復量
;#DIM 体力回復量
;#DIM TSP回復量
#DIM 精力回復率
#DIM 気力回復率
#DIM 体力回復率
#DIM TSP回復率
#DIM 満腹度
;回復率は0.1%単位

;TSP回復量 = 0
TSP回復率 = 0

;基本回復率と満腹度
SELECTCASE DISH_TYPE
	CASE "軽食"
		体力回復率 = 200
		気力回復率 = 300
		精力回復率 = 100
		満腹度 = 180
	CASE "主食"
		体力回復率 = 400
		気力回復率 = 400
		精力回復率 = 200
		満腹度 = 360
	CASE "甜点"
		体力回復率 = 100
		気力回復率 = 500
		精力回復率 = 100
		満腹度 = 180
ENDSELECT
;追加回復率
SIF STRCOUNT(DISH_TASTE, "神力/")
	TIMES 気力回復率, 1.2
SIF STRCOUNT(DISH_TASTE, "妖力/")
	TIMES 気力回復率, 1.2
SIF STRCOUNT(DISH_TASTE, "生命力/")
	TIMES 体力回復率, 1.2
SIF STRCOUNT(DISH_TASTE, "霊障/")
	TSP回復率 += 100
;鬼の魔力と区別
SIF STRCOUNT(DISH_TASTE, "/魔力/")
	TSP回復率 += 100
SIF STRCOUNT(DISH_TASTE, "仙術/")
	TSP回復率 += 100
SIF STRCOUNT(DISH_TASTE, "秘術/")
	TSP回復率 += 100
IF STRCOUNT(DISH_TASTE, "薬効/")
	気力回復率 += 100
ENDIF
IF STRCOUNT(DISH_TASTE, "薬膳/")
	気力回復率 += 100
	CALL BUFF_BASE(ARG, BASE_気力, MIN(MAXBASE:ARG:気力 * 10 / 100, 200))
ENDIF
	;IF STRCOUNT(DISH_TASTE, "鬼の魔力/")
	;	精力回復率 = 1000
	;	CALL BUFF_BASE(ARG, BASE_精力, MIN(MAXBASE:ARG:精力 * 50 / 100, 500))
	;ENDIF
	;IF STRCOUNT(DISH_TASTE, "血の魔力/") && ARG == MASTER
	;	TSP回復率 = 1000
	;	CALL BUFF_BASE(ARG, BASE_TSP, MIN(MAXBASE:ARG:TSP * 50 / 100, 500))
	;ENDIF
IF STRCOUNT(DISH_TASTE, "精力/")
	精力回復率 += 100
ENDIF
IF STRCOUNT(DISH_TASTE, "強精/")
	精力回復率 += 100
	CALL BUFF_BASE(ARG, BASE_精力, MIN(MAXBASE:ARG:精力 * 10 / 100, 200))
ENDIF
SIF STRCOUNT(DISH_TASTE, "奇跡/")
	TFLAG:幸運補正 = 5
SIF STRCOUNT(DISH_TASTE, "幸運/")
	TFLAG:幸運補正 = 3
;添加物
SELECTCASE DISH_DRG_2
CASE GETNUM(ITEM, "栄養強化剤")
	TIMES 気力回復率, 1.1
	TIMES 体力回復率, 1.1
	TIMES 精力回復率, 1.1
ENDSELECT
;特殊Buff系
;祝福：妊娠率がほんの少し上昇,妊娠対象に付与されていないと効果無（種側は効果無）
SIF STRCOUNT(DISH_TASTE, "祝福/")
	TCVAR:ARG:祝福 = 1
;魔性：弾幕勝負で自分の骰子目がUP,対戦相手に付与されてると対戦相手もUP
SIF STRCOUNT(DISH_TASTE, "魔性/")
	TCVAR:ARG:魔性 = 1
;加護：弾幕勝負で对手的骰子目がダウン,対戦相手に付与されてると自分もダウン
SIF STRCOUNT(DISH_TASTE, "加護/")
	TCVAR:ARG:加護 = 1
;豊穣：野菜収穫時の収穫量UP
SIF STRCOUNT(DISH_TASTE, "豊穣/")
	TCVAR:ARG:豊穣 = 1

;MASTER以外の処理
IF ARG != MASTER
	;味の評価,口上側で制御されている場合はカット
	SIF !TCVAR:ARG:料理評価値
		CALL REVIEW_DISH(ARG, DISH_TASTE)
	;仕込み
	SELECTCASE DISH_DRG
	CASE GETNUM(ITEM, "媚薬")
		TCVAR:ARG:媚薬 = 210
	CASE GETNUM(ITEM, "利尿剤")
		TCVAR:ARG:利尿剤 = 210
	CASE GETNUM(ITEM, "催情薬")
		TCVAR:ARG:催情薬 = 210
	CASE GETNUM(ITEM, "睡眠薬")
		IF TCVAR:ARG:睡眠薬強度 < 2
			TCVAR:ARG:睡眠薬強度 = 2
			TCVAR:ARG:睡眠薬茶使用 = 1
		ENDIF
	CASE GETNUM(ITEM, "新鲜精液")
	    TCVAR:ARG:新鲜精液 = 210
	ENDSELECT
	;;評価値補正,気力と体力のみ
	SELECTCASE TCVAR:ARG:料理評価値
	CASE IS < 400
		TIMES 体力回復率, 0.6
		TIMES 気力回復率, 0.6
	CASE IS < 600
		TIMES 体力回復率, 0.8
		TIMES 気力回復率, 0.8
	CASE IS < 750
		TIMES 体力回復率, 1.0
		TIMES 気力回復率, 1.0
	CASEELSE
		TIMES 体力回復率, 1.1
		TIMES 気力回復率, 1.1
	ENDSELECT
ENDIF

;回復量精算
;体力回復量 = MAXBASE:ARG:体力 * 体力回復率 / 1000
;気力回復量 = MAXBASE:ARG:気力 * 気力回復率 / 1000
;精力回復量 = MAXBASE:ARG:精力 * 精力回復率 / 1000
;TSP回復量 = MAXBASE:ARG:TSP * TSP回復率 / 1000

;跳蛋料理
SIF STRCOUNT(DISH_TASTE, "跳蛋/")
	EXP:ARG:Ｃ経験 += 30

;満腹度上昇
SIF STRCOUNT(DISH_TASTE, "丰盛/")
	TIMES 満腹度, 1.2
SIF STRCOUNT(DISH_TASTE, "简易/")
	TIMES 満腹度, 0.8
CALL 満腹度上昇(ARG, DISH_TYPE, 満腹度)

;回復処理
;CALL RECOVER(ARG, 体力回復量, "体力", "吃飯的回復")
;CALL RECOVER(ARG, 気力回復量, "気力", "吃飯的回復")
CALL RECOVER_PERMIL(ARG, 体力回復率, "体力", 0, "吃飯的回復")
CALL RECOVER_PERMIL(ARG, 気力回復率, "気力", 0, "吃飯的回復")
CALL RECOVER_PERMIL(ARG, 精力回復率, "精力", 0, "吃飯的回復")
IF ARG == MASTER
;	CALL RECOVER(ARG, 精力回復量, "精力", "吃飯的回復")
;	CALL RECOVER(ARG, TSP回復量, "TSP", "吃飯的回復")
	CALL RECOVER_PERMIL(ARG, TSP回復率, "TSP", 0, "吃飯的回復")
ELSE
	PRINTFORML %CALLNAME:ARG + "の評価…………", 40, LEFT%{TCVAR:ARG:料理評価値 / 10, 3}点
ENDIF

;動かないフラグ
TCVAR:ARG:なるべく動かない = 1

;-------------------------------------------------
;料理を評価する関数
;ARGS	DISH_TASTE
;-------------------------------------------------
@REVIEW_DISH(CHARA, ARGS)
#DIM CHARA
#DIMS TASTE_CHARA_GOOD
#DIMS TASTE_CHARA_BAD
#DIM TASTE_GOOD
#DIM TASTE_BAD

TASTE_CHARA_GOOD = %GET_STR(, "角色データ", CHARA, "料理：喜欢的味道")%
TASTE_CHARA_BAD = %GET_STR(, "角色データ", CHARA, "料理：讨厌的味道")%

;共通設定；好き
;甜点はみんな好き
TASTE_CHARA_GOOD += "甜点/"
;できたてはみんな好き
TASTE_CHARA_GOOD += "できたて/"
;[妖怪]は妖力が好き
SIF CHECK_CHARA(CHARA, "妖怪") && !CHECK_CHARA(CHARA, "半人类")
	TASTE_CHARA_GOOD += "妖力/"
;[神霊][人类]は神力が好き
SIF CHECK_CHARA(CHARA, "神霊") || (CHECK_CHARA(CHARA, "人类") && !CHECK_CHARA(CHARA, "半人类"))
	TASTE_CHARA_GOOD += "神力/"
;[妖精]は生命力が好き
SIF CHECK_CHARA(CHARA, "妖精")
	TASTE_CHARA_GOOD += "生命力/"
;[魔法使]は魔力が好き
SIF CHECK_CHARA(CHARA, "魔法使")
	TASTE_CHARA_GOOD += "魔力/"
;[幽霊][付喪神]は霊障が嫌いじゃないなら好き
SIF (CHECK_CHARA(CHARA, "幽霊") || CHECK_CHARA(CHARA, "付喪神")) && !STRCOUNT(TASTE_CHARA_BAD, "霊障/")
	TASTE_CHARA_GOOD += "霊障/"
;[付喪神]は鬼の魔力が好き
SIF CHECK_CHARA(CHARA, "付喪神")
	TASTE_CHARA_GOOD += "鬼の魔力/" * 3
;[幼稚]は母亲的味道が好き
SIF TALENT:CHARA:幼稚
	TASTE_CHARA_GOOD += "母亲的味道/"
;[幼稚]は儿童午餐が好き
SIF TALENT:CHARA:幼稚
	TASTE_CHARA_GOOD += "儿童午餐/" * 5
;DISH_ASSIなら嫉妬は好き（BOUNS）
SIF CHARA == DISH_ASSI
	TASTE_CHARA_GOOD += "嫉妬/" * 3
;BASEPOINTが500以上なら隠し味は好き（BOUNS）
SIF DISH_BASEPOINT >= 500
	TASTE_CHARA_GOOD += "隠し味/" * 2
;[動物耳]はまんまが好き
SIF TALENT:CHARA:動物耳
	TASTE_CHARA_GOOD += "まんま/" * 3
;夏は冷ものが、冬は温暖ものが好き（嫌いじゃなければ）
IF GET_MONTH() == "夏之月" && !STRCOUNT(TASTE_CHARA_BAD, "冷/")
	TASTE_CHARA_GOOD += "冷/"
ELSEIF GET_MONTH() == "冬之月" && !STRCOUNT(TASTE_CHARA_BAD, "温暖/")
	TASTE_CHARA_GOOD += "温暖/"
ENDIF
;愛情,ASSI→MASTERへの愛情であって点数化できないのでは？,今のところフレーバー


;共通設定：嫌い
;[劣化]はみんな嫌い
TASTE_CHARA_BAD += "劣化/" * 3
;[过季]はNG
TASTE_CHARA_BAD += "过季/" * 6
;[妖怪][幽霊][付喪神]は神力が好きじゃないなら嫌い
SIF (CHECK_CHARA(CHARA, "妖怪") || CHECK_CHARA(CHARA, "幽霊") || CHECK_CHARA(CHARA, "付喪神")) && !STRCOUNT(TASTE_CHARA_GOOD, "神力/")
	TASTE_CHARA_BAD  += "神力/"
;[人类][神霊]は妖力が好きじゃないなら嫌い
SIF (CHECK_CHARA(CHARA, "人类") || CHECK_CHARA(CHARA, "神霊")) && !STRCOUNT(TASTE_CHARA_GOOD, "妖力/")
	TASTE_CHARA_BAD  += "妖力/"
;[人类]は霊障が好きじゃないなら嫌い
SIF CHECK_CHARA(CHARA, "人类") && !CHECK_CHARA(CHARA, "半人类") && !STRCOUNT(TASTE_CHARA_GOOD, "霊障/")
	TASTE_CHARA_BAD  += "霊障/"
;DISH_ASSIでないなら嫉妬は嫌い（減点）
IF CHARA != DISH_ASSI
	IF TALENT:CHARA:恋人
		TASTE_CHARA_BAD  += "嫉妬/" * 5
	ELSEIF TALENT:CHARA:恋慕
		TASTE_CHARA_BAD  += "嫉妬/" * 3
	ELSEIF TALENT:CHARA:思慕
		TASTE_CHARA_BAD  += "嫉妬/"
	ENDIF
ENDIF
;BASEPOINTが500未満なら隠し味は嫌い（減点）
SIF DISH_BASEPOINT < 500
	TASTE_CHARA_BAD += "隠し味/" * 2

;評価得点,得点はブレる
TASTE_GOOD = 300 + (80 + RAND:20) * STR_MULTI_COUNT(TASTE_CHARA_GOOD, ARGS)
;評価減点,減点は1.5倍
TASTE_BAD = 300 + 150 * STR_MULTI_COUNT(TASTE_CHARA_BAD, ARGS)

;総評
TCVAR:CHARA:料理評価値 = TASTE_GOOD * DISH_BASEPOINT * 2 / (TASTE_GOOD + TASTE_BAD)
[IF_DEBUG]
	PRINTFORML 好き：%TASTE_CHARA_GOOD%
	PRINTFORML 嫌い：%TASTE_CHARA_BAD%
	PRINTFORML 品物：%ARGS%
	PRINTFORML 基本：{DISH_BASEPOINT}
	PRINTFORML 得点：{TASTE_GOOD}
	PRINTFORML 減点：{TASTE_BAD}
	PRINTFORML 結果：{TCVAR:CHARA:料理評価値}
	WAIT
[ENDIF]

;-------------------------------------------------
;主に旧口上に合わせるための刺激的味覚関数
;超辣/超甜/超酸には対応,芥末には対応できず……
;-------------------------------------------------
@TASTE_REACTION(ARGS)
#FUNCTION
SIF STRCOUNT(DISH_TASTE, ARGS + "/") >= 5
	RETURNF 1

;-------------------------------------------------
;満腹度上昇
;-------------------------------------------------
@満腹度上昇(ARG, ARGS, ARG:1 = 0)
#DIM 満腹度
IF ARG:1 == 0
	SELECTCASE ARGS
		CASE "軽食"
			満腹度 = 180
		CASE "主食"
			満腹度 = 360
		CASE "甜点"
			満腹度 = 180
	ENDSELECT
ELSE
	満腹度 = ARG:1
ENDIF

SIF TALENT:ARG:大胃王
	TIMES 満腹度, 0.7
IF ARGS == "甜点"
	TCVAR:ARG:甜点空腹時刻 = 1440 * DAY + TIME + 満腹度
ELSE
	TCVAR:ARG:空腹時刻 = 1440 * DAY + TIME + 満腹度
ENDIF
