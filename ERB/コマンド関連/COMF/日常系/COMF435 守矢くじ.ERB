;-------------------------------------------------
;守矢神簽
;日常系指令、レベル1
;-------------------------------------------------
;守矢ガｔ……くじ
;LOCAL
@COM435
#DIM ガチャ回数
#DIM 筹码支付
#DIMS 台詞
#DIMS 表情
VARSET LOCAL
ガチャ回数 = 0
IF !BETWEENTIME(600,1200)
	PRINTFORML 关门了……
	PRINTFORMW 好像是非营业时间
	RETURN -1
ENDIF
IF !GETBIT(FLAG:999, 2)
	PRINTFORMW 境内的角落里貼著一張紙
	PRINTFORML ┌─────────────┐
	PRINTFORML ｜　　　　　　　　　　　　　｜
	PRINTFORML ｜　　守矢神签　　　　　　　｜
	PRINTFORML ｜　　　　　絶賛販売中！　　｜
	PRINTFORML ｜　　　　　　　　　　　　　｜
	PRINTFORML ｜　　　　　　　　　　　　　｜
	PRINTFORML ｜　　　　　　　　　　　　　｜
	PRINTFORML ｜　　一定能让你捞回本哦　　｜
	PRINTFORML ｜　　　　　　　　　　　　　｜
	PRINTFORML ｜　　　　　　　　守矢神社　｜
	PRINTFORML └─────────────┘
	PRINTFORMW 
	PRINTFORML 幫幫忙吧、巫女装束（裙褲當然是綠色的）的少女帯著親切的笑臉看著這邊
	PRINTFORMW 而且、在少女旁邊放著寫著「布施箱」的箱子
	PRINTFORML …
	PRINTFORML ……
	PRINTFORMW 不知為什麼有種諾有諾無的厭惡感…

	SETBIT FLAG:999, 2
	;最初のくじのセットを作成
	CALL FISHER_YATES_SHAFFLE(300)
	ARRAYCOPY "SHAFFLE_ARRAY", "MORIYA_BOX"
ENDIF

IF FLAG:守矢神簽约会的对象
	CALL SPTALK, 0, @"通常", 15, @"「现在可不是抽签的时候吧」/「这是和神的约会权、一分一秒也不能浪费哦！」", 1
	RETURN -1
ENDIF
IF FLAG:守矢约会権 && !FLAG:约会的对象
	CALL ASK_YN(@"行使和%CALLNAME:(FLAG:守矢约会権)%的约会权", "抽签")
	IF !RESULT
		CALL DATE_RIGHT
		TIME += 3
		RETURN 1
	ENDIF
ENDIF
IF MONEY < 3000 && MONEY:2 < 20
	PRINTFORMW 打開錢包発現錢不夠
	PRINTFORMW 嗯、為了布施所以在籌錢
	RETURN -1
ENDIF

CALL SPTALK, 0, @"笑顔", 15, @"「守矢神签1回3000円或20籌碼、或者守矢之守札5枚」", 1
PRINTL 该怎么做呢？
CALL ASK_M("风险很大、算了罢",1,"用\\3000円试一下", MONEY >= 3000, "用20筹码试一下", MONEY:2 >= 20, "用5枚守矢之守札", ITEM:131>= 5, "\\27000円10连抽", MONEY >= 27000, "用180筹码10连抽", MONEY:2 >= 180, "用50枚守矢之守札10连抽", ITEM:131 >= 50)
SELECTCASE RESULT
	CASE 0
		IF 顔絵付きメッセージ
			IF !RAND:8
				台詞 = 「诶、只是看看吗」/「……会被作祟的哦？」
				表情 = 憤怒
			ELSE
				台詞 = 「%TEXTR("……没风险的哟？/是真的能赚到哟？/嗯、那就拜托下次了")%」
				表情 = 通常
			ENDIF
			CALL SPTALK, 0, @"%表情%", 15, @"%台詞%", 1
		ENDIF
		RETURN -1
	CASE 1
		MONEY -= 3000
	CASE 2
		MONEY:2 -= 20
	CASE 3
		ITEM:131 -= 5
	CASE 4
		ガチャ回数 = 9
		MONEY -= 27000
	CASE 5
		ガチャ回数 = 9
		MONEY:2 -= 180
	CASE 6
		ガチャ回数 = 9
		ITEM:131 -= 50
ENDSELECT

IF (RESULT == 1 || RESULT == 2) && ITEM:兎符「開運大紋」
	PRINTFORML 要使用兎符「開運大紋」麼？
	CALL ASK_YN
	IF !RESULT
		ITEM:兎符「開運大紋」 --
		TFLAG:194 = 5
	ENDIF
ENDIF

$RENZOKU

IF GETBIT(FLAG:999, 3)
	IF MORIYA_BOX:0 == -1
		PRINTFORMW 現在的签箱抽完了所以准备好了新的箱子
		CALL FISHER_YATES_SHAFFLE(300)
		ARRAYCOPY "SHAFFLE_ARRAY", "MORIYA_BOX"
	ELSE
		PRINTFORML 这个签箱里已经開出过特等奖了
		PRINTFORML 要廃棄掉这个签箱去抽新的签吗？
		PRINTFORML [1] 用下一个箱子抽
		PRINTFORML [0] 再继续抽这个箱子
		$LOOP_INPUT_2
		INPUT
		SELECTCASE RESULT
			CASE 0
				PRINTW 那么、从这个箱里抽
			CASE 1
				PRINTW 那么就更換签箱吧
				CLEARBIT FLAG:999, 3
				CALL FISHER_YATES_SHAFFLE(300)
				ARRAYCOPY "SHAFFLE_ARRAY", "MORIYA_BOX"
			CASEELSE
				GOTO LOOP_INPUT_2
		ENDSELECT
	ENDIF
ENDIF

PRINTL 把钱放到布施箱里、
PRINTL 从箱子里面抽了一根神签、
PRINTW 递给了接待员。
PRINTL ……
PRINTL …
CALL FOTUNE_MORIYA
TIME += 2
IF ガチャ回数 > 0
	PRINTFORMW 
	ガチャ回数 --
	GOTO RENZOKU
ENDIF

SIF 顔絵付きメッセージ
	CALL SPTALK, 0, @"笑顔", 15, @"「非常感谢」", 1
RETURN 1

;守谷くじ
@COM_ABLE435
;一括管理
SIF GLOBAL_COMABLE(435)
	RETURN RESULT
SIF !TFLAG:100
	RETURN 0
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;気力0では不可
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF !MORIYA_OMIKUJI(CFLAG:MASTER:現在位置)
	RETURN 0
RETURN 1

@MORIYA_OMIKUJI(ARG)
#FUNCTION
;守矢神社の社務所
SIF ARG == GET_MAP_REPLACEMENT(813)
	RETURNF 1
RETURNF 0
