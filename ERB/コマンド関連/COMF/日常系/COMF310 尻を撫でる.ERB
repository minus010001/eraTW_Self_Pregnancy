;-------------------------------------------------
;摸屁股
;日常系指令　レベル1
;-------------------------------------------------
@COM310
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 10
ELSE
	IF FLAG:甲斐性無 && !CFLAG:诶嘿嘿
		DRAWLINE
		PRINTFORMW %CALLNAME:MASTER%沒有那樣的膽量
		RETURN -1
	ENDIF
	LOCAL = RAND:110
	LOCAL:1 = MIN(109, 90 + GET_SUCCESS_RATE() / 5)
	CALL KOJO_FORCED_FAILURE(LOCAL, LOCAL:1)
	SIF TFLAG:193 == -2
		RETURN -1
	DOWNBASE:気力 += 50
	DOWNBASE:MASTER:気力 += 50
	TFLAG:理性削り = 1
	;信頼
	TFLAG:98 = -3

	;固定で獲得するソース
	SOURCE:歓楽 = 100
	SOURCE:情愛 = 100
	SOURCE:露出 = 200
	SOURCE:反感 = 500
	SOURCE:欲情 = 100
	SOURCE:受動 = 100

	;ABL:親密をみる
	IF ABL:親密 <= 5
		SOURCE:反感 -= (ABL:親密 * 5)

	ELSEIF ABL:親密 <= 7
		SOURCE:歓楽 += 400 + (ABL:親密 * 5)
		SOURCE:情愛 += 400 + (ABL:親密 * 10)
		SOURCE:反感 -= (ABL:親密 * 10)
		TFLAG:193 = RAND:2 + 2
	ELSEIF ABL:親密 <= 9
		SOURCE:歓楽 += 700 + (ABL:親密 * 10)
		SOURCE:情愛 += 600 + (ABL:親密 * 15)
		SOURCE:反感 -= (ABL:親密 * 15)
		SOURCE:欲情 += 200 * ABL:欲望
		SOURCE:受動 += 200 * ABL:従順
		TFLAG:193 = RAND:2 + 2
	ELSEIF !TALENT:恋慕
		SOURCE:歓楽 += 1200 + (ABL:親密 * 20)
		SOURCE:情愛 += 800 + (ABL:親密 * 25)
		SOURCE:反感 -= (ABL:親密 * 20)
		SOURCE:欲情 += 250 * ABL:欲望
		SOURCE:受動 += 250 * ABL:従順
		TFLAG:193 = RAND:2 + 2
	ELSE
		SOURCE:歓楽 += 2000 + (ABL:親密 * 20)
		SOURCE:情愛 += 1800 + (ABL:親密 * 30)
		SOURCE:欲情 += 1000 + 250 * ABL:欲望
		SOURCE:受動 += 1000 + 250 * ABL:従順
		SOURCE:反感 -= (ABL:親密 * 50)
		TFLAG:193 = RAND:2 + 3
	ENDIF
	SIF SOURCE:反感 < 0
		SOURCE:反感 = 0

	SIF SOURCE:歓楽 < 0
		SOURCE:歓楽 = 0
	;先制発生時は失敗しない
	SIF TFLAG:102 == 3 && TFLAG:192 != -1
		TFLAG:193 = MAX(TFLAG:193,2)

	LOCAL = 0
	;親密が低いと好感度低下
	IF ABL:親密 <= 5
		LOCAL -= 10
	ELSEIF ABL:親密 <= 7
		LOCAL -= 5
	ELSEIF ABL:親密 <= 9
		LOCAL -= 3
	ELSEIF TALENT:恋慕
		LOCAL -= 1
	ENDIF
	;好感度変化
	TFLAG:97 += LOCAL
	IF HITOKE(1)
		TIMES SOURCE:露出 , 2.00
		TIMES SOURCE:反感 , 2.00
	ENDIF
	IF TFLAG:193 == 1
		TIMES SOURCE:歓楽 , 0.10
		TIMES SOURCE:情愛 , 0.50
		TIMES SOURCE:受動 , 0.50
		TIMES SOURCE:欲情 , 0.50
		TIMES SOURCE:反感 , 1.50
	ELSEIF TFLAG:193 == 2
		TIMES SOURCE:歓楽 , 1.00
		TIMES SOURCE:情愛 , 1.00
		TIMES SOURCE:受動 , 1.00
		TIMES SOURCE:欲情 , 1.00
	ELSEIF TFLAG:193 == 3
		TIMES SOURCE:歓楽 , 1.50
		TIMES SOURCE:情愛 , 1.50
		TIMES SOURCE:受動 , 1.50
		TIMES SOURCE:欲情 , 1.50
	ELSE
		TIMES SOURCE:歓楽 , 2.00
		TIMES SOURCE:情愛 , 2.00
		TIMES SOURCE:受動 , 2.00
		TIMES SOURCE:欲情 , 2.00
		TIMES SOURCE:反感 , 0.75
	ENDIF
	TIME += 10
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE310
;摸屁股実行判定
SIF !TFLAG:100
	RETURN 0
SIF !TARGET
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(310)
	RETURN RESULT
;時間停止中はスキップ
IF !FLAG:70
	SIF CFLAG:睡眠 == 0 && GET_TARGETNUM() == 0
		RETURN 0
	;睡眠中
	SIF CFLAG:睡眠 && !CFLAG:恶作剧
		RETURN 0
	;仕事中
	SIF CFLAG:行動 == 5 && !(TARGET == [[美宵]])
		RETURN 0
	;陪睡中
	SIF CFLAG:陪睡中
		RETURN 0
	;约会中
	SIF CHK_DATENOW(CFLAG:约会中) && CFLAG:現在位置 != OMANEKIBEYA()
		RETURN 0
ENDIF
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;腕使用中
SIF TFLAG:61
	RETURN 0
SIF GETBIT (TCVAR:26,1)
	RETURN 0
RETURN 1

@KOJO_FORCED_FAILURE(ARG,ARG:1)
SELECTCASE TFLAG:192
	;通常は確率判定
	CASE 0
		IF ARG <= 9
			;大成功
			TFLAG:193 = 3
		ELSEIF ARG >= ARG:1
			;失敗
			TFLAG:193 = 1
		ELSE
			;残りは成功
			TFLAG:193 = 2
		ENDIF
	;強制失敗
	CASEELSE
		TFLAG:193 = TFLAG:192
ENDSELECT