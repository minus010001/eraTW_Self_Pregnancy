;『にゅーぷらん』を実行する関数
@INVITE_NEWPLAN
#DIM チェンジ回数
#DIM 指名回数
#DIM 希望
#DIM 位置 = 0, 5, 10, 0
#DIM 幅 = 5, 5, 5, 15
{
#DIMS AWAHIME_RACE = "人妻", "町娘", "泡姫", "鴉天狗", "河童",
					 "妖怪狸", "妖怪狐", "因幡", "白狼天狗", "妖怪猫",
					 "鬼", "女仆妖精", "僵尸妖精", "妖精", "サキュ浴室"
}
チェンジ回数 = 0
指名回数 = 0
TARGET = 0
RESULT = 0
VARSET LOCAL
;３日に一度販売員がお勤め
SIF ((GETTIME()/1000000000) % 100) % 3 == 2
	AWAHIME_RACE:5 = 販売員
IF !STRCOUNT(CSTR:0:0, "泡入浴/")
	CSTR:0:0 = %CSTR:0:0%泡入浴/
	PRINTL 「呣ー呀、不错的判断呀」
	PRINTW 「那么、就来一套『New-Plan』服务喵！」
	PRINTL 「来、从给你的这几张顔絵里、选择自己喜欢的雌性吧」
	PRINTL 「那之后、就会带喵桑你去相应的私人風呂的！」
	PRINTW 「只需稍等一会儿、就会有雌性来陪你一起風呂呀……」
	PRINTL 
	PRINTL 「那之后就不知道啦！　都和我无关啦喵」
	PRINTL 「喵桑和雌性、友好的咻咻呀、各种各样的咻咻呀、随你喜欢呀！」
	PRINTW 「時間呀是90分钟！　不都的话延長也是OK的呀」
	PRINTL
	PRINTL 「这边、这边！　打起干劲来呀？」
	PRINTW 前台的妖怪、露出了咯嘿嘿的讨嫌的笑容
ELSE
	PRINTL 「呣ー呀、喵桑阿、果然很喜欢这个呀！」
	PRINTL 前台的妖怪、照常露出了咯嘿嘿的讨嫌的笑容
ENDIF
PRINTL
$LOOP_SOAP0
PRINTFORML 「\@ !RESULT ? 那么 # 再来一遍 \@、喜欢的雌性的类型、来告诉我吧」
PRINTL [1] 普通の娘
PRINTL [2] 兽耳娘
PRINTL [3] 人外娘
PRINTL [4] 随便来点
PRINTL [5] 指名喜欢的孩子
PRINTL [0] 果然還是放棄
$LOOP_SOAP1
INPUT
SELECTCASE RESULT
	CASE 0
		SIF チェンジ回数 > 0 || 指名回数 > 0
			CALL SOAP_RESET
		IF TFLAG:211 >= 2
			PRINTL 「唔嘎ー！！　唔嘎ー！！　唔嘎ー！！」
			PRINTW 「你小子喵！　打扰到赚钱啦喵！」
			PRINTL 「哦ー咿！！　喵们！　出来吧！」
			PRINTW 「把这个家伙赶出去呀！！」
			CALL KOWAI_ONISAN
			RETURN 0
		ELSE
			PRINTL 「哈！？　什么叫算了呀！？」
			PRINTL 「不喜欢的话、就给我滚回家呀！」
			TFLAG:211 = 2
			RETURN -1
		ENDIF
	CASE 1 TO 5
		希望 = RESULT -1
	CASEELSE
		CLEARLINE 1
		REUSELASTLINE 「快告诉喜欢哪类呀！！」
		GOTO LOOP_SOAP1
ENDSELECT
$LOOP_SOAP2
SELECTCASE チェンジ回数 
	CASE 0
		IF 希望 == 3
			PRINTL 「哦哦哦、放心吧喵」
			PRINTW 「呜恰、这里的类型都齐全呀！」
		ELSEIF 希望 == 4
			PRINTL 「最喜欢的雌性、找到了喵？」
			PRINTW 「那么、就快来这边吧、好多好多的钱金扔在这里吧！」
		ELSE
			PRINTL 「哦哦哦、了解了呀」
			PRINTW 「喵桑最喜欢的雌性…」
		ENDIF
	CASE 1 TO 9, 11 TO 17
		PRINTFORML 「\@ チェンジ回数 < 10 ? 这些不行喵？ # 还没有做出决定喵… \@」
		PRINTW 「那样的话、其她的雌性…」
	CASE 10
		PRINTL 「真的、真是个挑剔的客人呀」
		PRINTW 「雌性啊、难道不是只要有个洞就足够了吗…」
	CASE 18
		PRINTL 「适可而止呀！」
		PRINTW 「不会再给更多的雌性了呀！！」
	CASE 19
		PRINTL 「唔嘎ー！」
		PRINTW 「你这家伙呀！　这里没有！　雌性了呀！！」
ENDSELECT
;リピート指名
IF 希望 == 4
	指名回数 ++
	CALL LOAD_MOBGIRL(1)
	SIF RESULT == -1
		GOTO LOOP_SOAP0
	CFLAG:RANDOM_CHARANUM:出禁 = 0
	;なぜか再登場は裸なので强行浴衣着せる。衣装周りは複雑でよくわからない
	EQUIP:RANDOM_CHARANUM:連衣裙 = 13
	CFLAG:RANDOM_CHARANUM:没穿内裤 = 1
	CFLAG:RANDOM_CHARANUM:上半身内衣確認 = 1
	CFLAG:RANDOM_CHARANUM:下半身内衣確認 = 1
	;攻守交代時すぐ萎えないように积攒度を補充
	CFLAG:RANDOM_CHARANUM:积攒度 = LIMIT(CFLAG:RANDOM_CHARANUM:积攒度 + RAND:200, 500, 1000)
	TALENT:RANDOM_CHARANUM:心情 = 0 + RAND:2
	TCVAR:RANDOM_CHARANUM:口服避孕薬 = 1
ELSE
	チェンジ回数 ++
	LOCAL = RAND(幅:希望) + 位置:希望
	SIF AWAHIME_RACE:LOCAL == "妖怪狸" && RAND:2
		AWAHIME_RACE:LOCAL = 茨狸
	CALL MAKE_YUKIZURI(RANDOM_CHARANUM, AWAHIME_RACE:LOCAL, 2)
ENDIF
TEQUIP:RANDOM_CHARANUM:上半身着衣状況 = 1
CALL CHARA_STATE(RANDOM_CHARANUM)
DRAWLINE
CALL COLORMESSAGE(@"処女喪失履歴：%CSTR:RANDOM_CHARANUM:処女喪失履歴%", C_PINK, 1)
PRINTFORML 
PRINTFORML 「这个雌性\@ 希望 == 4 ? 对吧 # 如何 \@ぎゃ？」
PRINTL [1] 是这孩子
PRINTL [2] 更换！
PRINTL [0] 果然還是放棄
$LOOP_SOAP3
INPUT
SELECTCASE RESULT
	CASE 0
		CALL SOAP_RESET
		IF TFLAG:211 >= 2
			PRINTL 「唔嘎ー！！　唔嘎ー！！　唔嘎ー！！」
			PRINTW 「喵这家伙打扰到赚钱啦喵！」
			PRINTL 「哦ー咿！！　喵们！　到你们出场啦！」
			PRINTW 「把这个家伙赶出去呀！！」
			CALL KOWAI_ONISAN
			RETURN 0
		ELSE
			PRINTL 「哈！？　什么叫算了呀！？」
			PRINTL 「不喜欢的话！　就给我滚回家呀！」
			TFLAG:211 = 2
			RETURN -1
		ENDIF
	CASE 1
		IF MONEY < 10000
			CALL SOAP_RESET
			PRINTW 「…………」
			;番台ブチギレ
			IF TFLAG:211 >= 2
				PRINTL 「唔嘎ー！！　唔嘎ー！！　唔嘎ー！！」
				PRINTW 「喵啊啊ー！　没有钱的话、还想玩什么呀！」
				PRINTL 「就那点钱呀！　也敢来打扰喵！！」
				PRINTW 「哦ー咿、喵们！　这个穷鬼抓起来呀！！」
				CALL KOWAI_ONISAN
				RETURN 0
			ELSE
				PRINTW 「钱、不够呀…」
				PRINTL 「这可不给穷人的！」
				PRINTW 「快点快点滚開啦！！」
				TFLAG:211 = 3
				RETURN -1
			ENDIF
		ELSE
			MONEY -= 10000
			PRINTFORML 「\@ チェンジ回数 < 10 ? 好、决定了 # 终于决定了 \@呀！」
			PRINTL 「这边！　客人桑！　快点快点来到这边吧！」
			PRINTFORML 被前台的妖怪指引着、%CALLNAME:PLAYER%移動到了私人風呂
			IF HAS_PENIS(PLAYER) && !CFLAG:RANDOM_CHARANUM:1001
				PRINTFORML %CALLNAME:PLAYER%的胸和股間、都随着初見女孩子的期待膨胀了起来…
				BASE:PLAYER:勃起 = MAX(BASE:PLAYER:勃起, 1000)
			ENDIF
			PRINTW 
			PRINTFORML 过了一会后裹着一条浴巾的%CALLNAME:RANDOM_CHARANUM%进来了
			;初対面の二人になぜか恋愛感情が生まれる
			IF 希望 != 4
				PRINTL 相遇的瞬間、二人坠入了爱河！！
				TALENT:RANDOM_CHARANUM:恋慕 = 1
				CFLAG:RANDOM_CHARANUM:好感度 = REQUIRED_FAVOR_恋慕
				CFLAG:RANDOM_CHARANUM:信賴度 = REQUIRED_TRUST_恋慕
				CFLAG:RANDOM_CHARANUM:既成事實 = 3
				CFLAG:RANDOM_CHARANUM:允许无套 = 2
				ABL:RANDOM_CHARANUM:親密 = REQUIRED_AFFECTION_恋慕
				ABL:RANDOM_CHARANUM:従順 = RAND:4 + TALENT:RANDOM_CHARANUM:風俗嬢 * 3
				ABL:RANDOM_CHARANUM:奉仕精神 = RAND:4 + TALENT:RANDOM_CHARANUM:風俗嬢 * 3
				;路人子相手のAct_COM过滤器をまとめてOFF、自動喘息をON
				VARSET CFLAG:RANDOM_CHARANUM:0, 1, 591, 593
				VARSET CFLAG:RANDOM_CHARANUM:0, 1, 595, 598
				CFLAG:RANDOM_CHARANUM:自動喘息 = 1
			;熟悉的客なら臨戦態勢にしておく
			ELSEIF CFLAG:RANDOM_CHARANUM:1001 > 1
				PALAM:RANDOM_CHARANUM:潤滑 = PALAMLV:MIN(1 + CFLAG:RANDOM_CHARANUM:1001, 6)
				PALAM:RANDOM_CHARANUM:欲情 = PALAMLV:MIN(3 + CFLAG:RANDOM_CHARANUM:1001, 8)
			ENDIF
			PRINTW 
			CALL TALK_MOBGIRL("泡姫挨拶")
			;交渉成立なら诶嘿嘿
			IF INRANGE(RESULT, 1, 3)
				SETBIT TFLAG:一日一回, 9
				LOCAL:0 = TIME / 60 * 60
				LOCAL:1 = LOCAL:0 + 600 > 1440 ? LOCAL:0 - 840 # LOCAL:0 + 600
				LOCAL:2 = RESULT
				IF MAIN_MAP == 9
					CFLAG:RANDOM_CHARANUM:拠点来訪 = 1
					CFLAG:RANDOM_CHARANUM:遭遇位置 = 934
				ELSE
					CFLAG:RANDOM_CHARANUM:拠点来訪 = 0
					CFLAG:RANDOM_CHARANUM:遭遇位置 = 940
				ENDIF
				;眠気で部屋逐出避けの為、今から活動に設定
				CFLAG:RANDOM_CHARANUM:来訪時間 = LOCAL:0
				CFLAG:RANDOM_CHARANUM:帰宅時間 = LOCAL:1
				CFLAG:RANDOM_CHARANUM:就寝時間 = LOCAL:1
				CFLAG:RANDOM_CHARANUM:起床時間 = LOCAL:0
				;约会中処理、@OSASOIを流用してお風呂(OMANEKIBEYA)へ移動
				CFLAG:RANDOM_CHARANUM:同室 = 1
				CFLAG:RANDOM_CHARANUM:约会中 = 9
				CFLAG:PLAYER:约会中 = 9
				FLAG:约会的对象 = RANDOM_CHARANUM
				CFLAG:PLAYER:同行 = 60
				CFLAG:RANDOM_CHARANUM:同行 = 60
				CALL OMANEKI_ENTER(RANDOM_CHARANUM)
				CALL TARGETSET_CHACK
				YOGORE:(OMANEKIBEYA()) = 1
				TCVAR:RANDOM_CHARANUM:连住禁止 = 1
				;専用フラグ立てて诶嘿嘿に移行
				TCVAR:PLAYER:亲热 = 3
				TCVAR:RANDOM_CHARANUM:亲热 = 3
				CFLAG:RANDOM_CHARANUM:延迟 = 30
				BASE:RANDOM_CHARANUM:情緒 = 700 + (500 * (TALENT:RANDOM_CHARANUM:風俗嬢 > 2))
				BASE:RANDOM_CHARANUM:理性 = 400 - (400 * (TALENT:RANDOM_CHARANUM:風俗嬢 > 2))
				TFLAG:泡風呂 = DAY * 1440 + TIME + 90
				IF !ITEM:20
					TFLAG:マット貸し出し = 1
					ITEM:20 ++
				ENDIF
				CFLAG:RANDOM_CHARANUM:诶嘿嘿 = 1
				CFLAG:PLAYER:诶嘿嘿 = 1
				UFUFU_START_TIME = TIME
				UFUFU_START_DAY = DAY
				;指名回数をカウント
				CFLAG:RANDOM_CHARANUM:1001 ++
				;会話で状態の変更
				SELECTCASE LOCAL:2
					CASE 2
						BASE:MASTER:勃起 = MAX(BASE:MASTER:勃起, 1000)
					CASE 3
						BASE:MASTER:勃起 = MIN(BASE:MASTER:勃起 + 500, 1500)
						CFLAG:MASTER:诶嘿嘿 = 2
						CFLAG:RANDOM_CHARANUM:诶嘿嘿 = 2
						TFLAG:COMABLE管理 = 3
						TCVAR:RANDOM_CHARANUM:26 = 0
						CALL CCOMF51(RANDOM_CHARANUM)
						SELECTCCOM:0:1 = 51
						TCVAR:RANDOM_CHARANUM:次回Counter = 51
				ENDSELECT
				RETURN 1
			ENDIF
			;交渉決裂で通常の温泉処理
			RETURN RESULT
		ENDIF
	CASE 2
		;チェンジしすぎで可怕的お兄さんやってくる
		IF チェンジ回数 >= 20 || 指名回数 >= 5
			PRINTL 「唔嘎ー！！　唔嘎ー！！　唔嘎ー！！」
			PRINTL 「喵啊啊ー！　已经受够了呀！」
			PRINTW 「如果谁都不喜欢的话！　我喵就给你找些合适的对象呀！！」
			PRINTW 「哦ー咿、喵们！　目标是这家伙！　交给你们了呀！！」
			CALL SOAP_RESET
			CALL KOWAI_ONISAN
			RETURN 0
		ELSEIF 希望 == 4
			PRINTL 「哈！？　什么鬼啊」
			PRINTL 
			GOTO LOOP_SOAP0
		ELSE
			GOTO LOOP_SOAP2
		ENDIF
	CASEELSE
		CLEARLINE 1
		REUSELASTLINE 「不要做蠢事、快点选择呀！！」
		GOTO LOOP_SOAP3
ENDSELECT

;お風呂サービス終了判定関数
;立てっぱ無だとマズいフラグあるので処理集約してフラグ折っておく
@泡風呂終了判定(ARGS)
#DIM CHR
#DIM 回復回数
#DIM 延長回数
#DIM 一日一回
#DIM 終了処理
終了処理 = 1
CHR = RANDOM_CHARANUM
IF 一日一回 != DAY
	延長回数 = 0
	回復回数 = 0
	一日一回 = DAY
ENDIF
SELECTCASE ARGS
	CASE "タイムUP"
		CALL TALK_MOBGIRL("泡姫タイムUP", 延長回数)
		RESETCOLOR
		延長回数 ++
		SIF RESULT
			RETURN
	CASE "指令から終了"
		CALL TALK_MOBGIRL("泡姫指令から終了", 延長回数)
	CASE "攻守交代時終了"
		CALL TALK_MOBGIRL("泡姫攻守交代時終了")
	CASE "体力限界"
		CALL TALK_MOBGIRL("泡姫体力限界", 回復回数)
		RESETCOLOR
		IF RESULT
			回復回数 ++
			BASE:CHR:体力 += MAXBASE:CHR:体力 / 3 * 2
			BASE:CHR:気力 += MAXBASE:CHR:気力 / 2
			RETURN
		ENDIF
	CASE "你限界"
		CALL TALK_MOBGIRL("你限界")
	;恋慕＋ＮＧ行為無しなので普通は怒られない。保険的な処理
	CASE "女の子不高興"
		PRINTFORMW %CALLNAME:MASTER%\@ TALENT:CHR:胆量 < 0 ? 被锁在了風呂外 # 被踢出了風呂 \@…
ENDSELECT
;诶嘿嘿を終了する処理
FOR COUNT, 0, 2
	CHR = COUNT ? RANDOM_CHARANUM # 0
	CALL ENDUFUFU(CHR)
	CALL SET_PRANK(0, CHR, 1)
	;接触/はだけをリセット
	CALL TOUCH_SET(0,0,0,1)
	TEQUIP:CHR:下半身挪開状態 = 0
	TEQUIP:CHR:上半身裸露状態 = 0
	CALL CLOTHES_SETTING_TRAIN(CHR)
	TEQUIP:CHR:上衣脱衣完毕 = 0
	TEQUIP:CHR:浴室PLAY = 0
	CFLAG:CHR:同行 = 0
	CFLAG:CHR:招待 = 0
NEXT
TFLAG:MASTERのＰ挿入中 = 0
;你の現在位置を戻してサービス終了
FLAG:约会的对象 = 0
CFLAG:MASTER:约会中 = 9
CFLAG:MASTER:現在位置 = CFLAG:RANDOM_CHARANUM:遭遇位置
TFLAG:泡風呂 = 0
ITEM:20 = TFLAG:マット貸し出し ? 0 # ITEM:20
CALL SOAP_STAND_BY_SUKIMA
RETURN

;こわいオニいさんがやってくる関数
@KOWAI_ONISAN
TFLAG:211 = 4
RESETCOLOR
PRINTL
CALL COLORMESSAGE("「喂！！　喝！！　哇！！　可别小看我们啊！！！」", C_RED, 1, 3)
PRINTW 突然间一群鬼冲了进来！！
PRINTFORML %CALLNAME:PLAYER%还没来得及时间停止就被狠狠地殴打了…
PRINTFORMW 鬼们把%CALLNAME:PLAYER%痛扁了一顿后抛到了店外…
CALL RECOVER(PLAYER, -1500, "体力" , "被狠狠地殴打了")
CALL RECOVER(PLAYER, -1500, "気力" , "打得破破烂烂的")
WAIT
RETURN

;出会った嬢を消す関数
@SOAP_RESET
CFLAG:RANDOM_CHARANUM:初期位置 = SUKIMA()
CFLAG:RANDOM_CHARANUM:現在位置 = SUKIMA()
CFLAG:RANDOM_CHARANUM:约会中 = MAIN_MAP
CALL 路人子画像リセット(RANDOM_CHARANUM)
FORGET_MOBGIRL = 1
DELCHARA RANDOM_CHARANUM
ADDCHARA RANDOM_CHARANUM
CALL TARGETSET_CHACK
RESETCOLOR
RETURN

;出会った嬢をSUKIMAに退避させる関数
@SOAP_STAND_BY_SUKIMA
CFLAG:RANDOM_CHARANUM:初期位置 = SUKIMA()
CFLAG:RANDOM_CHARANUM:現在位置 = SUKIMA()
CFLAG:RANDOM_CHARANUM:约会中 = MAIN_MAP
CFLAG:RANDOM_CHARANUM:出禁 = 2
CALL TARGETSET_CHACK
RESETCOLOR
RETURN