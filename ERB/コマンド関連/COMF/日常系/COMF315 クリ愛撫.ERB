;-------------------------------------------------
;陰蒂愛撫
;日常系指令　レベル3
;-------------------------------------------------
@COM315

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
TFLAG:理性削り = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:快Ｃ = 80
	IF ABL:PLAYER:指 >= 4
		SOURCE:快Ｃ += 50
	ELSE
		SOURCE:快Ｃ += ABL:PLAYER:指 * 10
	ENDIF
	;手を突っ込めない衣装
	IF TEQUIP:0 & 16
		TIMES SOURCE:快Ｃ , 0.80
		TIMES SOURCE:欲情 , 0.80
		TIMES SOURCE:露出 , 0.80
	;裙子以外の何かを履いてる
	ELSEIF TEQUIP > 1
		TIMES SOURCE:快Ｃ , 0.90
		TIMES SOURCE:欲情 , 0.90
		TIMES SOURCE:露出 , 0.90
	ENDIF
ELSE
	IF FLAG:甲斐性無 && !CFLAG:诶嘿嘿
		DRAWLINE
		PRINTFORMW %CALLNAME:MASTER%沒有那樣的膽量
		RETURN -1
	ENDIF
	;口上による強制失敗
	IF TFLAG:192 == -1
		RETURN -1
	ENDIF
	DOWNBASE:体力 += 5
	DOWNBASE:気力 += 50

	SOURCE:歓楽 = 100
	SOURCE:情愛 = 150
	SOURCE:欲情 = 150
	SOURCE:露出 = 250
	SOURCE:反感 = 600
	SOURCE:快Ｃ = 80
	SOURCE:受動 = 145 + 270 * ABL:従順
	IF ABL:PLAYER:指 >= 4
		SOURCE:快Ｃ += 50
	ELSE
		SOURCE:快Ｃ += ABL:PLAYER:指 * 10
	ENDIF
	;手を突っ込めない衣装
	IF TEQUIP:0 & 16
		TIMES SOURCE:快Ｃ , 0.80
		TIMES SOURCE:欲情 , 0.80
		TIMES SOURCE:露出 , 0.80
	;裙子以外の何かを履いてる
	ELSEIF TEQUIP > 1
		TIMES SOURCE:快Ｃ , 0.90
		TIMES SOURCE:欲情 , 0.90
		TIMES SOURCE:露出 , 0.90
	ENDIF
	IF HITOKE(1)
		TIMES SOURCE:露出 , 2.00
		TIMES SOURCE:反感 , 2.00
	ENDIF
	;信頼
	TFLAG:98 = -5

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 20
ENDIF
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＶ⇔調教者の口的污垢が移動
STAIN:3 |= STAIN:PLAYER:1
STAIN:PLAYER:1 |= STAIN:3
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 3
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 3
		EXP:MASTER:百合経験 += 3
	ENDIF
	TIME += 5
ENDIF
	EXP:PLAYER:手淫経験 ++
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE315
;陰蒂愛撫実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(315)
	RETURN RESULT
;時間停止中はスキップ
IF !FLAG:70
	;亲热強度2
	SIF (TCVAR:MASTER:亲热 < 2)  && CFLAG:睡眠 == 0
		RETURN 0
	;睡眠中
	SIF CFLAG:睡眠 && !CFLAG:恶作剧
		RETURN 0
	;陪睡中
	SIF CFLAG:陪睡中
		RETURN 0
	;仕事中
	SIF CFLAG:行動 == 5
		RETURN 0
	;约会中
	SIF CHK_DATENOW(CFLAG:约会中) && CFLAG:現在位置 != OMANEKIBEYA()
		RETURN 0
ENDIF
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;腕使用中
SIF TFLAG:61 && MASTER_POSE(SET_C,3) != TARGET && MASTER_POSE(SET_P,SET_YUBI) != TARGET && MASTER_POSE(SET_V,SET_YUBI) != TARGET && MASTER_POSE(SET_A,SET_YUBI) != TARGET
	RETURN 0
SIF GETBIT (TCVAR:26,5)
	RETURN 0
RETURN 1

