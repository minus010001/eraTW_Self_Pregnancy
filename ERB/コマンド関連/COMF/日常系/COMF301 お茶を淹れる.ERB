;-------------------------------------------------
;泡茶
;日常系指令　レベル1
;-------------------------------------------------
@COM301
SELECTCASE TFLAG:192
	CASE 0
		LOCAL = RAND:100
		LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
		SIF LOCAL:1 > 99
			LOCAL:1 = 99
		IF LOCAL <= 9
			;10％で大成功
			EXP:MASTER:会話経験 ++
			TFLAG:193 = 1
			TFLAG:98 = 2
		ELSEIF LOCAL >= LOCAL:1
			;10～1％で失敗
			TFLAG:193 = -1
			TFLAG:98 = -2
		ELSE
			;残りは成功
			TFLAG:193 = 0
			TFLAG:98 = 0
		ENDIF
	CASE 1
		IF RAND:100 <= 9
			;10％で大成功
			EXP:MASTER:会話経験 ++
			TFLAG:193 = 1
			TFLAG:98 = 2
		ELSE
			;残りは成功
			TFLAG:193 = 0
			TFLAG:98 = 0
		ENDIF
	CASE -1
		TFLAG:193 = -1
		TFLAG:98 = -1
	CASE -2
		RETURN 0
ENDSELECT

;先制発生時は失敗しない
SIF TCVAR:20
	TFLAG:193 = MAX(TFLAG:193,0)
DOWNBASE:MASTER:気力 += 100
DOWNBASE:MASTER:体力 += 50
TFLAG:情緒上昇抑制 = 1
;信頼
TFLAG:98 += 3

;その場に居るTARGET全員で回す
LOCAL:1 = TARGET
FOR LOCAL, 1, GET_TARGETNUM() + 1
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):睡眠
		CONTINUE
	TARGET = TARGET:LOCAL
	CALL COM301_1
NEXT
TARGET = LOCAL:1
SIF GETBIT(TCVAR:MASTER:特別的茶水, 0)
	CLEARBIT TCVAR:MASTER:特別的茶水, 0
TIME += 30
RETURN 1

@COM301_1

DOWNBASE:気力 += 10

;固定で獲得するソース
SOURCE:歓楽 = 500

;ABL:従順をみる
IF ABL:従順 <= 1
	SOURCE:歓楽 += (ABL:従順 * 60)
ELSEIF ABL:従順 <= 3
	SOURCE:歓楽 += 200 + (ABL:従順 * 60)
ELSEIF ABL:従順 <= 5
	SOURCE:歓楽 += 500 + (ABL:従順 * 60)
ELSEIF ABL:従順 <= 8
	SOURCE:歓楽 += 750 + (ABL:従順 * 75)
ELSEIF ABL:従順 <= 11
	SOURCE:歓楽 += 1000 + (ABL:従順 * 75)
ELSE
	SOURCE:歓楽 += 2000 + (ABL:従順 * 30)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0


SOURCE:受動 = 200 + 100 * ABL:従順
SOURCE:征服 = 200 + 100 * ABL:施虐属性

IF TFLAG:193 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
ENDIF

IF GETBIT(TCVAR:MASTER:特別的茶水, 0)
	SELECTCASE TARGET
		CASE 15, 16, 17, 38, 50, 54, 59, 75
			TIMES SOURCE:歓楽 , 2.00
			TIMES SOURCE:征服 , 2.00
			TIMES SOURCE:受動 , 2.00
		CASEELSE
			TIMES SOURCE:歓楽 , 1.50
			TIMES SOURCE:征服 , 1.50
			TIMES SOURCE:受動 , 1.50
	ENDSELECT
ENDIF

;茶を飲んだフラグ
TCVAR:300 = 1440 * DAY + TIME

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE301
;泡茶実行判定
SIF !TFLAG:100
	RETURN 0
SIF TFLAG:131
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(301)
	RETURN RESULT
;二時間間隔
;SIF TIME_PROGRESS(TCVAR:300) < 120
;魔改二次泡茶时间
SIF TIME_PROGRESS(TCVAR:300) < 60
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;陪睡中
SIF CFLAG:陪睡中
	RETURN 0
;浴場
;SIF CFLAG:MASTER:現在位置 == 13 || CFLAG:MASTER:現在位置 == 24 || CFLAG:MASTER:現在位置 == 30 || CFLAG:MASTER:現在位置 == 39
SIF BATHCHECK(CFLAG:MASTER:現在位置)
	RETURN 0
;宴会中
SIF CFLAG:350 == 49
	RETURN 0
;约会中
SIF CHK_DATENOW(CFLAG:约会中) && CFLAG:現在位置 != OMANEKIBEYA()
	RETURN 0
;外出先で実行時は親密3を要求
SIF (GET_MAPID(CFLAG:MASTER:現在位置) != MAIN_MAP) && ABL:親密 < 3
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;停止中は不可
SIF FLAG:70
	RETURN 0
RETURN 1

