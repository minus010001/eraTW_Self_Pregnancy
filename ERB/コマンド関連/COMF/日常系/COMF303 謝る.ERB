;-------------------------------------------------
;道歉
;日常系指令　レベル1
;-------------------------------------------------
@COM303
IF CFLAG:败犬 && MARK:反発刻印 >= 3
	PRINTFORMW %CALLNAME:MASTER%所做的事並不是道歉就能被饒恕的
	RETURN -1
ENDIF
SELECTCASE TFLAG:192
	;通常
	CASE 0
		LOCAL = RAND:100
		LOCAL:1 = ABL:MASTER:話術技能 * 2 + ABL:親密
		;大成功
		IF LOCAL < LOCAL:1
			TFLAG:193 = 1
			;信頼
			TFLAG:98 = 1
		;成功
		ELSEIF LOCAL < LOCAL:1 + 30
			TFLAG:193 = 0
		;失敗
		ELSE
			TFLAG:193 = -1
		ENDIF
	;強制成功or大成功
	CASE 1
		;大成功
		IF RAND:100 < ABL:MASTER:話術技能 * 2 + ABL:親密
			TFLAG:193 = 1
			;信頼
			TFLAG:98 = 1
		;成功
		ELSE
			TFLAG:193 = 0
		ENDIF
	;強制失敗
	CASE -1
		TFLAG:193 = -1
	;指令終了
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT

DOWNBASE:MASTER:体力 += 50
DOWNBASE:MASTER:気力 += 200

IF TARGET > 0 && CFLAG:ブチギレ && !CFLAG:睡眠
	;固定で獲得するソース
	SOURCE:征服 = 400
	;ABL:従順チェック
	IF ABL:従順 <= 1
		SOURCE:征服 += 2000 - (ABL:従順 * 30)
	ELSEIF ABL:従順 <= 3
		SOURCE:征服 += 1000 - (ABL:従順 * 30)
	ELSEIF ABL:従順 <= 5
		SOURCE:征服 += 750 - (ABL:従順 * 40)
	ELSEIF ABL:従順 <= 8
		SOURCE:征服 += 500 - (ABL:従順 * 20)
	ELSEIF ABL:従順 <= 11
		SOURCE:征服 += 200 - (ABL:従順 * 20)
	ELSE
		SOURCE:征服 += 100 - (ABL:従順 * 10)
	ENDIF
	
	;ABL:親密チェック
	IF ABL:親密 <= 1
		SOURCE:征服 -= (ABL:親密 * 60)
	ELSEIF ABL:親密 <= 3
		SOURCE:征服 -= (ABL:親密 * 60)
	ELSEIF ABL:親密 <= 5
		SOURCE:征服 -= (ABL:親密 * 60)
	ELSEIF ABL:親密 <= 8
		SOURCE:征服 -= (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 11
		SOURCE:征服 -= (ABL:親密 * 40)
	ELSE
		SOURCE:征服 -= (ABL:親密 * 30)
	ENDIF
	
	SIF SOURCE:征服 < 0
		SOURCE:征服 = 0
ENDIF
;成功していたら憤怒状態解除
IF TFLAG:193 == 0
	CFLAG:ブチギレ = 0
	CFLAG:心情不快 = 0
	BASE:憤怒 /= 2
ELSEIF TFLAG:193 == 1
	CFLAG:ブチギレ = 0
	CFLAG:心情不快 = 0
	BASE:憤怒 = 0
ELSE
	;何も無し！残念っ！
ENDIF
SIF BASE:憤怒 < 0
	BASE:憤怒 = 0
IF ITEM:點心盒 && MARK:TARGET:反発刻印 == 1
	IF TFLAG:193 == -1
		TFLAG:193 = -2
	ELSE
		MARK:TARGET:反発刻印 = 0
		TFLAG:193 = 2
		ITEM:點心盒 --
	ENDIF
ENDIF

IF !APLOGIZEDLC
	;所要時間
	IF ABL:従順 <= 5
		TIME += 90
	ELSEIF ABL:従順 <= 10
		TIME += 60
	ELSE
		TIME += 45
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE303
;道歉実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(303)
	RETURN RESULT
;憤怒中でない
SIF !CFLAG:ブチギレ && !(ITEM:點心盒 && MARK:TARGET:反発刻印 == 1)
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;停止中は不可
SIF FLAG:70
	RETURN 0
RETURN 1

