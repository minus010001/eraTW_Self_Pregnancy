;-------------------------------------------------
;搾乳器
;道具使用指令、レベル3
;-------------------------------------------------
@COM748
;着脱の処理
TEQUIP:PLAYER:搾乳機 = !TEQUIP:PLAYER:搾乳機

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:PLAYER:体力 += 50
DOWNBASE:PLAYER:気力 += 100

SOURCE:PLAYER:快Ｂ = 200
SOURCE:PLAYER:苦痛 = 100
SOURCE:PLAYER:露出 = 30
SOURCE:PLAYER:屈従 = 100
SOURCE:PLAYER:逸脱 = 100
SOURCE:PLAYER:反感 = 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;圧抑、抵抗持ちには、抑鬱ソース追加
SIF TALENT:PLAYER:自己愛 < 0 || TALENT:PLAYER:抵抗
	SOURCE:PLAYER:鬱屈 += 100

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SIF !FLAG:時間停止
	TIME += 5
CFLAG:PLAYER:自慰过了 ++
EXP:PLAYER:道具使用経験 ++

RETURN 1
;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM748(PLAYER)
;搾乳器装備中
PRINTL ＜搾乳器装備中＞
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
DOWNBASE:PLAYER:体力 += 25
DOWNBASE:PLAYER:気力 += 50

SOURCE:PLAYER:快Ｂ += 100
SOURCE:PLAYER:苦痛 += 50
SOURCE:PLAYER:露出 += 15
SOURCE:PLAYER:屈従 += 50
SOURCE:PLAYER:逸脱 += 50
SOURCE:PLAYER:反感 += 50

RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
@COM_ABLE748
;搾乳器実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(748)
	RETURN RESULT
;解除是つでも可能
SIF TEQUIP:17
	RETURN 1
;道具を持っているか、道具無設定でないとダメ
SIF ITEM:11 == 0 && NOITEM == 0
	RETURN 0
;母乳体質でないと使えない
SIF TALENT:母乳体質 == 0
	RETURN 0
;上半身着衣
SIF TEQUIP:1 > 1
	RETURN 0
;乳頭夾使用中は不可
SIF TEQUIP:16
	RETURN 0
;乳房電極装着中はダメ
SIF TEQUIP:24
	RETURN 0
;Ｂ感度2以下はダメ
SIF ABL:TARGET:Ｂ感覚 <= 2
	RETURN 0
;男人だとダメ
SIF !(TALENT:2 & 1)
	RETURN 0
;ASSIの場合、技巧3以上じゃないと使えない。
IF ASSIPLAY
	SIF ABL:ASSI:技巧 < 3
		RETURN 0
ENDIF
;淋浴中はダメ
SIF TEQUIP:32
	RETURN 0
;新妻PLAY中はダメ
SIF TEQUIP:33
	RETURN 0
;ベッドルームでないとダメ
SIF !BEDROOM(CFLAG:PLAYER:現在位置)
	RETURN 0
RETURN 1

