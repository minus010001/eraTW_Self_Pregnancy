;-------------------------------------------------
;振動棒
;道具使用指令、レベル2
;-------------------------------------------------
@COM744
;着脱の処理
TEQUIP:PLAYER:振動棒 = !TEQUIP:PLAYER:振動棒
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:PLAYER:体力 += 30
DOWNBASE:PLAYER:気力 += 100

SOURCE:PLAYER:快Ｖ = 250
SOURCE:PLAYER:苦痛 = 50
SOURCE:PLAYER:露出 = 20
SOURCE:PLAYER:逸脱 = 40
SOURCE:PLAYER:反感 = 40


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;圧抑、抵抗持ちには、抑鬱ソース追加
SIF TALENT:PLAYER:自己愛 < 0 || TALENT:PLAYER:抵抗
	SOURCE:PLAYER:鬱屈 += 40

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:PLAYER:処女 == 1
	SOURCE:PLAYER:苦痛 += 2000
	SOURCE:PLAYER:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:PLAYER:処女 == 2
	SOURCE:PLAYER:苦痛 += 500
	SOURCE:PLAYER:反感 += 1000
ENDIF

;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_V(PLAYER)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SIF !FLAG:時間停止
	TIME += 5
;挿入時はＶ経験+
EXP:PLAYER:Ｖ経験 ++

;異常経験
IF TALENT:PLAYER:処女 == 1 && !GETBIT(CFLAG:PLAYER:3,3)
	EXP:PLAYER:異常経験 ++
	SETBIT CFLAG:PLAYER:3,3
ENDIF

;自慰経験
CFLAG:PLAYER:自慰过了 ++

EXP:PLAYER:道具使用経験 ++

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM744(PLAYER)
;振動棒挿入中
PRINTL ＜振動棒挿入中＞

DOWNBASE:PLAYER:体力 += 10
DOWNBASE:PLAYER:気力 += 50

SOURCE:PLAYER:快Ｖ += 125
SOURCE:PLAYER:露出 += 10
SOURCE:PLAYER:逸脱 += 20

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;圧抑、抵抗持ちには、抑鬱ソース追加
SIF TALENT:PLAYER:自己愛 < 0 || TALENT:PLAYER:抵抗
	SOURCE:PLAYER:鬱屈 += 20

;Vへの苦痛は先に処理しておく
;EXP:Ｖ経験をみる（処女はありえない）
LOCAL = 25
LOCAL:1 = 20
IF EXP:PLAYER:Ｖ経験 < EXPLV:2
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 1.20
ELSEIF EXP:PLAYER:Ｖ経験 < EXPLV:3
	TIMES LOCAL , 0.40
ELSEIF EXP:PLAYER:Ｖ経験 < EXPLV:4
	TIMES LOCAL , 0.10
ELSEIF EXP:PLAYER:Ｖ経験 < EXPLV:5
	TIMES LOCAL , 0.00
ELSEIF EXP:PLAYER:Ｖ経験 >= EXPLV:5
	TIMES LOCAL , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:PLAYER:潤滑 < PALAMLV:1
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 1.50
ELSEIF PALAM:PLAYER:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.70
	TIMES LOCAL:1 , 1.20
ELSEIF PALAM:PLAYER:潤滑 < PALAMLV:3
	TIMES LOCAL , 0.40
ELSEIF PALAM:PLAYER:潤滑 < PALAMLV:4
	TIMES LOCAL , 0.10
ELSEIF PALAM:PLAYER:潤滑 >= PALAMLV:4
	TIMES LOCAL , 0.10
ENDIF
SOURCE:PLAYER:苦痛 += LOCAL
SOURCE:PLAYER:反感 += LOCAL:1

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;自慰実行時は経験＋１
SIF EXP_UP(22,PLAYER)
	EXP:PLAYER:Ｖ経験 += 1

RETURN 1


;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
@COM_ABLE744
;振動棒実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(744)
	RETURN RESULT
;解除是つでも可能
SIF TEQUIP:13
	RETURN 1
;男人だとダメ
SIF !(TALENT:2 & 1)
	RETURN 0
;道具を持っているか、道具無設定でないとダメ
SIF ITEM:4 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;処女で、従順4以下・百合属性4以下のASSIの場合振動棒はできない（施虐狂ならＯＫ）
IF TALENT:処女 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 4 || ABL:ASSI:百合属性 <= 4) && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF
;潤滑不足で、従順3以下・百合属性3以下のASSIの場合振動棒はできない（施虐狂ならＯＫ）
IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 3 || ABL:ASSI:百合属性 <= 3) && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF
RETURN 1

