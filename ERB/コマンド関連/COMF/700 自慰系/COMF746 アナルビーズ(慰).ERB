;-------------------------------------------------
;後庭拉珠
;道具使用指令、レベル3
;-------------------------------------------------
@COM746
;着脱の処理
TEQUIP:PLAYER:後庭拉珠 = !TEQUIP:PLAYER:後庭拉珠
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:PLAYER:体力 += 60
DOWNBASE:PLAYER:気力 += 150

SOURCE:PLAYER:快Ａ = 150
SOURCE:PLAYER:苦痛 = 200
SOURCE:PLAYER:露出 = 50
SOURCE:PLAYER:屈従 = 50
SOURCE:PLAYER:逸脱 = 60
SOURCE:PLAYER:反感 = 80

;装着時は快Aは下がる
SIF TEQUIP:PLAYER:後庭拉珠
	TIMES SOURCE:PLAYER:快Ａ , 0.80

;外す時は快Aは上がる
SIF !TEQUIP:PLAYER:後庭拉珠
	TIMES SOURCE:PLAYER:快Ａ , 3.00

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;圧抑、抵抗持ちには、抑鬱ソース追加
SIF TALENT:PLAYER:自己愛 < 0 || TALENT:PLAYER:抵抗
	SOURCE:PLAYER:鬱屈 += 70

;Aへの苦痛は先に処理しておく
CALL PAIN_CHECK_A(PLAYER)
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
STAIN:PLAYER:腸内 |= STAIN:PLAYER:Ａ
STAIN:PLAYER:Ａ |= STAIN:PLAYER:腸内
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SIF !FLAG:時間停止
	TIME += 5
SIF !TEQUIP:PLAYER:後庭拉珠
	EXP:PLAYER:Ａ経験 += 2

;自慰経験
CFLAG:PLAYER:自慰过了 ++

EXP:PLAYER:道具使用経験 ++

RETURN 1
;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM746(PLAYER)
;後庭拉珠挿入中

PRINTL ＜後庭拉珠挿入中＞

DOWNBASE:PLAYER:体力 += 60
DOWNBASE:PLAYER:気力 += 150

SOURCE:PLAYER:快Ａ += 75
SOURCE:PLAYER:露出 += 25
SOURCE:PLAYER:屈従 += 25
SOURCE:PLAYER:逸脱 += 25
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;圧抑、抵抗持ちには、抑鬱ソース追加
SIF TALENT:PLAYER:自己愛 < 0 || TALENT:PLAYER:抵抗
	SOURCE:PLAYER:鬱屈 += 35

;Aへの苦痛は先に処理しておく
;EXP:Ａ経験をみる
LOCAL = 100
LOCAL:1 = 25
IF EXP:PLAYER:Ａ経験 < EXPLV:1
	TIMES LOCAL , 3.00
	TIMES LOCAL:1 , 3.00
ELSEIF EXP:PLAYER:Ａ経験 < EXPLV:2
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 2.00
ELSEIF EXP:PLAYER:Ａ経験 < EXPLV:3
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 1.50
ELSEIF EXP:PLAYER:Ａ経験 < EXPLV:4
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 1.20
ELSEIF EXP:PLAYER:Ａ経験 < EXPLV:5
	TIMES LOCAL , 0.40
ELSEIF EXP:PLAYER:Ａ経験 >= EXPLV:5
	TIMES LOCAL , 0.20
ENDIF

;PALAM:潤滑をみる
IF PALAM:PLAYER:潤滑 < PALAMLV:1
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 2.00
ELSEIF PALAM:PLAYER:潤滑 < PALAMLV:2
	TIMES LOCAL , 1.00
	TIMES LOCAL:1 , 1.50
ELSEIF PALAM:PLAYER:潤滑 < PALAMLV:3
	TIMES LOCAL , 0.60
ELSEIF PALAM:PLAYER:潤滑 < PALAMLV:4
	TIMES LOCAL , 0.30
ELSEIF PALAM:PLAYER:潤滑 >= PALAMLV:4
	TIMES LOCAL , 0.20
ENDIF
SOURCE:PLAYER:苦痛 += LOCAL
SOURCE:PLAYER:反感 += LOCAL:1

RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
@COM_ABLE746
;後庭拉珠実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(746)
	RETURN RESULT
;解除是つでも可能
SIF TEQUIP:15
	RETURN 1
;道具を持っているか、道具無設定でないとダメ
SIF ITEM:6 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
SIF TEQUIP:0 > 1
	RETURN 0
;肛用振動棒使用中は不可
SIF TEQUIP:14
	RETURN 0
;普通の浣腸使用中は不可
SIF TEQUIP:21
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;電極使用中は不可
SIF TEQUIP:23
	RETURN 0
;新妻PLAY中はダメ
SIF TEQUIP:33
	RETURN 0
RETURN 1

