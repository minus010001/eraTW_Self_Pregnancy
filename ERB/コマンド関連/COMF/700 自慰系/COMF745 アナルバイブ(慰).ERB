;-------------------------------------------------
;肛用振動棒
;道具使用指令、レベル2
;-------------------------------------------------
@COM745
;着脱の処理
TEQUIP:PLAYER:肛用振動棒 = !TEQUIP:PLAYER:肛用振動棒 
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:PLAYER:体力 += 60
DOWNBASE:PLAYER:気力 += 150

SOURCE:PLAYER:快Ａ = 250
SOURCE:PLAYER:苦痛 = 250
SOURCE:PLAYER:露出 = 50
SOURCE:PLAYER:屈従 = 50
SOURCE:PLAYER:逸脱 = 50
SOURCE:PLAYER:反感 = 200
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;圧抑、抵抗持ちには、抑鬱ソース追加
SIF TALENT:PLAYER:自己愛 < 0 || TALENT:PLAYER:抵抗
	SOURCE:PLAYER:鬱屈 += 70

;Aへの苦痛は先に処理しておく
CALL PAIN_CHECK_A(PLAYER)
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
STAIN:PLAYER:腸内 |= STAIN:PLAYER:Ａ
STAIN:PLAYER:Ａ |= STAIN:PLAYER:腸内
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SIF !FLAG:時間停止
	TIME += 5
;Ａ経験
EXP:PLAYER:Ａ経験 ++

;自慰経験
CFLAG:PLAYER:自慰过了 ++

EXP:PLAYER:道具使用経験 ++

RETURN 1
;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM745(PLAYER)
;肛用振動棒挿入中
PRINTL ＜肛用振動棒挿入中＞

DOWNBASE:PLAYER:体力 += 30
DOWNBASE:PLAYER:気力 += 80

SOURCE:PLAYER:快Ａ += 125
SOURCE:PLAYER:苦痛 += 100
SOURCE:PLAYER:露出 += 25
SOURCE:PLAYER:逸脱 += 25
SOURCE:PLAYER:反感 += 40

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;圧抑、抵抗持ちには、抑鬱ソース追加
SIF TALENT:PLAYER:自己愛 < 0 || TALENT:PLAYER:抵抗
	SOURCE:PLAYER:鬱屈 += 35

;Aへの苦痛は先に処理しておく
;EXP:Ａ経験をみる
LOCAL = 100
LOCAL:1 = 40
IF EXP:PLAYER:Ａ経験 < EXPLV:1
	TIMES LOCAL , 3.00
	TIMES LOCAL:1 , 3.00
ELSEIF EXP:PLAYER:Ａ経験 < EXPLV:2
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 2.00
ELSEIF EXP:PLAYER:Ａ経験 < EXPLV:3
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 1.50
ELSEIF EXP:PLAYER:Ａ経験 < EXPLV:4
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 1.20
ELSEIF EXP:PLAYER:Ａ経験 < EXPLV:5
	TIMES LOCAL , 0.40
ELSEIF EXP:PLAYER:Ａ経験 >= EXPLV:5
	TIMES LOCAL , 0.20
ENDIF

;PALAM:潤滑をみる
IF PALAM:PLAYER:潤滑 < PALAMLV:1
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 2.00
ELSEIF PALAM:PLAYER:潤滑 < PALAMLV:2
	TIMES LOCAL , 1.00
	TIMES LOCAL:1 , 1.50
ELSEIF PALAM:PLAYER:潤滑 < PALAMLV:3
	TIMES LOCAL , 0.60
ELSEIF PALAM:PLAYER:潤滑 < PALAMLV:4
	TIMES LOCAL , 0.30
ELSEIF PALAM:PLAYER:潤滑 >= PALAMLV:4
	TIMES LOCAL , 0.20
ENDIF
SOURCE:PLAYER:苦痛 += LOCAL
SOURCE:PLAYER:反感 += LOCAL:1

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;自慰実行時は経験＋１
SIF EXP_UP(22,PLAYER)
	EXP:PLAYER:Ａ経験 ++

RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
@COM_ABLE745
;肛用振動棒実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(745)
	RETURN RESULT
;解除是つでも可能
SIF TEQUIP:14
	RETURN 1
;道具を持っているか、道具無設定でないとダメ
SIF ITEM:5 == 0 && NOITEM == 0
	RETURN 0
;潤滑不足で、従順3以下・百合属性3以下のASSIの場合肛用振動棒はできない（施虐狂ならＯＫ）
IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 3 || ABL:ASSI:百合属性 <= 3) && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF

;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;浣腸＋肛塞使用中はダメ
SIF TEQUIP:21
	RETURN 0
;後庭拉珠使用中は不可
SIF TEQUIP:15
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;電極使用中は不可
SIF TEQUIP:23
	RETURN 0
RETURN 1

