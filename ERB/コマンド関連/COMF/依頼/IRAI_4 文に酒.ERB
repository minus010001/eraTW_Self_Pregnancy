[SKIPSTART]
;--------------------------------------------
;基本条件いろいろ(5)
;--------------------------------------------
@IRAI_SETTING_4

IRAI_SETTING_DEADLINE:4 = 8
IRAI_SETTING_TARGET:4 = 29
IRAI_SETTING_CLIENT:4 = 29

IRAI_SETTING_COMMANDNAME:4 = 送出酒水


;--------------------------------------------
;依頼発生時のメッセージ
;--------------------------------------------
@IRAI_INITIALMESSAGE_4
PRINTFORML 「有点想喝酒了呢」
PRINTFORML 「什么様的酒都行、请帮我带3瓶来吧」
PRINTFORML 「一定会有丰厚的謝礼哦、如何呢？」
PRINTFORML 依頼内容：提供三瓶以上的酒
PRINTFORML 報酬　　：\\5000

;--------------------------------------------
;依頼実行時のメッセージやソースなどの処理
;--------------------------------------------
@IRAI_CARRYOUT_4
PRINTL 要交出什么呢？
CALL 酒一覧表示(3)
IF RESULT >= 51 && RESULT <= 53
	PRINTFORMW %CALLNAME:MASTER%把酒交给了%CALLNAME:TARGET%
	ITEM:RESULT -= 3
	;TFLAGに渡した酒番号を保存。報酬などを分岐させたい場合に。
	TFLAG:194 = RESULT
ELSE
	RETURN -1
ENDIF

;--------------------------------------------
;依頼失敗時のメッセージやソースなどの処理
;--------------------------------------------
@IRAI_FAILED_4
IF TALENT:29:恋慕
	PRINTFORML 「啊呀呀、有点迟了呢。不好意思呢、%CALLNAME:MASTER%君」
ELSE
	PRINTFORML 「虽然不好意思、这次你有些迟了呢」
ENDIF
;システムメッセージつき好感度変化
;最後の引数だけ好感度が変化
CALL CHANGE_CFLAG(2,TARGET,-20)

;システムメッセージつき信賴度変化
;最後の引数だけ信賴度が変化
CALL CHANGE_CFLAG(4,TARGET,-10)
;--------------------------------------------
;依頼達成時のメッセージやソースなどの処理
;--------------------------------------------
@IRAI_SUCCESS_4
IF TALENT:29:恋慕
	PRINTFORML 「非常感谢、%CALLNAME:MASTER%君♪」
ELSE
	PRINTFORML 「非、非常感谢您」
ENDIF
CALL KIGEN_CHANGE(TARGET,100,1)
SOURCE:TARGET:歓楽 += 2000
;システムメッセージつき好感度変化
;最後の引数だけ好感度が変化
CALL CHANGE_CFLAG(2,TARGET,30)

;システムメッセージつき信賴度変化
;最後の引数だけ信賴度が変化
CALL CHANGE_CFLAG(4,TARGET,10)

MONEY += 5000
;--------------------------------------------
;依頼実行条件
;--------------------------------------------
@IRAI_ACTABLE_4
SIF SHIRAHU(TARGET) == 1 && (ITEM:51 >= 3 || ITEM:52 >= 3 || ITEM:53 >= 3)
	RETURN 1

;--------------------------------------------
;依頼発生条件
;--------------------------------------------
@IRAI_HAPPEN_4
SIF !RAND:5
	RETURN 1

;--------------------------------------------
;依頼消滅条件
;--------------------------------------------
@IRAI_VANISH_4
SIF !RAND:3
	RETURN 1
[SKIPEND]
