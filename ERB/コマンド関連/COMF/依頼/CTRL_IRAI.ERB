;----------------------------------------------------------------------------------------------------
;依頼受託処理
;----------------------------------------------------------------------------------------------------
@SET_IRAI_SLOT(CHARA)
#DIM CHARA
#DIM IRAI_ID
#DIM CHK_SLOT
IRAI_ID = CFLAG:CHARA:依頼内容
CHK_SLOT = SEARCH_IRAI_EMPTY()
SIF !CHK_SLOT
	RETURN
IRAI_SLOT:CHK_SLOT     = IRAI_ID
IRAI_DATA:CHK_SLOT     = INT_DATA_IRAI(IRAI_ID, "依頼内容",     CHARA)
IRAI_SUB:CHK_SLOT      = INT_DATA_IRAI(IRAI_ID, "依頼サブ",     CHARA)
IRAI_DEADLINE:CHK_SLOT = INT_DATA_IRAI(IRAI_ID, "依頼期限", CHARA) + DAY
CALL SET_IRAI_PROGRESS(CHARA, "依頼中")



;----------------------------------------------------------------------------------------------------
;依頼終了処理
;----------------------------------------------------------------------------------------------------
@CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIM  CHK_SLOT
#DIMS SCENE
ARRAYREMOVE IRAI_SLOT,     CHK_SLOT, 1
ARRAYREMOVE IRAI_DATA,     CHK_SLOT, 1
ARRAYREMOVE IRAI_SUB,      CHK_SLOT, 1
ARRAYREMOVE IRAI_EX,       CHK_SLOT, 1
ARRAYREMOVE IRAI_DEADLINE, CHK_SLOT, 1
CALL SET_IRAI_PROGRESS(CHARA, SCENE)



;----------------------------------------------------------------------------------------------------
;依頼発生処理
;随意10角色見繕って依頼を作るよ
;----------------------------------------------------------------------------------------------------
@IRAI_HAPPEN
#DIM CHARA_ID, 10
[IF_DEBUG]
	;#;PRINTFORMW 依頼生成処理（全角色実行、ログをデ背包ウインドウに表示）
	;#;DEBUGCLEAR
	FOR LOCAL:0, 1, 人物数量上限
		SIF !CAN_MEET(LOCAL:0)
			CONTINUE
		SIF CFLAG:(LOCAL:0):依頼状況
			CONTINUE
		CALL SET_NEW_IRAI(LOCAL:0)
	NEXT
	;#;PRINTFORML 依頼生成完了、デ背包文を削除します
	;#;FORCEWAIT
	;#;DEBUGCLEAR
	RETURN
[ENDIF]
CALL ARRAY_RAND(CHARA_ID, 10, 1, 人物数量上限)
FOR LOCAL:0, 0, 10
	SIF !CAN_MEET(CHARA_ID:(LOCAL:0))
		CONTINUE
	SIF CFLAG:(CHARA_ID:(LOCAL:0)):依頼状況
		CONTINUE
	CALL SET_NEW_IRAI(CHARA_ID:(LOCAL:0))
NEXT



;----------------------------------------------------------------------------------------------------
;各角色の依頼発生処理
;依頼無/一般依頼/固有依頼
;----------------------------------------------------------------------------------------------------
@SET_NEW_IRAI(CHARA)
#DIM CHARA
SELECTCASE IRAI_HIT(CHARA, "HAPPEN")
CASE IS <= 0
	RETURN
CASE 1
	LOCAL = IRAI_HIT(CHARA, "COMMON")
	SIF !LOCAL
		RETURN
CASE 2
	LOCAL = IRAI_HIT(CHARA, "UNIQUE")
	SIF LOCAL <= 100
		RETURN
ENDSELECT
CALL SET_IRAI_PROGRESS(CHARA, "依頼発生直後")
CFLAG:CHARA:依頼内容 = MAKE_IRAI_ID(CHARA, LOCAL)



;----------------------------------------------------------------------------------------------------
;依頼抽選処理はまとめてここに
;----------------------------------------------------------------------------------------------------
@IRAI_HIT(CHARA, TYPE)
#FUNCTION
#DIM  CHARA
#DIMS TYPE
#DIM CONST LAST_COMMON = 99 ;一般依頼IDの末端
#DIM       LAST_UNIQUE      ;その角色に実装されている固有依頼IDの末端
#DIM       HIT_LIST, 100    ;抽選確率配列
#DIM       ID_LIST,  100    ;抽選を対象依頼IDの一覧
#DIM       CNT_LIST         ;抽選対象となる依頼の数
DEBUGPRINTFORML ＞IRAI_HIT{CHARA}　%TYPE%
SELECTCASE TYPE
CASE "HAPPEN"
;依頼発生抽選
	HIT_LIST:0 = GET_INT(0, "角色データ", CHARA, "没有依頼発生率")
	HIT_LIST:1 = GET_INT(0, "角色データ", CHARA, "一般依頼発生率")
	HIT_LIST:2 = GET_INT(0, "角色データ", CHARA, "固有依頼発生率")
	SIF !SUMARRAY(HIT_LIST, 0, 3)
		HIT_LIST = 7, 3, 0
	DEBUGPRINTFORML ＞IRAI_HIT{CHARA}　{HIT_LIST:0}/{HIT_LIST:1}/{HIT_LIST:2}
	RETURNF ARRAY_HIT(HIT_LIST, 3)
CASE "COMMON"
;一般依頼の抽選
	VARSET HIT_LIST
	VARSET ID_LIST
	VARSET CNT_LIST
	FOR LOCAL:0, 1, LAST_COMMON + 1
		SIF !INT_DATA_IRAI(MAKE_IRAI_ID(CHARA, LOCAL:0), "依頼発生条件", CHARA)
			CONTINUE
		;SIF GETBIT(CFLAG:CHARA:一般依頼禁止, LOCAL:0)
		;	CONTINUE
		ID_LIST:CNT_LIST  = LOCAL:0
		HIT_LIST:CNT_LIST = INT_DATA_IRAI(MAKE_IRAI_ID(CHARA, LOCAL:0), "依頼発生率", CHARA)
		CNT_LIST ++
	NEXT
	SIF !CNT_LIST
		RETURNF 0
	RETURNF ID_LIST:(ARRAY_HIT(HIT_LIST, CNT_LIST))
CASE "UNIQUE"
;固有依頼の抽選
	VARSET HIT_LIST
	VARSET ID_LIST
	VARSET CNT_LIST
	LAST_UNIQUE = GET_INT(0, "角色データ", CHARA, "固有依頼最終要素")
	FOR LOCAL:0, 101, LAST_UNIQUE + 1
		SIF !INT_DATA_IRAI(MAKE_IRAI_ID(CHARA, LOCAL:0), "依頼発生条件", CHARA)
			CONTINUE
		ID_LIST:CNT_LIST  = LOCAL:0
		HIT_LIST:CNT_LIST = INT_DATA_IRAI(MAKE_IRAI_ID(CHARA, LOCAL:0), "依頼発生率", CHARA)
		CNT_LIST ++
	NEXT
	SIF !CNT_LIST
		RETURNF 0
	RETURNF ID_LIST:(ARRAY_HIT(HIT_LIST, CNT_LIST))
ENDSELECT



;----------------------------------------------------------------------------------------------------
;依頼消滅処理
;全角色が対象となり大規模化した
;----------------------------------------------------------------------------------------------------
@IRAI_VANISH
#DIM CHK_SLOT
#DIM CHARA
;#;PRINTFORMW 依頼経時処理（全角色実行、ログをデ背包ウインドウに表示）
;#;DEBUGCLEAR
;全角色対象、受託前の/既に報告の終わった依頼の処理
FOR CHARA, 1, CHARANUM
	SELECTCASE IRAI_CHARA_TO_PROGRESS(CHARA)
	;これらはまだ委托接口に入っていない
	CASE "依頼発生直後"
		CALL SET_IRAI_PROGRESS(CHARA, "依頼未受託")
		
	CASE "依頼未受託"
		SIF INT_DATA_IRAI(CFLAG:CHARA:依頼内容, "依頼消滅率", CHARA)
			CALL SET_IRAI_PROGRESS(CHARA, "依頼消滅")
	[SKIPSTART]
	依頼中のケースは委托接口見たほうがはやい
	CASE "依頼中"
	
	基本的にこの4段階は未報告状態、依頼主のリアクション待ちです（報告必須）
	CASE "依頼達成"
	CASE "期限切れ"
	CASE "報告忘れ"
	CASE "依頼失敗"
	[SKIPEND]
	CASE "依頼終了（成功）", "依頼終了（期限切れ）", "依頼終了（報告忘れ）", "依頼終了（失敗）"
	;依頼が終了し、依頼人に報告を行なったパターン（スロットに入ってない）
		CALL SET_IRAI_PROGRESS(CHARA, "依頼消滅")
	ENDSELECT
NEXT
;#;PRINTFORML 依頼経時処理完了、デ背包文を削除します
;#;FORCEWAIT
;#;DEBUGCLEAR

SIF !COUNT_IRAI_SLOT()
	RETURN

;#;PRINTFORMW 依頼期限処理（全角色実行、ログをデ背包ウインドウに表示）
;依頼の期限判定
;スロットは空けないことにした（依頼主への報告を強制させる）
;依頼報告不要時にスロットを空ける処理が必要になった
;スロット1から見ていくと依頼のリセットによってIDズレが発生するので末尾からチェックする
FOR CHK_SLOT, COUNT_IRAI_SLOT(), 0, -1
	CHARA = IRAI_SLOT_TO_CLIENT(CHK_SLOT)
	;期限による依頼の失敗判定
	;期限が残ってるならスルー
	SIF IRAI_DEADLINE:CHK_SLOT && DAY <= IRAI_DEADLINE:CHK_SLOT
		CONTINUE
	SELECTCASE IRAI_CHARA_TO_PROGRESS(CHARA)
	CASE "依頼中"
		CALL SET_IRAI_PROGRESS(CHARA, "期限切れ")
	CASE "依頼達成"
		IF STRLENS(STR_DATA_IRAI(IRAI_SLOT:CHK_SLOT, "依頼報告不要", CHARA))
			CALL IRAI_CARRYOUT(CHARA, IRAI_SLOT:CHK_SLOT, "依頼報告不要")
			CALL SHOW_IRAI_MESSAGE(CHARA, IRAI_SLOT:CHK_SLOT, "依頼報告不要")
			CALL CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, "依頼消滅")
		ELSE
			CALL SET_IRAI_PROGRESS(CHARA, "報告忘れ")
		ENDIF
	ENDSELECT
NEXT
;#;PRINTFORML 依頼期限処理完了、デ背包文を削除します
;#;FORCEWAIT
;#;DEBUGCLEAR


;----------------------------------------------------------------------------------------------------
;依頼内容の実行処理
;実行時のSOURCEの変化の他、報酬や失敗による処理も含む
;----------------------------------------------------------------------------------------------------
@IRAI_CARRYOUT(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
RESULT = 0
IF IS_COMMON_IRAI(IRAI_ID)
	TRYCCALLFORM IRAI_一般{IRAI_ID % 1000}(CHARA, IRAI_ID, SCENE)
	CATCH
		PRINTFORML ＊エラー発生＊
		PRINTFORML 　CHARA:{CHARA}　SCENE:%SCENE%　IRAI_ID:{IRAI_ID}
		PRINTFORML 　指定の関数 @IRAI_一般{IRAI_ID % 1000}は存在しません
		PRINTFORML 　エラーログをコピペして報告して下さい
		PRINTFORML 
		PRINTFORMW 　入力待機中
	ENDCATCH
ELSE
	TRYCCALLFORM IRAI_固有{IRAI_ID_TO_CLIENT(IRAI_ID)}(CHARA, IRAI_ID, SCENE)
	CATCH
		PRINTFORML ＊エラー発生＊
		PRINTFORML 　CHARA:{CHARA}　SCENE:%SCENE%　IRAI_ID:{IRAI_ID}
		PRINTFORML 　指定の関数 @IRAI_固有{IRAI_ID_TO_CLIENT(IRAI_ID)}は存在しません
		PRINTFORML 　エラーログをコピペして報告して下さい
		PRINTFORML 
		PRINTFORMW 　入力待機中
	ENDCATCH
ENDIF
RETURN RESULT
;RESULTと依頼実行判定結果の対応はIRAI_CONST_RESULT(TYPE)を参照のこと



;----------------------------------------------------------------------------------------------------
;受託済み依頼情報表示関数
;----------------------------------------------------------------------------------------------------
@SHOW_ALL_IRAI_SLOT
#DIM CHK_SLOT
#DIM IRAI_NUM
IRAI_NUM = COUNT_IRAI_SLOT()
FOR CHK_SLOT, 1, IRAI_NUM + 1
	CALL SHOW_IRAI_DATA(IRAI_SLOT:CHK_SLOT, CHK_SLOT)
	PRINTFORML 
NEXT



;----------------------------------------------------------------------------------------------------
;依頼情報表示関数
;依頼ID単体でも使用できるが受託済みの場合は簡略化のためにSLOT番号を渡す
;フルネーム表記なのはわざとです（そのほうがお硬い文面っぽく見えるので）
;----------------------------------------------------------------------------------------------------
@SHOW_IRAI_DATA(IRAI_ID, CHK_SLOT)
#DIM IRAI_ID   ;対象の依頼ID（依頼人情報含む）
#DIM CHK_SLOT  ;判定対象とする委托接口、未受託の状態では0で読んで下さい
#DIM CLIENT_ID ;依頼人ID
#DIM CHK_ID    ;依頼自体のID
#DIM DEAD_LINE ;期限日数
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
CHK_ID    = IRAI_ID % 1000
DEAD_LINE = INT_DATA_IRAI(IRAI_ID, "依頼期限", CLIENT_ID)
SIF CHK_SLOT
	PRINTFORML 依頼%TOFULL(TOSTR(CHK_SLOT))%
PRINTFORML 　＜%STR_DATA_IRAI(IRAI_ID, "依頼名", CLIENT_ID)%＞
PRINTFORML 　　委託者：%NAME:CLIENT_ID%    -    %NAME_FROM_PLACE(CFLAG:CLIENT_ID:現在位置)%
PRINTFORML 　委託内容：%STR_DATA_IRAI(IRAI_ID, "依頼内容表示", CLIENT_ID)%
PRINTFORML 　　　報酬：%STR_DATA_IRAI(IRAI_ID, "依頼報酬表示", CLIENT_ID)%
SIF STRLENS(STR_DATA_IRAI(IRAI_ID, "依頼報告不要", CLIENT_ID))
	PRINTFORML 　　　報告：%STR_DATA_IRAI(IRAI_ID, "依頼報告不要", CLIENT_ID)%
SIF STRLENS(STR_DATA_IRAI(IRAI_ID, "依頼備考表示", CLIENT_ID))
	PRINTFORML 　　　備註：%STR_DATA_IRAI(IRAI_ID, "依頼備考表示", CLIENT_ID)%
;#;PRINTFORML 　　依頼ID：{IRAI_ID}　CLIENT_ID{CLIENT_ID}

;未受託の場合は期限のみ表示しておしまい
IF !CHK_SLOT
	IF !DEAD_LINE
		PRINTFORML 　　　期限：今日内
	ELSE
		PRINTFORML 　　　期限：{DEAD_LINE}日以内
	ENDIF
	RETURN
ELSE
	PRINTFORML 　　　期限：あと{IRAI_DEADLINE:CHK_SLOT - DAY}日
ENDIF

;ここから受託済み、業種別の表示
SELECTCASE IRAI_SLOT_TO_TASKNAME(CHK_SLOT)
CASE "調達"
	PRINTFORML 　　要求品：%ITEMNAME:(IRAI_SLOT_TO_ITEM(CHK_SLOT))%×{IRAI_SUB:CHK_SLOT}
	SIF ITEMNAME:IRAI_SLOT_TO_ITEM(CHK_SLOT) == "好吃的魚"
		PRINTFORML 　　　　　（虹鱒・鮭魚・西太公魚・八目鰻・雷魚）
CASE "伝令（人物）"
	PRINTFORML 　目標人物：%NAME:(IRAI_SLOT_TO_PERSON(CHK_SLOT))%    -    %NAME_FROM_PLACE(CFLAG:(IRAI_SLOT_TO_PERSON(CHK_SLOT)):現在位置)%
	;PRINTFORML 　現在位置：%NAME_FROM_PLACE(CFLAG:(IRAI_SLOT_TO_PERSON(CHK_SLOT)):現在位置)% 
	PRINTFORML 　作息時間：{CFLAG:(IRAI_SLOT_TO_PERSON(CHK_SLOT)):就寝時間 / 60,2,RIGHT}～{CFLAG:(IRAI_SLOT_TO_PERSON(CHK_SLOT)):起床時間 / 60,2,RIGHT}時

CASE "伝令（場所）"
	PRINTFORML 　　目的地：%GET_PLACENAME(IRAI_SLOT_TO_PLACE(CHK_SLOT))%
;納茲琳に聞いても忘れる你(わたし)へ
CASE "捜索（人物）"
	IF IRAI_EX:CHK_SLOT
		PRINTFORML 　参考人物：%CALLNAME:(IRAI_EX:CHK_SLOT)%    -    %NAME_FROM_PLACE(CFLAG:(IRAI_SLOT_TO_PERSON(CHK_SLOT)):現在位置)%
		;PRINTFORML 　現在位置：%NAME_FROM_PLACE(CFLAG:(IRAI_EX:CHK_SLOT):現在位置)% 
		PRINTFORML 　作息時間：{CFLAG:(IRAI_EX:CHK_SLOT):就寝時間 / 60,2,RIGHT}～{CFLAG:(IRAI_EX:CHK_SLOT):起床時間 / 60,2,RIGHT}時
	ELSE
		PRINTFORM 　手がかり：
		CALL SHOW_IRAI_HINT("捜索（人物）", IRAI_SLOT_TO_PERSON(CHK_SLOT), IRAI_SUB:CHK_SLOT)
	ENDIF
CASE "捜索（場所）"
	IF IRAI_EX:CHK_SLOT > 5000
		PRINTFORML 　　推定地：%STR:(5000 + GET_MAPID(IRAI_EX:CHK_SLOT-5000))%
	ELSEIF IRAI_EX:CHK_SLOT
		PRINTFORML 　　推定地：%STR:(5000 + GET_MAPID(IRAI_EX:CHK_SLOT))%の%GET_PLACENAME(IRAI_EX:CHK_SLOT)%
	ELSE
		PRINTFORM 　手がかり：
		CALL SHOW_IRAI_HINT("捜索（場所）", IRAI_SLOT_TO_PLACE(CHK_SLOT), IRAI_SUB:CHK_SLOT)
	ENDIF
CASE "討伐"
	PRINTFORML 　討伐対象：%NAME:(IRAI_SLOT_TO_PERSON(CHK_SLOT))%    -    %NAME_FROM_PLACE(CFLAG:(IRAI_SLOT_TO_PERSON(CHK_SLOT)):現在位置)%
	PRINTFORM 　战力指数：
	CALL PRINT_RANK(ABL:IRAI_SLOT_TO_PERSON(CHK_SLOT):戦闘能力, "MAX_6")
	PRINTL
	;PRINTFORML 　現在位置：%NAME_FROM_PLACE(CFLAG:(IRAI_SLOT_TO_PERSON(CHK_SLOT)):現在位置)% 
	;PRINTFORML 　就寝時間：{CFLAG:(IRAI_SLOT_TO_PERSON(CHK_SLOT)):就寝時間 / 60,2,RIGHT}時 
	PRINTFORML 　作息時間：{CFLAG:(IRAI_SLOT_TO_PERSON(CHK_SLOT)):就寝時間 / 60,2,RIGHT}～{CFLAG:(IRAI_SLOT_TO_PERSON(CHK_SLOT)):起床時間 / 60,2,RIGHT}時
ENDSELECT
CALL IRAI_CARRYOUT(CLIENT_ID, IRAI_ID, "依頼条件表示")

;確定してる場合はその旨を表示しておしまい
IF GROUPMATCH(IRAI_CHARA_TO_PROGRESS(CLIENT_ID), "期限切れ", "報告忘れ", "依頼失敗")
	PRINTFORML 　達成状況：失敗
	RETURN
ELSEIF GROUPMATCH(IRAI_CHARA_TO_PROGRESS(CLIENT_ID), "依頼達成")
	PRINTFORML 　達成状況：已達成（未報告）
	RETURN
ELSEIF !DEAD_LINE
	PRINTFORML 　　　期限：今日内
ELSEIF DAY > IRAI_DEADLINE:CHK_SLOT
	PRINTFORML 　　　期限：還剩{IRAI_DEADLINE:CHK_SLOT - DAY}日
ELSEIF DAY == IRAI_DEADLINE:CHK_SLOT
	PRINTFORML 　　　期限：今日内
ENDIF

;----------------------------------------------------------------------------------------------------
;依頼情報内のヒント表示
;DYNAMIC部分はSHOW_TASTEを参考,助手効果のデータだけを抽出する
;----------------------------------------------------------------------------------------------------
@SHOW_IRAI_HINT(TASKNAME, ARG, パターン)
#DIMS TASKNAME
#DIM CHARA
#DIM PLACE
#DIM パターン
#DIM 洞察力
#DIMS DYNAMIC LIST, 100 ;列表
#DIM  DYNAMIC CNT,  100 ;列表各要素の重複個数
#DIM  DYNAMIC NUM       ;種類の総数
;◆洞察力に関して
;元々はEXP:依頼達成経験とABL:依頼こ無レベルを設定して、依頼をこなすほど洞察力(=依頼レベル)が増えるという風にしようと思っていた
;が、わざわざ作らなくてもプレイヤー側のリアル洞察力の方が必要なので凍結させました。
LOCALS = 
;持ち主を推定する,現状パターンは1～3,パターン0は場所の方へ
IF TASKNAME == "捜索（人物）" && パターン > 0
	CHARA = ARG
	洞察力 = 100
	CALL DISH_TASTE_FROM_CHARA(CHARA, "OUTPUT")
	CALLF CNT_SPLIT(RESULTS, LIST, CNT, NUM)
	FOR LOCAL, 0, NUM
		SELECTCASE LIST:LOCAL
		CASE "神力"
				LOCALS += "蕴含着神力。"
		CASE "妖力"
				LOCALS += "飘散着淡淡的妖気。"
		CASE "霊障"
				LOCALS += "感受到灵的力量。"
		CASE "魔力"
				LOCALS += "蕴含着魔力。"
		CASE "生命力"
				LOCALS += "蕴藏着自然的力量。"
		CASE "仙術", "秘術"
				LOCALS += "感受到神秘的能量。"
		CASE "鬼の魔力", "血の魔力"
				LOCALS += "散放着暴力的魔力。"
		CASE "女僕流"
				LOCALS += "女仆的道具。"
		CASE "天狗流"
				LOCALS += "写着天狗文字。"
		CASE "冥界風"
				LOCALS += "是冥界使用的东西。"
		CASE "天界風"
				LOCALS += "天界的产物。"
		CASE "地底風"
				LOCALS += "在地底有类似的。"
		CASE "地獄風"
				LOCALS += "沾上了地獄的砂。"
		CASE "畜生流"
				LOCALS += "畜生界で見かける道具だ。"
		CASE "月都風"
				LOCALS += "只有在月之都才有的东西。"
		CASE "玉兎流"
				LOCALS += "有兔子的印章。"
		CASE "魔界風"
				LOCALS += "使用魔界的石头。"
		CASE "夢幻風"
				LOCALS += "感受到梦幻世界的妖气。"
		CASE "外界風"
				LOCALS += "外面的世界的东西。"
		CASE "化学的"
				LOCALS += "精密的制作品。"
		CASE "小人族秘伝的味道"
				LOCALS += "是小人的工具。"
		CASE "稗田家秘伝の味"
				LOCALS += "有稗田家的紋章。"
		CASE "母亲的味道"
				LOCALS += "是家常的工具 。"
;		CASE "プロの味"
;				LOCALS += ""
		;天候と時間によってきらきらしなくなったりもする
		CASE "闪耀"
				LOCALS += "闪闪发光。"
		CASE "純化的味道"
				LOCALS += "一丝浑濁都没有。"
		CASE "薬効"
				LOCALS += "似乎是医療道具。"
		CASE "魔性"
				LOCALS += "被施加了強化魔法。"
		CASE "奇跡", "幸運"
				LOCALS += "有好事要发生的预感。"
		CASE "加護", "豊穣"
				LOCALS += "似乎很霊験的様子。"
		CASE "嫉妬"
				LOCALS += "感到了執念…。"
		CASE "道具の共鳴"
				LOCALS += "是付喪神的伙伴。"
;		CASE "浓郁"
;				LOCALS += ""
;		CASE "薄味"
;				LOCALS += ""
;		CASE "隠し味"
;				LOCALS += ""
		CASE "心跳味"
				LOCALS += "似乎有隐藏的装置机关。"
		CASE "未知の味"
				LOCALS += "不可思议的东西。"
		CASE "氷結"
				LOCALS += "寒冷。"
		CASE "大火力"
				LOCALS += "很温暖。"
		CASE "彩り"
				LOCALS += "有七彩色的光辉。"
		CASE "絢爛"
				LOCALS += "非常浮华。"
		CASE "貧乏飯"
				LOCALS += "破烂的。"
		CASE "切れ味"
				LOCALS += "像武器一样。"
		CASE "美味的水"
				LOCALS += "是防水的。"
		CASE "刺激的"
				LOCALS += "有一个骷髅标记…。"
		ENDSELECT
	NEXT
	SELECTCASE パターン
	CASE 1
		IF TALENT:CHARA:擅長用針
			LOCALS += "尖尖的。"
		ELSEIF TALENT:CHARA:日光浴
			LOCALS += "有太阳的标志。"
		ELSEIF TALENT:CHARA:月光浴
			LOCALS += "有月亮的标志。"
		ELSEIF TALENT:CHARA:幼稚
			LOCALS += "像玩具一样。"
		ENDIF
	CASE 2
		IF ABL:CHARA:音楽技能 >= 2 && ABL:MASTER:音楽技能 >= 1
			LOCALS += "关于乐器。"
		ELSEIF TALENT:CHARA:釣魚Lv >= 2 && TALENT:MASTER:釣魚Lv >= 1
			LOCALS += "是钓鱼的工具。"
		ELSEIF ABL:CHARA:料理技能 >= 1 && ABL:MASTER:料理技能 >= 1
			LOCALS += "做饭用的工具。"
		ELSEIF ABL:CHARA:教養 >= 4 && ABL:MASTER:教養 >= 3
			LOCALS += "很难理解的书。"
		ENDIF
	CASEELSE
		SIF TALENT:CHARA:胆量 < 0
			LOCALS += "像护身符一样。"
		SIF TALENT:CHARA:開朗／阴郁 < 0
			LOCALS += "暗色调。"
		SIF TALENT:CHARA:開朗／阴郁 > 0
			LOCALS += "明亮的色调。"
		SIF TALENT:CHARA:喜歡引人注目
			LOCALS += "自信。"
		SIF TALENT:CHARA:汚臭耐性 < 0
			LOCALS += "被擦得很干净。"
		SIF TALENT:CHARA:汚臭耐性 > 0
			LOCALS += "污垢明显。"
		SIF TALENT:CHARA:狐 || TALENT:CHARA:妖狐
			LOCALS += "带有动物毛。"
		SIF TALENT:CHARA:吸血鬼
			LOCALS += "有一个模仿蝙蝠的印章。"
		SIF TALENT:CHARA:水棲
			LOCALS += "湿漉的。"
	ENDSELECT
	;ある程度まで行くと断定できる
	IF ABL:CHARA:親密 >= 20 || TALENT:CHARA:恋人
		LOCALS = 这是%CALLNAME:CHARA%的东西。
	ELSEIF ABL:CHARA:親密 >= 10
		LOCALS += "就像那个孩子曾有的一样…。"
	ELSEIF ABL:CHARA:親密 >= 5
		LOCALS += "我好像在某人那里看过…。"
	ENDIF
ELSEIF TASKNAME == "捜索（場所）" || (TASKNAME == "捜索（人物）" && !パターン)
	;落とした場所を推定する,現状パターンは1～2
	IF TASKNAME == "捜索（場所）"
		PLACE = ARG
	;落とし物の対象が人物になっているパターン0
	;一応報酬で判別可能だがいきなり人物を推定するのはオカシイので人物の自宅位置＝落とした場所を推定する
	ELSEIF TASKNAME == "捜索（人物）" && !パターン
		IF CFLAG:ARG:自宅位置
			PLACE = CFLAG:ARG:自宅位置
		ELSE
			PRINTFORML 找不到任何线索…。
			RETURN
		ENDIF
	ENDIF
	洞察力 = 100
	SELECTCASE GET_MAPID(PLACE)
	CASE 0
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "好像我在博麗神社附近丢了。"
;		ELSEIF 洞察力>= 3
;			LOCALS += "博麗神社に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 1
			IF パターン == 1
				LOCALS += "我去参观的那天丢了。"
			ELSE
				LOCALS += "好像是宴会的那天丢了。"
			ENDIF
		ENDIF
	CASE 1
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "好像我在命蓮寺附近丢了。"
;		ELSEIF 洞察力>= 3
;			LOCALS += "命蓮寺に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 1
			IF パターン == 1
				LOCALS += "我去参观的那天丢了。"
			ELSE
				LOCALS += "好像在人群中丢了。"
			ENDIF
		ENDIF
	CASE 2
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "好像我在人間之里附近丢了。"
;		ELSEIF 洞察力>= 3
;			LOCALS += "人里に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 1
			IF パターン == 1
				LOCALS += "好像是宴会的那天丢了。"
			ELSE
				LOCALS += "好像在人群中丢了。"
			ENDIF
		ENDIF
	CASE 3
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "好像在红魔馆附近丢了。"
;		ELSEIF 洞察力>= 4
;			LOCALS += "紅魔館に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 2
			IF パターン == 1
				LOCALS += "好像是宴会的那天丢了。"
			ELSE
				LOCALS += "好像在去钓鱼的那天丢了。"
			ENDIF
		ENDIF
	CASE 4
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "好像在永遠亭附近丢了。"
;		ELSEIF 洞察力>= 5
;			LOCALS += "永遠亭に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 3
			IF パターン == 1
				LOCALS += "好象在黑暗中丢了。"
			ELSE
				LOCALS += "好象在迷路的时候丢了。"
			ENDIF
		ENDIF
	CASE 5
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "好像在魔法之森丢了。"
;		ELSEIF 洞察力>= 5
;			LOCALS += "魔法之森に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 3
			IF パターン == 1
				LOCALS += "好象在黑暗中丢了。"
			ELSE
				LOCALS += "好象在迷路的时候丢了。"
			ENDIF
		ENDIF
	CASE 6
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "好像在冥界那里丢了。"
;		ELSEIF 洞察力>= 6
;			LOCALS += "冥界方面に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 4
			LOCALS += "意识到的时候好像已经丢了。"
		ENDIF
	CASE 7
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "好像在山麓那里丢了。"
;		ELSEIF 洞察力>= 4
;			LOCALS += "好像在山麓那里丢了。"
		ELSEIF 洞察力 >= 2
			IF パターン == 1
				LOCALS += "好象在妖怪的山上丢了。"
			ELSE
				LOCALS += "好像在去钓鱼的那天丢了。"
			ENDIF
		ENDIF
	CASE 8
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "好象在山顶附近丢了。"
;		ELSEIF 洞察力>= 4
;			LOCALS += "好象在山顶附近丢了。"
		ELSEIF 洞察力 >= 2
			IF パターン == 1
				LOCALS += "好象在妖怪的山上丢了。"
			ELSE
				LOCALS += "我去参观的那天丢了。"
			ENDIF
		ENDIF
	CASE 9
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "好像在地底那里丢了。"
;		ELSEIF 洞察力>= 6
;			LOCALS += "好像在地底那里丢了。"
		ELSEIF 洞察力 >= 4
			IF パターン == 1
				LOCALS += "好象在黑暗中丢了。"
			ELSE
				LOCALS += "好像在去钓鱼的那天丢了。"
			ENDIF
		ENDIF
	CASE 10
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "好象在月之都附近丢了。"
;		ELSEIF 洞察力>= 6
;			LOCALS += "月之都に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 4
			LOCALS += "意识到的时候好像已经丢了。"
		ENDIF
	ENDSELECT
	SIF 洞察力 >= 2 && TASKNAME == "捜索（人物）" && !パターン
		LOCALS += "因为是显眼的东西、很有可能已经被人捡到了。"
ENDIF
SIF LOCALS == ""
	LOCALS = 找不到任何线索…。
PRINTFORML %LOCALS%

;----------------------------------------------------------------------------------------------------
;依頼関連の口上呼び出し関数
;----------------------------------------------------------------------------------------------------
@SHOW_IRAI_MESSAGE(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
RESULTS =
IF SCENE == "依頼報告不要"
	CALL KOJO_MESSAGE_SEND("IRAI", CHARA, IRAI_ID, 0, 0, "NO_REPORT", SCENE)
ELSEIF CHARA == IRAI_ID_TO_CLIENT(IRAI_ID)
;対象が依頼者である
	CALL KOJO_MESSAGE_SEND("IRAI", CHARA, IRAI_ID, 0, 0, "CLIENT", SCENE)
ELSE
;この場合はおおむねSCENEは"依頼実行時"になるが一応渡す
	CALL KOJO_MESSAGE_SEND("IRAI", CHARA, IRAI_ID, 0, 0, "TARGET", SCENE)
ENDIF
RESETCOLOR


;----------------------------------------------------------------------------------------------------
;依頼リセット関数
;依頼関連変数の全初期化
;----------------------------------------------------------------------------------------------------
@IRAI_RESET
PRINTFORML 重置所有的委托。
PRINTFORML 可以吗？
CALL ASK_YN
IF !RESULT
	CVARSET CFLAG, 403
	CVARSET CFLAG, 404
	VARSET IRAI_SLOT
	VARSET IRAI_DATA
	VARSET IRAI_SUB
	VARSET IRAI_DEADLINE
	VARSET IRAI_CLIENT
	VARSET IRAI_EX
	PRINTFORMW 已经重置。
ENDIF



;----------------------------------------------------------------------------------------------------
;依頼期限通知関数
;毎朝の通知
;----------------------------------------------------------------------------------------------------
@MEMO_IRAI_DEADLINE
#DIM CLIENT
;受託済み依頼の期限の処理
FOR LOCAL,1, 10
	SIF !IRAI_SLOT:LOCAL
		CONTINUE
	CLIENT = IRAI_SLOT_TO_CLIENT(LOCAL)
	SIF GROUPMATCH(IRAI_CHARA_TO_PROGRESS(CLIENT), "依頼中", "依頼達成", "依頼失敗")
		PRINTFORML %CALLNAME:CLIENT%的委託的期限還有{IRAI_DEADLINE:LOCAL - DAY}天
NEXT



;----------------------------------------------------------------------------------------------------
;「訪問房間」での依頼関連のアクション
;----------------------------------------------------------------------------------------------------
@IRAI_VISIT_HOME(CHARA)
#DIM PREV_TARGET     ;TARGET退避
#DIM CHARA           ;対象きゃら
#DIM IRAI_NUM        ;受託依頼数
#DIM CHK_SLOT        ;判定対象スロット
#DIM DYNAMIC CNT_DO  ;実行/報告した依頼数＝戻り値
PREV_TARGET = TARGET
TARGET = CHARA
IRAI_NUM = COUNT_IRAI_SLOT()
FOR CHK_SLOT, 1, IRAI_NUM + 1
	SIF !CAN_IRAI_VISIT_HOME_PERSON(CHARA, CHK_SLOT)
		CONTINUE
	PRINTFORML ＜%STR_DATA_IRAI(IRAI_SLOT:CHK_SLOT, "依頼名", CHARA)%＞
	PRINTFORMW 　→[%IRAI_SLOT_TO_ACTNAME(CHK_SLOT)%]
	CALL IRAI_APPLY(CHK_SLOT)
	SIF RESULT > 0
		CNT_DO ++
NEXT

IRAI_NUM = COUNT_IRAI_SLOT()
FOR CHK_SLOT, IRAI_NUM, 0, -1
	SIF !CAN_IRAI_VISIT_HOME_CLIENT(CHARA, CHK_SLOT)
		CONTINUE
	PRINTFORML ＜%STR_DATA_IRAI(IRAI_SLOT:CHK_SLOT, "依頼名", CHARA)%＞
	PRINTFORMW 　→委託報告
	CALL IRAI_REPORT(CHARA)
	SIF RESULT > 0
		CNT_DO ++
NEXT
TARGET = PREV_TARGET
RETURN CNT_DO

;一般依頼の抑制
@BAN_COMMON_IRAI(C_ID, IRAI_ID)
#DIM C_ID
#DIM IRAI_ID
SETBIT CFLAG:C_ID:一般依頼禁止, IRAI_ID

;掃除を手伝って（発生条件：TARGETがメインマップ在住）
;SETBIT CFLAG:XX:一般依頼禁止, 1
;お花に水をあげて（発生条件：TARGETがMASTER侵入可能の自宅住まいかつ、メインマップ在住かつ、親密２以上）
;SETBIT CFLAG:XX:一般依頼禁止, 2
;いっしょにあそぼ（発生条件：TARGETがTALENT:幼稚持ち）
;SETBIT CFLAG:XX:一般依頼禁止, 3
;料理上手になりたい（発生条件：TARGETの料理技能がMASTER未満かつ、MASTERの料理技能が２以上かつ、親密が３以上）
;SETBIT CFLAG:XX:一般依頼禁止, 4
;特訓につきあって（発生条件：TARGETの戦闘能力がMASTER以下かつ、親密が２以上かつ人里以外）
;SETBIT CFLAG:XX:一般依頼禁止, 5
;誰か教えて！（発生条件：TARGETの教養がMASTER以下）
;SETBIT CFLAG:XX:一般依頼禁止, 6
;大掃除が終わらない（発生条件：TARGETがメインマップ在住かつ、冬の15日から31日までかつ、親密が３以上）
;SETBIT CFLAG:XX:一般依頼禁止, 7
;手紙を届けて（発生条件：TARGETの信頼度が30以上かつ、TARGETに文通相手が設定されている（FUNC_IRAI.ERBの965目以降に存在）
;SETBIT CFLAG:XX:一般依頼禁止, 8
;誰かの落とし物（発生条件：TARGETの信頼度が30以上）
;SETBIT CFLAG:XX:一般依頼禁止, 9
;どうしてもみつからない(そのまま落ちてる)（発生条件：TARGETの信頼度が30以上）
;SETBIT CFLAG:XX:一般依頼禁止, 10
;どうしてもみつからない(誰かが拾ってる)（発生条件：TARGETの信頼度が30以上）
;SETBIT CFLAG:XX:一般依頼禁止, 11
;懲らしめて！（発生条件：TARGETのCSVに戦闘能力が１以上ある場合）
;SETBIT CFLAG:XX:一般依頼禁止, 12
;うまい魚が食べたいなぁ（発生条件：わかさぎ姫以外）
;SETBIT CFLAG:XX:一般依頼禁止, 13
;あなたに抱かれたい（発生条件：セフレor愛欲かつ溜まってる度700以上 or ヤリマン）
;SETBIT CFLAG:XX:一般依頼禁止, 15
;お悩み相談（発生条件：MASTERの話術技能が１以上）
;SETBIT CFLAG:XX:一般依頼禁止, 16
