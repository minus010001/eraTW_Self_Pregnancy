;存否関数
;F関数はTRYCALL系が使えないので通常関数で存否配列を作っちゃう
	;暫定的コメントアウト
;@EXIST_一般依頼14
;#LOCALSIZE 1
;#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依頼14(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依頼名"
	CALLF MAKE_STR(V_NAME, "正在砍柴")
CASE "依頼内容表示"
	CALLF MAKE_STR(V_NAME, @"运送木柴")
CASE "依頼報酬表示"
	CALLF MAKE_STR(V_NAME, @"平价食材３個")
CASE "依頼実行指令名"
	CALLF MAKE_STR(V_NAME, "给5块木柴")
CASE "依頼内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(GETNUM(ITEM, "木材"), "調達"))
CASE "依頼サブ"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依頼期限"
;発生時ではなく受託時に確定します
	CALLF MAKE_INT(V_NAME, 5)
CASE "依頼発生率"
	IF DAY:2 == 4
	;冬は発生しやすい
		CALLF MAKE_INT(V_NAME, 10)
	ELSE
		CALLF MAKE_INT(V_NAME, 3)
	ENDIF
CASE "依頼発生条件"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依頼受託可能条件"
;木材を持ってる
	SIF ITEM:木材
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼実行可能条件"
	SIF ITEM:木材 >= 5
		CALLF MAKE_INT(V_NAME, 1)
;成功/失敗処理はCARRYOUTで直接書く
;SOURCEやITEM入手、消費が煩雑になるので
CASE "依頼消滅条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼人連れ出し可能"
ENDSELECT



@IRAI_一般14(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE SCENE
CASE "依頼条件表示"
;何か専用の表示情報を設けたい時用
;受託済みの依頼表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
;ここ以外の表示項目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが見栄えが良いです

CASE "依頼実行時"
;このCASEに限り、RESULTが意味を持ちます
;依頼実行時のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な引数は"中止", "実行して成功", "実行して失敗", "実行して未解決"です
;独自処理を仕込みたい時は"実行して未解決"で先延ばしするなりしてください
	PRINTFORML %CALLNAME:MASTER%把木柴交给了%CALLNAME:TARGET%
	IF DAY:2 == 4
	;冬はメッセージあり
		PRINTFORML 这样就不会因为寒冷而发抖了
		SOURCE:TARGET:歓楽 += 2000
	ELSE
		PRINTFORML 
		SOURCE:TARGET:歓楽 += 1000
	ENDIF
	CALL LOST_ITEM("木材", 5, 2)
	RETURN IRAI_CONST_RESULT("実行して成功")
CASE "依頼成功時"
;成功報告時の報酬など
	CALL CHANGE_CFLAG(2, TARGET, 30)
	CALL CHANGE_CFLAG(4, TARGET, 20)
	TALENT:心情 = LIMIT(TALENT:心情 + 1, -1, 1)
	CALL GET_ITEM("平价食材", 3, 2)
CASE "依頼失敗時"
;失敗報告時の処理
	CALL CHANGE_CFLAG(2, TARGET, -5)
	CALL CHANGE_CFLAG(4, TARGET, -5)
ENDSELECT
