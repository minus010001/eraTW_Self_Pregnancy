;存否関数
;F関数はTRYCALL系が使えないので通常関数で存否配列を作っちゃう
@EXIST_一般依頼16
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依頼16(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依頼名"
	CALLF MAKE_STR(V_NAME, "倾听烦恼！")
CASE "依頼内容表示"
	CALLF MAKE_STR(V_NAME, @"倾听烦恼的事情")
CASE "依頼報酬表示"
	CALLF MAKE_STR(V_NAME, @"野菜或果物（如果解决的话）")
CASE "依頼実行指令名"
	CALLF MAKE_STR(V_NAME, "烦恼相談")
CASE "依頼内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "其他"))
CASE "依頼サブ"
CASE "依頼期限"
;発生時ではなく受託時に確定します
CASE "依頼発生率"
;これは他の一般依頼との相対確率なんで10％ではありません
	CALLF MAKE_INT(V_NAME, 5)
CASE "依頼発生条件"
	SIF ABL:MASTER:話術技能 >= 1
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼受託可能条件"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依頼実行可能条件"
	SIF TARGET == ARG && SHIRAHU(ARG)
		CALLF MAKE_INT(V_NAME, 1)
;成功/失敗処理はCARRYOUTで直接書く
;SOURCEやITEM入手、消費が煩雑になるので
CASE "依頼消滅条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼人連れ出し可能"
ENDSELECT



@IRAI_一般16(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM 正解の選択肢
#DIM 逆張りナシ
#DIM お悩みナンバー
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE SCENE
CASE "依頼条件表示"
;何か専用の表示情報を設けたい時用
;受託済みの依頼表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
;ここ以外の表示項目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが見栄えが良いです

CASE "依頼実行時"
;このCASEに限り、RESULTが意味を持ちます
;依頼実行時のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な引数は"中止", "実行して成功", "実行して失敗", "実行して未解決"です
;独自処理を仕込みたい時は"実行して未解決"で先延ばしするなりしてください
	VARSET RESULT
	VARSET LOCAL
	;口上に委託するか判定
	CALL KOJO_MESSAGE_SEND("IRAI", TARGET, IRAI_ID, 0, 0, "TARGET", "口上委託率")
	;LOCAL:1～に発生させない番号を保存
	FOR LOCAL, 1, 100
		LOCAL:LOCAL = RESULT:LOCAL
	NEXT
	;口上専用ルート
	IF RESULT:100 > RAND:100
		VARSET RESULT
		VARSET RESULTS
		CALL KOJO_MESSAGE_SEND("IRAI", TARGET, IRAI_ID, 0, 0, "TARGET", "口上専用")
	;汎用ルート
	ELSE
		RESULT = 0
		WHILE !RESULT
			RESULT = 0
			;お悩みパターンを増やしたらここを調整（あえて稍微多めにしてます）
			お悩みナンバー = RAND:26
			SIF LOCAL:お悩みナンバー
				CONTINUE
			CALL IRAI_一般16_お悩みデータベース(お悩みナンバー, TARGET)
		WEND
	ENDIF
	正解の選択肢 = RESULT:1
	逆張りナシ = RESULT:2
	DRAWLINE
	[IF_DEBUG]
		PRINTFORML ｺﾀｴﾊ No.{正解の選択肢} ﾀﾞﾖ
	[ENDIF]
	PRINTFORML ～%CALLNAME:TARGET%的今天的烦悩～
	PRINTFORML 　　　『%RESULTS%』
SIF 正解の選択肢 == 1 || 正解の選択肢 == 7
	SETCOLOR C_YELLOW
SIF 正解の選択肢 == 4 || 正解の選択肢 == 8
	SETCOLOR C_D_RED
	PRINTFORML [ 1] - %RESULTS:1%
RESETCOLOR

SIF 正解の選択肢 == 2 || 正解の選択肢 == 7
	SETCOLOR C_YELLOW
SIF 正解の選択肢 == 5 || 正解の選択肢 == 8
	SETCOLOR C_D_RED
	PRINTFORML [ 2] - %RESULTS:2%
RESETCOLOR

SIF 正解の選択肢 == 3 || 正解の選択肢 == 7
	SETCOLOR C_YELLOW
SIF 正解の選択肢 == 6 || 正解の選択肢 == 8
	SETCOLOR C_D_RED
	PRINTFORML [ 3] - %RESULTS:3%
RESETCOLOR
	INPUT
	LOCAL = RESULT
	TIME += 10
	;ひとつだけハズレのパターン（"正解の選択肢"がハズレの選択肢）
	IF GROUPMATCH(正解の選択肢, 4, 5, 6)
		IF LOCAL == 正解の選択肢 - 3
			CALL COLORMESSAGE(@"＜BAD…＞", C_RED, 0)
			PRINTFORML 【%RESULTS:LOCAL%】
			PRINTFORMW %CALLNAME:TARGET%对%CALLNAME:MASTER%的回答不満意…
			SOURCE:歓楽 = 50 + ABL:親密 * 5
			SOURCE:反感 = 100
			TFLAG:193 = -1
			RETURN IRAI_CONST_RESULT("実行して失敗")
		ELSE
			CALL COLORMESSAGE(@"＜NICE!!＞", C_YELLOW, 0)
			PRINTFORML 【%RESULTS:LOCAL%】
			PRINTFORMW %CALLNAME:TARGET%对%CALLNAME:MASTER%的回答很満意！
			TFLAG:193 = 1
			SOURCE:歓楽 = 500 + ABL:親密 * 50
			EXP:MASTER:会話経験 += 5
			TFLAG:好感度BOUNS = ABL:親密 * 5
			TFLAG:信賴度管理 = 2
			RETURN IRAI_CONST_RESULT("実行して成功")
		ENDIF
	;全部正解のパターン
	ELSEIF 正解の選択肢 == 7
		CALL COLORMESSAGE(@"＜NICE!!＞", C_YELLOW, 0)
		PRINTFORML 【%RESULTS:LOCAL%】
		PRINTFORMW %CALLNAME:TARGET%对%CALLNAME:MASTER%的回答很満意！
		TFLAG:193 = 1
		SOURCE:歓楽 = 500 + ABL:親密 * 50
		EXP:MASTER:会話経験 += 5
		TFLAG:好感度BOUNS = ABL:親密 * 5
		TFLAG:信賴度管理 = 2
		RETURN IRAI_CONST_RESULT("実行して成功")
	;全部ハズレのパターン
	ELSEIF 正解の選択肢 == 8
		CALL COLORMESSAGE(@"＜BAD…＞", C_RED, 0)
		PRINTFORML 【%RESULTS:LOCAL%】
		PRINTFORMW %CALLNAME:TARGET%对%CALLNAME:MASTER%的回答不満意…
		SOURCE:歓楽 = 50 + ABL:親密 * 5
		SOURCE:反感 = 100
		TFLAG:193 = -1
		RETURN IRAI_CONST_RESULT("実行して失敗")
	ELSE
		;もし正解の選択肢がおかしい場合、警告を出して修正する
		IF !GROUPMATCH(正解の選択肢, 1, 2, 3)
			PRINTFORMW ※正解的選択肢发生異常（{正解の選択肢}）
			正解の選択肢 = RAND:3 + 1
		ENDIF
		;正解の選択肢を選ぶ
		IF LOCAL == 正解の選択肢
			;6分の5の確率でそのまま正解
			IF RAND:6 || 逆張りナシ
				CALL COLORMESSAGE(@"＜NICE!!＞", C_YELLOW, 0)
				PRINTFORML 【%RESULTS:LOCAL%】
				PRINTFORMW %CALLNAME:TARGET%对%CALLNAME:MASTER%的回答很満意！
				TFLAG:193 = 1
				SOURCE:歓楽 = 500 + ABL:親密 * 50
				EXP:MASTER:会話経験 += 5
				TFLAG:好感度BOUNS = ABL:親密 * 5
				TFLAG:信賴度管理 = 2
				RETURN IRAI_CONST_RESULT("実行して成功")
			;6分の1の確率で逆にハズレ
			ELSE
				CALL COLORMESSAGE(@"＜SAD…＞", C_P_PURPLE, 0)
				PRINTFORML 【%RESULTS:LOCAL%】
				PRINTFORMW %CALLNAME:TARGET%对%CALLNAME:MASTER%老套的回答感到失望…
				SOURCE:歓楽 = 100 + ABL:親密 * 10
				SOURCE:反感 = 50
				EXP:MASTER:会話経験 += 1
				TFLAG:193 = -2
				RETURN IRAI_CONST_RESULT("実行して失敗")
			ENDIF
		;6分の1の確率で逆に正解
		ELSEIF !RAND:6 && !逆張りナシ
			CALL COLORMESSAGE(@"＜COOL!!＞", C_AQUA, 0)
			PRINTFORML 【%RESULTS:LOCAL%】
			PRINTFORMW %CALLNAME:TARGET%因%CALLNAME:MASTER%意外的回答而惊讶！
			TFLAG:193 = 2
			SOURCE:歓楽 = 2000 + ABL:親密 * 100
			EXP:MASTER:会話経験 += 10
			TFLAG:好感度BOUNS = ABL:親密 * 10
			TFLAG:信賴度管理 = 5
			RETURN IRAI_CONST_RESULT("実行して成功")
		;ハズレの選択肢を選ぶ
		ELSE
			CALL COLORMESSAGE(@"＜BAD…＞", C_RED, 0)
			PRINTFORML 【%RESULTS:LOCAL%】
			PRINTFORMW %CALLNAME:TARGET%对%CALLNAME:MASTER%的回答不満意…
			SOURCE:歓楽 = 50 + ABL:親密 * 5
			SOURCE:反感 = 100
			TFLAG:193 = -1
			RETURN IRAI_CONST_RESULT("実行して失敗")
		ENDIF
	ENDIF
CASE "依頼成功時"
;成功報告時の報酬など
	IF TFLAG:192 == 2
		CALL CHANGE_CFLAG(4, TARGET, 10 + ABL:親密 * 2)
		TALENT:心情 = MIN(TALENT:心情 + 1, 1)
		LOCAL:1 = 3
	ELSE
		CALL CHANGE_CFLAG(4, TARGET, 5 + ABL:親密)
		TALENT:心情 = MIN(TALENT:心情 + 1, 0)
		LOCAL:1 = 1
	ENDIF
	;野菜召喚の儀
	LOCALS = 
	WHILE LOCALS == ""
		LOCAL = RAND:40 + GETNUM(ITEM, "捲心菜")
		LOCALS = %ITEMNAME:LOCAL%
	WEND
	CALL GET_ITEM(LOCALS, LOCAL:1, 2)
CASE "依頼失敗時"
;失敗報告時の処理
	IF TFLAG:192 == -2
		CALL CHANGE_CFLAG(4, TARGET, -5)
	ELSE
		CALL CHANGE_CFLAG(4, TARGET, -1)
	ENDIF
ENDSELECT


;---------------------------------------
;お悩み相談のデータ
;ARG           お悩みナンバー
;RETURN        発生判定
;RESULT:1      正解の選択肢
;RESULT:2      逆張り正解／ハズレ判定の消去
;RESULTS:0     お悩み文章
;RESULTS:1～3  選択肢１～３
;---------------------------------------
@IRAI_一般16_お悩みデータベース(ARG, CHARA)
#DIM CHARA
VARSET RESULT
VARSET RESULTS
VARSET LOCAL
SELECTCASE ARG
CASE 1, 2
	;弾幕ごっこの悩み
	;もともとの戦闘能力がないなら除外
	SIF CSVABL(CHARA, GETNUM(ABL, "戦闘能力")) <= 0
		RETURN
	RESULTS:0 = 新的符卡该怎么设计才好%性格傾向語尾(CHARA, "呐？")%
	IF ARG % 2 == 0
		RESULTS:1 = 分散弾幕
		RESULTS:2 = 封位弾幕
		RESULTS:3 = 誘導弾幕
		SELECTCASE CFLAG:CHARA:性格傾向
		;気弱系
		CASE 1
			RESULT:1 = 3
		;強気系
		CASE 2
			RESULT:1 = 2
		;坦率系
		CASE 3
			RESULT:1 = 1
		;真面目系
		CASE 4
			RESULT:1 = 3
		;其他
		CASEELSE
			RESULT:1 = 1
		ENDSELECT
	ELSE
		RESULTS:1 = 重視外观的美丽弾幕
		RESULTS:2 = 殺意外露的鬼畜弾幕
		RESULTS:3 = 令人幻惑的不思議弾幕
		SELECTCASE CFLAG:CHARA:性格傾向
		;気弱系
		CASE 1
			RESULT:1 = 1
		;強気系
		CASE 2
			RESULT:1 = 2
		;坦率系
		CASE 3
			RESULT:1 = 2
		;真面目系
		CASE 4
			RESULT:1 = 1
		;其他
		CASEELSE
			RESULT:1 = 3
		ENDSELECT
	ENDIF
CASE 3, 4
	;晩酌の悩み
	;酒が飲めないなら除外
	SIF TALENT:CHARA:酒耐性 <= 0
		RETURN
	IF ARG % 2 == 0
		RESULTS:0 = 今天的晩酌喝什么好呢？你%性格傾向語尾(CHARA, "认为呢？")%
		RESULTS:1 = 啤酒
		RESULTS:2 = 葡萄酒
		RESULTS:3 = 日本酒
		IF STRCOUNT(GET_STR(, "角色データ", CHARA, "料理：喜欢的味道"), "和食")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "料理：喜欢的味道"), "洋食")
			RESULT:1 = 2
		ELSE
			SELECTCASE CFLAG:CHARA:性格傾向
			;気弱系
			CASE 1
				RESULT:1 = 2
			;強気系
			CASE 2
				RESULT:1 = 3
			;坦率系
			CASE 3
				RESULT:1 = 1
			;真面目系
			CASE 4
				RESULT:1 = 2
			;其他
			CASEELSE
				RESULT:1 = 1
			ENDSELECT
		ENDIF
	ELSE
		RESULTS:0 = 今天的晩酌%性格傾向語尾(CHARA, "怎么办？")%
		RESULTS:1 = 在足以洗浴的啤酒之中深酔
		RESULTS:2 = 在微甜的香甜酒中沉酔
		RESULTS:3 = 今日禁酒！
		IF TALENT:CHARA:酒耐性 > 2
			RESULT:1 = 1
		ELSEIF TALENT:CHARA:酒耐性 < 1
			RESULT:1 = 2
		ELSE
			SELECTCASE CFLAG:CHARA:性格傾向
			;気弱系
			CASE 1
				RESULT:1 = 3
			;強気系
			CASE 2
				RESULT:1 = 1
			;坦率系
			CASE 3
				RESULT:1 = 1
			;真面目系
			CASE 4
				RESULT:1 = 3
			;其他
			CASEELSE
				RESULT:1 = 2
			ENDSELECT
		ENDIF
	ENDIF
CASE 5, 6
	;本の悩み
	;本を読まないようなら除外
	SIF ABL:CHARA:教養 <= 0
		RETURN
	RESULTS:0 = 読哪一本新书%性格傾向語尾(CHARA, "呐？")%
	IF ARG % 2 == 0
		RESULTS:1 = 恋愛小説
		RESULTS:2 = 推理小説
		RESULTS:3 = 科幻小説
		IF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(壮大|不思議|狂熱|未来)")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(威厳|知的|繊細|奇妙|懷舊)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(可愛的|活潑|洛麗塔)")
			RESULT:1 = 1
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ELSE
		RESULTS:1 = 流行的轻小说
		RESULTS:2 = 厳肃的純文学
		RESULTS:3 = 狂人奇書
		IF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(狂熱|知的|奇妙)")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(威厳|懷舊|別緻)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(可愛的|活潑|洛麗塔)")
			RESULT:1 = 1
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ENDIF
CASE 7, 8
	;香料の悩み
	;条件無
	RESULTS:0 = 房间里放置哪种香料比较好%性格傾向語尾(CHARA, "呐？")%
	IF ARG % 2 == 0
		RESULTS:1 = 清新系
		RESULTS:2 = 醇厚系
		RESULTS:3 = 浓郁系
		IF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(妖術|不思議|威厳)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(芸術的|時尚|繊細)")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(自然|樸素)")
			RESULT:1 = 1
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ELSE
		RESULTS:1 = 简单的消臭剤
		RESULTS:2 = 芳香精油
		RESULTS:3 = %CALLNAME:CHARA%的气味就很好
		IF TALENT:CHARA:汚臭耐性 < 0
			RESULT:1 = 1
		ELSEIF GROUPMATCH(GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")), "妖獣", "妖鳥", "妖蟲", "鬼")
			RESULT:1 = 3
		ELSEIF GROUPMATCH(GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")), "吸血鬼", "悪魔")
			RESULT:1 = 2
		ELSEIF GROUPMATCH(GET_TRIBENAME(CHARA, GETNUM(TALENT, "人类")), "天人", "月人", "魔界人")
			RESULT:1 = 2
		ELSEIF TALENT:CHARA:妖精
			RESULT:1 = 2
		ELSEIF TALENT:CHARA:調合知識
			RESULT:1 = 2
		ELSE
			RESULT:1 = 1
		ENDIF
	ENDIF
CASE 9, 10
	;音楽の悩み
	;条件無
	IF ARG % 2 == 0
		RESULTS:0 = 想転換心情该听哪种音楽%性格傾向語尾(CHARA, "呐？")%
		RESULTS:1 = 时尚的爵士乐
		RESULTS:2 = 超酷的摇滚乐
		RESULTS:3 = 苦涩的经典乐
		IF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(帥氣的|活潑|時尚|不思議)")
			RESULT:1 = 1
		ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(可怕的|時尚|充滿力量|誇張)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(美的|芸術的|知的|威厳|壮大)")
			RESULT:1 = 3
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ELSE
		RESULTS:0 = 哪种東方曲最好%性格傾向語尾(CHARA, "呐？")%
		RESULTS:1 = 果然是Boss曲！
		RESULTS:2 = 通过道中曲！
		RESULTS:3 = 旧作曲也很棒！
		;めんどいけどちまちま
		IF GROUPMATCH(CHARA, [[魅魔]], [[里香]], [[明羅]], [[艾倫]], [[喀娜]], [[理香子]], [[千百合]], [[夢美]], [[留琴]])
			RESULT:1 = 3
		ELSEIF GROUPMATCH(CHARA, [[胡桃]], [[艾麗]], [[夢月]], [[幻月]], [[露易茲]], [[雪]], [[​舞​]], [[夢子]], [[神綺]])
			RESULT:1 = 3
		ELSEIF GROUPMATCH(CHARA, [[阿求]], [[幽香]], [[蓮子]], [[梅莉]], [[豊姫]], [[依姫]], [[鈴仙二號]], [[朱鷺子]], [[小鈴]], [[典]])
			RESULT:1 = 3
		ELSEIF GROUPMATCH(CHARA, [[大妖精]], [[小悪魔]], [[莉莉Ｗ]], [[莉莉Ｂ]], [[帝]], [[静葉]], [[椛]], [[琪斯美]], [[屠自古]])
			RESULT:1 = 2
		ELSEIF CSVABL(CHARA, GETNUM(ABL, "戦闘能力")) >= 5
			RESULT:1 = 1
		ELSE
			;3番にはならない
			RESULT:1 = RAND:2 + 1
		ENDIF
	ENDIF
CASE 11, 12
	;服の悩み
	;条件無
	RESULTS:0 = 该换什么样的衣服%性格傾向語尾(CHARA, "呐？")%
	IF ARG % 2 == 0
		RESULTS:1 = 可爱的连衣裙
		RESULTS:2 = 酷酷的敞领衬衫
		RESULTS:3 = 性感的吊带背心
		IF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "可愛的")
			RESULT:1 = 1
		ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "帥氣的")
			RESULT:1 = 2
		ELSE
			;可怕的/美的派
			RESULT:1 = 3
		ENDIF
	ELSE
		RESULTS:1 = 斬新的时尚搭配
		RESULTS:2 = 沉稳的休闲搭配
		RESULTS:3 = 喜欢现在的服装
		IF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(芸術的|活潑|時尚|誇張)")
			RESULT:1 = 1
		ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(樸素|別緻|繊細|知的)")
			RESULT:1 = 2
		ELSE
			RESULT:1 = 3
		ENDIF
	ENDIF
CASE 13
	;髪型の悩み
	;条件無
	RESULTS:0 = 想要剪髪、要什么髪型%性格傾向語尾(CHARA, "呐？")%
	RESULTS:1 = 喜欢现在的髪型
	;めんどいけどちまちま
	IF GROUPMATCH(CHARA, [[小町]], [[荷取]], [[桑尼]], [[琪斯美]], [[極]], [[小鈴]], [[千百合]])
		RESULTS:2 = 单马尾
	ELSE
		RESULTS:2 = 双马尾
	ENDIF
	;参考『東方ロング化タグ』,ありがとうございます
	IF GROUPMATCH(CHARA, [[魅魔]], [[明羅]], [[艾倫]], [[理香子]], [[胡桃]], [[艾麗]], [[神綺]])
		RESULTS:3 = 短髪
	ELSEIF GROUPMATCH(CHARA, [[美鈴]], [[小悪魔]], [[帕秋莉]], [[莉莉Ｗ]], [[莉莉Ｂ]], [[紫]], [[萃香]])
		RESULTS:3 = 短髪
	ELSEIF GROUPMATCH(CHARA, [[慧音]], [[鈴仙]], [[永琳]], [[輝夜]], [[妹紅]], [[早苗]], [[天子]], [[勇儀]], [[阿空]])
		RESULTS:3 = 短髪
	ELSEIF GROUPMATCH(CHARA, [[白蓮]], [[布都]], [[心]], [[影狼]], [[弁弁]], [[克勞恩皮絲]], [[純狐]])
		RESULTS:3 = 短髪
	ELSEIF GROUPMATCH(CHARA, [[紫苑]], [[合歡乃]], [[阿吽]], [[成美]], [[隠岐奈]], [[斯塔]], [[豊姫]], [[依姫]])
		RESULTS:3 = 短髪
	ELSE
		RESULTS:3 = 长髪
	ENDIF
	IF TALENT:CHARA:性的兴趣 < 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:胆量 < 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:自尊心 > 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:羞恥心 > 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:開朗／阴郁 < 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:性的兴趣 > 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:開朗／阴郁 > 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:羞恥心 < 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:応答 < 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:態度 < 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:応答 > 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:自己愛 > 0
		RESULT:1 = 3
	ELSE
		;1番にはならない
		RESULT:1 = RAND:2 + 2
	ENDIF
CASE 14
	;参拝の悩み
	;所属している人らは除外
	IF GROUPMATCH(CHARA, [[霊夢]], [[留琴]], [[喀娜]], [[魅魔]], [[萃香]], [[心]], [[針妙丸]], [[阿吽]])
		RETURN
	ELSEIF GROUPMATCH(CHARA, [[納茲琳]], [[小傘]], [[鵺]], [[白蓮]], [[猯藏]], [[一輪]], [[村紗]], [[星]], [[響子]], [[女苑]])
		RETURN
	ELSEIF GROUPMATCH(CHARA, [[早苗]], [[神奈子]], [[諏訪子]])
		RETURN
	;月の人たちと若鷺姫
	ELSEIF GROUPMATCH(CHARA, [[依姫]], [[豊姫]], [[鈴仙二號]], [[探女]], [[若鷺姫]])
		RETURN
	ENDIF
	;ほぼ運ゲーです
	RESULTS:0 = 去哪里祈願最好%性格傾向語尾(CHARA, "呐？")%
	RESULTS:1 = 博麗神社
	RESULTS:2 = 守矢神社
	RESULTS:3 = 命蓮寺
	;命蓮寺は不要！（3番にはならない）
	IF GROUPMATCH(CHARA, [[芳香]], [[青娥]], [[屠自古]], [[布都]], [[神子]])
		RESULT:1 = RAND:2 + 1
	ELSEIF GROUPMATCH(CHARA, [[阿燐]], [[阿空]], [[帕露西]], [[琪斯美]], [[山女]], [[村紗]], [[星]], [[響子]], [[女苑]])
		RESULT:1 = RAND:2 + 1
	ELSEIF GROUPMATCH(CHARA, [[早苗]], [[神奈子]], [[諏訪子]])
		RESULT:1 = RAND:2 + 1
	ELSEIF TALENT:CHARA:妖精
		RESULT:1 = RAND:2 + 1
	ELSE
		RESULT:1 = RAND:3 + 1
	ENDIF
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 15
	;ダイエットの悩み
	;条件無
	RESULTS:0 = 很在意肚子上的肉、该怎么办%性格傾向語尾(CHARA, "呐？")%
	RESULTS:1 = 每天節食！
	RESULTS:2 = 健康的運動！
	RESULTS:3 = 薬力！
	IF TALENT:CHARA:大胃王
		RESULT:1 = 1
	ELSEIF CSVABL(CHARA, GETNUM(ABL, "戦闘能力")) <= 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:調合知識
		RESULT:1 = 3
	ELSEIF ABL:CHARA:料理技能 >= 3
		RESULT:1 = 1
	ELSEIF ABL:CHARA:戦闘能力 <= 3
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:調合Lv >= 3
		RESULT:1 = 3
	ELSE
		SELECTCASE CFLAG:CHARA:性格傾向
		;気弱系
		CASE 1
			RESULT:1 = 1
		;強気系
		CASE 2
			RESULT:1 = 2
		;坦率系
		CASE 3
			RESULT:1 = 1
		;真面目系
		CASE 4
			RESULT:1 = 2
		;其他
		CASEELSE
			RESULT:1 = 2
		ENDSELECT
	ENDIF
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 16
	;菜園の悩み
	;冬は除外
	SIF DAY:2 == 4
		RETURN
	RESULTS:0 = 家庭菜園里种什么%性格傾向語尾(CHARA, "呐？")%
	;答え＝DAY:2だと簡単すぎるのでシャッフルする
	RESULT:1 = RAND:3 + 1
	FOR LOCAL, 1, 4
		;LOCAL:2に季節を入れる
		;答えはDAY:2
		IF LOCAL == RESULT:1
			LOCAL:2 = DAY:2
		;１つ目の間違い
		ELSEIF !LOCAL:3
			LOCAL:2 = DAY:2 + 1
			LOCAL:3 ++
		;２つ目の間違い
		ELSE
			;秋なら1つ前、それ以外なら2つ先の季節
			IF DAY:2 == 3
				LOCAL:2 = DAY:2 - 1
			ELSE
				LOCAL:2 = DAY:2 + 2
			ENDIF
		ENDIF
		;捲心菜を基準に季節の作物を抽選
		LOCAL:1 = GETNUM(ITEM, "捲心菜") + (10 * LOCAL:2) - 10 + RAND:3
		RESULTS:LOCAL = %ITEMNAME:(LOCAL:1)%
	NEXT
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 17
	;菜園の悩み～花編～
	;条件無
	RESULTS:0 = 花盆里种植什么%性格傾向語尾(CHARA, "呐？")%
	;答え＝DAY:2だと簡単すぎるのでシャッフルする
	RESULT:1 = RAND:3 + 1
	FOR LOCAL, 1, 4
		;答えには正解の花
		IF LOCAL == RESULT:1
			;関数化してもいいけどするほどでもないかなと思い随意
			SELECTCASE DAY:2
			CASE 1
				RESULTS:LOCAL '= TEXTR("郁金香/秋牡丹/紫罗兰")
			CASE 2
				RESULTS:LOCAL '= TEXTR("喇叭花/桔梗/百合")
			CASE 3
				RESULTS:LOCAL '= TEXTR("波斯菊/菊花/赏月草")
			CASE 4
				RESULTS:LOCAL '= TEXTR("雛菊/水仙/雪花莲")
			ENDSELECT
		ELSE
			;LOCAL:2に季節を入れる
			;１つ目の間違い
			IF !LOCAL:3
				LOCAL:2 = DAY:2 + 1
				LOCAL:3 ++
			;２つ目の間違い
			ELSE
				;秋なら1つ前、それ以外なら2つ先の季節
				IF DAY:2 == 3
					LOCAL:2 = DAY:2 - 1
				ELSE
					LOCAL:2 = DAY:2 + 2
				ENDIF
			ENDIF
			SELECTCASE LOCAL:2
			CASE 1
				RESULTS:LOCAL '= TEXTR("郁金香/秋牡丹/紫罗兰")
			CASE 2
				RESULTS:LOCAL '= TEXTR("喇叭花/桔梗/百合")
			CASE 3
				RESULTS:LOCAL '= TEXTR("波斯菊/菊花/赏月草")
			CASE 4
				RESULTS:LOCAL '= TEXTR("雛菊/水仙/雪花莲")
			ENDSELECT
		ENDIF
	NEXT
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 18
	;休日の悩み
	;条件無,仕事ない角色もいるけど
	RESULTS:0 = 下个假期做什么%性格傾向語尾(CHARA, "呐？")%
	RESULTS:1 = 外出散步
	RESULTS:2 = 町上购物
	RESULTS:3 = 在家无所事事
	SELECTCASE CFLAG:CHARA:性格傾向
	;気弱系
	CASE 1
		RESULT:1 = 1
	;強気系
	CASE 2
		RESULT:1 = 1
	;坦率系
	CASE 3
		RESULT:1 = 3
	;真面目系
	CASE 4
		RESULT:1 = 2
	;其他
	CASEELSE
		RESULT:1 = 2
	ENDSELECT
CASE 19
	;人気の悩み
	;条件無
	RESULTS:0 = 最近有人気下降了的%性格傾向語尾(CHARA, "感觉")%……
	RESULTS:1 = 没有那回事！
	RESULTS:2 = 磨炼自己吧！
	RESULTS:3 = 只有脱一个选择了
	SELECTCASE CFLAG:CHARA:性格傾向
	;気弱系
	CASE 1
		RESULT:1 = 2
	;強気系
	CASE 2
		RESULT:1 = 2
	;坦率系
	CASE 3
		RESULT:1 = 1
	;真面目系
	CASE 4
		RESULT:1 = 2
	;其他
	CASEELSE
		;3番にはならない）
		RESULT:1 = RAND:2 + 1
	ENDSELECT
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 20
	;集中力の悩み
	;条件無
	RESULTS:0 = 最近打不起精神%性格傾向語尾(CHARA, "了啊")%……
	RESULTS:1 = 还是好好睡一觉比较好
	RESULTS:2 = 散散心吧
	RESULTS:3 = 闘魂注入掌！！
	SELECTCASE CFLAG:CHARA:性格傾向
	;気弱系
	CASE 1
		RESULT:1 = 3
	;強気系
	CASE 2
		RESULT:1 = 3
	;坦率系
	CASE 3
		RESULT:1 = 2
	;真面目系
	CASE 4
		RESULT:1 = 1
	;其他
	CASEELSE
		RESULT:1 = 2
	ENDSELECT
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 21
	;御三家の悩み
	RESULTS:0 = 最初的三匹之中%性格傾向語尾(CHARA, "该选哪一个？")%
	RESULTS:1 = 草系
	RESULTS:2 = 火系
	RESULTS:3 = 水系
	IF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(土|植物|緑|虫)")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(火|日|赤|朱)")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(水|氷|青|魚)")
		RESULT:1 = 3
	ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "自然")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "充滿力量")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "知的")
		RESULT:1 = 3
	ELSE
		RESULT:1 = RAND:3 + 1
	ENDIF
CASE 22
	;進化の悩み
	RESULTS:0 = %性格傾向語尾(CHARA, "让它")%進化成哪个属性呢？
	RESULTS:1 = 水系
	RESULTS:2 = 电系
	RESULTS:3 = 火系
	IF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(水|氷|青|魚)")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(雷|星|黄)")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(火|日|赤|朱)")
		RESULT:1 = 3
	ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(美的|知的)")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(帥氣的|誇張)")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "角色データ", CHARA, "感性：好き"), "(可愛的|充滿力量)")
		RESULT:1 = 3
	ELSE
		RESULT:1 = RAND:3 + 1
	ENDIF
CASEELSE
	;禮物の悩み,一番クイズっぽいので確率を稍微上げてます
	;相性のいい角色を尋找
	CALL FISHER_YATES_SHAFFLE(CHARANUM)
	FOR LOCAL:1, 1, CHARANUM
		LOCAL = SHAFFLE_ARRAY:(LOCAL:1)
		IF RELATION:CHARA:LOCAL > 100
			LOCAL:2 = LOCAL
			BREAK
		ENDIF
	NEXT
	;いないなら除外
	SIF !LOCAL:2
		RETURN
	;友人角色を隔離
	LOCAL:10 = LOCAL:2
	[IF_DEBUG]
		PRINTFORML %CALLNAME:(LOCAL:10)%
	[ENDIF]
	;答えを先に入れるパターン
	LOCAL = RAND:3 + 1
	;答えの選択肢に高得点を入れる,あえて701点ではなく700点から
	CALL CREATE_GIFT_FROM_CHARA(LOCAL:10, 700, 1000)
	LOCALS:LOCAL = %RESULTS%
	;ハズレの選択肢にダミーを入れる
	FOR LOCAL:1, 1, 4
		SIF LOCAL:1 == LOCAL
			CONTINUE
		CALL CREATE_GIFT_FROM_CHARA(LOCAL:10, 0, 699)
		LOCALS:(LOCAL:1) = %RESULTS%
	NEXT
	;関数を呼んだのでこの時点ではRESULT/RESULTSがぐちゃってます
	RESULTS:0 = 送给%CALLNAME:(LOCAL:10)%的礼物哪个更好%性格傾向語尾(CHARA, "呐？")%
	RESULTS:1 = %LOCALS:1%
	RESULTS:2 = %LOCALS:2%
	RESULTS:3 = %LOCALS:3%
	RESULT:1 = LOCAL
	RESULT:2 = 0
[SKIPSTART]
CASE 
	RESULTS:0 = 
	RESULTS:1 = 
	RESULTS:2 = 
	RESULTS:3 = 
	RESULT:1 = 
[SKIPEND]
ENDSELECT
RETURN 1

;性格傾向によって汎用台詞の語尾を変える
@性格傾向語尾(ARG, ARGS)
#FUNCTIONS
;気弱系＆坦率系＆其他系はそのまま返す
SIF !GROUPMATCH(CFLAG:ARG:性格傾向, 2, 4)
	RETURNF ARGS
SELECTCASE ARGS
CASE "呐？"
	SELECTCASE CFLAG:ARG:性格傾向
	;強気系
	CASE 2
		RETURNF "啊？"
	;真面目系
	CASE 4
		RETURNF "呢？"
	ENDSELECT
CASE "认为呢？"
	SELECTCASE CFLAG:ARG:性格傾向
	;強気系
	CASE 2
		RETURNF "觉得呢？"
	;真面目系
	CASE 4
		RETURNF "怎么看？"
	ENDSELECT
CASE "怎么办？"
	SELECTCASE CFLAG:ARG:性格傾向
	;強気系
	CASE 2
		RETURNF "该怎么做啊？"
	;真面目系
	CASE 4
		RETURNF "做些什么？"
	ENDSELECT
CASE "该选哪一个？"
	SELECTCASE CFLAG:ARG:性格傾向
	;強気系
	CASE 2
		RETURNF "究竟选哪一个啊？"
	;真面目系
	CASE 4
		RETURNF "选哪一个？"
	ENDSELECT
CASE "让它"
	SELECTCASE CFLAG:ARG:性格傾向
	;強気系
	CASE 2
		RETURNF "应该让它"
	;真面目系
	CASE 4
		RETURNF "将它"
	ENDSELECT
CASE "感觉"
	SELECTCASE CFLAG:ARG:性格傾向
	;強気系
	CASE 2
		RETURNF "感觉哇"
	;真面目系
	CASE 4
		RETURNF "感觉的"
	ENDSELECT
CASE "了啊"
	SELECTCASE CFLAG:ARG:性格傾向
	;強気系
	CASE 2
		RETURNF "了哇"
	;真面目系
	CASE 4
		RETURNF "了呢"
	ENDSELECT
ENDSELECT
