;-------------------------------------------------
;潤滑液
;特殊指令
;-------------------------------------------------
@COM180
#DIM 巧克力
巧克力 = 0
ITEM:40 -= 1
;自動補充
IF !ITEM:40 && MONEY >= 200 && FLAG:1001
	MONEY -= 200
	ITEM:40 ++
ENDIF
IF ITEM:121 || ITEM:122 || ITEM:123
	PRINTL 要玩巧克力PLAY吗？
	PRINTL [0]是 [1]否
	INPUT
	IF !RESULT
		PRINTL 要用什么巧克力？
		PRINTFORM [0]果然还是算了　
		SIF ITEM:121
			PRINTFORM [1]%ITEMNAME:(121)%　
		SIF ITEM:122
			PRINTFORM [2]%ITEMNAME:(122)%　
		SIF ITEM:123
			PRINTFORM [3]%ITEMNAME:(123)%　
		INPUT
		IF RESULT == 1 && ITEM:121
			巧克力 = 1
			ITEM:121 -= 1
		ELSEIF RESULT == 2 && ITEM:122
			巧克力 = 2
			ITEM:122 -= 1
		ELSEIF RESULT == 3 && ITEM:123
			巧克力 = 3
			ITEM:123 -= 1
		ELSE
		ENDIF
	ELSE
	ENDIF
ENDIF
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 0
DOWNBASE:気力 += 0

SOURCE:液体 = 5000
SIF TALENT:TARGET:体型 == -5
	SOURCE:液体 += 20000
IF 巧克力
	STAIN:1 += 128
	STAIN:5 += 128
	STAIN:PLAYER:1 +=128
ENDIF

IF FLAG:70 == 1

ELSE

	SOURCE:露出 = 100

	SELECTCASE 巧克力
	CASE 1
		SOURCE:不潔 = 100
		SOURCE:逸脱 = 100
	CASE 2
		SOURCE:不潔 = 80
		SOURCE:逸脱 = 80
	CASE 3
		SOURCE:情愛 = 30
		SOURCE:不潔 = 60
		SOURCE:逸脱 = 60
		EXP:愛情経験 ++
	ENDSELECT

ENDIF
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE180
;潤滑液実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(180)
	RETURN RESULT
;道具を持っているか、道具無設定でないとダメ
SIF ITEM:40 == 0 && NOITEM == 0
	RETURN 0

;淋浴中はダメ
SIF TEQUIP:32
	RETURN 0
RETURN 1

