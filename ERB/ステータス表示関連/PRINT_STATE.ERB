;どこにもCALLされてなく惑わしいからコメントアウトしておきます
[SKIPSTART]
@PRINT_STATE(選択中角色ID, 選択中页番号, 調教中フラグ)
#DIM 選択中角色ID
#DIM 調教中フラグ
#DIM REF 選択中页番号
#DIM CONST 前一位角色を表示 = 4
#DIM CONST 下一位角色を表示 = 6
#DIM CONST 返回よ = 5

$PRINT_LOOP
	DRAWLINEFORM ━
	PRINT ■
	PRINTS NAME:選択中角色ID
	PRINT (好感度:
	CALL PRINT_RANK(CFLAG:選択中角色ID:2, "MAX_50000")
	PRINTFORM  {CFLAG:選択中角色ID:2} 信賴度:
	CALL PRINT_RANK(CFLAG:選択中角色ID:信賴度, "MAX_20000")
	PRINTFORM  {CFLAG:選択中角色ID:信賴度}) 
	;MASTERならば所持金を記載
	SIF 選択中角色ID == MASTER
		PRINTFORM (%"所持金:\\" + TOSTR(MONEY,"#,###")%)
	IF NOTICE_CYCLE(選択中角色ID)
		IF 調教中フラグ
			CALL CYCLE_PRINT(選択中角色ID)
		ELSE
			CALL CYCLE_PRINT翌日(選択中角色ID)
		ENDIF
		SETCOLOR C_AQUA
	ENDIF
	IF CFLAG:選択中角色ID:允许无套 == 2
		SETCOLOR C_PINK
		PRINT 危険日无套Ｈ❤也OK哦
		RESETCOLOR
	ELSEIF CFLAG:選択中角色ID:允许无套 == 1
		SETCOLOR C_AQUA
		PRINT 无套Ｈ❤也不是不行呢
		RESETCOLOR
	ELSEIF GETBIT(CFLAG:選択中角色ID:既成事實,1)
		SETCOLOR C_YELLOW
		PRINT Ｈ也是可以的❤
		RESETCOLOR
	ENDIF
	RESETCOLOR
	PRINTL 　
;*******************************************
;1页目
;*******************************************
IF 選択中页番号 == 0
	PRINT 体力 
				CALL PRINT_COLORBAR, BASE:選択中角色ID:0, MAXBASE:選択中角色ID:0, 28, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("赤"), RESULT:1
				PRINTFORML  ({BASE:選択中角色ID:体力,5}/{MAXBASE:選択中角色ID:体力,5}) 
	PRINT 気力 
				CALL PRINT_COLORBAR, BASE:選択中角色ID:1, MAXBASE:選択中角色ID:1, 28, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("緑"), RESULT:1
				PRINTFORML  ({BASE:選択中角色ID:気力,5}/{MAXBASE:選択中角色ID:気力,5}) 
	IF BASE:選択中角色ID:15
	PRINT 酒気 
				CALL PRINT_COLORBAR, BASE:選択中角色ID:15, MAXBASE:選択中角色ID:15, 28, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("青"), RESULT:1
				PRINTFORML  ({BASE:選択中角色ID:酒気,5}/{MAXBASE:選択中角色ID:酒気,5}) 
	ENDIF
	SELECTCASE CFLAG:選択中角色ID:积攒度
		CASE 201 TO 499
			SETCOLOR C_AQUA
		CASE 500 TO 799
			SETCOLOR C_YELLOW
		CASE 800 TO 999
			SETCOLOR C_PINK
		CASE IS >= 1000
			SETCOLOR C_RED
	ENDSELECT
	SIF NOTICE_LUST(選択中角色ID)
		IF CFLAG:選択中角色ID:积攒度 >=1000
		PRINTFORML 欲求：{CFLAG:選択中角色ID:积攒度 / 10}％ ❤饥渴❤难耐❤
		ELSEIF CFLAG:選択中角色ID:积攒度 >=800
		PRINTFORML 欲求：{CFLAG:選択中角色ID:积攒度 / 10}％ ❤焦躁难忍❤
		ELSEIF CFLAG:選択中角色ID:积攒度 >=500
		PRINTFORML 欲求：{CFLAG:選択中角色ID:积攒度 / 10}％ 欲求不满❤
		ELSEIF CFLAG:選択中角色ID:积攒度 >=200
		PRINTFORML 欲求：{CFLAG:選択中角色ID:积攒度 / 10}％ 想发泄
		ELSE
		PRINTFORML 欲求：{CFLAG:選択中角色ID:积攒度 / 10}％
		ENDIF
	RESETCOLOR

	CALL SHOW_SIZE(選択中角色ID)
	PRINTL [99] 画像切换
	IF FLAG:画像表示
		CALL PRINT_STATE_IMAGE(選択中角色ID)
	ENDIF

	CALL PRINT_STATE_CLOTH(選択中角色ID)
	;道具というか道具だけど他が2文字だしねぇ
	IF 選択中角色ID == MASTER
		PRINT □ 
		CALL PRINTBUTTON_EX("道具",167,TFLAG:167)
		PRINTL  □---------------------------------------------------------------------------
		SIF !TFLAG:道具表示
			CALL LIST_ITEM("全て")
	ENDIF
	CALL PRINT_STATE_ABL(選択中角色ID)
	PRINTL
	CALL PRINT_STATE_TALENT(選択中角色ID)
	PRINTL
	IF 選択中角色ID != MASTER
		PRINTL
		PRINT □ 
		CALL PRINTBUTTON_EX("刻印",166,TFLAG:166)
		PRINTL  □---------------------------------------------------------------------------
		;PRINT_MARK 選択中角色ID
		CALL PRINT_刻印(選択中角色ID)
	ENDIF
;*******************************************
;2页目
;*******************************************
ELSEIF 選択中页番号 == 1
	PRINTL
	CALL PRINT_STATE_EXP(選択中角色ID)
	PRINTL
	SIF TALENT:選択中角色ID:妊娠 || CFLAG:選択中角色ID:兒童人数 > 0
		CALL 妊娠関連(選択中角色ID)
	SIF 選択中角色ID != MASTER && !調教中フラグ
		CALL PRINT_STATE_JUEL(選択中角色ID)
	SIF 調教中フラグ
		CALL PRINT_STATE_EX(選択中角色ID)
	PRINTL
	SIF 選択中角色ID
		CALL 胖次収集状況(選択中角色ID)
;*******************************************
;3页目
;*******************************************
ELSEIF 選択中页番号 == 2
	CALL PRINT_STATE_PERSONAL(選択中角色ID)
	CALL 肉体情報(選択中角色ID)
	CALL SHOW_RELATION(選択中角色ID)
;*******************************************
;4页目
;*******************************************
ELSEIF 選択中页番号 == 3
	CALL PRINT_STATE_PREFERENCE(選択中角色ID)
;*******************************************
;5页目
;*******************************************
ELSEIF 選択中页番号 == 4
	IF !選択中角色ID
		CALL 予定
	ELSE
		CALL 陥落条件表示(選択中角色ID)
		CALL 浮気把握状況(選択中角色ID)
	ENDIF
ENDIF
IF 調教中フラグ
	DRAWLINE
	IF 選択中页番号 > 0
		PRINTC [0] 察看前一頁
	ELSE
		PRINTC 　
	ENDIF
	PRINTC 　
	IF 選択中页番号 < 詳細能力表示最大页数的番号
		PRINTC [1] 察看下一頁
	ELSE
		PRINTC 　
	ENDIF
	PRINTL
		PRINTC  
		PRINTFORMC [{返回よ}] 返回
	INPUT
	SELECTCASE RESULT
		CASE 0
		SIF 選択中页番号 > 0
			選択中页番号 --
			GOTO PRINT_LOOP
		CASE 1
			SIF 選択中页番号 < 詳細能力表示最大页数的番号
				選択中页番号 ++
				GOTO PRINT_LOOP
		CASE 160 TO 167
			TFLAG:RESULT = !TFLAG:RESULT
			GOTO PRINT_LOOP
		;この辺消しちゃってもいいか
		CASE 前一位角色を表示
			IF !調教中フラグ
				選択中角色ID--
			ENDIF
			IF 選択中角色ID < 0
				選択中角色ID = CHARANUM - 1
			ENDIF
			GOTO PRINT_LOOP
		CASE 下一位角色を表示
			IF !調教中フラグ
				選択中角色ID++
			ENDIF
			IF 選択中角色ID >= CHARANUM
				選択中角色ID = 0
			ENDIF
			GOTO PRINT_LOOP
		CASE 返回よ
			RETURN 0
		CASE 画像切换
			FLAG:画像表示 = !FLAG:画像表示
			GOTO PRINT_LOOP
		CASEELSE
			RETURN 0
	ENDSELECT
ENDIF
[SKIPEND]

@SHOW_ALLITEM
#DIM LSTART
DRAWLINE
PRINTL
PRINT □ 所持道具一覧
PRINTL  □------------------------------------------------------------------------------------------------------
LSTART = LINECOUNT
PRINTL
CALL LIST_ITEM("全て")
WHILE 20-(LINECOUNT-LSTART) > 0
PRINTL
WEND
DRAWLINE
PRINTL [999] 返回
INPUT

@SHOW_ALLITEM_INFO
#DIM LSTART
PRINTL
PRINT □ 陥落情報
PRINTL  □------------------------------------------------------------------------------------------------------
PRINTL
CALL COLORMESSAGE(@"不知道从哪里飘过来了一张未署名的纸条，似乎是写给你的：保持处女达成某种条件可以使其内心的欲望彻底觉醒",C_RED,1)
PRINTL
DRAWLINE
PRINT □ 所持道具一覧
PRINTL  □------------------------------------------------------------------------------------------------------
LSTART = LINECOUNT
PRINTL
CALL LIST_ITEM("全て")
WHILE 20-(LINECOUNT-LSTART) > 0
PRINTL
WEND



@PRINT_STATE_CLOTH(選択中角色ID)
#DIM 選択中角色ID
#DIM 装備部位
PRINTL
PRINT □ 
CALL PRINTBUTTON_EX("服装", 161, TFLAG:161)
PRINTL  □---------------------------------------------------------------------------
IF TFLAG:服装表示
	PRINTFORML 
	RETURN
ENDIF
IF !EQUIP:選択中角色ID:0 && !TEQUIP:選択中角色ID:下半身着衣状況 && !TEQUIP:選択中角色ID:上半身着衣状況
	PRINTFORML 　＜全裸＞
	SIF INROOM(CFLAG:選択中角色ID:現在位置) == 1
		RETURN
ENDIF
SIF CFLAG:選択中角色ID:恶作剧 > 1
	PRINTFORML ＜衣不蔽體＞
CALL SHOW_CLOTHES(選択中角色ID, "現在的衣装")




@PRINT_STATE_ABL(選択中角色ID)
#DIM 選択中角色ID
#DIM 表示項目数
PRINT □ 
CALL PRINTBUTTON_EX("能力", 162, TFLAG:162)
PRINTL  □---------------------------------------------------------------------------
IF !TFLAG:能力表示
	表示項目数 = 0
	FOR LOCAL, 0, 50
		SIF !STRLENS(ABLNAME:LOCAL)
			CONTINUE
		SIF LOCAL > 50 && 選択中角色ID != MASTER
			CONTINUE
		;同性愛関係
		SIF GROUPMATCH(ABLNAME:LOCAL, "百合属性", "百合中毒") && !GETBIT(TALENT:選択中角色ID:2, 0)
			CONTINUE
		SIF GROUPMATCH(ABLNAME:LOCAL, "断袖属性", "断袖中毒") && GETBIT(TALENT:選択中角色ID:2, 0)
			CONTINUE
		PRINTFORM %ABLNAME:LOCAL,12%:
		IF LOCAL < 40
			CALL PRINT_RANK(ABL:選択中角色ID:LOCAL, "MAX_99")
		ELSE
			CALL PRINT_RANK(ABL:選択中角色ID:LOCAL, "MAX_6")
		ENDIF
		PRINTFORM  {ABL:選択中角色ID:LOCAL,5,LEFT} 
		表示項目数 ++
		SIF 表示項目数 % 4 == 0
			PRINTL 
	NEXT
ENDIF
PRINTL
PRINTL
PRINT □ 
CALL PRINTBUTTON_EX("性技",164,TFLAG:164)
PRINTL  □---------------------------------------------------------------------------
表示項目数 = 0
IF !TFLAG:164
	FOR LOCAL, 50, 60
		SIF !STRLENS(ABLNAME:LOCAL)
			CONTINUE
		SIF !(TALENT:選択中角色ID:2 & 1) && (LOCAL == 52 || LOCAL == 54)
			CONTINUE
		PRINTFORM %ABLNAME:LOCAL,12%:
		CALL PRINT_RANK(ABL:選択中角色ID:LOCAL, "MAX_6")
		PRINTFORM  {ABL:選択中角色ID:LOCAL,5,LEFT} 
		表示項目数 ++
		SIF 表示項目数 % 4 == 0
			PRINTL 
	NEXT
ENDIF



@PRINT_STATE_EXP(選択中角色ID)
#DIM 選択中角色ID
#DIM 表示項目数
#DIM 表示項目数1
PRINT □ 
CALL PRINTBUTTON_EX("経験",163,TFLAG:163)
PRINTL  □---------------------------------------------------------------------------
表示項目数 = 0
IF !TFLAG:経験表示
	FOR LOCAL, 0, 100
		;EXP.CSVに登録されていなければCONTINUE
		;SIF !STRLENS(EXPNAME:LOCAL)
		;	CONTINUE
		;未経験を表示させない
		SIF !EXP:選択中角色ID:LOCAL
			CONTINUE
		;同性愛関係
		IF !(TALENT:選択中角色ID:2 & 1) && LOCAL == 51
			CONTINUE
		ELSEIF TALENT:選択中角色ID:2 & 1 && LOCAL == 52
			CONTINUE
		ENDIF
		PRINTFORM %EXPNAME:LOCAL,14%:{EXP:選択中角色ID:LOCAL,5,LEFT} 
		表示項目数 += 1
		SIF 表示項目数 % 5 == 0
			PRINTL 
	NEXT
PRINTL
PRINTL □ 特殊 □---------------------------------------------------------------------------
	表示項目数1 = 0
	FOR LOCAL, 100, 125
		SIF !EXP:選択中角色ID:LOCAL
			CONTINUE
		PRINTFORM %EXPNAME:LOCAL,20%:{EXP:選択中角色ID:LOCAL,5,LEFT} 
		表示項目数1 += 1
		SIF 表示項目数1 % 3 == 0
			PRINTL 
	NEXT
ENDIF
PRINTL



@PRINT_STATE_TALENT(選択中角色ID)
#DIM 選択中角色ID
#DIM TLNT_CNT, 6
#DIM TLNT_NO, 6, 100
#DIMS 種族文本

VARSET TLNT_CNT
VARSET TLNT_NO

PRINT □ 
CALL PRINTBUTTON_EX("素質",165,TFLAG:165)
PRINTL  □---------------------------------------------------------------------------
IF !TFLAG:165
	;TALENT:200(汎用角色)は表示させない
	FOR LOCAL, 0, 300
		SIF !STRLENS(GET_TALENTNAME(LOCAL, TALENT:選択中角色ID:LOCAL))
			CONTINUE
		LOCALS = {LOCAL}/
		IF GROUPMATCH(LOCAL,90,91,164,165,166,189,190,191,192,193,194,195,196,197,198)
			;種族,132=吸血鬼は被るので身体特徴に移動
			TLNT_NO:0:(TLNT_CNT:0) = LOCAL
			TLNT_CNT:0 ++
		ELSEIF GROUPMATCH(LOCAL,0,1,3,4,5,6,7,8,9,60,70,72,73,74,75,76,77,78,82,83,122,142,152,183,184,185,250,254,255,256,257,258,259,260,261,262,263,264)
			;性的特徴
			TLNT_NO:1:(TLNT_CNT:1) = LOCAL
			TLNT_CNT:1 ++
		ELSEIF GROUPMATCH(LOCAL,2,16,17,40,41,54,56,57,92,100,101,102,103,104,105,106,121,130,132,133,134,137,138,143,146,149)
			;身体的特徴
			TLNT_NO:2:(TLNT_CNT:2) = LOCAL
			TLNT_CNT:2 ++
		ELSEIF GROUPMATCH(LOCAL,9,10,11,12,13,14,18,19,20,21,22,23,24,25,26,30,31,32,33,61,62,71,80,81,84,85,86,94,151,155)
			;精神的特徴
			TLNT_NO:3:(TLNT_CNT:3) = LOCAL
			TLNT_CNT:3 ++
		ELSEIF GROUPMATCH(LOCAL,46, 47, 48, 49, 50,51,52,53,55,93,135,136,144,161,162,199)
			;技術的特徴
			TLNT_NO:4:(TLNT_CNT:4) = LOCAL
			TLNT_CNT:4 ++
		ELSE
			;其他
			TLNT_NO:5:(TLNT_CNT:5) = LOCAL
			TLNT_CNT:5 ++
		ENDIF
	NEXT

	FOR LOCAL:2, 0, 6
		SELECTCASE LOCAL:2
			CASE 0
				PRINT 　 種族 　：
			CASE 1
				PRINT 　性的特徴：
			CASE 2
				PRINT 身体的特徴：
			CASE 3
				PRINT 精神的特徴：
			CASE 4
				PRINT 技術的特徴：
			CASE 5
				PRINT 　 其他 　：
		ENDSELECT
		LOCALS = ""
		FOR LOCAL:1, 0, TLNT_CNT:(LOCAL:2)
			LOCAL = TLNT_NO:(LOCAL:2):(LOCAL:1)
			;童貞
			SIF LOCAL == 1 && !(TALENT:選択中角色ID:性別 & 2)
				CONTINUE
			;男絶壁
			IF LOCAL == 105 && (TALENT:選択中角色ID:性別 == 2)
				SIF (TALENT:選択中角色ID:105 == -2) && (TALENT:選択中角色ID:性別 == 2)
					CONTINUE
				SIF (TALENT:選択中角色ID:105 == 0) && (TALENT:選択中角色ID:性別 == 2)
					PRINTFORM [普乳]
			ENDIF
			;掃除係は住んでる時だけ表示
			SIF LOCAL == GETNUM(TALENT, "掃除係") && !CFLAG:選択中角色ID:神社在住
				CONTINUE
			LOCALS += GET_TALENTNAME(LOCAL, TALENT:選択中角色ID:LOCAL)
			IF STRLENS(LOCALS) > 60
				LOCALS = " "
				PRINTL
				PRINT 　　　　　　
			ENDIF
			;素質によっては色変え
			IF GROUPMATCH(LOCAL,0,3,4,6,7,73,74,75,76,77,78,122,142) || (LOCAL == 199 && TALENT:選択中角色ID:LOCAL == 2)
				SETCOLOR 255, 120, 200
				FONTBOLD
			ELSEIF GROUPMATCH(LOCAL, 8, 199)
				SETCOLOR C_PINK
			ENDIF
			PRINTFORM [%GET_TALENTNAME(LOCAL,TALENT:選択中角色ID:LOCAL)%]
			RESETCOLOR
			FONTREGULAR
			IF LOCAL:2 == 0
				種族文本 += @"%GET_TALENTNAME(LOCAL,TALENT:選択中角色ID:LOCAL)%"
			ELSE
				種族文本 = 
			ENDIF
		NEXT
		SIF LOCAL:2 == 0
			CALL PRINT_種族抽出(選択中角色ID, 種族文本)
		SIF LOCALS != " "
			PRINTL 
	NEXT
	;口上自定义
	CALL showKojoCustomTalent(選択中角色ID)
ENDIF

@PRINT_種族抽出(選択中角色ID, 種族文本)
#DIM 選択中角色ID
#DIM TMP_RESULT
#DIMS STR_LIST, 10
#DIMS 種族文本

VARSET STR_LIST
TMP_RESULT:0 = RESULT
LOCALS =  %CSVCSTR(選択中角色ID,10)%
IF STRFIND(LOCALS, "●") > -1
	SPLIT LOCALS, "●", STR_LIST
	FOR LOCAL, 0, RESULT
		IF STRFIND(STR_LIST:LOCAL, "種族：") > -1
			LOCALS:1 = %REPLACE(STR_LIST:LOCAL, "種族：", "")%
			LOCALS:1 = %REPLACE(LOCALS:1, "　", "")%
			SIF STRFIND(種族文本, LOCALS:1) < 0
				PRINTFORM 〈%LOCALS:1%〉
			BREAK
		ENDIF
	NEXT
;ELSEIF TALENT:選択中角色ID:路人 とどっちがいいんでしょうね？
ELSEIF 選択中角色ID == RANDOM_CHARANUM
	PRINTFORM [%CSTR:選択中角色ID:路人子種族%]
ENDIF
RESULT = TMP_RESULT:0


@PRINT_刻印(選択中角色ID)
#DIM 選択中角色ID
IF !TFLAG:166
	FOR LOCAL, 0, 6
		SIF !STRLENS(MARKNAME:LOCAL)
			CONTINUE
		PRINTFORM 　%MARKNAME:LOCAL,12%:
		IF LOCAL == 3
			SETCOLOR C_RED
			PRINTFORML %"×" * (MARK:選択中角色ID:LOCAL)%
		ELSE
			SETCOLOR 255, 255, 0
			PRINTFORML %"★" * (MARK:選択中角色ID:LOCAL)%
		ENDIF
		RESETCOLOR
	NEXT
ELSE
	PRINTL
ENDIF



@PRINT_字母表顺序評価(選択中角色ID,ARG,表示ステータス番号)
;ARG(1.好感度 2.信賴度 3.性技及び技能 4.其他ABL)
;「表示ステータス番号」はARGが3または4のときのみに使用
#DIM 選択中角色ID
#DIM 表示ステータス番号
IF ARG == 1
	IF CFLAG:選択中角色ID:2 >= 50000
		SETCOLOR C_CREAM
		PRINT Ex
	ELSEIF CFLAG:選択中角色ID:2 >= 30000
		SETCOLOR C_WHITE
		PRINT SS
	ELSEIF CFLAG:選択中角色ID:2 >= 10000
		SETCOLOR C_P_PURPLE
		PRINT  S
	ELSEIF CFLAG:選択中角色ID:2 >= 5000
		SETCOLOR C_RED
		PRINT  A
	ELSEIF CFLAG:選択中角色ID:2 >= 3000
		SETCOLOR C_PINK
		PRINT  B
	ELSEIF CFLAG:選択中角色ID:2 >= 1500
		SETCOLOR C_ORANGE
		PRINT  C
	ELSEIF CFLAG:選択中角色ID:2 >= 1000
		SETCOLOR C_YELLOW
		PRINT  D
	ELSEIF CFLAG:選択中角色ID:2 >= 500
		SETCOLOR C_GREEN
		PRINT  E
	ELSEIF CFLAG:選択中角色ID:2 >= 100
		SETCOLOR C_AQUA
		PRINT  F
	ELSE
		SETCOLOR C_GRAY
		PRINT  G
	ENDIF
	RESETCOLOR
	RETURN 0
	ELSEIF ARG == 2
	IF CFLAG:選択中角色ID:信賴度 >= 20000
		SETCOLOR C_CREAM
		PRINT Ex
	ELSEIF CFLAG:選択中角色ID:信賴度 >= 10000
		SETCOLOR C_WHITE
		PRINT SS
	ELSEIF CFLAG:選択中角色ID:信賴度 >= 3000
		SETCOLOR C_P_PURPLE
		PRINT  S
	ELSEIF CFLAG:選択中角色ID:信賴度 >= 1000
		SETCOLOR C_RED
		PRINT  A
	ELSEIF CFLAG:選択中角色ID:信賴度 >= 500
		SETCOLOR C_PINK
		PRINT  B
	ELSEIF CFLAG:選択中角色ID:信賴度 >= 150
		SETCOLOR C_ORANGE
		PRINT  C
	ELSEIF CFLAG:選択中角色ID:信賴度 >= 100
		SETCOLOR C_YELLOW
		PRINT  D
	ELSEIF CFLAG:選択中角色ID:信賴度 >= 50
		SETCOLOR C_GREEN
		PRINT  E
	ELSEIF CFLAG:選択中角色ID:信賴度 >= 10
		SETCOLOR C_AQUA
		PRINT  F
	ELSE
		SETCOLOR C_GRAY
		PRINT  G
	ENDIF
	RESETCOLOR
	RETURN 0
ELSEIF ARG == 3
	IF ABL:選択中角色ID:表示ステータス番号 == 6
		SETCOLOR C_RED
		PRINT Ex
	ELSEIF ABL:選択中角色ID:表示ステータス番号 == 5
		SETCOLOR C_PINK
		PRINT  S
	ELSEIF ABL:選択中角色ID:表示ステータス番号 == 4
		SETCOLOR C_ORANGE
		PRINT  A
	ELSEIF ABL:選択中角色ID:表示ステータス番号 == 3
		SETCOLOR C_YELLOW
		PRINT  B
	ELSEIF ABL:選択中角色ID:表示ステータス番号 == 2
		SETCOLOR C_GREEN
		PRINT  C
	ELSEIF ABL:選択中角色ID:表示ステータス番号 == 1
		SETCOLOR C_AQUA
		PRINT  D
	ELSE
		SETCOLOR C_GRAY
		PRINT  E
	ENDIF
	RESETCOLOR
	RETURN 0
ELSEIF ARG == 4
	IF ABL:選択中角色ID:表示ステータス番号 == 99
		SETCOLOR C_CREAM
		PRINT Ex
	ELSEIF ABL:選択中角色ID:表示ステータス番号 >= 75
		SETCOLOR C_WHITE
		PRINT SS
	ELSEIF ABL:選択中角色ID:表示ステータス番号 >= 50
		SETCOLOR C_P_PURPLE
		PRINT  S
	ELSEIF ABL:選択中角色ID:表示ステータス番号 >= 20
		SETCOLOR C_RED
		PRINT  A
	ELSEIF ABL:選択中角色ID:表示ステータス番号 >= 10
		SETCOLOR C_PINK
		PRINT  B
	ELSEIF ABL:選択中角色ID:表示ステータス番号 >= 5
		SETCOLOR C_ORANGE
		PRINT  C
	ELSEIF ABL:選択中角色ID:表示ステータス番号 >= 3
		SETCOLOR C_YELLOW
		PRINT  D
	ELSEIF ABL:選択中角色ID:表示ステータス番号 >= 2
		SETCOLOR C_GREEN
		PRINT  E
	ELSEIF ABL:選択中角色ID:表示ステータス番号 >= 1
		SETCOLOR C_AQUA
		PRINT  F
	ELSE
		SETCOLOR C_GRAY
		PRINT  G
	ENDIF
	RESETCOLOR
	RETURN 0
ELSE
ENDIF
