;-------------------------------------------------
;関数名:MOON_WATCH
;概　要:どんな月が見えるかを返す関数
;ARG	DAY:3＝日にち
;RETURN -1:月の見えない状態, 0:月は出ていない, 1:新月, 2:二日月, 3:三日月, 4:四～七日月, 5:上弦, 6:九～十二日月, 7:十三夜, 8:待宵, 9:満月, 10:十六夜, 11:十七～二十二夜, 12:下弦, 13:二十四～二十九夜, 14:三十日月
;-------------------------------------------------
@MOON_WATCH(ARG)
#FUNCTION
;夜＋晴天＋屋外or神社内の室外
IF TIME:2 >= 5 && TIME:2 <= 7 && TIME:5 < 2 && MOON_WATCH_POINT(CFLAG:MASTER:現在位置)
	;月の見える時間
	SELECTCASE ARG
	;新月
	CASE 1,31
		RETURNF 1
	;二日月（19時～20時）
	CASE 2
		SELECTCASE TIME:0
		CASE 1140 TO 1200
		RETURNF 2
		CASEELSE
		RETURNF 0
		ENDSELECT
	;三日月（19時～21時）
	CASE 3
		SELECTCASE TIME:0
		CASE 1140 TO 1260
		RETURNF 3
		CASEELSE
		RETURNF 0
		ENDSELECT
	;四,五（19時～22時）
	CASE 4,5
		SELECTCASE TIME:0
		CASE 1140 TO 1320
		RETURNF 4
		CASEELSE
		RETURNF 0
		ENDSELECT
	;六,七（19時～23時）
	CASE 6,7
		SELECTCASE TIME:0
		CASE 1140 TO 1380
		RETURNF 4
		CASEELSE
		RETURNF 0
		ENDSELECT
	;上弦（19時～24時）
	CASE 8
		SELECTCASE TIME:0
		CASE 1140 TO 1440
		RETURNF 5
		CASEELSE
		RETURNF 0
		ENDSELECT
	;九,十（19時～25時）
	CASE 9,10
		SELECTCASE TIME:0
		CASE 0 TO 60,1140 TO 1440
		RETURNF 6
		CASEELSE
		RETURNF 0
		ENDSELECT
	;十一,十二（19時～26時）
	CASE 11,12
		SELECTCASE TIME:0
		CASE 0 TO 120,1140 TO 1440
		RETURNF 6
		CASEELSE
		RETURNF 0
		ENDSELECT
	;十三夜（19時～27時）
	CASE 13
		SELECTCASE TIME:0
		CASE 0 TO 180,1140 TO 1440
		RETURNF 7
		CASEELSE
		RETURNF 0
		ENDSELECT
	;待宵（19時～27時半）
	CASE 14
		SELECTCASE TIME:0
		CASE 0 TO 210,1140 TO 1440
		RETURNF 8
		CASEELSE
		RETURNF 0
		ENDSELECT
	;満月（19時～28時）
	CASE 15
		RETURNF 9
	;十六夜（19時半～28時）
	CASE 16
		SELECTCASE TIME:0
		CASE 0 TO 240,1170 TO 1440
		RETURNF 10
		CASEELSE
		RETURNF 0
		ENDSELECT
	;十七,十八,十九（20時半～28時）
	CASE 17,18,19
		SELECTCASE TIME:0
		CASE 0 TO 240,1230 TO 1440
		RETURNF 11
		CASEELSE
		RETURNF 0
		ENDSELECT
	;二十,二十一,二十二（22時半～28時）
	CASE 20,21,22
		SELECTCASE TIME:0
		CASE 0 TO 240,1350 TO 1440
		RETURNF 11
		CASEELSE
		RETURNF 0
		ENDSELECT
	;下弦（24時～28時）
	CASE 23
		SELECTCASE TIME:0
		CASE 0 TO 240
		RETURNF 12
		CASEELSE
		RETURNF 0
		ENDSELECT
	;二十四,二十五,二十六（25時～28時）
	CASE 24,25,26
		SELECTCASE TIME:0
		CASE 60 TO 240
		RETURNF 13
		CASEELSE
		RETURNF 0
		ENDSELECT
	;二十七,二十八,二十九（26時～28時）
	CASE 27,28,29
		SELECTCASE TIME:0
		CASE 120 TO 240
		RETURNF 13
		CASEELSE
		ENDSELECT
	;三十（27時半～28時）
	CASE 30
		SELECTCASE TIME:0
		CASE 180 TO 240
		RETURNF 14
		CASEELSE
		RETURNF 0
		ENDSELECT
	ENDSELECT
	ELSE
	RETURNF -1
ENDIF

@NAME_MOON(MOON)
#FUNCTIONS
#DIM  MOON	;MOONにはDAY:3を渡すこと
#DIMS CONST NAME_MOON = "", "朔月", "初月", "三日月", "上弦四分月", "上弦月", "上弦八分月", "十三夜", "待宵", "望月", "十六夜", "下弦八分月", "下弦八分月", "下弦月", "下弦四分月", "三十日月"

MOON = MAX(MOON_WATCH(MOON), 0)
SIF DAY:2 == 3 && MOON == 9
	RETURNF "十五夜 -中秋的名月- "
RETURNF NAME_MOON:MOON
