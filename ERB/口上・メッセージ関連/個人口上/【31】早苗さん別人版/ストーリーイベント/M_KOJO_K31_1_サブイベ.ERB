
@K31_サブイベント1_表示条件(ARGS)
;IFの条件がストーリ表示条件
;ARGSは指令の名前（口上の各指令部分で引数として手動で持ってきてるので注意）（←ガバガバコーディング）
;RETURN1で表示フラグON
	IF ARGS == "会話" && CFLAG:31:1012 >= 5 && CFLAG:31:1001 > 1
	;ストーリー2話以降で発生、発生後5日以上経つと再発生
		CFLAG:31:1012 = 0
		;CFLAG:31:1012は口上イベントERBで毎日1増える
		RETURN 1
	ENDIF
RETURN 0


@K31_サブイベント1_出力
;ストーリーはここにゴリゴリ書く
;LOCALに今の所持金を退避
	FONTSTYLE 1
	PRINTFORMDW 　　◆サブイベント◆「酒定期納入」
	PRINTL 
	FONTREGULAR
	
	IF CFLAG:31:1011 == 0
		PRINTFORMW 「あっ、%CALLNAME:MASTER%さん」
		PRINTFORMW 「%CALLNAME:MASTER%さんの酒造りの腕はなかなかのものだとお聞きしました」
		PRINTFORMW 「もし何か酒が余っていたら定期的に買い取りますよ」
		PRINTFORMW 「もちろん余裕が無いときは断ってもらってOKです」
		PRINTFORMW 「それでは、そういうことでよろしくお願いしますねっ」
		CFLAG:31:1011 = 1
	ELSEIF CFLAG:31:1011 == 1
		PRINTFORML 「あっ、%CALLNAME:MASTER%さん」
		PRINTFORMW 「余った酒が那个ば売ってもらえませんか？」
		PRINTFORML
		PRINTFORMDL [0]-良いよ
		PRINTFORMDL [1]-また今度で
		INPUT	
		PRINTFORML
		IF RESULT == 0
			PRINT 何を売ろうか…　
			LOCAL = MONEY
			PRINTFORML 所持金:%金額表示(MONEY)%
			CALL 酒一覧表示(0, "売却", 70)
			IF RESULT == 0
				PRINTFORML 「やっぱりやめますか？」
				PRINTFORML 「了解ですっ」
				PRINTFORMW 「また今度お聞きしますねっ」
				RETURN -1
			ENDIF
			IF ITEM:RESULT == 0
				PRINTW 品物がないので売ろうにも売れない
				PRINTFORML 「モノがないみたいですね…」
				PRINTFORMW 「急ぎでもありませんし、また今度お聞きしますねっ」
				RETURN -1
			ENDIF
			SIF ALC_NAME(RESULT) != ""
				PRINTFORMW 「おっ、%ALC_NAME(RESULT)%ですか」
			SELECTCASE RESULT
				CASE 51,56;濁酒・甜酒
					PRINTFORMW 「これくらいなら私も飲めそうです」
				CASE 52,55;清酒・御神酒
					PRINTFORMW 「お供え物にいいですね、ありがとうございます」
				CASE 59;薬酒
					PRINTFORMW 「健康増進に効き目がありそうです」
				CASE 53,58;鬼殺酒・蒸留酒
					PRINTFORMW 「私には強いですけど、好きな人も多いですね」
				CASEELSE
					PRINTFORMW 「おお…？これはなかなか珍しいものを……」
			ENDSELECT
			ITEM:RESULT -= 1
			;CALL GET_MONEY_YEN(1000, 2)
			MONEY += ITEMPRICE:RESULT * 70 / 100
			PRINTL
			PRINTFORML 所持金:%金額表示(LOCAL)%　→　%金額表示(MONEY)%
			PRINTL
			PRINTFORMW 「ありがとうございました、またお願いしますねっ」
			CALL CHANGE_CFLAG(2, 31, 50)
			CALL CHANGE_CFLAG(4, 31, 30)
			CALL KIGEN_CHANGE(31,100,1)
		ELSEIF RESULT == 1
			PRINTFORML 「了解ですっ」
			PRINTFORMW 「また今度お聞きしますねっ」
		ENDIF
	ENDIF
	

RETURN 1
