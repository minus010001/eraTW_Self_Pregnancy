;-------------------------------------------------
; eraTheWorld 4.841用 ○○別人口上（キャラクターの名前を入れる、下にコンセプトなど一言あれば入れる）
;-------------------------------------------------
;他者による修正、加筆、改変については、同梱のライセンス条項に従ってください。

;------ ｷﾘﾄﾘ ｶｲｼ -----------
;eraTheWorld proto4.07パッチ取り込み用 Ver1.13
;テンプレートの使い方
;初めに置換コマンド等により K49 を K(キャラNo) に変更して下さい  
;キャラNoはCSVフォルダのCharaファイルを参照　例(霧雨 魔理沙の場合)：K49 =置換=> K11
;
;------ ｷﾘﾄﾘ ｵﾜﾘ -----------

;**** FlagManagement **********************************************************
;本口上にて使用しているCFLAG・TCVAR等
;
;
;
;******************************************************************************

;### 口上存在判定 ###################################
@M_KOJO_K49_2(ARG)
;別人口上判定ですX2を同キャラ口上の通し番号に置き換えてください（一つ目の別人口上なら1、二つ目の別人口上なら2
;複数の同キャラ口上が同時表示されないために必要です、必ず49Xを変更してください
RESULTS = _NK
;初期口上とは違う事を明示するため、口上選択時に表示されます、必ずＸＸＸを変更してください
RESULTS:1 = 的试做版_NK

;口上存在判定です、けしてコメントアウトしないでください
;他の処理は一切行わないように
RETURN 1

;### 口上基本フラグ設定 #############################
@M_KOJO_NK_FLAGSETTING_K49
;[SKIPSTART]
;基本フラグ設定はここで行ってください
;共通の口上フラグは必要な箇所のコメントアウトを外してご利用ください
;口上専用フラグはご自由に
;Xにはキャラ番号を入れてください
IF TARGET==49
	SELECTCASE 关系_310()
		CASE 0
			TFLAG:好感度管理-=5
			TFLAG:信賴度管理-=3
			TFLAG:好感度BOUNS-=40			
		CASE IS >=105
			IF 关系_310()>110&&!CFLAG:TARGET:膣内射精&&CFLAG:TARGET:积攒度<500
				CFLAG:TARGET:积攒度+=500
			ENDIF
			TFLAG:好感度管理+=5
			TFLAG:信賴度管理+=3
			TFLAG:好感度BOUNS+=25
		CASE 102 TO 104
			TFLAG:好感度管理+=3
			TFLAG:信賴度管理+=2
			TFLAG:好感度BOUNS+=15
		CASE 101
			TFLAG:好感度管理+=2
			TFLAG:信賴度管理++
			TFLAG:好感度BOUNS+=5
		CASE 4 TO 16
			IF 关系_310()>10&&!CFLAG:TARGET:膣内射精&&CFLAG:TARGET:积攒度<500
				CFLAG:TARGET:积攒度+=500
			ENDIF
			TFLAG:好感度管理+=3
			TFLAG:信賴度管理+=2
			TFLAG:好感度BOUNS+=15
		CASE 2 TO 3
			TFLAG:好感度管理++
			TFLAG:好感度BOUNS+=5
		CASE 1
			TFLAG:好感度管理-=2
			TFLAG:信賴度管理--
			TFLAG:好感度BOUNS-=10		
	ENDSELECT	
ENDIF
;眠姦口上有、時間停止口上有、なりきりに1を入れると、それぞれ時間停止時、眠姦時、なりきり時にコマンド口上が読み込まれるように
;入れないと時間停止時、眠姦時、なりきり時は一切喋りません
CFLAG:49:眠姦口上有 = 0
CFLAG:49:時間停止口上有 = 0
CFLAG:49:扮演口上有 = 0

;破瓜キャンセル口上有に1を入れると、@M_KOJO_EVENT_K49_LOST_VIRGIN_STOPでキャンセルの成否を操作できます
CFLAG:49:破瓜中止口上有 = 0

;口上内抱き寄せ判定_初回と口上内抱き寄せ判定_通常に1を入れると、M_KOJO_EVENT_K49_PERMISSIONで押し倒しの成否を操作できます
CFLAG:49:口上内抱き寄せ判定_初回 = 0
CFLAG:49:口上内抱き寄せ判定_通常 = 0

;推倒禁止に1を入れると、自分から押し倒してこなくなります
;以下は恋慕かセフレがつくまで押し倒してこなくなる使用例
IF 关系_310()<4
	CFLAG:49:推倒禁止 = 1
ELSE
	CFLAG:49:推倒禁止 = 0
ENDIF

;来訪不能に1を入れると、来訪してこなくなります
;以下は面識があるまで来訪してこなくなる使用例
IF !CFLAG:49:面識
	CFLAG:49:来訪不能 = 1
ELSE
	CFLAG:49:来訪不能 = 0
ENDIF
;[SKIPEND]
RETURN 1
;### 口上色判定 ###################################
@M_KOJO_NK_COLOR_K49

;ここでSETCOLORすることで口上に色を付けられます
;例：SETCOLOR 0xC07070
SETCOLOR 0xCC3388
;※※※※※※※※※※※※※※※※※※※※※※※※※※
;"UPDATE判定"
;※※※※※※※※※※※※※※※※※※※※※※※※※※
@M_KOJO_NK_UPDATE_K49
;口上独自のUPDATE用
;ENCOUNTER専用に独自分岐フラグを仕込んだ場合等にどうぞ
;UPDATE判定でCFLAG等のキャラ変数を使う場合、キャラ番号の指定が必要になります
;PRINTFORML 小五最可爱了～
CALL UPDATE_310
RETURN 1

@UPDATE_310
#DIM CONST UPDATE_310 = 1310

$UPDT
SIF FLAG:口上色
	CALLFORM M_KOJO_NK_COLOR_K49
PRINTFORML
PRINTFORML
PRINTFORML ————————————————————————————————————以下，古明地觉口上UPDATE设定————————————————————————————————————————————————
PRINTFORML
PRINTFORML
PRINTFORML
PRINTFORML
IF !GETBIT(CFLAG:49:UPDATE_310,0)
	PRINTFORMW 没猜错的话，%CALLNAME:MASTER%应该是第一次打开这个UPDATE吧？
	PRINTFORML 啊啊，初次见面怎么就知道是叫什么的问题，就不必多言啦～
	PRINTFORMW 那么，稍微给一些介绍吧：
	PRINTFORML 以下选项，对于了解与更好游玩本口上大有裨益，
	PRINTFORML 强烈推荐首先查看该口上的简介，以对本口上有所初步了解；
	PRINTFORML 其次就是，对于基本素质的改变，以期更好契合本口上设定；
	PRINTFORMW 至于其它的选项嘛…有需要的话再说吧？
	PRINTFORML 好啦好啦，不妨自己动手试试看～
	SETBIT CFLAG:49:UPDATE_310,0
ENDIF
IF CFLAG:38:口上セレクタ != 2 
	IF !GETBIT(CFLAG:49:UPDATE_310,54)
		PRINTFORML 啊嘞？另一个古明地用的不是这份口上吗？
		PRINTFORMW 部分函数可是通用挪到这边来的，要是不换过来的话……
		FONTBOLD
		PRINTFORMW 出现奇怪的事情概不负责哦？
		FONTREGULAR
		PRINTFORML 所以呀，这里给个机会，方便换过来呢；
		PRINTFORML 毕竟，常规游玩也没法换口上不是(^^;
		PRINTFORMW 怎么样，要换过来吗？
		SETBIT CFLAG:49:UPDATE_310,54
	ELSE
		PRINTFORMW 要变更古明地恋的口上为本口上对应的吗？
	ENDIF
	CALL ASK_YN
		IF !RESULT
			CFLAG:38:口上セレクタ = 2
			PRINTFORML 明智的决定。
			PRINTFORML 好啦，变更完成啦；
			PRINTFORMW 呼呼，那么预祝游玩愉快～
		ELSE
			PRINTFORMW 啊啊，所以说冒着被做奇怪事情的风险，也要找别的口上吗？
			PRINTFORML 嘛，我是不会去劝阻你的；
			PRINTFORMW 只不过，要是反悔的话，我也是，会在这里等着的。
		ENDIF
ENDIF
PRINTFORMDL 
IF GETBIT(CFLAG:49:UPDATE_310,1)
	SETCOLOR 50,50,50
		PRINTFORML [1] 基本素质设定
	RESETCOLOR
ELSE
	PRINTFORMDL [1] 基本素质设定
ENDIF
PRINTFORMDL [2] 口上介绍
SIF CFLAG:38:3100 && 关系_310()>=105
	PRINTFORMDL [3] 角色精神素质设置
PRINTFORMDL
PRINTFORMDL [0] 退出
PRINTFORMDL 
IF FLAG:口上色
	CALLFORM M_KOJO_NK_COLOR_K49
ENDIF
$UPDT_SELECT
INPUT
IF !RESULT
	PRINTFORMW 嗯哼，那么就退出吧～
	RESETCOLOR
	RETURN 1
ELSEIF RESULT==1
	IF !GETBIT(CFLAG:49:UPDATE_310,1)
		PRINTFORML 本口上与TW原版觉设定出入较大，
		PRINTFORMW 请慎重考虑是否允许本口上变更经验、能力及素质。
		RESETCOLOR
		PRINTL ※変更的经验・能力
		;PRINTFORML 获得[自制心][阴郁][害羞][接受快感];[不信任人類][冷漠](会在后续消去)，失去[不在乎貞操]；
		RESETCOLOR
		IF ABL:教養 < 5
			PRINT 	教養(
			CALL PRINT_RANK(ABL:教養,"MAX_6")
			PRINTFORM  {ABL:教養} →
			CALL PRINT_RANK(5,"MAX_6")
			RESETCOLOR
			PRINTL  5)
		ENDIF
		IF EXP:学習経験 < 2000
			PRINTFORML 	学習経験( {EXP:学習経験} + 2000)
		ENDIF
	
		IF ABL:料理技能 < 4
			PRINT 	料理技能(
			CALL PRINT_RANK(ABL:料理技能,"MAX_6")
			PRINTFORM  {ABL:料理技能} →
			CALL PRINT_RANK(4,"MAX_6")
			RESETCOLOR
			PRINTL  4)
		ENDIF
		IF EXP:料理経験 < 1000
			PRINTFORML 	料理経験( {EXP:料理経験} + 1000)	
		ENDIF
	
		IF ABL:清掃技能 < 3
			PRINT 	清掃技能(
			CALL PRINT_RANK(ABL:清掃技能,"MAX_6")
			PRINTFORM  {ABL:清掃技能} →
			CALL PRINT_RANK(3,"MAX_6")
			RESETCOLOR
			PRINTL  3)
		ENDIF
		IF EXP:清掃経験 < 500
			PRINTFORML 	清掃経験( {EXP:清掃経験} + 500)
		ENDIF
	
		IF ABL:音楽技能 < 3
			PRINT 	音楽技能(
			CALL PRINT_RANK(ABL:音楽技能,"MAX_6")
			PRINTFORM  {ABL:音楽技能} →
			CALL PRINT_RANK(3,"MAX_6")
			RESETCOLOR
			PRINTL  3)
		ENDIF
		IF EXP:演奏経験 < 1000
			PRINTFORML 	演奏経験( {EXP:演奏経験} + 1000)
		ENDIF		
		IF FLAG:口上色
			CALLFORM M_KOJO_NK_COLOR_K49
		ENDIF
		PRINTFORMW 允许本口上对觉的素质进行改动吗？
		CALL ASK_YN
		IF !RESULT
			PRINTFORML 好的～
			PRINTFORML 感谢您的配合，
			PRINTFORMW 那么变更完成了～
			TALENT:24=-1;阴郁
			TALENT:自制心=1
			TALENT:羞恥心=1;害羞
			TALENT:快感応答=1;接受快感
			TALENT:性別嗜好=3;不信任人類
			TALENT:貞操=0
			TALENT:冷漠=1
	
			ABL:教養=MAX(ABL:教養,5)
			;EXP(5+3)=2000
			EXP:学習経験=MAX(EXP:学習経験,2000)
	
			ABL:清掃技能=MAX(ABL:清掃技能,3)
			;EXP(3+3)=500
			EXP:清掃経験=MAX(EXP:清掃経験,500)
	
			ABL:音楽技能=MAX(ABL:音楽技能,3)
			;EXP(3+3)*2=1000
			EXP:演奏経験=MAX(EXP:演奏経験,1000)
	
			ABL:料理技能=MAX(ABL:料理技能,4)
			;EXP(4+3)=1000
			EXP:料理経験=MAX(EXP:料理経験,1000)
	
			SETBIT CFLAG:49:UPDATE_310,1
		ELSE
			PRINTFORML 诶诶？还没准备好吗？
			PRINTFORMW 反悔的话随时可以回来哦～
		ENDIF
	ELSE
		PRINTFORMW 嗯哼？素质变更不是已经完成过了吗？
		PRINTFORML 是出现BUG要再次变更素质对吗？
		CALL ASK_YN
			IF !RESULT
				PRINTFORML 这样啊…
				PRINTFORML 那还真是，不幸呢……
				PRINTFORML 还请去论坛里向作者反馈吧，把遇到的情况什么的尽量说清楚些；
				PRINTFORMW 这样的话，才方便修呢——
				PRINTFORML 论坛地址什么的已经贴到readme了，还请访问一下。
				PRINTFORML 好啦，那么这次，就先重置一下吧；
				PRINTFORMW 就，不妨碍继续重置了～
				CLEARBIT CFLAG:49:UPDATE_310,1
			ELSE
				PRINTFORML 什么啊！
				PRINTFORML 还以为又写出什么bug了呢……
				PRINTFORMW 没别的什么事的话，就请回去吧。
			ENDIF
	ENDIF
ELSEIF RESULT==2
	PRINTFORML 本口上，与其它口上最大的不同，就是采用了另一套陷落判定方法，
	PRINTFORML 就算取得思慕恋慕也不会对口上内容造成实质性影响。
	PRINTFORMW 所以，看到没有对应口上时，也不要感到奇怪～
	PRINTFORML 此外，功能性的，还有在达成某条件时，可以开启下一周目；
	PRINTFORMW 请做好备份，比如save99之类的……
	PRINTFORML 在完成以上事件后，攻略到一定进度再回来UPDATE的话，会有额外功能出现；
	PRINTFORMW 尽情期待啦，别忘了回来看看呢～
	PRINTFORML 好啦，记得多陪陪觉做点能加经验的事情——
	PRINTFORML 学习呀，料理呀之类的；
	PRINTFORMW 说不定能事半功倍呢，欸嘿嘿～
ELSEIF RESULT==3 && (CFLAG:38:3100 && 关系_310()>=105)
	IF !GETBIT(CFLAG:49:UPDATE_310,3)
		PRINTFORML 恭喜你发现了多周目奖励！
		PRINTFORML 只要是关系达到恋慕程度的角色，都可以委托古明地小姐催眠改造！
		SETBIT CFLAG:49:UPDATE_310,3
	ENDIF
	PRINTFORMW 那么，想要改造的已恋慕对象是谁呢？
	$SETCHARA
	RESETCOLOR
	FOR TARGET ,1, CHARANUM
		IF !TALENT:TARGET:恋慕
			CONTINUE
		ENDIF
		PRINTFORMLC [{TARGET}], %NAME:TARGET% 
	NEXT
	PRINTL 
	DRAWLINE
	PRINTFORMDL [0] 退出
	IF FLAG:口上色
		CALLFORM M_KOJO_NK_COLOR_K49
	ENDIF
	INPUT
	IF RESULT==0
		PRINTFORML 嗯哼，已经满意了吗？
		PRINTFORMW 那就，返回UPDATE界面吧～
	ELSEIF !TALENT:RESULT:恋慕
		PRINTFORML 调皮了哦，%CALLNAME:MASTER%；
		PRINTFORMW 擅自做这样的事情是会变得不幸的！
		PRINTFORML 嘛，再给你一次机会吧；
		PRINTFORMW 可不能再调皮了哦～
		GOTO SETCHARA
	ELSE
		TARGET=RESULT
		PRINTFORML 是%CALLNAME:TARGET%没错吗？
		CALL ASK_YN
			IF RESULT
				PRINTFORMW 要看清楚再选啊。
				GOTO SETCHARA
			ELSE
				PRINTFORMW 呼姆呼姆，想要变更什么呢？
				CALL SET_CHARA_310(TARGET)
				IF FLAG:口上色
					CALLFORM M_KOJO_NK_COLOR_K49
				ENDIF
				PRINTFORMW 要继续变更下一位吗？
				CALL ASK_YN
				IF !RESULT
					PRINTFORML 好的～请选择需要修改的已恋慕角色～
					GOTO SETCHARA
				ELSE
					PRINTFORML 好的～全新的%CALLNAME:TARGET%已经改造完成了！
					PRINTFORMW 请好好对待哦～
				ENDIF
			ENDIF
	ENDIF
ELSE
	PRINTFORML 不对不对，要照着选项输入～
	GOTO UPDT_SELECT
ENDIF
GOTO UPDT
RESETCOLOR
RETURN 1


@SET_CHARA_310(TARGET)
PRINTL
PRINTL
RESETCOLOR
FOR LOCAL,10,86
	IF (LOCAL==34|(LOCAL<=54&&LOCAL>=51))||(LOCAL<=78&&LOCAL>=73)
		CONTINUE
	ELSEIF (LOCAL<=17&&LOCAL>=15)||(LOCAL<=49&&LOCAL>=41)
		CONTINUE	
	ELSE
		SELECTCASE TALENTNAME:LOCAL
		;TALENT.CSVに登録されていない
		CASE ""
			CONTINUE
		;オンオフ型の素質(TALENTNAMEと素質表示が一致)
		CASE GET_TALENTNAME(LOCAL,1)
			SIF TALENT:TARGET:LOCAL
				SETCOLOR C_AQUA
			LOCALS = [{LOCAL,3}]%TALENTNAME:LOCAL%
		;量的素質
		CASEELSE
			LOCALS =  [{LOCAL,3}]%TALENTNAME:LOCAL%
			IF STRLENS(GET_TALENTNAME(LOCAL, TALENT:TARGET:LOCAL))
				LOCALS:1 = 【%GET_TALENTNAME(LOCAL, TALENT:TARGET:LOCAL)%】
				SETCOLOR C_AQUA
			ELSE
				LOCALS:1 = 【－】
			ENDIF
			LOCALS += LOCALS:1
		ENDSELECT
	ENDIF
	IF LOCAL:1 % 3 == 0
		PRINTL 
		PRINT   
	ENDIF
	PRINTFORMLC %LOCALS, 30, LEFT%
	LOCAL:1 ++
	RESETCOLOR
NEXT
PRINTL
PRINTL
PRINTL   [9999] 返回
PRINTL
INPUT
SIF RESULT == 9999
	RETURN
LOCAL=RESULT
IF TALENTNAME:LOCAL == GET_TALENTNAME(LOCAL,1)
	TALENT:TARGET:LOCAL = !TALENT:TARGET:LOCAL
ELSE
;他は端まで行くと-1を掛ける
	IF STRLENS(GET_TALENTNAME(LOCAL,TALENT:TARGET:LOCAL + 1)) || TALENT:TARGET:LOCAL == -1
		TALENT:TARGET:LOCAL ++
	ELSE
		TALENT:TARGET:LOCAL *= -1
	ENDIF
ENDIF
RESTART
;※※※※※※※※※※※※※※※※※※※※※※※※※※
;"ENCOUNTER"系イベント（エクストラ構文）
;※※※※※※※※※※※※※※※※※※※※※※※※※※
@M_KOJO_NK_ENCOUNTER_K49
;初対面、挨拶のタイミング
;TALENTの変更はここでどうぞ。
;経験の変更は齟齬が出る場合がありますので、CSVを修正し同梱する事を推奨します
SIF FLAG:口上色
	CALLFORM M_KOJO_NK_COLOR_K49
IF !CFLAG:38:3100
	PRINTFORML 「外来的幻想乡管理者，就是你吗？」
	PRINTFORMDW   那个粉色少女正用胸口的眼球瞪着。
	PRINTFORML 「所见的简介并没有讲明我的身份」
	PRINTFORMDW   嗯嗯嗯？所见的简介？这都能看到吗？
	PRINTFORML 「我是觉，负责管理幻想乡的怨灵」
	PRINTFORMDW   没兴趣搭理这疑问，自顾自开始自我介绍。
	PRINTFORML 「将之囚闭地灵殿，交付宠物吞食」
	PRINTFORMDW   复又打量一番，停留在瞳孔。
	PRINTFORML 「不错，他们本是人类，与你一样」
	PRINTFORML 「如若你治理失当…我会看着的」
	PRINTFORMDW   似乎是想到什么，将走之际，又补上几句。
	PRINTFORML 「那么，不要急于重置世界」
	PRINTFORML 「惟因你不能逆转时间」
ELSE
	PRINTFORML 「啊啦，已经是第几次了呢？」
	PRINTFORMDW   还是那个熟悉的声音。
	PRINTFORML 「那些孩子们的记忆，已经全部抹除了哦」
	PRINTFORML 「肉体也已经全部复原了哦」
	PRINTFORML 「还满意吗？」
	PRINTFORML 「世界的管理者啊」
	PRINTFORML 「所管理的世界遭受厌弃，也没关系」
	PRINTFORML 「我也还会继续，做好所当做的一切」
ENDIF
RESETCOLOR
RETURN 1