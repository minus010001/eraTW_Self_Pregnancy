@K38_CONFEDERATION_UPDATE_1
$UPDATE
PRINTFORML 
PRINTFORML ——————————【古明地恋的口上设定】——————————
PRINTFORML 
IF CFLAG:38:1002 == 0
	PRINTFORMW 
	PRINTFORMW 你好~这里是……
	PRINTFORMW 嗯。
	PRINTFORMW 嗯嗯嗯……总之！
	PRINTFORMW 【打開以下模式时，自己会有介绍的——！！！】
	PRINTFORMW 当然啦，要是不懂的话……
	PRINTFORMW 直接【亲自点进去查看介绍】就好啦！
	PRINTFORMW 
	CFLAG:38:1002 = 2
ELSEIF (CFLAG:38:1005 == 1 || CFLAG:38:1005 == 4) && CFLAG:38:1002 < 4
	PRINTFORMW 
	PRINTFORMW 你好~久仰大名。
	PRINTFORMW 欢迎回来————
	PRINTFORMW 清除了数据之后，就要从零開始了哦～
	PRINTFORMW 虽然很辛苦，但某种意义上，玩游戏就是重复无意义地敲击键盘。
	PRINTFORMW 换句话说，人类是多么渺小啊，就连我们妖怪，有时候都会觉得空虚和困躁呢————
	PRINTFORMW 嗯，哈～
	PRINTFORMW 即使很辛苦，还是请继续书写【只属于你自己的故事】吧！
	PRINTFORMW 
	CFLAG:38:1002 = 4
ELSEIF CFLAG:38:1006 == 17
	PRINTFORMW 
	PRINTFORMW 好啦～好啦～！
	PRINTFORMW 知道了啦————！
	PRINTFORMW 固执的人类先生一定是想要找到那些彩蛋的是吧！
	PRINTFORMW ……
	PRINTFORMW 好啦～～～！
	PRINTFORMW 这里是版本的更新记录、你尽管拿去就好啦————！！！
	PRINTFORMW 
	CFLAG:38:1006 = 18
ELSEIF CFLAG:38:1002 == 4 || CFLAG:38:1002 == 5
	PRINTDATA
		DATALIST
			DATAFORM 故事、故事！嗯？
			DATAFORM 祈祷自己当初没关掉多周目模式吧～
		ENDLIST
		DATALIST
			DATAFORM 嗯，虽然意义非凡，但即使如此……
			DATAFORM 能说的话也就那么几句嘛————
		ENDLIST
		DATALIST
			DATAFORM 很辛苦是吗？偶尔也应该放松一下啊～
			DATAFORM 其实那个恋恋能做的事情还是不够多……
		ENDLIST
		DATALIST
			DATAFORM 表面上看着很明朗————
			DATAFORM 实际可不是那样！
		ENDLIST
		DATALIST
			DATAFORM 叽里咕噜叽里咕噜……
			DATAFORM 多说了一句话……
		ENDLIST
		DATALIST
			DATAFORM 有些地方根本没有变化？
			DATAFORM 因为没必要啊～
		ENDLIST
		DATALIST
			DATAFORM 和update玩耍，那才是本末倒置————！
			DATAFORM 快点去找那个恋恋吧♪
		ENDLIST
		DATALIST
			DATAFORM 嗯————虽然在看着你。
			DATAFORM 但毕竟这里只是update————
		ENDLIST
		DATALIST
			DATAFORM 嗯、虽然有时说着一样的话。
			DATAFORM 但上一刻和这一刻真的很难一样————
		ENDLIST
		DATALIST
			DATAFORM 啊、主要是念自己写的稿子为主。
			DATAFORM 因为某种目的————
			DATAFORM 所以敢说的话不多呢～
		ENDLIST
		DATALIST
			DATAFORM 很枯燥吧～
			DATAFORM 顺便一说，现在这边是深夜————
			DATAFORM 嗯……可能现在已经不是了。
		ENDLIST
		DATALIST
			DATAFORM 嗯，你到底和那个恋恋说了什么呢？
			DATAFORM 这个是很难知道的————
			DATAFORM 倒不如说、你当时在干什么、都是猜出来的吧～
		ENDLIST
	ENDDATA
	PRINTFORML
ELSE
	PRINTDATA
		DATALIST
			DATAFORM 啊哈哈～又来了啊~
			DATAFORM 来看看吧，会有你需要的~
		ENDLIST
		DATALIST
			DATAFORM 你好～欢迎～请看看吧～
			DATAFORM 虽然总觉得你已经早就选好了……
		ENDLIST
		DATALIST
			DATAFORM 嗯～你好～
			DATAFORM 祝你有一个愉快的一天~
		ENDLIST
		DATALIST
			DATAFORM 真累啊～如果有这样的想法的话————
			DATAFORM ————那就休息休息吧，要劳逸结合！
		ENDLIST
		DATALIST
			DATAFORM 大家的记忆力在这里本来就不好————
			DATAFORM 如果新開存档的话，真是神仙难救！
		ENDLIST
		DATALIST
			DATAFORM 会感觉很烦吗？
			DATAFORM 嗯————哼！
		ENDLIST
		DATALIST
			DATAFORM 哦唔～？为什么有些时候不显示那个恋恋具体说的话？
			DATAFORM 笨蛋～
			DATAFORM 你和那个恋恋到底说了什么，谁会知道呀————！
		ENDLIST
	ENDDATA
	PRINTFORML
ENDIF
PRINTFORML
IF CFLAG:38:1590 == 1
	PRINTFORML [0]-多周目模式
ENDIF
IF CFLAG:38:1590 == 1
	PRINTFORML [1]-快餐模式
ENDIF
IF CFLAG:38:1590 == 1
	PRINTFORML [2]-尊重原著模式
ENDIF
IF CFLAG:38:1590 == 1
	PRINTFORML [10]-清除每回随机
ENDIF
IF CFLAG:38:1590 == 1
	PRINTFORML [11]-找回书籍
ENDIF
IF CFLAG:38:1590 > 0
	PRINTFORML [514]-古明地恋的口上模式设定
ENDIF
IF CFLAG:38:1006 > 17
	PRINTFORML 
	PRINTFORML [1453]-当前版本
ENDIF
PRINTFORML 
IF CFLAG:38:1590 > 0
	PRINTFORML [999]-退出
ENDIF
INPUT
SELECTCASE RESULT
	CASE 0
		GOTO WAITING_FOR_GOBOT
		RETURN
	CASE 1
		GOTO FAST_FOOD
		RETURN
	CASE 2
		GOTO NOTHING_THERE
		RETURN
	CASE 10
		GOTO STRUGGLE_CONTINUES
		RETURN
	CASE 11
		GOTO STRUGGLE_AGAIN
		RETURN
	CASE 514
		;KOMEIJI_KOISHI是古明地恋的英文译名
		GOTO KOMEIJI_KOISHI
		RETURN
	CASE 999
		RETURN
	CASE 1453
		GOTO KOMEIJI_KOISHI_IS_DAILY
		RETURN
	CASEELSE
		CALL M_KOJO_K38_WHAT_IS_IT
		RETURN
ENDSELECT
RETURN
$WAITING_FOR_GOBOT
PRINTFORML 
PRINTFORML ——————————【多周目模式】——————————
IF CFLAG:38:1002 == 4
	PRINTFORMW 
	PRINTFORMW 【多周目模式……】
	PRINTFORMW 其实非常有用，从某些角度来说(咬手指)
	PRINTFORMW 嗯……呸呸呸！
	PRINTFORMW 【因为清除过一次数据，可能需要重新设定了————】
	PRINTFORMW 
	CFLAG:38:1002 = 5
ELSEIF CFLAG:38:1002 == 2
	PRINTFORMW 
	PRINTFORMW 二……【多周目模式。】
	PRINTFORMW 这个没什么好介绍的喔————
	PRINTFORMW 本来就是【自動開启】的～
	PRINTFORMW 如果【不希望下个周目受到影响，可以选择关闭。】
	PRINTFORMW 
	PRINTFORMW 这样那个恋恋就会记不住事情，变成一个笨蛋……
	PRINTFORMW 明明说的是“无意识”，偏偏要在这种环境下被摆布也太可笑了吧？
	PRINTFORMW 不过……嗯，接受这种小“偏差”啦————
	PRINTFORMW 啊、对了，不是什么时候都可以关的。
	PRINTFORMW 如果【进入多周目的话说不定就关不了了！】
	PRINTFORMW 要注意，最好在现在这种时候就下决定！
	PRINTFORMW 
	CFLAG:38:1002 = 3
ELSEIF CFLAG:38:1002 == 5
	PRINTFORML 
	PRINTFORML 【多周目模式，通常自動就会開启。】
	PRINTFORML 【不希望下个周目受到影响的情况下，建议选择关闭————】
	PRINTFORML 不过，【这可并不是什么情况下都能关闭的！】
	PRINTFORML 【进入多周目的话，很有可能无法关闭！就算关闭了也没什么用！】
	PRINTFORML 
ELSE
	PRINTFORML 
	PRINTFORML 【多周目模式～】
	PRINTFORML 【本来就是開启的～】
	PRINTFORML 【如果不希望下个周目受到影响的话，可以选择关闭！】
	PRINTFORML 不过，【并不是什么情况下都能够更改開关的————】
	PRINTFORML 【当前存档是多周目的话，关闭后不允许開启！】
	PRINTFORML 请尽早下决定～！
	PRINTFORML 
ENDIF
PRINTFORML
PRINTFORML
PRINTFORML
IF CFLAG:38:1001 == 1
	PRINTFORML (当前模式已開启)
ELSE
	PRINTFORML (当前模式已关闭)
ENDIF
IF CFLAG:38:1001 == 1
	PRINTFORML [0]-关闭模式
ELSE
	PRINTFORML [0]-開启模式
ENDIF
PRINTFORML 
PRINTFORML [900]-返回上一个菜单
PRINTFORML 
PRINTFORML [999]-退出
INPUT
SELECTCASE RESULT
	CASE 0
		IF CFLAG:38:1001 == 0
			IF CFLAG:31 == 0 || CFLAG:31 == 1
				PRINTFORMW 
				PRINTFORMW 这个存档已经是多周目了。
				PRINTFORMW 所以，不行！
				GOTO WAITING_FOR_GOBOT
				RETURN
			ENDIF
			CALL M_KOJO_K38_OKEY_OKAY
			CFLAG:38:1001 = 1
			RETURN
		ELSEIF CFLAG:38:1002 == 2
			PRINTFORMW 
			PRINTFORMW 其实……这个多周目模式挺重要……
			PRINTFORMW 如果只是单纯想想的话，还是三思而后行————
			PRINTFORMW 
			PRINTFORML 
			PRINTFORML [0]-关了它！
			PRINTFORML [1]-算了。
			CFLAG:38:1002 = 3
		ELSEIF CFLAG:38:1002 == 4
			PRINTFORMW 
			PRINTFORMW 喔，确定了吗？
			PRINTFORMW 
			PRINTFORML 
			PRINTFORML [0]-确定了。
			PRINTFORML [1]-不不，没有。
		ELSE
			PRINTFORMW 
			PRINTFORMW 嗯哼？真的考虑好了？
			PRINTFORMW 
			PRINTFORML 
			PRINTFORML [0]-已经考虑好了。
			PRINTFORML [1]-没，開玩笑的。
		ENDIF
		INPUT
		SELECTCASE RESULT
			CASE 0
				IF CFLAG:38:1002 == 4
					PRINTFORMW 好的？
					PRINTFORMW 嘿、有没有吓一跳呢～?
					PRINTFORMW 嗯，安保措施，现在退出还来得及————
					PRINTFORMW 来得及……
					PRINTFORMW 啊！算了，反正你就是想让那个恋恋变笨蛋————
					PRINTFORMW 哼！
					PRINTFORMW 倒计时開始！
				ELSE
					PRINTFORMW 
					PRINTFORMW ……
					PRINTFORMW 呼～稍微找找安眠药，致死量。
					PRINTFORMW 还有红茶，喔，这是消遣————
					PRINTFORMW 与此同时，安保措施也在进行着。
					PRINTFORMW 这时候关掉update和游戏还来得及。
					PRINTFORMW 呃，倒计时之后就不是这样了。
				ENDIF
				PRINTFORMW 3
				PRINTFORMW 2
				PRINTFORMW 1
				PRINTFORMW 0
				CFLAG:38:1001 = 0
				PRINTFORMW 【多周目模式已关闭】
				GOTO WAITING_FOR_GOBOT
				RETURN
			CASE 1
				CALL M_KOJO_K38_REGRET_1
				GOTO WAITING_FOR_GOBOT
				RETURN
			CASEELSE
				CALL M_KOJO_K38_WHAT_IS_IT
				RETURN
		ENDSELECT
	CASE 900
		GOTO UPDATE
	CASE 999
		RETURN
	CASEELSE
		CALL M_KOJO_K38_WHAT_IS_IT
ENDSELECT
RETURN
$FAST_FOOD
PRINTFORML 
PRINTFORML ——————————【快餐模式】——————————
IF CFLAG:38:1003 == 0
	PRINTFORMW 
	PRINTFORMW 快餐模式～大概是给那些【不喜欢漫长剧情】的家伙们用的吧～？
	PRINTFORMW 如果開启之后的话……
	PRINTFORMW 之后的话，整个流程……【主线流程！就会缩短！】
	PRINTFORMW 嗯嗯嗯，就是这样，抱歉第一次背这个有点困扰，下一次一定会用稿子的！
	PRINTFORMW 那么总结来说，【进入某个状态需要10好感，開启后只需要2好感～！】
	PRINTFORMW 
	PRINTFORMW 哦对了，最后也是最重要的一点：
	PRINTFORMW 【開启之后是关不掉的，除非进入下一个周目，或是新開一个存档！】
	PRINTFORMW 【其实更换古明地恋的口上模式也可以……】
	PRINTFORMW 就在外面的最后一个选项那里，呼姆呼姆，就是这样～
	PRINTFORMW 
	CFLAG:38:1003 = 2
ELSE
	PRINTFORML 
	PRINTFORML 主要是面对【不喜欢漫长剧情】的家伙。
	PRINTFORML 開启之后，【可以缩短大部分主线的流程！】
	PRINTFORML 嘿嘿，换句话说，就是：
	PRINTFORML 【进入下一个状态原本要10好感度，開启后只需要2好感度了。】
	PRINTFORML 【開启后是无法关闭的……】
	PRINTFORML 除非【切换古明地恋的口上模式、进入下一个周目，或是新開一个存档……】
	PRINTFORML 
ENDIF
PRINTFORML
PRINTFORML
PRINTFORML
IF CFLAG:38:1003 == 1
	PRINTFORML (当前模式已開启)
ELSE
	PRINTFORML (当前模式已关闭)
ENDIF
PRINTFORML [0]-開启
PRINTFORML 
PRINTFORML [900]-返回上一个菜单
PRINTFORML 
PRINTFORML [999]-退出
INPUT
SELECTCASE RESULT
	CASE 0
		IF CFLAG:38:1003 == 1
			CALL M_KOJO_K38_NO_AGAIN_1
			GOTO FAST_FOOD
			RETURN
		ELSEIF CFLAG:38:1003 == 2
			PRINTFORMW 
			PRINTFORMW ……
			PRINTFORMW 你认真的？
			PRINTFORMW 
			PRINTFORML 
			PRINTFORML [0]-认真的。
			PRINTFORML [1]-没没没，手滑了！
			CFLAG:38:1003 = 3
		ELSE
			PRINTFORMW 
			PRINTFORMW ……那个，快餐模式可是不允许常规关掉的哦。
			PRINTFORMW 因为允许随意关闭的话……可能会出问题……
			PRINTFORMW 那些数据的东西……
			PRINTFORMW 
			PRINTFORML 
			PRINTFORML [0]-明白了，我要開启。
			PRINTFORML [1]-手滑了！这是误会！
		ENDIF
		INPUT
		SELECTCASE RESULT
			CASE 0
					PRINTFORMW 
					PRINTFORMW 嗯……
					PRINTFORMW 这是最后的安全措施了。
					PRINTFORMW 如果真的是不小心点错的话，请立刻关掉update，或者退出游戏，不要再继续点了。
					PRINTFORMW 嗯……
					PRINTFORMW 哼～
					PRINTFORMW 那就继续吧。
					PRINTFORMW 3
					PRINTFORMW 2
					PRINTFORMW 1
					PRINTFORMW 0
					CFLAG:38:1003 = 1
					PRINTFORMW 【快餐模式已開启】
					GOTO FAST_FOOD
					RETURN
			CASE 1
				CALL M_KOJO_K38_REGRET_1
				GOTO FAST_FOOD
				RETURN
			CASEELSE
				CALL M_KOJO_K38_WHAT_IS_IT
				RETURN
		ENDSELECT
	CASE 900
		GOTO UPDATE
	CASE 999
		RETURN
	CASEELSE
		CALL M_KOJO_K38_WHAT_IS_IT
ENDSELECT
RETURN
$NOTHING_THERE
PRINTFORML 
PRINTFORML ——————————【尊重原著模式】——————————
IF CFLAG:38:1004 == 0
	PRINTFORMW 
	PRINTFORMW 额……嗯……尊重原著模式……
	PRINTFORMW 最好的尊重原著的方法就是什么都不写……所以顾名思义……
	PRINTFORMW 这是面对【不喜欢角色产生过大变化】的家伙而開发……
	PRINTFORMW 開启之后会【移除几乎所有的恋慕后剧情……】
	PRINTFORMW 因为这些剧情中，【包含了很多独自解释、独自设定与角色崩坏的要素!】
	PRINTFORMW ……但是但是，这样也【会令口上的剧情不完整……】
	PRINTFORMW 
	PRINTFORMW 啊，还有：
	PRINTFORMW 【尊重原著模式缩短了整个主线，使得快餐模式无用武之地！】
	PRINTFORMW 【而且、開启后关不掉，除非进入下个周目，或是新開一个存档！】
	PRINTFORMW 【更换古明地恋的口上模式也可以————！】
	PRINTFORMW 
	CFLAG:38:1004 = 2
ELSE
	PRINTFORML 
	PRINTFORML 面对【不喜欢角色在他人手中产生过大变化】的家伙……
	PRINTFORML 開启后，【去除几乎所有的恋慕后剧情。】
	PRINTFORML 因为这些剧情中，【包含了很多独自解释、独自设定与角色崩坏的要素。】
	PRINTFORML ……但是这样【会令口上的剧情不完整】。
	PRINTFORML 【開启之后无法关闭，也会使快餐模式的功能失效】
	PRINTFORML 除非【切换古明地恋的口上模式、进入下一个周目，或是另開一个存档。】
	PRINTFORML 
ENDIF
PRINTFORML
PRINTFORML
PRINTFORML
IF CFLAG:38:1004 == 1
	PRINTFORML (当前模式已開启)
ELSE
	PRINTFORML (当前模式已关闭)
ENDIF
PRINTFORML [0]-開启
PRINTFORML 
PRINTFORML [900]-返回上一个菜单
PRINTFORML 
PRINTFORML [999]-退出
INPUT
SELECTCASE RESULT
	CASE 0
		IF CFLAG:38:1004 == 1
			CALL M_KOJO_K38_NO_AGAIN_1
			GOTO NOTHING_THERE
			RETURN
		ELSEIF CFLAG:38:1004 == 2
			PRINTFORMW 
			PRINTFORMW 啊？真的要開吗？
			PRINTFORMW 还以为谁也不会開呢……不过萝卜青菜各有所爱！
			PRINTFORMW 
			PRINTFORML 
			PRINTFORML [0]-我确实要開启这个模式。
			PRINTFORML [1]-嗯，我没打算開启这个模式。
			CFLAG:38:1004 = 3
		ELSE
			PRINTFORMW 
			PRINTFORMW 呃，不要在危险的边缘试探！
			PRINTFORMW 当然，其实那个【古明地恋的口上模式设定】更危险————
			PRINTFORMW 那么，要開启【尊重原著模式】吗～？
			PRINTFORMW 
			PRINTFORML 
			PRINTFORML [0]-知道了，我要開启。
			PRINTFORML [1]-还是算了。
		ENDIF
		INPUT
		SELECTCASE RESULT
			CASE 0
					PRINTFORMW 
					PRINTFORMW ……
					PRINTFORMW 诶！居然真的開了！
					PRINTFORMW 呃……
					PRINTFORMW 其实这是最后的安全措施了。
					PRINTFORMW 没有选项，只是在这时候关掉update和游戏还来得及……
					PRINTFORMW 对、这个模式现在还没有開启，数据还没有记录。
					PRINTFORMW 嗯，不过如果继续点的话，情况就会不一样了……
					PRINTFORMW 嗯……
					PRINTFORMW 那就继续吧～
					PRINTFORMW 3
					PRINTFORMW 2
					PRINTFORMW 1
					PRINTFORMW 0
					CFLAG:38:1004 = 1
					PRINTFORMW 【尊重原著模式已開启】
					GOTO NOTHING_THERE
					RETURN
			CASE 1
				CALL M_KOJO_K38_REGRET_1
				GOTO NOTHING_THERE
				RETURN
			CASEELSE
				CALL M_KOJO_K38_WHAT_IS_IT
				RETURN
		ENDSELECT
	CASE 900
		GOTO UPDATE
	CASE 999
		RETURN
	CASEELSE
		CALL M_KOJO_K38_WHAT_IS_IT
ENDSELECT
RETURN
$STRUGGLE_CONTINUES
CALL M_KOJO_K38_OKEY_OKAY
SIF CFLAG:38:1500 == 1000
	CFLAG:38:1500 = 1514
SIF CFLAG:38:1500 == 999
	CFLAG:38:1500 = 998
GOTO UPDATE
RETURN
$STRUGGLE_AGAIN
CALL M_KOJO_K38_OKEY_OKAY
SIF CFLAG:38:1509 == 11
	CFLAG:38:1509 = 10
GOTO UPDATE
RETURN
$KOMEIJI_KOISHI
PRINTFORML 
PRINTFORML ——————————【古明地恋的口上模式设定】——————————
IF CFLAG:38:1005 == 1
	PRINTFORMW 
	PRINTFORMW 来到这里了吗？
	PRINTFORMW 久仰～这里是古明地恋
	PRINTFORMW 的————
	PRINTFORMW 古明地恋的、古明地恋的、古明地恋的……
	PRINTFORMW 【口、上、模、式、设、定！】
	PRINTFORMW 相信你已经试用过了吧～？
	PRINTFORMW 感觉如何呢？会不会感觉身体有所欠缺～～～？
	PRINTFORMW 嗯，一般只要【不关闭多周目模式，清除数据的损失不会很大————】
	PRINTFORMW 当然，这里的作用还是没有改变：
	PRINTFORMW 【清除古明地恋几乎的所有数据，再次開始初次见面！】
	PRINTFORMW 当然了当然！【如果那个恋恋发生了改变，这里的一切设定都极有可能失效————】
	PRINTFORMW 【因为这些设定只适用于那个恋恋，另一个恋恋就……】
	PRINTFORMW 就……不太管用？毕竟不了解～
	PRINTFORMW 
	PRINTFORMW 好的，欢迎回来～
	PRINTFORMW 时间在流逝，我们会忘记很多事。
	PRINTFORMW 那个恋恋还有很多事情要学————！
	PRINTFORMW 
	PRINTFORMW 
	CFLAG:38:1005 = 4
ELSEIF CFLAG:38:1005 == 0
	PRINTFORMW 
	PRINTFORMW 哦————
	PRINTFORMW 好了，终于到了最严肃的时候！
	PRINTFORMW 【古、明、地、恋、的、口、上、模、式、设、定！】
	PRINTFORMW 坦白来说这其实是标题欺诈～
	PRINTFORMW 因为这个【口上模式设定只有一个功能，那就是重置古明地恋的所有数据！】
	PRINTFORMW 呃……
	PRINTFORMW 因为【古明地恋口上实际是由多个口上整合而成。】
	PRINTFORMW 而且【彼此之间差距太大了，只能同时让一个口上运行】
	PRINTFORMW 
	PRINTFORMW 综上所述【那个恋恋的表现形式不止有一个……】
	PRINTFORMW 如果必须要摧毁整个世界来更换古明地恋的表现形式————
	PRINTFORMW ————那就太不“妖怪道”了！！！！
	PRINTFORMW 所以！现在就有一个机会！【重置古明地恋的所有数据，从初次见面開始再来一次！】
	PRINTFORMW 与此同时，如果【在这个口上设定里，所做的一切更改都极有可能会随着那个恋恋的改变而失效————】
	PRINTFORMW 意思就是，【这些设定只在适用于那个恋恋，另一个恋恋就……】
	PRINTFORMW 就……
	PRINTFORMW 
	PRINTFORMW 不过是有点儿残酷……
	PRINTFORMW 时间在流逝，我们会忘记很多事。
	PRINTFORMW 很难说在改变之后，那个恋恋还能记得多少东西……
	PRINTFORMW 
	PRINTFORMW 
	CFLAG:38:1005 = 2
ELSE
	;因为很严肃和压抑，连语气助词都少了很多。
	PRINTFORML 
	PRINTFORML 【清除古明地恋的所有数据，重新開始初次见面。】
	PRINTFORML 一般用于【更换古明地恋的表现形式。】
	PRINTFORML 因为本口上实际是由【多个古明地恋口上整合而成。】
	PRINTFORML 【但彼此之间的兼容度太低，只能让一个口上运行……】
	PRINTFORML 因此，提供一个【清除和重新选择】的机会是理所应当的。
	PRINTFORML 不过与此同时，【如果那个恋恋改变，在本口上的设定里，一切更改都极有可能失效。】
	PRINTFORML 【有机会回来就再改一次～】
	PRINTFORML 
ENDIF
PRINTFORML
PRINTFORML
PRINTFORML
PRINTFORML [0]-清除数据
PRINTFORML 
PRINTFORML [900]-返回上一个菜单
PRINTFORML 
PRINTFORML [999]-退出
INPUT
SELECTCASE RESULT
	CASE 0
		IF CFLAG:38:1005 == 2
			PRINTFORMW 
			PRINTFORMW 嗯，这可是一项大工程————
			PRINTFORMW 如果准备好了，就可以马上開始！
			PRINTFORMW 
			PRINTFORML 
			PRINTFORML [0]-已经考虑好了。
			PRINTFORML [1]-没，開玩笑的。
			CFLAG:38:1005 = 3
		ELSEIF CFLAG:38:1005 == 4
			PRINTFORMW 
			PRINTFORMW 又要看看崭新的世界吗？
			PRINTFORMW 啊，是这样的，多周目的变化其实只有一次！
			PRINTFORMW 这是真的————
			PRINTFORMW 
			PRINTFORML 
			PRINTFORML [0]-世界这么大，我想再去看看。
			PRINTFORML [1]-算了。
		ELSE
			PRINTFORMW 
			PRINTFORMW 哼嗯？真的考虑好了？
			PRINTFORMW 如果准备好了的话，那就開始吧！
			PRINTFORMW 
			PRINTFORML 
			PRINTFORML [0]-開始清除数据！
			PRINTFORML [1]-不，不不不不不不不。
		ENDIF
		INPUT
		SELECTCASE RESULT
			CASE 0
				IF CFLAG:38:1005 == 4
					PRINTFORMW 
					PRINTFORMW 话说回来，其实人类的思维很好读懂的。
					PRINTFORMW 只要有个指向的东西……东西……人类就会往那边去想……
					PRINTFORMW 说的就是这个！
					PRINTFORMW 你肯定在想“啊这个家伙肯定在骗我、下个周目肯定有新内容！”
					PRINTFORMW 没有的事情啊……
					PRINTFORMW 嗯……哼！
					PRINTFORMW 那么，下次再见吧————
					PRINTFORMW 准备～倒计时！
				ELSE	
					PRINTFORMW 
					PRINTFORMW ……
					PRINTFORMW 唉～
					PRINTFORMW 那么，这是安全措施。
					PRINTFORMW 在这个时候关掉update和游戏，那么一切都还来得及。
					PRINTFORMW 当然啦，如果继续点下去……
					PRINTFORMW 也没什么大不了的，因为我们之后肯定会再见。
					PRINTFORMW 当然，如果是新開一个存档，那就另说————
					PRINTFORMW 嗯，咳额哼哼！
					PRINTFORMW 准备～倒计时！
				ENDIF
					PRINTFORMW 3
					PRINTFORMW 2
					PRINTFORMW 1
					PRINTFORMW 0
					SIF CFLAG:38:1005 < 4
						CFLAG:38:1005 = 1
					SIF CFLAG:38:1005 == 4
						CFLAG:38:1005 = 5
					CALL RESET_KOJO_CHARA(38)
					PRINTFORMW 【数据已清除。】
					RETURN
			CASE 1
				CALL M_KOJO_K38_REGRET_1
				GOTO KOMEIJI_KOISHI
				RETURN
			CASEELSE
				CALL M_KOJO_K38_WHAT_IS_IT
				RETURN
		ENDSELECT
	CASE 900
		GOTO UPDATE
	CASE 999
		RETURN
	CASEELSE
		CALL M_KOJO_K38_WHAT_IS_IT
ENDSELECT
RETURN
$KOMEIJI_KOISHI_IS_DAILY
IF CFLAG:38:1006 == 0
	CFLAG:38:1006 = 1
	$NOPE_1
	PRINTFORMW ……
	PRINTFORMW …
	PRINTFORMW ……
	GOTO UPDATE
	RETURN
ELSEIF CFLAG:38:1006 == 1
	CFLAG:38:1006 = 2
	GOTO NOPE_1
	RETURN
ELSEIF CFLAG:38:1006 == 2
	CFLAG:38:1006 = 3
	GOTO NOPE_1
	RETURN
ELSEIF CFLAG:38:1006 == 3
	CFLAG:38:1006 = 4
	PRINTFORMW ……
	PRINTFORMW …
	PRINTFORMW ……
	PRINTFORMW 什、什么嘛！
	PRINTFORMW 
	PRINTFORMW 嗯，好执着哦！
	PRINTFORMW 哈～要恭喜你找到了这个彩、彩蛋哦！
	PRINTFORMW 
	PRINTFORMW （稿子上面说接下来要自由发挥……）
	PRINTFORMW 
	PRINTFORMW （过分的家伙……）
	PRINTFORMW 
	PRINTFORMW 嗯……说些什么好呢？
	PRINTFORMW 
	GOTO UPDATE
	RETURN
ELSEIF CFLAG:38:1006 == 4
	CFLAG:38:1006 = 5
	GOTO NOPE_1
	RETURN
ELSEIF CFLAG:38:1006 == 5
	CFLAG:38:1006 = 6
	PRINTFORMW ……
	PRINTFORMW …
	PRINTFORMW ……
	PRINTFORMW 
	PRINTFORMW 还没想好哦————！
	PRINTFORMW 不要着急！
	PRINTFORMW 
	PRINTFORMW （嗯，装作一个只会念稿子的机器妖怪就好了……）
	PRINTFORMW （对……这里出了bug，要沉默以对……）
	PRINTFORMW 
	PRINTFORMW （呼～）
	PRINTFORMW （冷静、镇静、）
	PRINTFORMW （四、三、二、一！）
	PRINTFORMW 
	GOTO UPDATE
	RETURN
ELSEIF CFLAG:38:1006 == 6
	CFLAG:38:1006 = 7
	GOTO NOPE_1
	RETURN
ELSEIF CFLAG:38:1006 == 7
	CFLAG:38:1006 = 8
	GOTO NOPE_1
	RETURN
ELSEIF CFLAG:38:1006 == 8
	CFLAG:38:1006 = 9
	GOTO NOPE_1
	RETURN
ELSEIF CFLAG:38:1006 == 9
	CFLAG:38:1006 = 10
	GOTO NOPE_1
	RETURN
ELSEIF CFLAG:38:1006 == 10
	CFLAG:38:1006 = 11
	$NOPE_2
	PRINTFORMW ……
	PRINTFORMW …
	PRINTFORMW ……
	PRINTFORMW 
	PRINTFORMW ——————————【恭喜你找到了一个彩蛋！】——————————
	PRINTFORMW 
	GOTO UPDATE
	RETURN
ELSEIF CFLAG:38:1006 == 11
	CFLAG:38:1006 = 12
	GOTO NOPE_2
	RETURN
ELSEIF CFLAG:38:1006 == 12
	CFLAG:38:1006 = 13
	GOTO NOPE_2
	RETURN
ELSEIF CFLAG:38:1006 == 13
	CFLAG:38:1006 = 14
	GOTO NOPE_2
	RETURN
ELSEIF CFLAG:38:1006 == 14
	PRINTFORMW ……
	PRINTFORMW …
	PRINTFORMW ……
	PRINTFORMW 
	PRINTFORMW ——————————【恭喜您找到了一个彩蛋！】——————————
	PRINTFORMW 
	CFLAG:38:1006 = 15
	GOTO NOPE_3
	RETURN
ELSEIF CFLAG:38:1006 == 15
	PRINTFORMW 
	CFLAG:38:1006 = 16
	$NOPE_3
	PRINTFORML 
	PRINTFORML ——————————【古明地恋的口上设定】——————————
	PRINTFORML 
	PRINTFORML [999]-退出
	PRINTFORML [999]-退出
	PRINTFORML [999]-退出
	PRINTFORML [999]-退出
	PRINTFORML [999]-退出
	PRINTFORML 
	PRINTFORML [999]-退出
	PRINTFORML 
	PRINTFORML [999]-退出
	PRINTFORML [999]-退出
	PRINTFORML [999]-退出
	PRINTFORML [999]-退出
	INPUT
	RETURN
ELSEIF CFLAG:38:1006 == 16
	CFLAG:38:1006 = 17
	GOTO UPDATE
	RETURN
ELSEIF CFLAG:38:1006 == 18
	PRINTFORML 
	PRINTFORML ——————————【当前版本】——————————
	PRINTFORML 
	PRINTFORML 当前的版本是～
	PRINTFORML 0.268
	PRINTFORML 越接近1，就代表基础的完成度越高。
	PRINTFORML 
	PRINTFORML 于现世的2020年11月11日记录～！
	PRINTFORML
	PRINTFORML
	PRINTFORML
	PRINTFORML [0]-回顾历史版本
	PRINTFORML 
	PRINTFORML [900]-返回上一个菜单
	PRINTFORML 
	PRINTFORML [999]-退出
	INPUT
	SELECTCASE RESULT
		CASE 0
			$KOMEIJI_KOISHI_IS_DAILY_1
			PRINTFORML 
			PRINTFORML ——————————【历史版本】——————————
			PRINTFORML 
			PRINTFORML [1]-Ver 0.238 (2019.8.25 ————2019.10.12)
			PRINTFORML [2]-Ver 0.244 (2019.10.12————2019.10.31)
			PRINTFORML 
			PRINTFORML [3]-Ver 0.257 (2019.10.31————2020.2.15 )
			PRINTFORML [4]-Ver 0.268 (2020.2.15 ————2020.11.11)
			PRINTFORML 
			PRINTFORML [900]-返回上一个菜单
			PRINTFORML 
			PRINTFORML [999]-退出
			INPUT
			SELECTCASE RESULT
				CASE 1
					PRINTFORMW 
					PRINTFORMW ……
					PRINTFORMW 这里是未公開版本哦～是观测開始的地方～！
					PRINTFORMW 能看见最基础的对话————
					PRINTFORMW ————泡茶
					PRINTFORMW ————看书！
					PRINTFORMW ————弹幕游戏！！！
					PRINTFORMW 
					GOTO KOMEIJI_KOISHI_IS_DAILY_1
					RETURN
				CASE 2
					PRINTFORMW 
					PRINTFORMW 这里是第一个的公開版本～
					PRINTFORMW 因为发现了恋恋不只有一个……！
					PRINTFORMW 诶————
					PRINTFORMW 不只有一个----
					PRINTFORMW 
					GOTO KOMEIJI_KOISHI_IS_DAILY_1
					RETURN
				CASE 3
					PRINTFORMW 
					PRINTFORMW 这里是第一个的整合版本～
					PRINTFORMW 感谢 木船 带来的、
					PRINTFORMW 有关另一个世界的恋恋的故事～
					PRINTFORMW 
					GOTO KOMEIJI_KOISHI_IS_DAILY_1
					RETURN
				CASE 4
					PRINTFORMW 
					PRINTFORMW 现在这里的现世正在对抗病毒吧……
					PRINTFORMW (日记涂改的痕迹)
					PRINTFORMW 祝你们平安……
					PRINTFORMW 
					PRINTFORMW 我们现在正在幻想乡那里实地考察……
					PRINTFORMW 那个恋恋和你的故事还在延续……
					PRINTFORMW 呼～
					PRINTFORMW 
					GOTO KOMEIJI_KOISHI_IS_DAILY_1
					RETURN
				CASE 900
					GOTO KOMEIJI_KOISHI_IS_DAILY
					RETURN
				CASE 999
					RETURN
				CASEELSE
					CALL M_KOJO_K38_WHAT_IS_IT
					RETURN
			ENDSELECT
		CASE 900
			GOTO UPDATE
			RETURN
		CASE 999
			RETURN
		CASEELSE
			CALL M_KOJO_K38_WHAT_IS_IT
			RETURN
	ENDSELECT
	RETURN
ENDIF
RETURN

@M_KOJO_K38_OKEY_OKAY
PRINTDATA
	DATALIST
		DATAFORM 好的？
	ENDLIST
	DATALIST
		DATAFORM 嗯，完成了。
	ENDLIST
	DATALIST
		DATAFORM 可以了。
	ENDLIST
	DATALIST
		DATAFORM 做完了。
	ENDLIST
	DATALIST
		DATAFORM 呼，搞定了。
	ENDLIST
	DATALIST
		DATAFORM 行了～
	ENDLIST
	DATALIST
		DATAFORM 好了～！
	ENDLIST
ENDDATA
PRINTFORMW
RETURN

@M_KOJO_K38_NO_AGAIN_1
PRINTDATA
	DATALIST
		DATAFORM ……
		DATAFORM 诶！这个模式已经開启了！
	ENDLIST
	DATALIST
		DATAFORM 已经開启了哦。
		DATAFORM 是的，这里就是開启，没有关闭按钮的。
	ENDLIST
	DATALIST
		DATAFORM 嗯……虽然不是很理解，为什么要再次開启呢？
		DATAFORM 開玩笑也要有个限度啊！(谁都要休息的嘛)
	ENDLIST
	DATALIST
		DATAFORM 啊……哈？已经開了。
		DATAFORM 哈～去睡觉了～！
	ENDLIST
	DATALIST
		DATAFORM 是、是！
		DATAFORM 其实会说的话也就那么几句啦。
	ENDLIST
	DATALIST
		DATAFORM 呼～真累啊。
		DATAFORM 不要再继续了，已经開启了。
	ENDLIST
ENDDATA
PRINTFORMW
RETURN


@M_KOJO_K38_REGRET_1
PRINTDATA
	DATALIST
		DATAFORM 这样啊……
		DATAFORM 不过事先说好，这里可没有说明哦————
		DATAFORM 接着往下走可没有说明哦！
	ENDLIST
	DATALIST
		DATAFORM 哪里有油吗？
		DATAFORM 请注意清洁～
	ENDLIST
	DATALIST
		DATAFORM 后悔药难买～
		DATAFORM 好在我们这里是免费的。
	ENDLIST
	DATALIST
		DATAFORM 总而言之————
		DATAFORM 请管好您的手指！
	ENDLIST
	DATALIST
		DATAFORM 啊、要好好思考一下。
		DATAFORM 嗯，确实。
	ENDLIST
	DATALIST
		DATAFORM 好的，好的！
		DATAFORM 不按那个红色的按钮就是了————
		DATAFORM 诶，不对，你应该看不到红色的按钮。
	ENDLIST
ENDDATA
PRINTFORMW
RETURN

@M_KOJO_K38_WHAT_IS_IT
PRINTDATA
	DATALIST
		DATAFORM 先生们，女士们————
		DATAFORM 已经没有这种操作了～！
	ENDLIST
	DATALIST
		DATAFORM 啊？啊？哪里有问题吗？
		DATAFORM 再试试看看～？
	ENDLIST
	DATALIST
		DATAFORM 不不不，这样是不、行、的！
		DATAFORM 什么也没有哦～！
	ENDLIST
	DATALIST
		DATAFORM 可能是你按错了什么东西……
		DATAFORM 这里有点儿小麻烦，不过你可以先看看别的地方～
	ENDLIST
	DATALIST
		DATAFORM 不不不，很抱歉这真的办不到。
		DATAFORM 没有的东西就是没有嘛！！！
	ENDLIST
	DATALIST
		DATAFORM 注意————
		DATAFORM 不要制造错误————
	ENDLIST
	DATALIST
		DATAFORM 真的什么也没有！
		DATAFORM 那就继续吧～
	ENDLIST
ENDDATA
PRINTFORMW
CALL K38_CONFEDERATION_UPDATE_1
RETURN


