;-------------------------------------------------
;手淫
;奉仕系コマンド、レベル1
;-------------------------------------------------
@COM80
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = TARGET
;奉仕快楽
TCVAR:108 = 1

CSTR:PLAYER:10 += @"{TARGET}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:PLAYER:2 = 3
;強度
TCVAR:PLAYER:4 = 700

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
IF ABL:指 < 4 || FLAG:70
	SOURCE:PLAYER:快Ｃ += 80
ELSE
	SOURCE:PLAYER:快Ｃ += ABL:指 * 20
ENDIF

;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 40

ELSE

	DOWNBASE:体力 += 10
	DOWNBASE:気力 += 100

	SOURCE:情愛 = 50
	SOURCE:性行動 = 250
	SOURCE:達成 = 50
	SOURCE:屈従 = 350
	SOURCE:逸脱 = 100
	SOURCE:反感 = 100

	;上のほうで計算した汚れデータ
	SOURCE:不潔 = Y*10 + 60


	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 200
ENDIF
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷の指⇔調教者のＰの汚れが移動
STAIN:1 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:1

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;手淫経験
SIF !FLAG:70 && !CFLAG:イタズラ
	EXP:手淫経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1 || CFLAG:イタズラ
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ENDIF
RETURN 1

@CAN_COM80(ARG)
SKIPDISP ARG
PRINTL 手淫
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
IF FLAG:70 == 1 || CFLAG:イタズラ

ELSE
	EXP:手淫経験 ++
	A = 0
	S = 0

	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		A += ABL:欲望 * 1
		PRINT 欲望
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		A += ABL:奉仕精神 * 4
		PRINT 奉仕精神
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
		S = 1
	ENDIF
	;ABL:精液中毒
	IF ABL:精液中毒
		SIF S
			PRINT  + 
		A += ABL:精液中毒 * 1
		PRINT 精液中毒
		PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 1,')
		S = 1
	ENDIF

	IF MARK:快楽刻印
		SIF S
			PRINT  + 
		A += MARK:快楽刻印 * 1
		PRINT 快楽刻印
		PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF GETPALAMLV(PALAM:欲情,5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:欲情,5) * 1
		PRINT 欲情
		PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 1,')
		S = 1
	ENDIF
	;恥じらい
	IF TALENT:羞恥心 > 0
		PRINT  - 
		A -= 1
		PRINT 恥じらい
		PRINTV '(,1,')
		S = 1
	ENDIF
	;汚臭鈍感
	IF TALENT:汚臭耐性 > 0
		SIF S
			PRINT  + 
		A += 1
		PRINT 汚臭鈍感
		PRINTV '(,1,')
		S = 1
	ENDIF
	;汚臭敏感
	IF TALENT:汚臭耐性 < 0
		A -= 3
		PRINT 汚臭敏感
		PRINTV '(,3,')
		S = 1
	ENDIF
	;献身的
	IF TALENT:献身的
		SIF S
			PRINT  + 
		A += 6
		PRINT 献身的
		PRINTV '(,6,')
		S = 1
	ENDIF
	;快感の否定（淫乱、娼婦で無効）
	IF TALENT:快感応答 < 0 && !TALENT:淫乱
		PRINT  - 
		A -= 1
		PRINT 快感の否定
		PRINTV '(,1,')
		S = 1
	ENDIF
	;男嫌い（恋慕、親愛で無効）
	IF OTOKOGIRAI(TARGET)
		PRINT  - 
		A -= 5
		PRINT 男嫌い
		PRINTV '(,5,')
		S = 5
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 5
		PRINT 恋慕
		PRINTV '(,5,')
		S = 5
	ENDIF
	;親愛
	IF TALENT:恋慕 > 1 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 5
		PRINT 親愛
		PRINTV '(,5,')
		S = 5
	ENDIF

	;調教者がふたなり
	IF (TALENT:PLAYER:2 & 3) == 3
		SIF S
			PRINT  + 
		A += 8
		PRINT ふたなり
		PRINTV '(,8,')
		S = 1
	ENDIF

	Y = 0
	;愛液の汚れ
	SIF STAIN:PLAYER:2 & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:PLAYER:2 & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:PLAYER:2 & 8
		Y += 7
	SIF STAIN:PLAYER:2 & 16
		Y += 15
	;手扱きでは汚れがそれほど影響しない
	Y /= 3
	Y = Y * (2 - TALENT:汚臭耐性) / 2

	;汚れあり
	IF Y
		PRINT  - 
		A -= Y
		PRINT 汚れあり
		;汚臭鈍感
		IF TALENT:汚臭耐性 > 0
			PRINT 、汚臭鈍感
		ELSEIF TALENT:汚臭耐性 < 0
			PRINT 、汚臭敏感
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF


	;合計を表示(14以上で実行)
	PRINT  = 
	PRINTV A

	V = 14
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V

	WAIT
	SKIPDISP 0
	;実行できない
	SIF A < V
		RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE80
;手淫実行判定
SIF !TFLAG:100
	RETURN 0
SIF GLOBAL_COMABLE(80)
	RETURN RESULT
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:PLAYER:2 & 2)
	RETURN 0
;時間停止中は許可
IF FLAG:70 != 1
	;縄使用中は不可
	SIF TEQUIP:縄
		RETURN 0
ENDIF
RETURN 1

