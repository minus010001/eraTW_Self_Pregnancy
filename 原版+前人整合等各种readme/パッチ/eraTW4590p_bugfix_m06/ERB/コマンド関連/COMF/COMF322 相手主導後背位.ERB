;-------------------------------------------------
;相手主導後背位
;セックス系コマンド、レベル1
;-------------------------------------------------
@COM322
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;室内でないorバスルームでプレイマットが無いと立位に
SIF !INROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20)
	TFLAG:193 = 1

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER

;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1


CSTR:PLAYER:10 += @"{TARGET}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:PLAYER:2 = 1
;強度
TCVAR:PLAYER:4 = 1800

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 13
;強度
TCVAR:TARGET:4 = 1500


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
DOWNBASE:PLAYER:体力 += 80
SOURCE:PLAYER:快Ｃ = 450
DOWNBASE:体力 += 40
DOWNBASE:気力 += 50

SOURCE:快Ｖ = 500
SOURCE:情愛 = 200
SOURCE:苦痛 = 300
SOURCE:露出 = 150
SIF TFLAG:193
	SOURCE:露出 = 300

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*10 + 60
CALL 特殊ソース処理
;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_V(TARGET)
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
CALL STAIN_膣内Ｐ挿入
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:PLAYER:挿入経験 ++
EXP:Ｖ経験 ++
;同性愛　レズなら戻り値1ゲイなら戻り値2

IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
ENDIF
EXP:Ｖ性交経験 ++
IF !ASSIPLAY
	EXP:愛情経験 += TALENT:恋慕 * 3
	;処女
	SIF TALENT:処女 == 1 && TALENT:恋慕
		EXP:愛情経験 += 10
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE322
SIF !TFLAG:100
	RETURN 0
SIF TCVAR:20 != 43
	RETURN 0
;調教者がオトコ、ふたなりの場合はある程度勃起してないとダメ
SIF (TALENT:PLAYER:2 & 2) && BASE:PLAYER:勃起 < 1000
	RETURN 0
;バイブ挿入中はダメ
SIF TEQUIP:13
	RETURN 0
;オトコだとダメ
SIF !(TALENT:2 & 1)
	RETURN 0
;体格差がある場合は経験がないとダメ
SIF 挿入不可(TARGET) == 1 && EXP:Ｖ経験 < 50 && !TALENT:PLAYER:禁断の知識
	RETURN 0
;さらに体格差があると禁断の知識が必要
SIF 挿入不可(TARGET) == 2 && (!TALENT:PLAYER:禁断の知識 || EXP:Ｖ経験 < 50)
	RETURN 0
SIF 挿入不可(TARGET) == 3
	RETURN 0
;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF !(TALENT:PLAYER:2 & 2) && ITEM:PBAND == 0
	RETURN 0
;処女で、従順4以下・レズっ気4以下のASSIの場合はできない（サドならＯＫ）
IF TALENT:処女 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 4 || ABL:ASSI:レズっ気 <= 4) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF

;対面座位からはダメ
SIF TEQUIP:体位 == 4
	RETURN 0
;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF

RETURN 1